0 POKE55,240:POKE56,120:CLR
1 QO=53282
2 QP=53283
3 QQ=53281
4 QR=53280
5 QS=49152
6 V=53248
7 QT=30960
8 Q1=0
9 Q2=1
10 Q3=2
11 Q4=3
12 Q5=4
13 Q6=5
14 Q7=6
15 Q8=7
16 Q9=8
17 QA=9
18 QB=10
19 CB=11
20 QC=12
21 QD=13
22 QE=14
23 QF=15
24 QG=8
25 QH=9
26 QI=10
27 QJ=11
28 QK=12
29 QL=13
30 QM=14
31 QN=15
32 MQ=14
33 IC=19
34 HC=14
35 EC=12
36 IX=99
37 TM=23
38 RS=165
39 DD$="{home}{down:20}{white}"
40 CD$="{down:25}"
41 CT$="{home}{down}{white}{right}                                      "
42 DIMM$(MQ),MD$(MQ)
43 DIMMH(MQ),MM(MQ)
44 DIMMA(MQ),MD(MQ)
45 DIMMS(MQ)
46 DIMMB(MQ),MX(MQ)
47 DIMFH(11),FT(11),FC(11)
48 DIMFM(11),FS(11),FX(11),FY(11)
49 DIMFA(11),FD(11),F1(11),F2(11)
50 FORI=0TO3:FX(I)=37:FY(I)=5+3*I:NEXT
51 DIMI$(IC),IA(IC)
52 DIMID(IC),IS(IC)
53 DIMIE(IC),IT(IC)
54 DIMH(HC)
55 DIMER(EC),EX(EC)
56 DIMEY(EC),ET(EC)
57 DIMIN(IX)
58 DIMT$(TM)
59 DIMTN$(TM)
60 DIMTT(IX)
61 FORI=0TOMQ-1
62 READM$(I),MH(I),MM(I),MS(I)
63 READMD$(I),MA(I),MD(I),MB(I),MX(I)
64 NEXT
65 FORI=0TO3
66 READP$(I),PH(I),PM(I),PS(I)
67 READPD$(I),PA(I),PD(I),PW(I),PR(I)
68 NEXT
69 PM$="{rvrs on}{lt. red}UV{down}{left:2}WX{rvrs off}"
70 FORI=0TOTM-1
71 READT$(I)
72 NEXT
73 FORI=0TOIC-1
74 READI$(I),IA(I),ID(I),IS(I),IE(I),IT(I)
75 NEXT
76 POKEV+21,0
77 POKE648,192
78 POKE56576,0
79 POKE53272,8
80 POKE53270,PEEK(53270)OR16
81 POKEQO,QA
82 POKEQP,Q1
83 POKEQR,Q1
84 POKEQQ,Q7
85 PRINT"{white}"+CHR$(147)
86 Z=-1
87 FORA=0TO255
88 B=0
89 Z=Z+1
90 IFZ=16THENPRINT:Z=0
91 IFA>=0ANDA<=31THENC=A+64:PRINTCHR$(C);
92 IFA>=32ANDA<=63THENC=A+00:PRINTCHR$(C);
93 IFA>=64ANDA<=95THENC=A+32:PRINT"{gray1}"+CHR$(C);
94 IFA>=96ANDA<=127THENC=A+64:PRINT"{gray1}"+CHR$(C);
95 IFA=162THENPRINT" ";:GOTO100
96 IFA>=128ANDA<=159THENC=A-64:PRINT"{gray1}"+CHR$(18)+CHR$(C);
97 IFA>=160ANDA<=191THENC=A-128:PRINT"{gray1}"+CHR$(18)+CHR$(C);
98 IFA>=192ANDA<=254THENC=A:PRINT"{gray1}"+CHR$(18)+CHR$(C);
99 IFA=255THENC=A-64:PRINT"{gray1}"+CHR$(18)+CHR$(C);
100 NEXTA
101 PRINTCHR$(5)
102 STOP
103 GOTO647
104 POKE1020,Q7
105 POKE1021,Q7
106 POKE1022,74
107 SYS820
108 GOSUB442
109 GOSUB449
110 PRINT"{home}{down}{right}{white}                                      ";
111 POKE1020,Q7
112 GOSUB547
113 IFPEEK(2)=1THENCP=30960+RS*(CR-0)
114 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)PM$;
115 OX=ZX:OY=ZY
116 ZX=X:ZY=Y
117 GOSUB738
118 IFA$=CHR$(145)ORA$="W"THENPRINTCT$;:ZY=Y-1:GOTO124
119 IFA$=CHR$(157)ORA$="A"THENPRINTCT$;:ZX=X-1:GOTO124
120 IFA$=CHR$(29)ORA$="D"THENPRINTCT$;:ZX=X+1:GOTO124
121 IFA$=CHR$(17)ORA$="S"THENPRINTCT$;:ZY=Y+1:GOTO124
122 IFA$=CHR$(13)THEN181
123 GOTO117
124 AA=0
125 FORI=0TOEC
126 IFER(I)=CRANDEX(I)=ZXANDEY(I)=ZYTHENAA=1
127 NEXTI
128 IFZX=-1ORZX=20ORZY=-1ORZY=8THEN140
129 C=PEEK(CP+ZX+ZY*20)
130 IFC=1THEN161
131 IFC=2THEN154
132 IFC=4THEN154
133 IFC=8THEN167
134 IFC=9THEN154
135 IFC=13THEN180
136 IFC=14THEN171
137 IFC=17THEN167
138 IFC=21THEN154
139 IFC>9THEN115
140 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)T$(PEEK(CP+X+Y*20))
141 IFRND(1)*100>97ANDPEEK(CP+164)>0THENFF=0:GOTO257
142 X=ZX:Y=ZY
143 IFX=-1ANDPEEK(CP+160)=255THENX=0:GOTO114
144 IFX=-1THENCR=PEEK(CP+160):X=19:GOTO111
145 IFX=20ANDPEEK(CP+161)=255THENX=19:GOTO114
146 IFX=20THENCR=PEEK(CP+161):X=0:GOTO111
147 IFY=-1ANDPEEK(CP+162)=255THENY=0:GOTO114
148 IFY=-1THENCR=PEEK(CP+162):Y=7:GOTO111
149 IFY=8ANDPEEK(CP+163)=255THENY=7:GOTO114
150 IFY=8THENCR=PEEK(CP+163):Y=0:GOTO111
151 IFC=5THEN161
152 IFC=6THEN161
153 GOTO114
154 IFCR=5THENCR=2:X=3:Y=3:GOTO110
155 IFCR=2THENCR=5:X=11:Y=4:GOTO110
156 IFCR=0THENCR=6:X=9:Y=6:GOTO110
157 IFCR=6THENCR=0:X=3:Y=4:GOTO110
158 IFCR=11THENCR=13:X=5:Y=6:GOTO110
159 IFCR=13THENCR=11:X=2:Y=4:GOTO110
160 IFCR=16THEN676
161 IFC=1ANDCR=4ANDZX=6THENH(0)=1-H(0):GOSUB560:GOSUB576:GOTO111
162 IFC=1ANDCR=4ANDZX=14THENH(1)=1-H(1):GOSUB560:GOSUB576:GOTO115
163 IFC=5ANDCR=14ANDX=7ANDH(6)=0THENH(6)=1:GOSUB560:GOTO110
164 IFC=5ANDCR=14ANDX=17ANDH(7)=0THENH(7)=1:GOSUB560:GOTO110
165 IFC=6ANDCR=12ANDY=2ANDH(12)=0THENH(12)=1:GOSUB560:GOTO110
166 IFC=6ANDCR=12ANDY=6ANDH(13)=0THENH(13)=1:GOSUB560:GOTO110
167 IFC=8ANDAA=0THENGOTO115
168 IFC=8ANDAA=1THENPRINT"{home}{white}{down}{right}IN DER TRUHE IST WAS!":GOSUB759:GOSUB522:GOTO115
169 IFC=17ANDAA=0THENGOTO115
170 IFC=17ANDAA=1THENPRINT"{home}{white}{down}{right}DER BAUM SIEHT INTRESSANT AUS!":GOSUB759:GOSUB522:GOTO115
171 IFCR=0THENPRINT"{white}{home}{down}{right:2}EIN HEILZAUBER IST UNTER DEM BAUM":GOSUB759:GOSUB522:GOTO115
172 IFCR=6THENPRINT"{white}{home}{down}{right:2}EIN FEUERZAUBER IST IN DEN KATAKOMBEN":GOSUB759:GOSUB522:GOTO115
173 IFCR=1THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB759:H(2)=1:GOSUB560:P3(1)=1:GOTO104
174 IFCR=4THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB759:H(5)=1:GOSUB560:P3(2)=1:GOTO104
175 IFCR=9ANDX<=2THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB759:H(9)=1:GOSUB560:P3(3)=1:GOTO104
176 IFCR=3THENPRINT"{white}{home}{down}{right:2}DU KANNST HIER NICHT DURCH!":GOSUB759:FF=4:H(4)=1:GOSUB560:GOTO257
177 IFCR=9ANDX>3THENPRINT"{white}{home}{down}{right:2}ACH WIE SUESS!":GOSUB759:FF=9:H(3)=1:GOSUB560:GOTO257
178 IFCR=15THENPRINT"{white}{home}{down}{right:2}DU WILLST MICH BESIEGEN!":GOSUB759:FF=13:H(8)=1:GOSUB560:GOTO257
179 IFCR=16THENPRINT"{white}{home}{down}{right:2}DU HAST MEINE ERWARTUNG UEBERTROFFEN!":GOSUB759:FF=14:H(10)=1:GOSUB560:GOTO257
180 IFC=13THENPRINT"{home}{white}{down}{right}DU BIST GEHEILT!":GOSUB759:GOSUB437:GOSUB449:GOSUB522:GOTO115
181 PRINT"{home}{down}{white}{right}  UNTERSUCHE  EQUIPMENTS  INVENTAR"
182 M=0
183 POKEQS+42+M*12,35
184 GOSUB722
185 POKEQS+42+M*12,32
186 IFA$="A"THENM=M-1-3*(M=0):GOTO183
187 IFA$="D"THENM=M+1+3*(M=2):GOTO183
188 IFA$=CHR$(13)THENPOKEQS+42+M*7,32:GOTO190
189 GOTO183
190 PRINT"{home}{down}{white}{right}                                      ";
191 ONM+1GOTO245,205,192
192 POKE1020,Q1
193 GOSUB518
194 PRINT"{home}{down}{right}{white}INVENTAR: WAEHLE DEIN ITEM           ";
195 MT=2:MX=2:MY=4:MC=14:GOSUB460
196 IFIN(M)=9THEN110
197 IFIN(M)=0ORIT(IN(M))<>MTTHEN110
198 IFIN(M)=3THENGOSUB201:IN(M)=0:GOSUB449:GOTO110
199 IFIN(M)=8THENGOSUB203:IN(M)=0:GOSUB449:GOTO110
200 GOTO110
201 FORI=0TO3:PH(I)=PH(I)+IE(IN(M)):IFPH(I)>P1(I)THENPH(I)=P1(I)
202 FH(I)=PH(I):NEXT:RETURN
203 FORI=0TO3:PM(I)=PM(I)+IE(IN(M)):IFPM(I)>P2(I)THENPM(I)=P2(I)
204 FM(I)=PM(I):NEXT:RETURN
205 POKE1020,Q1
206 GOSUB524
207 PRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINEN SPIELER     ";
208 P=0
209 CP=QS+2+(4-8*(P>1))*40:IFP=1ORP=3THENCP=CP+20
210 POKECP,35
211 GOSUB722
212 POKECP,32
213 IFP=0ANDA$="D"ANDP3(1)=1THENP=1:GOTO209
214 IFP=0ANDA$="S"ANDP3(2)=1THENP=2:GOTO209
215 IFP=0ANDA$="D"ANDP3(1)=OANDP3(2)=0ANDP3(3)=1THENP=3:GOTO209
216 IFP=0ANDA$="S"ANDP3(1)=OANDP3(2)=0ANDP3(3)=1THENP=3:GOTO209
217 IFP=1ANDA$="A"ANDP3(0)=1THENP=0:GOTO209
218 IFP=1ANDA$="S"ANDP3(3)=1THENP=3:GOTO209
219 IFP=2ANDA$="D"ANDP3(3)=1THENP=3:GOTO209
220 IFP=2ANDA$="W"ANDP3(0)=1THENP=0:GOTO209
221 IFP=3ANDA$="A"ANDP3(2)=1THENP=2:GOTO209
222 IFP=3ANDA$="W"ANDP3(1)=1THENP=1:GOTO209
223 IFP=3ANDA$="A"ANDP3(2)=OANDP3(1)=0ANDP3(0)=1THENP=0:GOTO209
224 IFP=3ANDA$="W"ANDP3(2)=OANDP3(1)=0ANDP3(0)=1THENP=0:GOTO209
225 IFA$=CHR$(13)THEN227
226 GOTO210
227 SL=0:CP=CP+81
228 PRINT"{home}{down}{right}{white}EQUIPMENT: WAFFE ODER RUESTUNG       ";
229 POKECP+SL*40,35
230 GOSUB722
231 POKECP+SL*40,32:IFA$="W"ORA$="S"THENSL=1-SL:GOTO229
232 IFA$=CHR$(13)THEN234
233 GOTO229
234 MY=4:MX=23:MC=14:IFP=1ORP=3THENMX=3
235 IFSL=0THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE WAFFFE       ";
236 IFSL=1THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE RUESTUNG     ";
237 PRINT"{home}"LEFT$(CD$,MY-1)SPC(MX-3);"{brown}{$c1}{$c2:18}{$c3}";
238 FORI=0TO13:PRINT"{down}{left:20}{$c4}                  {$c5}";:NEXT
239 PRINT"{down}{left:20}{$c6}{$c7:18}{$c8}";
240 MT=SL:GOSUB460
241 IFIT(IN(M))=9THEN110
242 IFIN(M)<>0ANDIT(IN(M))<>SLTHEN110
243 IFSL=0THENQ=PW(P):PW(P)=IN(M):IN(M)=Q:GOTO110
244 IFSL=1THENQ=PR(P):PR(P)=IN(M):IN(M)=Q:GOTO110
245 GOSUB522
246 GOSUB764
247 EI=0
248 IFER(EI)<>CROREX(EI)<>OXOREY(EI)<>OYTHEN253
249 NI=ET(EI):GOSUB555
250 IFIS=IXTHENPRINT"{home}{white}{down}{right}DU KANNST NICHT MEHR TRAGEN!":GOSUB759:GOSUB522:GOTO115
251 ER(EI)=-1
252 PRINT"{home}{white}{down}{right}GEFUNDEN: ";I$(IN(IS));:GOSUB750:GOSUB522:GOTO115
253 EI=EI+1
254 IFEI=ECTHEN256
255 GOTO248
256 PRINT"{home}{down}{right}{white}NICHTS BESONDERES!":GOSUB759:GOSUB522:GOTO115
257 FORI=4TO11:FT(I)=-1:NEXT
258 PP=4:CC=4:WX=0
259 FORI=0TO3:FT(I)=-2:FH(I)=PH(I):FM(I)=PM(I):FS(I)=PS(I)
260 IFP3(I)=0THENFH(I)=-1
261 IFFH(I)<=0THENFT(I)=-1:PP=PP-1
262 FA(I)=PA(I)+IA(PW(I)):FD(I)=PD(I)+ID(PR(I)):F1(I)=P1(I):F2(I)=P2(I)
263 NEXT
264 PRINT"{brown}{home}{$c1}{$c2:38}{$c3}";
265 PRINT"{$c4}                                      {$c5}";
266 PRINT"{$c6}{$c7:38}{$c8}";
267 PRINT"{home}{down:3}{white}";:FORI=0TO15:PRINT"                                        ";:NEXT
268 PRINTDD$;"{brown}{up}{$c1}{$c2:7}{white}HP{brown}{$c2}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
269 GOSUB418
270 GOSUB449
271 GOSUB457
272 FORI=0TO11
273 IFFT(I)<>-1THENFC(I)=INT(RND(0)*FD(I))
274 NEXT
275 CC=CC-4
276 PRINT"{home}{down}{right}                       ";:GOSUB587
277 FORI=4TO11:IFFT(I)=-1THEN279
278 PRINT"{home}"LEFT$(CD$,FY(I));SPC(FX(I));MD$(FT(I))
279 NEXT
280 C=0
281 IFFT(C)<>-1ANDFC(C)>=100THENFC(C)=FC(C)-100:GOTO284
282 C=C+1:IFC<12THEN281
283 FORI=0TO11:FC(I)=FC(I)+FS(I):NEXT:GOTO280
284 IFFT(C)=-2THEN286
285 GOTO367
286 PRINT"{home}{white}{down}{right}"P$(C)": IST AM ZUG                     "
287 PRINTDD$;SPC(16)"{white} ANGRIFF {down}{left:9} INVENTAR";:M=2
288 PRINTDD$;SPC(16)"{down:2}         {down}{left:9}         ";
289 IFPM(C)>0THENPRINTDD$;SPC(16)"{down:2} MAGIE   ";:M=3
290 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(37)"  {down}{left:2}  ";
291 PRINT"{left:3}{up}"PD$(C);
292 MY=0:FZ=0
293 POKEQS+816+MY*40,35
294 GOSUB722
295 IFA$="S"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY+1:GOTO299
296 IFA$="W"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY-1:GOTO299
297 IFA$=CHR$(13)THENFZ=0:GOTO302
298 GOTO294
299 IFMY>=MTHENMY=0
300 IFMY<0THENMY=M-1
301 GOTO293
302 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(36)"{white}  {down}{left:2}  {left}{up}"PD$(C);
303 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
304 ONMY+1GOTO330,322
305 MT=3:MX=16:MY=20:MC=4:GOSUB460
306 IFIT(IN(M))=9THEN286
307 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         {down}{left:9}         ";
308 IFIN(M)=0ORIT(IN(M))<>3THEN286
309 M=IN(M)
310 IFIE(M)>FM(C)THENPRINT"{home}{down}{right}NICHT GENUG MANA                     ";:GOSUB759:GOTO286
311 GOSUB603
312 GOTO629
313 AP=IE(M)+2-INT(RND(0)*5):FM(C)=FM(C)-IE(M):PM(C)=FM(C):GOSUB449
314 AP=AP+PL(C)*3
315 FH(CE)=FH(CE)-AP:IFFH(CE)<0THENFH(CE)=0
316 IFCE<4THENPH(CE)=FH(CE)
317 IFFH(CE)>0THENGOTO352
318 IFFT(CE)<>-2THENCC=CC-1:WX=WX+MX(FT(CE))
319 IFFT(CE)=-2THENPP=PP-1
320 FT(CE)=-1
321 GOTO352
322 MT=2:MX=16:MY=20:MC=4:GOSUB460
323 IFIT(IN(M))=9THEN286
324 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
325 PRINT"{down}{left:9}         ";
326 IFIN(M)=0ORIT(IN(M))<>MTTHEN286
327 IFIN(M)=3THENGOSUB201:IN(M)=0:GOSUB449:GOTO276
328 IFIN(M)=8THENGOSUB203:IN(M)=0:GOSUB449:GOTO276
329 GOTO276
330 GOSUB603
331 AP=FA(C)-FD(CE)
332 IFAP<=0THENPRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{white}MISS";:GOSUB757:GOTO344
333 AP=INT(RND(0)*AP)+1
334 FH(CE)=FH(CE)-AP:IFFH(CE)<0THENFH(CE)=0
335 IFCE<4THENPH(CE)=FH(CE)
336 IFFH(CE)>0THENGOTO341
337 IFFT(CE)<>-2THENCC=CC-1:WX=WX+MX(FT(CE))
338 IFFT(CE)=-2THENPP=PP-1
339 FT(CE)=-1
340 GOTO341
341 POKEV,(FX(CE)*8+21)AND255:POKEV+1,FY(CE)*8+50:POKE50168,16:POKEV+28,0
342 POKEV+16,-(FX(CE)>30):POKEV+21,1
343 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{white}";AP
344 FORI=0TO5:POKEV,PEEK(V)+2:POKEV+1,PEEK(V+1)+2:GOSUB758:NEXTI
345 POKEV+21,0:IFCE<4THENGOSUB449
346 GOSUB757
347 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
348 IFFT(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE));"  {down}{left:2}  ";
349 IFCC=0THEN375
350 IFPP=0THEN403
351 GOTO276
352 POKEV,(FX(CE)*8+24)AND255:POKEV+1,FY(CE)*8+50
353 IFM=4THENPOKEV+37,8:POKEV+38,2:POKEV+39,7:POKEV+28,1
354 B=17:IFM=6THENB=19:POKEV+28,0
355 IFM=5THENB=21:POKEV+28,0
356 IFM=7THENB=23:POKEV+28,0
357 POKEV+16,-(FX(CE)>30):POKE50168,B:POKEV+21,1
358 PRINT"{white}";:IFM=9THENPRINT"{green}";
359 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}";AP
360 FORK=0TO3:FORI=0TO1:POKE50168,B+I:FORJ=1TO40:NEXTJ,I,K
361 POKEV+21,0:IFCE<4THENGOSUB449
362 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
363 IFFT(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE));"  {down}{left:2}  ";
364 IFCC=0THEN375
365 IFPP=0THEN403
366 GOTO276
367 GOSUB757
368 PRINT"{white}{home}{down}{right}"M$(FT(C))": GREIFT AN                    "
369 FC(C)=FC(C)-100
370 IFFT(C)=7THENIFFM(C)>=IE(9)THENM=9:GOSUB757:GOSUB412:GOTO632
371 IFFT(C)=9THENIFFM(C)>=IE(6)THENM=6:GOSUB757:GOSUB406:GOTO313
372 IFFT(C)=10THENIFFM(C)>=IE(4)THENM=4:GOSUB757:GOSUB406:GOTO313
373 IFFT(C)=11THENIFFM(C)>=IE(12)THENM=12:GOSUB757:GOSUB406:GOTO639
374 GOSUB757:GOSUB406:GOTO331
375 POKE1020,Q1
376 PRINTDD$;"{brown}{up}{$c1}{$c2:10}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}{white}";
377 PRINT"{home}{down}{right}DU HAST GEWONNEN                     ";:GOSUB587
378 GOSUB518
379 FORI=0TO3:IFP3(I)=0THEN383
380 PRINT"{home}{white}{right:3}";:IFPH(I)=0THENPRINT"{red}";
381 PRINTLEFT$(CD$,4+I*3)P$(I)
382 PRINT"{home}"LEFT$(CD$,5+I*3)"{right:3}LV";PL(I)+1;" EX";PX(I)
383 NEXT
384 FORI=0TO3:IFPH(I)=0ORP3(I)=0THEN394
385 TX=WX
386 PRINT"{home}{white}"LEFT$(CD$,5+I*3)SPC(20)"EXPERIENCE {left}"TX"{left} ";
387 IFTX=0THEN394
388 XS=1:IFTX>9THENXS=10
389 IFTX>99THENXS=100
390 TX=TX-XS
391 PX(I)=PX(I)+XS
392 LL=10+PL(I)*PL(I)*10:IFPX(I)>=LLTHENGOSUB396:GOTO392
393 PRINT"{home}{white}"LEFT$(CD$,5+I*3)"{right:3}LV";PL(I)+1;" EX";PX(I):GOTO386
394 NEXTI
395 GOTO401
396 PL(I)=PL(I)+1:PA(I)=PA(I)+1:PD(I)=PD(I)+1:PS(I)=PS(I)+1
397 P1(I)=P1(I)+PL(I)*2:IFP2(I)>0THENP2(I)=P2(I)+PL(I)*3
398 IFP1(I)>999THENP1(I)=999
399 IFP2(I)>999THENP2(I)=999
400 RETURN
401 GOSUB750
402 GOTO110
403 PRINT"{home}{down}{right}{white}DU HAST VERLOREN                     "
404 GOSUB750
405 GOTO647
406 RE=INT(RND(0)*4):CE=0
407 IFFT(CE)=-2ANDPH(CE)>0THEN410
408 CE=CE+1:IFCE=4THENCE=0
409 GOTO407
410 IFRE>0THENRE=RE-1:GOTO408
411 RETURN
412 RE=INT(RND(0)*8):CE=4
413 IFFT(CE)>=0THEN416
414 CE=CE+1:IFCE=12THENCE=4
415 GOTO413
416 IFRE>0THENRE=RE-1:GOTO414
417 RETURN
418 IFFF>0THENL=FF:GOTO420
419 L=PEEK(CP+164):IFL=0THENRETURN
420 IFL=3THENA=0:LC=INT(RND(0))*2+2:FORI=0TOLC:GOSUB581:NEXT:RETURN
421 IFL=1THENA=1:FORI=0TO2:GOSUB581:NEXT:RETURN
422 IFL=2THENA=1:FORI=0TO1:GOSUB581:NEXT:A=2:FORI=0TO1:GOSUB581:NEXT:RETURN
423 IFL=4THENA=3:GOSUB581:A=1:GOSUB581:GOSUB581:RETURN
424 IFL=5THENA=4:LC=INT(RND(0)*3)+1:FORI=0TOLC:GOSUB581:NEXT:RETURN
425 IFL=6THENONINT(RND(1)*2)+1GOTO435,436
426 IFL=7THENLC=INT(RND(0)*3)+1:A=6:FORI=0TOLC:GOSUB581:NEXT:RETURN
427 IFL=8THENA=7:GOSUB581:LC=INT(RND(0)*2)+1:A=6:GOSUB581:RETURN
428 IFL=9THENA=9:GOSUB581:A=8:GOSUB581:A=5:GOSUB581:RETURN
429 IFL=10THENA=6:LC=INT(RND(0)*2)+1:FORI=1TOLC:GOSUB581:NEXT:RETURN
430 IFL=11THENA=7:GOSUB581:A=10:GOSUB581:RETURN
431 IFL=12ANDINT(RND(1)*2)=1THENA=11:GOSUB581:RETURN
432 IFL=12THENA=5:GOSUB581:GOSUB581:A=9:GOSUB581:RETURN
433 IFL=13THENA=9:GOSUB581:A=12:GOSUB581:A=9:GOSUB581:RETURN
434 IFL=14THENA=9:GOSUB581:A=13:GOSUB581:A=7:GOSUB581:RETURN
435 A=5:LC=INT(RND(1)*2)+1:FORI=0TOLC:GOSUB581:NEXT:RETURN
436 A=9:LC=INT(RND(1)*2)+1:FORI=1TOLC:GOSUB581:NEXT:RETURN
437 FORI=0TO3
438 IFP3(I)=0THEN441
439 PH(I)=P1(I)
440 PM(I)=P2(I)
441 NEXT:RETURN
442 PRINT"{brown}{home}{$c1}{$c2:38}{$c3}";
443 PRINT"{$c4}                                      {$c5}";
444 PRINT"{$c6}{$c7:38}{$c8}";
445 PRINTDD$;"{brown}{up}{$c1}{$c2:10}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
446 FORI=0TO3:PRINT"{$c4}          {$c5}{$c4}            {$c5}{$c4}            {$c5}";:NEXT
447 PRINT"{$c6}{$c7:10}{$c8}{$c6}{$c7:12}{$c8}{$c6}{$c7:12}";
448 POKE50151,72:POKE56295,9:RETURN
449 PRINTDD$;
450 FORI=0TO3:IFP3(I)=0THENPRINTSPC(40);:GOTO456
451 PRINT"{white}";:IFPH(I)=0THENPRINT"{red}";
452 PRINTSPC(27)P$(I)" ";:A=PH(I):GOSUB720
453 PRINTD$;
454 A=PM(I):GOSUB720
455 PRINT" "D$"{right}";
456 NEXT:RETURN
457 FORI=0TO3:IFPH(I)<=0ORP3(I)=0THENNEXT:RETURN
458 PRINT"{home}"LEFT$(CD$,5+I*3);SPC(37);PD$(I):NEXT
459 RETURN
460 GOSUB495
461 MI=-1:M=0:MO=0
462 MI=MO-1:CY=MY:MM=0
463 IFMC=4THENGOSUB490
464 MI=MI+1
465 PRINT"{home}{white}"LEFT$(CD$,CY)SPC(MX)" ";
466 IFIT(IN(MI))<>MTTHENPRINT"{cyan}";
467 IFIT(IN(MI))=9THENPRINT"{white}";
468 IFIT(IN(MI))=-1THENPRINT"{cyan}";
469 PRINTI$(IN(MI));
470 CY=CY+1
471 MM=MM+1
472 IFMM=MCTHEN474
473 IFMI<13THEN464
474 CP=QS+MX+MY*40
475 POKECP+(M-MO)*40,35
476 GOSUB722
477 POKECP+(M-MO)*40,32
478 IFA$="W"THENM=M-1:GOTO482
479 IFA$="S"THENM=M+1:GOTO482
480 IFA$=CHR$(13)THENRETURN
481 GOTO475
482 IFM=-1THENM=14-1:MO=14-MC:GOTO462
483 IFM=14THENM=0:MO=0:GOTO462
484 M%=0
485 IFM<MOTHENMO=MO-MC:M%=1:GOTO485
486 IFM>=MO+MCTHENMO=MO+MC:M%=1
487 IFMO<0THENMO=0:M%=1
488 IFM%=1THEN462
489 GOTO475
490 FORI=0TO3
491 PRINT"{home}{white}"LEFT$(CD$,CY+I)SPC(MX)" ";
492 PRINT"        ";
493 NEXTI
494 RETURN
495 A=0
496 FORI=0TO99:TT(I)=0:NEXTI
497 FORI=0TO99:TT(I)=IN(I):IN(I)=0:NEXTI
498 IFMT=0THENGOSUB503:GOSUB506:A=50:GOSUB509:GOSUB512:GOSUB515
499 IFMT=1THENGOSUB503:GOSUB509:A=50:GOSUB506:GOSUB512:GOSUB515
500 IFMT=2THENGOSUB503:GOSUB512:A=50:GOSUB506:GOSUB509:GOSUB515
501 IFMT=3THENGOSUB503:GOSUB515:A=50:GOSUB506:GOSUB509:GOSUB512
502 RETURN
503 FORI=0TO99:IFIT(TT(I))=9THENIN(A)=TT(I):A=A+1
504 NEXTI
505 RETURN
506 FORI=0TO99:IFIT(TT(I))=0THENIN(A)=TT(I):A=A+1
507 NEXTI
508 RETURN
509 FORI=0TO99:IFIT(TT(I))=1THENIN(A)=TT(I):A=A+1
510 NEXTI
511 RETURN
512 FORI=0TO99:IFIT(TT(I))=2THENIN(A)=TT(I):A=A+1
513 NEXTI
514 RETURN
515 FORI=0TO99:IFIT(TT(I))=3THENIN(A)=TT(I):A=A+1
516 NEXTI
517 RETURN
518 PRINT"{brown}{home}{down:3}{$c1}{$c2:38}{$c3}";
519 FORI=0TO13
520 PRINT"{$c4}                                      {$c5}";:NEXT
521 PRINT"{$c6}{$c7:38}{$c8}";:RETURN
522 PRINT"{home}{down}{right}{white}                                      ";
523 RETURN
524 PRINT"{brown}{home}{down:3}{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
525 FORI=0TO5
526 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
527 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
528 PRINT"{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
529 FORI=0TO5
530 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
531 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
532 DX=3:DY=4:P=0:GOSUB535:DX=23:P=1:GOSUB535
533 DX=3:DY=12:P=2:GOSUB535:DX=23:P=3:GOSUB535
534 RETURN
535 IFP3(P)=0THENRETURN
536 PRINT"{home}{white}";:IFPH(P)=0THENPRINT"{red}";
537 PRINT"{home}"LEFT$(CD$,DY)SPC(DX-1)" "P$(P)"  LV"PL(P)+1
538 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX)" "I$(PW(P));
539 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX)" "I$(PR(P));
540 T1=PA(P)+IA(PW(P))
541 T2=PD(P)+ID(PR(P))
542 IFT1>999THENT1=999
543 IFT2>999THENT2=999
544 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX+9)"ATK"T1;
545 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX+9)"DEF"T2;
546 RETURN
547 IFPEEK(2)=1THENSP=30960+RS*(CR-0)
548 FORI=0TO7
549 PRINT"{home}{down:3}"LEFT$(CD$,I*2);
550 FORJ=0TO19
551 PRINTT$(PEEK(SP))"{up}";
552 SP=SP+1
553 NEXTJ,I
554 RETURN
555 IS=0
556 IFIN(IS)=0THENIN(IS)=NI:NI=-1:RETURN
557 IS=IS+1
558 IFIS=IXTHENRETURN
559 GOTO556
560 F=13-H(0)*13:C=30960+4*RS:POKEC+8+6*20,F:POKEC+9+6*20,F:POKEC+10+6*20,F
561 F=13-H(1)*13:C=30960+2*RS:POKEC+12+1*20,F:POKEC+12+2*20,F:POKEC+11+1*20,F
562 POKE30960+1*RS+6+6*20,14-H(2)*14
563 POKE30960+9*RS+6+4*20,14-H(3)*14
564 POKE30960+3*RS+10+6*20,14-H(4)*14
565 POKE30960+4*RS+14+6*20,14-H(5)*14
566 POKE30960+15*RS+9+4*20,14-H(8)*14
567 POKE30960+9*RS+1+2*20,14-H(9)*14
568 POKE30960+16*RS+8+2*20,14-H(10)*14
569 POKE30960+14*RS+3+5*20,16-H(6)*16
570 POKE30960+14*RS+8+2*20,11-H(7)*11
571 POKE30960+12*RS+15+2*20,6-H(12)*6
572 POKE30960+12*RS+15+6*20,6-H(13)*6
573 T=18:IFH(12)+H(13)=2THENT=0
574 POKE30960+12*RS+8+0,T
575 RETURN
576 IFCR=4ANDZX=6ANDZY=4ANDH(0)=0THENPRINT"{home}{white}{down}{right}DER SCHALTER IST AUS!";:GOSUB759:GOSUB522
577 IFCR=4ANDZX=6ANDZY=4ANDH(0)=1THENPRINT"{home}{white}{down}{right}DER SCHALTER IST EIN!";:GOSUB759:GOSUB522
578 IFCR=4ANDZX=14ANDZY=4ANDH(1)=0THENPRINT"{home}{white}{down}{right}DER SCHALTER IST AUS!";:GOSUB759:GOSUB522
579 IFCR=4ANDZX=14ANDZY=4ANDH(1)=1THENPRINT"{home}{white}{down}{right}DER SCHALTER IST EIN!";:GOSUB759:GOSUB522
580 RETURN
581 IFCC>=11THENRETURN
582 FH(CC)=MH(A):FM(CC)=MM(A):FT(CC)=A:FC(CC)=INT(RND(0)*MS(A)):FS(CC)=MS(A)
583 FX(CC)=2:FY(CC)=5+CC*3:FA(CC)=MA(A):FD(CC)=MD(A):FB(CC)=MB(A)
584 F1(CC)=MH(A):F2(CC)=MM(A)
585 IFCC>=4THENFX(CC)=5:FY(CC)=5+(CC-4)*3
586 CC=CC+1:RETURN
587 ZM=0
588 FORJ=4TO7
589 IFFT(J)=-1THENZM=ZM-1
590 ZM=ZM+1
591 NEXTJ
592 IFZM<>CMTHENPRINTDD$;"{right}          {down}{left:10}          {down}{left:10}          {down}{left:10}          ";
593 PRINTDD$;
594 CM=0
595 FORI=4TO7
596 IFFT(I)=-1THENNEXT:RETURN
597 A=FH(I):GOSUB720
598 PRINT"{home}{right}"LEFT$(CD$,20+CM)M$(FT(I));
599 PRINT"{home}{right}"LEFT$(CD$,20+CM);"{right:7}";D$
600 CM=CM+1
601 NEXT
602 RETURN
603 CE=4:PRINT"{home}{down}{right}{white}"P$(C)": WAHELE DEIN ZIEL               "
604 IFFT(CE)=-1THEN613
605 POKEQS-1+FX(CE)+FY(CE)*40,35
606 GOSUB722
607 IFA$=CHR$(13)THENPOKEQS-1+FX(CE)+FY(CE)*40,32:RETURN
608 IFA$="S"THENGOTO613
609 IFA$="W"THENGOTO621
610 IFA$="D"ANDCE>3THENPOKEQS-1+FX(CE)+FY(CE)*40,32:CE=0:GOTO604
611 IFA$="A"ANDCE<4THENPOKEQS-1+FX(CE)+FY(CE)*40,32:CE=4:GOTO604
612 GOTO606
613 CO=CE
614 POKEQS-1+FX(CE)+FY(CE)*40,32
615 CE=CE+1
616 IFCE>=12THENCE=0
617 IFCO<4ANDCE>3THENCE=CO
618 IFCO>3ANDCE<4THENCE=CO
619 IFFT(CE)<>-1THEN605
620 GOTO615
621 CO=CE
622 POKEQS-1+FX(CE)+FY(CE)*40,32
623 CE=CE-1
624 IFCE<0THENCE=11
625 IFCO<4ANDCE>3THENCE=CO
626 IFCO>3ANDCE<4THENCE=CO
627 IFFT(CE)<>-1THEN605
628 GOTO623
629 IFFT(C)=-2THEN645
630 IFFT(C)=7ANDFM(C)>=IE(9)THENM=9:GOTO632
631 GOTO313
632 AP=IE(M)*4+2-INT(RND(0)*5):FM(C)=FM(C)-IE(M):IFC<4THENPM(C)=FM(C):GOSUB449
633 FH(CE)=FH(CE)+AP:IFFH(CE)>F1(CE)THENFH(CE)=F1(CE)
634 IFCE<4THENPH(CE)=FH(CE):GOSUB449
635 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{green}";AP
636 GOSUB764
637 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
638 GOTO276
639 FM(C)=FM(C)-IE(M):IFC<4THENPM(C)=FM(C):GOSUB449
640 FS(CE)=FS(CE)/2:IFFS(CE)<5THENFS(CE)=5
641 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{green}SLOW";
642 GOSUB764
643 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
644 GOTO276
645 IFM=9THEN632
646 GOTO313
647 FORI=0TOHC-1:H(I)=0:NEXT
648 FORI=0TO3:PX(I)=0:NEXT
649 PA(0)=5:PD(0)=2:PA(1)=4:PD(1)=3:PA(2)=1:PD(2)=1:PA(3)=1:PD(3)=1:GOSUB560
650 FORI=0TO13:IN(I)=0:NEXT
651 IN(0)=18
652 ER(0)=3:EX(0)=12:EY(0)=5:ET(0)=4
653 ER(1)=6:EX(1)=16:EY(1)=5:ET(1)=3
654 ER(2)=8:EX(2)=14:EY(2)=3:ET(2)=8
655 ER(3)=9:EX(3)=1:EY(3)=4:ET(3)=5
656 ER(4)=13:EX(4)=18:EY(4)=4:ET(4)=16
657 ER(5)=13:EX(5)=18:EY(5)=6:ET(5)=17
658 ER(6)=13:EX(6)=15:EY(6)=2:ET(6)=13
659 ER(7)=14:EX(7)=10:EY(7)=2:ET(7)=15
660 ER(8)=12:EX(8)=1:EY(8)=2:ET(8)=11
661 ER(9)=15:EX(9)=16:EY(9)=4:ET(9)=7
662 ER(10)=0:EX(10)=8:EY(10)=5:ET(10)=9
663 ER(11)=6:EX(11)=3:EY(11)=5:ET(11)=15
664 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
665 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
666 PRINT"{$c6}{$c7:38}{up:3}";
667 POKE50151,72:POKE56295,9
668 X=8:Y=3:CR=0
669 P3(0)=1:P3(1)=0:P3(2)=0:P3(3)=0
670 P1(0)=15:P1(1)=17:P1(2)=9:P1(3)=9:P2(0)=0:P2(1)=0:P2(2)=7:P2(3)=23
671 FORI=0TO3:PH(I)=P1(I):PM(I)=P2(I):PX(I)=0:PW(I)=0:PR(I)=0:NEXT
672 PW(0)=1:PR(0)=2
673 GOSUB560
674 AA$="INTRO":GOSUB708
675 GOTO104
676 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
677 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
678 PRINT"{$c6}{$c7:38}{up:3}";
679 POKE50151,72:POKE56295,9
680 AA$="ENDE":GOSUB708
681 GOTO104
682 OPEN1,8,4,"PFSAVE":OPEN2,8,15:INPUT#2,ER,M$,T,SS
683 IFER<>0THENPRINT"{home}{white}{down}{right}ERROR LOADING    ";:CLOSE2:CLOSE1:GOTO647
684 FORI=0TO3:INPUT#1,PH(I):INPUT#1,PM(I):INPUT#1,PS(I):INPUT#1,P1(I)
685 INPUT#1,P2(I):INPUT#1,P3(I)
686 INPUT#1,PA(I):INPUT#1,PD(I):INPUT#1,PW(I):INPUT#1,PR(I):INPUT#1,PX(I)
687 INPUT#1,PL(I):NEXT
688 FORI=0TO13:INPUT#1,IN(I):NEXT:FORI=0TOHC-1:INPUT#1,H(I):NEXT
689 FORI=0TOEC-1:INPUT#1,ER(I):NEXT
690 INPUT#1,X:INPUT#1,Y:INPUT#1,CR
691 CLOSE1:CLOSE2:GOSUB560:RETURN
692 OPEN15,8,15,"S:PFSAVE":CLOSE15
693 OPEN1,8,4,"PFSAVE,W"
694 FORI=0TO3:PRINT#1,PH(I):PRINT#1,PM(I):PRINT#1,PS(I):PRINT#1,P1(I)
695 PRINT#1,P2(I):PRINT#1,P3(I)
696 PRINT#1,PA(I):PRINT#1,PD(I):PRINT#1,PW(I):PRINT#1,PR(I):PRINT#1,PX(I)
697 PRINT#1,PL(I)
698 NEXT
699 FORI=0TO13:PRINT#1,IN(I):NEXT:FORI=0TOHC-1:PRINT#1,H(I):NEXT
700 FORI=0TOEC-1:PRINT#1,ER(I):NEXT
701 PRINT#1,X:PRINT#1,Y:PRINT#1,CR
702 CLOSE1:RETURN
703 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
704 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
705 PRINT"{$c6}{$c7:38}{up:3}";
706 POKE50151,72:POKE56295,9
707 RETURN
708 POKE1020,Q7
709 POKE56322,224
710 IFAA$="INTRO"THENOPEN1,8,4,"TXT.INTRO,S,R"
711 IFAA$="ENDE"THENOPEN1,8,4,"TXT.ENDE,S,R"
712 IFAA$=""THENRETURN
713 GET#1,B$
714 IFB$="$"THENPRINT"{home}{down:2}{right:2}{white}";:GOTO713
715 IFB$="%"THENPRINT"{right:2}{white}";:GOTO713
716 IFB$="#"THENCLOSE1:POKE56322,255:GOSUB750:RETURN
717 PRINTB$;
718 IFB$<>" "THENPOKE162,0:WAIT162,4
719 GOTO713
720 T$=STR$(A)
721 D$=RIGHT$("000"+RIGHT$(T$,LEN(T$)-1),3):RETURN
722 A$=""
723 J=PEEK(56320)
724 IFJ=127THEN726
725 GOTO723
726 J=PEEK(56320)
727 IFJ=127ANDA$=CHR$(13)THENRETURN
728 IFJ=107THENA$="E":GOSUB755:RETURN
729 IFJ=103THENA$="E":GOSUB755:RETURN
730 IFJ=110THENA$="E":GOSUB755:RETURN
731 IFJ=109THENA$="E":GOSUB755:RETURN
732 IFJ=123THENA$="A":GOSUB755:RETURN
733 IFJ=119THENA$="D":GOSUB755:RETURN
734 IFJ=126THENA$="W":GOSUB755:RETURN
735 IFJ=125THENA$="S":GOSUB755:RETURN
736 IFJ=111THENA$=CHR$(13)
737 GOTO726
738 A$=""
739 J=PEEK(56320)
740 IFJ<>111ANDJ<>107ANDJ<>103ANDJ<>110ANDJ<>109THEN742
741 GOTO739
742 J=PEEK(56320)
743 IFJ=127ANDA$=CHR$(13)THENRETURN
744 IFJ=123THENA$="A":GOSUB756:RETURN
745 IFJ=119THENA$="D":GOSUB756:RETURN
746 IFJ=126THENA$="W":GOSUB756:RETURN
747 IFJ=125THENA$="S":GOSUB756:RETURN
748 IFJ=111THENA$=CHR$(13)
749 GOTO742
750 A$=""
751 J=PEEK(56320)
752 IFJ=127ANDA$=CHR$(13)THENRETURN
753 IFJ=111THENA$=CHR$(13)
754 GOTO751
755 POKE162,0:WAIT162,16:RETURN
756 POKE162,0:WAIT162,8:RETURN
757 POKE162,0:WAIT162,32:RETURN
758 POKE162,0:WAIT162,2:RETURN
759 POKE162,0:WAIT162,32
760 POKE162,0:WAIT162,32
761 POKE162,0:WAIT162,32
762 POKE162,0:WAIT162,32
763 RETURN
764 POKE162,0:WAIT162,32
765 RETURN
766 POKE162,0:WAIT162,4
767 RETURN
768 DATA"IMP",5,,8,"{lt. green}{$cd}{$ce}{down}{left:2}{$cf}{$d0}",4,1,,3
769 DATA"SNAKE",8,,12,"{gray3}{$d8}{$d9}{down}{left:2}{$da}{$db}",6,1,,5
770 DATA"VIPER",12,,18,"{gray1}{$d8}{$d9}{down}{left:2}{$da}{$db}",6,1,,9
771 DATA"NACHO",30,,22,"{lt. red}{$cd}{$ce}{down}{left:2}{$cb}{$cc}",15,5,,50
772 DATA"GOLEM",20,,4,"{gray2}{$af}{$b0}{down}{left:2}{$b1}{$b2}",20,8,,17
773 DATA"MOSS",30,,7,"{lt. green}{$af}{$b0}{down}{left:2}{$b1}{$b2}",26,9,,21
774 DATA"BONES",50,,20,"{brown}{$b3}{$b4}{down}{left:2}{$b5}{$b6}",18,8,,15
775 DATA"WISP",25,50,15,"{green}{$bb}{$bc}{down}{left:2}{lt. green}{$bd}{$be}",,8,,17
776 DATA"TROLL",100,,9,"{gray3}{$af}{$b0}{down}{left:2}{$b1}{$b2}",40,13,,100
777 DATA"SPARK",30,50,22,"{yellow}{$bb}{$bc}{down}{left:2}{gray3}{$bd}{$be}",,8,,17
778 DATA"BURNER",80,90,30,"{gray3}{$b3}{$b4}{down}{left:2}{$b5}{$b6}",25,13,,42
779 DATA"TBERRY",120,10,60,"{lt. green}{$b3}{$b4}{down}{left:2}{$b5}{$b6}",60,20,,150
780 DATA"DRACUL",250,60,50,"{gray2}{rvrs on}DE{down}{left:2}FG{rvrs off}",70,25,,500
781 DATA"GLADOS",500,70,60,"{gray2}{rvrs on}HI{down}{left:2}JK{rvrs off}",80,30,,1000
782 DATA"KRON",15,,10,"{lt. red}{$c9}{$ca}{down}{left:2}{$cb}{$cc}",4,3,,  
783 DATA"LENA",17,,10,"{gray2}{rvrs on}${rvrs off}{$ca}{down}{left:2}{$cb}{$cc}",3,3,,    
784 DATA"DOLM",9,7,8,"{lt. green}{$c9}{$ca}{down}{left:2}{$cb}{$cc}",1,1,, 
785 DATA"MIRA",9,23,7,"{orange}{rvrs on}${rvrs off}{$ca}{down}{left:2}{$cb}{$cc}",1,1,1,2 
786 DATA"{lt. green}{rvrs off}{$d3}{rvrs off}{lt. green}{$d4}{down}{left:2}{rvrs off}{lt. green}{$d4}{rvrs off}{lt. green}{$d3}"
787 DATA"{brown}{rvrs off}{$a3}{rvrs off}{brown}{$a4}{down}{left:2}{rvrs off}{brown}{$d4}{rvrs off}{brown}{$d3}"
788 DATA"{lt. red}{rvrs off}{$bf}{rvrs on}{lt. red}@{down}{left:2}{rvrs on}{lt. red}A{rvrs on}{lt. red}B"
789 DATA"{lt. green}{rvrs off}{$d3}{rvrs off}{lt. green}{$d5}{down}{left:2}{rvrs off}{lt. green}{$d5}{rvrs off}{lt. green}{$d3}"
790 DATA"{orange}{rvrs off}{$d6}{rvrs off}{orange}{$d6}{down}{left:2}{rvrs off}{orange}{$d6}{rvrs off}{orange}{$d6}"
791 DATA"{brown}{rvrs off}{$d3}{rvrs off}{brown}{$d4}{down}{left:2}{rvrs off}{brown}{$d4}{rvrs off}{brown}{$d3}"
792 DATA"{lt. green}{rvrs on}Q{rvrs on}{lt. green}R{down}{left:2}{rvrs on}{lt. green}S{rvrs on}{lt. green}T"
793 DATA"{brown}{rvrs off}7{rvrs off}{brown}7{down}{left:2}{rvrs off}{brown}7{rvrs off}{brown}7"
794 DATA"{gray1}{rvrs off}{$a5}{rvrs off}{gray1}{$a6}{down}{left:2}{rvrs off}{gray1}{$a7}{rvrs off}{gray1}{cmdr pound}"
795 DATA"{brown}{rvrs off}{$dc}{rvrs off}{brown}{$dd}{down}{left:2}{rvrs off}{brown}{$de}{rvrs off}{brown}{$df}"
796 DATA"{lt. red}{rvrs off}{$d1}{rvrs off}{lt. red}{$d1}{down}{left:2}{rvrs off}{lt. red}{$d1}{rvrs off}{lt. red}{$d1}"
797 DATA"{lt. red}{rvrs off}{$d2}{rvrs off}{lt. red}{$d2}{down}{left:2}{rvrs off}{lt. red}{$d2}{rvrs off}{lt. red}{$d2}"
798 DATA"{lt. green}{rvrs off}{$d7}{rvrs off}{lt. green}{$d6}{down}{left:2}{rvrs off}{lt. green}{$d6}{rvrs off}{lt. green}{$d7}"
799 DATA"{brown}{rvrs off}{$a1}{rvrs off}{brown}{$a2}{down}{left:2}{rvrs off}{brown}{$a2}{rvrs off}{brown}{$a1}"
800 DATA"{gray1}{rvrs off}{$aa}{rvrs on}{gray1}V{down}{left:2}{rvrs on}{gray1}W{rvrs on}{gray1}X"
801 DATA"{gray2}{rvrs off}{$ab}{rvrs off}{gray2}{$ac}{down}{left:2}{rvrs off}{gray2}{$ad}{rvrs off}{gray2}{$ae}"
802 DATA"{gray1}{rvrs off}{$b7}{rvrs off}{gray1}{$b8}{down}{left:2}{rvrs off}{gray1}{$b9}{rvrs off}{gray1}{$ba}"
803 DATA"{lt. green}{rvrs on}L{rvrs on}{lt. green}M{down}{left:2}{rvrs on}{lt. green}N{rvrs on}{lt. green}O"
804 DATA"{brown}{rvrs on}P{rvrs on}{brown}P{down}{left:2}{rvrs on}{brown}P{rvrs on}{brown}P"
805 DATA"{brown}{rvrs on}{^}{rvrs on}{brown}{^}{down}{left:2}{rvrs on}{brown}]{rvrs on}{brown}]"
806 DATA"{brown}{rvrs on}{^}{rvrs on}{brown}{back arrow}{down}{left:2}{rvrs on}{brown}]{rvrs on}{brown}]"
807 DATA"{gray1}{rvrs on}Y{rvrs on}{gray1}Z{down}{left:2}{rvrs on}{gray1}[{rvrs on}{gray1}{pound}"
808 DATA"{brown}{rvrs on}{back arrow}{rvrs on}{brown}{^}{down}{left:2}{rvrs on}{brown}]{rvrs on}{brown}]"
809 DATA"        ",0    ,0    ,0    ,0    ,-1 
810 DATA"DOLCH   ",3    ,0    ,0    ,0    ,0  
811 DATA"HELM    ",0    ,2    ,-1   ,0    ,1  
812 DATA"KRAEUTER",0    ,0    ,-1   ,10   ,2  
813 DATA"FEUER   ",0    ,0    ,0    ,5    ,3  
814 DATA"EIS     ",0    ,0    ,0    ,8    ,3  
815 DATA"MUT     ",0    ,0    ,0    ,23   ,3  
816 DATA"BOMBE   ",0    ,0    ,0    ,50   ,3  
817 DATA"KIRSCHE ",0    ,0    ,-1   ,15   ,2  
818 DATA"HEILEN  ",0    ,0    ,0    ,4    ,3  
819 DATA"SKULL   ",0    ,0    ,0    ,0    ,4  
820 DATA"FLEGEL  ",7    ,0    ,-1   ,0    ,0  
821 DATA"LAHM    ",0    ,0    ,0    ,4    ,3  
822 DATA"STAB    ",12   ,0    ,0    ,0    ,0  
823 DATA"FEDER   ",0    ,10   ,0    ,0    ,1  
824 DATA"STOCK   ",23   ,0    ,0    ,0    ,0  
825 DATA"WESTE   ",0    ,14   ,0    ,0    ,1  
826 DATA"SCHILD  ",0    ,24   ,0    ,0    ,1  
827 DATA"ZURUECK ",0    ,0    ,0    ,0    ,9  
828 END
