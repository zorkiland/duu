0 POKE55,240:POKE56,120:CLR
1 QO=53282
2 QP=53283
3 QQ=53281
4 QR=53280
5 QS=49152
6 V=53248
7 QT=30960
8 Q1=0
9 Q2=1
10 Q3=2
11 Q4=3
12 Q5=4
13 Q6=5
14 Q7=6
15 Q8=7
16 Q9=8
17 QA=9
18 QB=10
19 CB=11
20 QC=12
21 QD=13
22 QE=14
23 QF=15
24 QG=8
25 QH=9
26 QI=10
27 QJ=11
28 QK=12
29 QL=13
30 QM=14
31 QN=15
32 MQ=14
33 IC=19
34 HC=14
35 EC=12
36 IX=99
37 TM=80
38 RS=165
39 DD$="{home}{down:20}{white}"
40 CD$="{down:25}"
41 CT$="{home}{down}{white}{right}                                      "
42 DIMM$(MQ),MD$(MQ)
43 DIMMH(MQ),MM(MQ)
44 DIMMA(MQ),MD(MQ)
45 DIMMS(MQ)
46 DIMMB(MQ),MX(MQ)
47 DIMFH(11),FT(11),FC(11)
48 DIMFM(11),FS(11),FX(11),FY(11)
49 DIMFA(11),FD(11),F1(11),F2(11)
50 FORI=0TO3:FX(I)=37:FY(I)=5+3*I:NEXT
51 DIMI$(IC),IA(IC)
52 DIMID(IC),IS(IC)
53 DIMIE(IC),IT(IC)
54 DIMH(HC)
55 DIMER(EC),EX(EC)
56 DIMEY(EC),ET(EC)
57 DIMIN(IX)
58 DIMT$(TM)
59 DIMTN$(TM)
60 DIMTT(IX)
61 FORI=0TOMQ-1
62 READM$(I),MH(I),MM(I),MS(I)
63 READMD$(I),MA(I),MD(I),MB(I),MX(I)
64 NEXT
65 FORI=0TO3
66 READP$(I),PH(I),PM(I),PS(I)
67 READPD$(I),PA(I),PD(I),PW(I),PR(I)
68 NEXT
69 PM$="{rvrs on}{lt. red}UV{down}{left:2}WX{rvrs off}"
70 FORI=0TOTM-1
71 READT$(I)
72 NEXT
73 FORI=0TOIC-1
74 READI$(I),IA(I),ID(I),IS(I),IE(I),IT(I)
75 NEXT
76 POKEV+21,0
77 POKE648,192
78 POKE56576,0
79 POKE53272,8
80 POKE53270,PEEK(53270)OR16
81 POKEQO,QA
82 POKEQP,Q1
83 POKEQR,Q1
84 POKEQQ,Q7
85 PRINT"{white}"+CHR$(147)
86 Z=-1
87 FORA=0TO255
88 B=0
89 Z=Z+1
90 IFZ=16THENPRINT:Z=0
91 IFA>=0ANDA<=31THENC=A+64:PRINTCHR$(C);
92 IFA>=32ANDA<=63THENC=A+00:PRINTCHR$(C);
93 IFA>=64ANDA<=95THENC=A+32:PRINT"{gray1}"+CHR$(C);
94 IFA>=96ANDA<=127THENC=A+64:PRINT"{gray1}"+CHR$(C);
95 IFA=162THENPRINT" ";:GOTO100
96 IFA>=128ANDA<=159THENC=A-64:PRINT"{gray1}"+CHR$(18)+CHR$(C);
97 IFA>=160ANDA<=191THENC=A-128:PRINT"{gray1}"+CHR$(18)+CHR$(C);
98 IFA>=192ANDA<=254THENC=A:PRINT"{gray1}"+CHR$(18)+CHR$(C);
99 IFA=255THENC=A-64:PRINT"{gray1}"+CHR$(18)+CHR$(C);
100 NEXTA
101 GOSUB760
102 POKE1020,Q7
103 POKE1021,Q1
104 SYS820
105 PRINT"{white}"+CHR$(147)
106 FORI=0TO4
107 PRINT"{home}{down:3}"LEFT$(CD$,I*2);
108 FORJ=0TO15
109 PRINTT$(SP)"{up}";
110 SP=SP+1
111 NEXTJ,I
112 PRINTCHR$(5)
113 STOP
114 GOTO657
115 POKE1020,Q7
116 POKE1021,Q7
117 SYS820
118 GOSUB452
119 GOSUB459
120 PRINT"{home}{down}{right}{white}                                      ";
121 POKE1020,Q7
122 GOSUB557
123 IFPEEK(2)=1THENCP=30960+RS*(CR-0)
124 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)PM$;
125 OX=ZX:OY=ZY
126 ZX=X:ZY=Y
127 GOSUB748
128 IFA$=CHR$(145)ORA$="W"THENPRINTCT$;:ZY=Y-1:GOTO134
129 IFA$=CHR$(157)ORA$="A"THENPRINTCT$;:ZX=X-1:GOTO134
130 IFA$=CHR$(29)ORA$="D"THENPRINTCT$;:ZX=X+1:GOTO134
131 IFA$=CHR$(17)ORA$="S"THENPRINTCT$;:ZY=Y+1:GOTO134
132 IFA$=CHR$(13)THEN191
133 GOTO127
134 AA=0
135 FORI=0TOEC
136 IFER(I)=CRANDEX(I)=ZXANDEY(I)=ZYTHENAA=1
137 NEXTI
138 IFZX=-1ORZX=20ORZY=-1ORZY=8THEN150
139 C=PEEK(CP+ZX+ZY*20)
140 IFC=1THEN171
141 IFC=2THEN164
142 IFC=4THEN164
143 IFC=8THEN177
144 IFC=9THEN164
145 IFC=13THEN190
146 IFC=14THEN181
147 IFC=17THEN177
148 IFC=21THEN164
149 IFC>9THEN125
150 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)T$(PEEK(CP+X+Y*20))
151 IFRND(1)*100>97ANDPEEK(CP+164)>0THENFF=0:GOTO267
152 X=ZX:Y=ZY
153 IFX=-1ANDPEEK(CP+160)=255THENX=0:GOTO124
154 IFX=-1THENCR=PEEK(CP+160):X=19:GOTO121
155 IFX=20ANDPEEK(CP+161)=255THENX=19:GOTO124
156 IFX=20THENCR=PEEK(CP+161):X=0:GOTO121
157 IFY=-1ANDPEEK(CP+162)=255THENY=0:GOTO124
158 IFY=-1THENCR=PEEK(CP+162):Y=7:GOTO121
159 IFY=8ANDPEEK(CP+163)=255THENY=7:GOTO124
160 IFY=8THENCR=PEEK(CP+163):Y=0:GOTO121
161 IFC=5THEN171
162 IFC=6THEN171
163 GOTO124
164 IFCR=5THENCR=2:X=3:Y=3:GOTO120
165 IFCR=2THENCR=5:X=11:Y=4:GOTO120
166 IFCR=0THENCR=6:X=9:Y=6:GOTO120
167 IFCR=6THENCR=0:X=3:Y=4:GOTO120
168 IFCR=11THENCR=13:X=5:Y=6:GOTO120
169 IFCR=13THENCR=11:X=2:Y=4:GOTO120
170 IFCR=16THEN686
171 IFC=1ANDCR=4ANDZX=6THENH(0)=1-H(0):GOSUB570:GOSUB586:GOTO121
172 IFC=1ANDCR=4ANDZX=14THENH(1)=1-H(1):GOSUB570:GOSUB586:GOTO125
173 IFC=5ANDCR=14ANDX=7ANDH(6)=0THENH(6)=1:GOSUB570:GOTO120
174 IFC=5ANDCR=14ANDX=17ANDH(7)=0THENH(7)=1:GOSUB570:GOTO120
175 IFC=6ANDCR=12ANDY=2ANDH(12)=0THENH(12)=1:GOSUB570:GOTO120
176 IFC=6ANDCR=12ANDY=6ANDH(13)=0THENH(13)=1:GOSUB570:GOTO120
177 IFC=8ANDAA=0THENGOTO125
178 IFC=8ANDAA=1THENPRINT"{home}{white}{down}{right}IN DER TRUHE IST WAS!":GOSUB769:GOSUB532:GOTO125
179 IFC=17ANDAA=0THENGOTO125
180 IFC=17ANDAA=1THENPRINT"{home}{white}{down}{right}DER BAUM SIEHT INTRESSANT AUS!":GOSUB769:GOSUB532:GOTO125
181 IFCR=0THENPRINT"{white}{home}{down}{right:2}EIN HEILZAUBER IST UNTER DEM BAUM":GOSUB769:GOSUB532:GOTO125
182 IFCR=6THENPRINT"{white}{home}{down}{right:2}EIN FEUERZAUBER IST IN DEN KATAKOMBEN":GOSUB769:GOSUB532:GOTO125
183 IFCR=1THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB769:H(2)=1:GOSUB570:P3(1)=1:GOTO115
184 IFCR=4THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB769:H(5)=1:GOSUB570:P3(2)=1:GOTO115
185 IFCR=9ANDX<=2THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB769:H(9)=1:GOSUB570:P3(3)=1:GOTO115
186 IFCR=3THENPRINT"{white}{home}{down}{right:2}DU KANNST HIER NICHT DURCH!":GOSUB769:FF=4:H(4)=1:GOSUB570:GOTO267
187 IFCR=9ANDX>3THENPRINT"{white}{home}{down}{right:2}ACH WIE SUESS!":GOSUB769:FF=9:H(3)=1:GOSUB570:GOTO267
188 IFCR=15THENPRINT"{white}{home}{down}{right:2}DU WILLST MICH BESIEGEN!":GOSUB769:FF=13:H(8)=1:GOSUB570:GOTO267
189 IFCR=16THENPRINT"{white}{home}{down}{right:2}DU HAST MEINE ERWARTUNG UEBERTROFFEN!":GOSUB769:FF=14:H(10)=1:GOSUB570:GOTO267
190 IFC=13THENPRINT"{home}{white}{down}{right}DU BIST GEHEILT!":GOSUB769:GOSUB447:GOSUB459:GOSUB532:GOTO125
191 PRINT"{home}{down}{white}{right}  UNTERSUCHE  EQUIPMENTS  INVENTAR"
192 M=0
193 POKEQS+42+M*12,35
194 GOSUB732
195 POKEQS+42+M*12,32
196 IFA$="A"THENM=M-1-3*(M=0):GOTO193
197 IFA$="D"THENM=M+1+3*(M=2):GOTO193
198 IFA$=CHR$(13)THENPOKEQS+42+M*7,32:GOTO200
199 GOTO193
200 PRINT"{home}{down}{white}{right}                                      ";
201 ONM+1GOTO255,215,202
202 POKE1020,Q1
203 GOSUB528
204 PRINT"{home}{down}{right}{white}INVENTAR: WAEHLE DEIN ITEM           ";
205 MT=2:MX=2:MY=4:MC=14:GOSUB470
206 IFIN(M)=9THEN120
207 IFIN(M)=0ORIT(IN(M))<>MTTHEN120
208 IFIN(M)=3THENGOSUB211:IN(M)=0:GOSUB459:GOTO120
209 IFIN(M)=8THENGOSUB213:IN(M)=0:GOSUB459:GOTO120
210 GOTO120
211 FORI=0TO3:PH(I)=PH(I)+IE(IN(M)):IFPH(I)>P1(I)THENPH(I)=P1(I)
212 FH(I)=PH(I):NEXT:RETURN
213 FORI=0TO3:PM(I)=PM(I)+IE(IN(M)):IFPM(I)>P2(I)THENPM(I)=P2(I)
214 FM(I)=PM(I):NEXT:RETURN
215 POKE1020,Q1
216 GOSUB534
217 PRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINEN SPIELER     ";
218 P=0
219 CP=QS+2+(4-8*(P>1))*40:IFP=1ORP=3THENCP=CP+20
220 POKECP,35
221 GOSUB732
222 POKECP,32
223 IFP=0ANDA$="D"ANDP3(1)=1THENP=1:GOTO219
224 IFP=0ANDA$="S"ANDP3(2)=1THENP=2:GOTO219
225 IFP=0ANDA$="D"ANDP3(1)=OANDP3(2)=0ANDP3(3)=1THENP=3:GOTO219
226 IFP=0ANDA$="S"ANDP3(1)=OANDP3(2)=0ANDP3(3)=1THENP=3:GOTO219
227 IFP=1ANDA$="A"ANDP3(0)=1THENP=0:GOTO219
228 IFP=1ANDA$="S"ANDP3(3)=1THENP=3:GOTO219
229 IFP=2ANDA$="D"ANDP3(3)=1THENP=3:GOTO219
230 IFP=2ANDA$="W"ANDP3(0)=1THENP=0:GOTO219
231 IFP=3ANDA$="A"ANDP3(2)=1THENP=2:GOTO219
232 IFP=3ANDA$="W"ANDP3(1)=1THENP=1:GOTO219
233 IFP=3ANDA$="A"ANDP3(2)=OANDP3(1)=0ANDP3(0)=1THENP=0:GOTO219
234 IFP=3ANDA$="W"ANDP3(2)=OANDP3(1)=0ANDP3(0)=1THENP=0:GOTO219
235 IFA$=CHR$(13)THEN237
236 GOTO220
237 SL=0:CP=CP+81
238 PRINT"{home}{down}{right}{white}EQUIPMENT: WAFFE ODER RUESTUNG       ";
239 POKECP+SL*40,35
240 GOSUB732
241 POKECP+SL*40,32:IFA$="W"ORA$="S"THENSL=1-SL:GOTO239
242 IFA$=CHR$(13)THEN244
243 GOTO239
244 MY=4:MX=23:MC=14:IFP=1ORP=3THENMX=3
245 IFSL=0THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE WAFFFE       ";
246 IFSL=1THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE RUESTUNG     ";
247 PRINT"{home}"LEFT$(CD$,MY-1)SPC(MX-3);"{brown}{$c1}{$c2:18}{$c3}";
248 FORI=0TO13:PRINT"{down}{left:20}{$c4}                  {$c5}";:NEXT
249 PRINT"{down}{left:20}{$c6}{$c7:18}{$c8}";
250 MT=SL:GOSUB470
251 IFIT(IN(M))=9THEN120
252 IFIN(M)<>0ANDIT(IN(M))<>SLTHEN120
253 IFSL=0THENQ=PW(P):PW(P)=IN(M):IN(M)=Q:GOTO120
254 IFSL=1THENQ=PR(P):PR(P)=IN(M):IN(M)=Q:GOTO120
255 GOSUB532
256 GOSUB774
257 EI=0
258 IFER(EI)<>CROREX(EI)<>OXOREY(EI)<>OYTHEN263
259 NI=ET(EI):GOSUB565
260 IFIS=IXTHENPRINT"{home}{white}{down}{right}DU KANNST NICHT MEHR TRAGEN!":GOSUB769:GOSUB532:GOTO125
261 ER(EI)=-1
262 PRINT"{home}{white}{down}{right}GEFUNDEN: ";I$(IN(IS));:GOSUB760:GOSUB532:GOTO125
263 EI=EI+1
264 IFEI=ECTHEN266
265 GOTO258
266 PRINT"{home}{down}{right}{white}NICHTS BESONDERES!":GOSUB769:GOSUB532:GOTO125
267 FORI=4TO11:FT(I)=-1:NEXT
268 PP=4:CC=4:WX=0
269 FORI=0TO3:FT(I)=-2:FH(I)=PH(I):FM(I)=PM(I):FS(I)=PS(I)
270 IFP3(I)=0THENFH(I)=-1
271 IFFH(I)<=0THENFT(I)=-1:PP=PP-1
272 FA(I)=PA(I)+IA(PW(I)):FD(I)=PD(I)+ID(PR(I)):F1(I)=P1(I):F2(I)=P2(I)
273 NEXT
274 PRINT"{brown}{home}{$c1}{$c2:38}{$c3}";
275 PRINT"{$c4}                                      {$c5}";
276 PRINT"{$c6}{$c7:38}{$c8}";
277 PRINT"{home}{down:3}{white}";:FORI=0TO15:PRINT"                                        ";:NEXT
278 PRINTDD$;"{brown}{up}{$c1}{$c2:7}{white}HP{brown}{$c2}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
279 GOSUB428
280 GOSUB459
281 GOSUB467
282 FORI=0TO11
283 IFFT(I)<>-1THENFC(I)=INT(RND(0)*FD(I))
284 NEXT
285 CC=CC-4
286 PRINT"{home}{down}{right}                       ";:GOSUB597
287 FORI=4TO11:IFFT(I)=-1THEN289
288 PRINT"{home}"LEFT$(CD$,FY(I));SPC(FX(I));MD$(FT(I))
289 NEXT
290 C=0
291 IFFT(C)<>-1ANDFC(C)>=100THENFC(C)=FC(C)-100:GOTO294
292 C=C+1:IFC<12THEN291
293 FORI=0TO11:FC(I)=FC(I)+FS(I):NEXT:GOTO290
294 IFFT(C)=-2THEN296
295 GOTO377
296 PRINT"{home}{white}{down}{right}"P$(C)": IST AM ZUG                     "
297 PRINTDD$;SPC(16)"{white} ANGRIFF {down}{left:9} INVENTAR";:M=2
298 PRINTDD$;SPC(16)"{down:2}         {down}{left:9}         ";
299 IFPM(C)>0THENPRINTDD$;SPC(16)"{down:2} MAGIE   ";:M=3
300 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(37)"  {down}{left:2}  ";
301 PRINT"{left:3}{up}"PD$(C);
302 MY=0:FZ=0
303 POKEQS+816+MY*40,35
304 GOSUB732
305 IFA$="S"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY+1:GOTO309
306 IFA$="W"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY-1:GOTO309
307 IFA$=CHR$(13)THENFZ=0:GOTO312
308 GOTO304
309 IFMY>=MTHENMY=0
310 IFMY<0THENMY=M-1
311 GOTO303
312 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(36)"{white}  {down}{left:2}  {left}{up}"PD$(C);
313 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
314 ONMY+1GOTO340,332
315 MT=3:MX=16:MY=20:MC=4:GOSUB470
316 IFIT(IN(M))=9THEN296
317 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         {down}{left:9}         ";
318 IFIN(M)=0ORIT(IN(M))<>3THEN296
319 M=IN(M)
320 IFIE(M)>FM(C)THENPRINT"{home}{down}{right}NICHT GENUG MANA                     ";:GOSUB769:GOTO296
321 GOSUB613
322 GOTO639
323 AP=IE(M)+2-INT(RND(0)*5):FM(C)=FM(C)-IE(M):PM(C)=FM(C):GOSUB459
324 AP=AP+PL(C)*3
325 FH(CE)=FH(CE)-AP:IFFH(CE)<0THENFH(CE)=0
326 IFCE<4THENPH(CE)=FH(CE)
327 IFFH(CE)>0THENGOTO362
328 IFFT(CE)<>-2THENCC=CC-1:WX=WX+MX(FT(CE))
329 IFFT(CE)=-2THENPP=PP-1
330 FT(CE)=-1
331 GOTO362
332 MT=2:MX=16:MY=20:MC=4:GOSUB470
333 IFIT(IN(M))=9THEN296
334 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
335 PRINT"{down}{left:9}         ";
336 IFIN(M)=0ORIT(IN(M))<>MTTHEN296
337 IFIN(M)=3THENGOSUB211:IN(M)=0:GOSUB459:GOTO286
338 IFIN(M)=8THENGOSUB213:IN(M)=0:GOSUB459:GOTO286
339 GOTO286
340 GOSUB613
341 AP=FA(C)-FD(CE)
342 IFAP<=0THENPRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{white}MISS";:GOSUB767:GOTO354
343 AP=INT(RND(0)*AP)+1
344 FH(CE)=FH(CE)-AP:IFFH(CE)<0THENFH(CE)=0
345 IFCE<4THENPH(CE)=FH(CE)
346 IFFH(CE)>0THENGOTO351
347 IFFT(CE)<>-2THENCC=CC-1:WX=WX+MX(FT(CE))
348 IFFT(CE)=-2THENPP=PP-1
349 FT(CE)=-1
350 GOTO351
351 POKEV,(FX(CE)*8+21)AND255:POKEV+1,FY(CE)*8+50:POKE50168,16:POKEV+28,0
352 POKEV+16,-(FX(CE)>30):POKEV+21,1
353 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{white}";AP
354 FORI=0TO5:POKEV,PEEK(V)+2:POKEV+1,PEEK(V+1)+2:GOSUB768:NEXTI
355 POKEV+21,0:IFCE<4THENGOSUB459
356 GOSUB767
357 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
358 IFFT(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE));"  {down}{left:2}  ";
359 IFCC=0THEN385
360 IFPP=0THEN413
361 GOTO286
362 POKEV,(FX(CE)*8+24)AND255:POKEV+1,FY(CE)*8+50
363 IFM=4THENPOKEV+37,8:POKEV+38,2:POKEV+39,7:POKEV+28,1
364 B=17:IFM=6THENB=19:POKEV+28,0
365 IFM=5THENB=21:POKEV+28,0
366 IFM=7THENB=23:POKEV+28,0
367 POKEV+16,-(FX(CE)>30):POKE50168,B:POKEV+21,1
368 PRINT"{white}";:IFM=9THENPRINT"{green}";
369 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}";AP
370 FORK=0TO3:FORI=0TO1:POKE50168,B+I:FORJ=1TO40:NEXTJ,I,K
371 POKEV+21,0:IFCE<4THENGOSUB459
372 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
373 IFFT(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE));"  {down}{left:2}  ";
374 IFCC=0THEN385
375 IFPP=0THEN413
376 GOTO286
377 GOSUB767
378 PRINT"{white}{home}{down}{right}"M$(FT(C))": GREIFT AN                    "
379 FC(C)=FC(C)-100
380 IFFT(C)=7THENIFFM(C)>=IE(9)THENM=9:GOSUB767:GOSUB422:GOTO642
381 IFFT(C)=9THENIFFM(C)>=IE(6)THENM=6:GOSUB767:GOSUB416:GOTO323
382 IFFT(C)=10THENIFFM(C)>=IE(4)THENM=4:GOSUB767:GOSUB416:GOTO323
383 IFFT(C)=11THENIFFM(C)>=IE(12)THENM=12:GOSUB767:GOSUB416:GOTO649
384 GOSUB767:GOSUB416:GOTO341
385 POKE1020,Q1
386 PRINTDD$;"{brown}{up}{$c1}{$c2:10}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}{white}";
387 PRINT"{home}{down}{right}DU HAST GEWONNEN                     ";:GOSUB597
388 GOSUB528
389 FORI=0TO3:IFP3(I)=0THEN393
390 PRINT"{home}{white}{right:3}";:IFPH(I)=0THENPRINT"{red}";
391 PRINTLEFT$(CD$,4+I*3)P$(I)
392 PRINT"{home}"LEFT$(CD$,5+I*3)"{right:3}LV";PL(I)+1;" EX";PX(I)
393 NEXT
394 FORI=0TO3:IFPH(I)=0ORP3(I)=0THEN404
395 TX=WX
396 PRINT"{home}{white}"LEFT$(CD$,5+I*3)SPC(20)"EXPERIENCE {left}"TX"{left} ";
397 IFTX=0THEN404
398 XS=1:IFTX>9THENXS=10
399 IFTX>99THENXS=100
400 TX=TX-XS
401 PX(I)=PX(I)+XS
402 LL=10+PL(I)*PL(I)*10:IFPX(I)>=LLTHENGOSUB406:GOTO402
403 PRINT"{home}{white}"LEFT$(CD$,5+I*3)"{right:3}LV";PL(I)+1;" EX";PX(I):GOTO396
404 NEXTI
405 GOTO411
406 PL(I)=PL(I)+1:PA(I)=PA(I)+1:PD(I)=PD(I)+1:PS(I)=PS(I)+1
407 P1(I)=P1(I)+PL(I)*2:IFP2(I)>0THENP2(I)=P2(I)+PL(I)*3
408 IFP1(I)>999THENP1(I)=999
409 IFP2(I)>999THENP2(I)=999
410 RETURN
411 GOSUB760
412 GOTO120
413 PRINT"{home}{down}{right}{white}DU HAST VERLOREN                     "
414 GOSUB760
415 GOTO657
416 RE=INT(RND(0)*4):CE=0
417 IFFT(CE)=-2ANDPH(CE)>0THEN420
418 CE=CE+1:IFCE=4THENCE=0
419 GOTO417
420 IFRE>0THENRE=RE-1:GOTO418
421 RETURN
422 RE=INT(RND(0)*8):CE=4
423 IFFT(CE)>=0THEN426
424 CE=CE+1:IFCE=12THENCE=4
425 GOTO423
426 IFRE>0THENRE=RE-1:GOTO424
427 RETURN
428 IFFF>0THENL=FF:GOTO430
429 L=PEEK(CP+164):IFL=0THENRETURN
430 IFL=3THENA=0:LC=INT(RND(0))*2+2:FORI=0TOLC:GOSUB591:NEXT:RETURN
431 IFL=1THENA=1:FORI=0TO2:GOSUB591:NEXT:RETURN
432 IFL=2THENA=1:FORI=0TO1:GOSUB591:NEXT:A=2:FORI=0TO1:GOSUB591:NEXT:RETURN
433 IFL=4THENA=3:GOSUB591:A=1:GOSUB591:GOSUB591:RETURN
434 IFL=5THENA=4:LC=INT(RND(0)*3)+1:FORI=0TOLC:GOSUB591:NEXT:RETURN
435 IFL=6THENONINT(RND(1)*2)+1GOTO445,446
436 IFL=7THENLC=INT(RND(0)*3)+1:A=6:FORI=0TOLC:GOSUB591:NEXT:RETURN
437 IFL=8THENA=7:GOSUB591:LC=INT(RND(0)*2)+1:A=6:GOSUB591:RETURN
438 IFL=9THENA=9:GOSUB591:A=8:GOSUB591:A=5:GOSUB591:RETURN
439 IFL=10THENA=6:LC=INT(RND(0)*2)+1:FORI=1TOLC:GOSUB591:NEXT:RETURN
440 IFL=11THENA=7:GOSUB591:A=10:GOSUB591:RETURN
441 IFL=12ANDINT(RND(1)*2)=1THENA=11:GOSUB591:RETURN
442 IFL=12THENA=5:GOSUB591:GOSUB591:A=9:GOSUB591:RETURN
443 IFL=13THENA=9:GOSUB591:A=12:GOSUB591:A=9:GOSUB591:RETURN
444 IFL=14THENA=9:GOSUB591:A=13:GOSUB591:A=7:GOSUB591:RETURN
445 A=5:LC=INT(RND(1)*2)+1:FORI=0TOLC:GOSUB591:NEXT:RETURN
446 A=9:LC=INT(RND(1)*2)+1:FORI=1TOLC:GOSUB591:NEXT:RETURN
447 FORI=0TO3
448 IFP3(I)=0THEN451
449 PH(I)=P1(I)
450 PM(I)=P2(I)
451 NEXT:RETURN
452 PRINT"{brown}{home}{$c1}{$c2:38}{$c3}";
453 PRINT"{$c4}                                      {$c5}";
454 PRINT"{$c6}{$c7:38}{$c8}";
455 PRINTDD$;"{brown}{up}{$c1}{$c2:10}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
456 FORI=0TO3:PRINT"{$c4}          {$c5}{$c4}            {$c5}{$c4}            {$c5}";:NEXT
457 PRINT"{$c6}{$c7:10}{$c8}{$c6}{$c7:12}{$c8}{$c6}{$c7:12}";
458 POKE50151,72:POKE56295,9:RETURN
459 PRINTDD$;
460 FORI=0TO3:IFP3(I)=0THENPRINTSPC(40);:GOTO466
461 PRINT"{white}";:IFPH(I)=0THENPRINT"{red}";
462 PRINTSPC(27)P$(I)" ";:A=PH(I):GOSUB730
463 PRINTD$;
464 A=PM(I):GOSUB730
465 PRINT" "D$"{right}";
466 NEXT:RETURN
467 FORI=0TO3:IFPH(I)<=0ORP3(I)=0THENNEXT:RETURN
468 PRINT"{home}"LEFT$(CD$,5+I*3);SPC(37);PD$(I):NEXT
469 RETURN
470 GOSUB505
471 MI=-1:M=0:MO=0
472 MI=MO-1:CY=MY:MM=0
473 IFMC=4THENGOSUB500
474 MI=MI+1
475 PRINT"{home}{white}"LEFT$(CD$,CY)SPC(MX)" ";
476 IFIT(IN(MI))<>MTTHENPRINT"{cyan}";
477 IFIT(IN(MI))=9THENPRINT"{white}";
478 IFIT(IN(MI))=-1THENPRINT"{cyan}";
479 PRINTI$(IN(MI));
480 CY=CY+1
481 MM=MM+1
482 IFMM=MCTHEN484
483 IFMI<13THEN474
484 CP=QS+MX+MY*40
485 POKECP+(M-MO)*40,35
486 GOSUB732
487 POKECP+(M-MO)*40,32
488 IFA$="W"THENM=M-1:GOTO492
489 IFA$="S"THENM=M+1:GOTO492
490 IFA$=CHR$(13)THENRETURN
491 GOTO485
492 IFM=-1THENM=14-1:MO=14-MC:GOTO472
493 IFM=14THENM=0:MO=0:GOTO472
494 M%=0
495 IFM<MOTHENMO=MO-MC:M%=1:GOTO495
496 IFM>=MO+MCTHENMO=MO+MC:M%=1
497 IFMO<0THENMO=0:M%=1
498 IFM%=1THEN472
499 GOTO485
500 FORI=0TO3
501 PRINT"{home}{white}"LEFT$(CD$,CY+I)SPC(MX)" ";
502 PRINT"        ";
503 NEXTI
504 RETURN
505 A=0
506 FORI=0TO99:TT(I)=0:NEXTI
507 FORI=0TO99:TT(I)=IN(I):IN(I)=0:NEXTI
508 IFMT=0THENGOSUB513:GOSUB516:A=50:GOSUB519:GOSUB522:GOSUB525
509 IFMT=1THENGOSUB513:GOSUB519:A=50:GOSUB516:GOSUB522:GOSUB525
510 IFMT=2THENGOSUB513:GOSUB522:A=50:GOSUB516:GOSUB519:GOSUB525
511 IFMT=3THENGOSUB513:GOSUB525:A=50:GOSUB516:GOSUB519:GOSUB522
512 RETURN
513 FORI=0TO99:IFIT(TT(I))=9THENIN(A)=TT(I):A=A+1
514 NEXTI
515 RETURN
516 FORI=0TO99:IFIT(TT(I))=0THENIN(A)=TT(I):A=A+1
517 NEXTI
518 RETURN
519 FORI=0TO99:IFIT(TT(I))=1THENIN(A)=TT(I):A=A+1
520 NEXTI
521 RETURN
522 FORI=0TO99:IFIT(TT(I))=2THENIN(A)=TT(I):A=A+1
523 NEXTI
524 RETURN
525 FORI=0TO99:IFIT(TT(I))=3THENIN(A)=TT(I):A=A+1
526 NEXTI
527 RETURN
528 PRINT"{brown}{home}{down:3}{$c1}{$c2:38}{$c3}";
529 FORI=0TO13
530 PRINT"{$c4}                                      {$c5}";:NEXT
531 PRINT"{$c6}{$c7:38}{$c8}";:RETURN
532 PRINT"{home}{down}{right}{white}                                      ";
533 RETURN
534 PRINT"{brown}{home}{down:3}{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
535 FORI=0TO5
536 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
537 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
538 PRINT"{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
539 FORI=0TO5
540 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
541 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
542 DX=3:DY=4:P=0:GOSUB545:DX=23:P=1:GOSUB545
543 DX=3:DY=12:P=2:GOSUB545:DX=23:P=3:GOSUB545
544 RETURN
545 IFP3(P)=0THENRETURN
546 PRINT"{home}{white}";:IFPH(P)=0THENPRINT"{red}";
547 PRINT"{home}"LEFT$(CD$,DY)SPC(DX-1)" "P$(P)"  LV"PL(P)+1
548 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX)" "I$(PW(P));
549 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX)" "I$(PR(P));
550 T1=PA(P)+IA(PW(P))
551 T2=PD(P)+ID(PR(P))
552 IFT1>999THENT1=999
553 IFT2>999THENT2=999
554 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX+9)"ATK"T1;
555 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX+9)"DEF"T2;
556 RETURN
557 IFPEEK(2)=1THENSP=30960+RS*(CR-0)
558 FORI=0TO7
559 PRINT"{home}{down:3}"LEFT$(CD$,I*2);
560 FORJ=0TO19
561 PRINTT$(PEEK(SP))"{up}";
562 SP=SP+1
563 NEXTJ,I
564 RETURN
565 IS=0
566 IFIN(IS)=0THENIN(IS)=NI:NI=-1:RETURN
567 IS=IS+1
568 IFIS=IXTHENRETURN
569 GOTO566
570 F=13-H(0)*13:C=30960+4*RS:POKEC+8+6*20,F:POKEC+9+6*20,F:POKEC+10+6*20,F
571 F=13-H(1)*13:C=30960+2*RS:POKEC+12+1*20,F:POKEC+12+2*20,F:POKEC+11+1*20,F
572 POKE30960+1*RS+6+6*20,14-H(2)*14
573 POKE30960+9*RS+6+4*20,14-H(3)*14
574 POKE30960+3*RS+10+6*20,14-H(4)*14
575 POKE30960+4*RS+14+6*20,14-H(5)*14
576 POKE30960+15*RS+9+4*20,14-H(8)*14
577 POKE30960+9*RS+1+2*20,14-H(9)*14
578 POKE30960+16*RS+8+2*20,14-H(10)*14
579 POKE30960+14*RS+3+5*20,16-H(6)*16
580 POKE30960+14*RS+8+2*20,11-H(7)*11
581 POKE30960+12*RS+15+2*20,6-H(12)*6
582 POKE30960+12*RS+15+6*20,6-H(13)*6
583 T=18:IFH(12)+H(13)=2THENT=0
584 POKE30960+12*RS+8+0,T
585 RETURN
586 IFCR=4ANDZX=6ANDZY=4ANDH(0)=0THENPRINT"{home}{white}{down}{right}DER SCHALTER IST AUS!";:GOSUB769:GOSUB532
587 IFCR=4ANDZX=6ANDZY=4ANDH(0)=1THENPRINT"{home}{white}{down}{right}DER SCHALTER IST EIN!";:GOSUB769:GOSUB532
588 IFCR=4ANDZX=14ANDZY=4ANDH(1)=0THENPRINT"{home}{white}{down}{right}DER SCHALTER IST AUS!";:GOSUB769:GOSUB532
589 IFCR=4ANDZX=14ANDZY=4ANDH(1)=1THENPRINT"{home}{white}{down}{right}DER SCHALTER IST EIN!";:GOSUB769:GOSUB532
590 RETURN
591 IFCC>=11THENRETURN
592 FH(CC)=MH(A):FM(CC)=MM(A):FT(CC)=A:FC(CC)=INT(RND(0)*MS(A)):FS(CC)=MS(A)
593 FX(CC)=2:FY(CC)=5+CC*3:FA(CC)=MA(A):FD(CC)=MD(A):FB(CC)=MB(A)
594 F1(CC)=MH(A):F2(CC)=MM(A)
595 IFCC>=4THENFX(CC)=5:FY(CC)=5+(CC-4)*3
596 CC=CC+1:RETURN
597 ZM=0
598 FORJ=4TO7
599 IFFT(J)=-1THENZM=ZM-1
600 ZM=ZM+1
601 NEXTJ
602 IFZM<>CMTHENPRINTDD$;"{right}          {down}{left:10}          {down}{left:10}          {down}{left:10}          ";
603 PRINTDD$;
604 CM=0
605 FORI=4TO7
606 IFFT(I)=-1THENNEXT:RETURN
607 A=FH(I):GOSUB730
608 PRINT"{home}{right}"LEFT$(CD$,20+CM)M$(FT(I));
609 PRINT"{home}{right}"LEFT$(CD$,20+CM);"{right:7}";D$
610 CM=CM+1
611 NEXT
612 RETURN
613 CE=4:PRINT"{home}{down}{right}{white}"P$(C)": WAHELE DEIN ZIEL               "
614 IFFT(CE)=-1THEN623
615 POKEQS-1+FX(CE)+FY(CE)*40,35
616 GOSUB732
617 IFA$=CHR$(13)THENPOKEQS-1+FX(CE)+FY(CE)*40,32:RETURN
618 IFA$="S"THENGOTO623
619 IFA$="W"THENGOTO631
620 IFA$="D"ANDCE>3THENPOKEQS-1+FX(CE)+FY(CE)*40,32:CE=0:GOTO614
621 IFA$="A"ANDCE<4THENPOKEQS-1+FX(CE)+FY(CE)*40,32:CE=4:GOTO614
622 GOTO616
623 CO=CE
624 POKEQS-1+FX(CE)+FY(CE)*40,32
625 CE=CE+1
626 IFCE>=12THENCE=0
627 IFCO<4ANDCE>3THENCE=CO
628 IFCO>3ANDCE<4THENCE=CO
629 IFFT(CE)<>-1THEN615
630 GOTO625
631 CO=CE
632 POKEQS-1+FX(CE)+FY(CE)*40,32
633 CE=CE-1
634 IFCE<0THENCE=11
635 IFCO<4ANDCE>3THENCE=CO
636 IFCO>3ANDCE<4THENCE=CO
637 IFFT(CE)<>-1THEN615
638 GOTO633
639 IFFT(C)=-2THEN655
640 IFFT(C)=7ANDFM(C)>=IE(9)THENM=9:GOTO642
641 GOTO323
642 AP=IE(M)*4+2-INT(RND(0)*5):FM(C)=FM(C)-IE(M):IFC<4THENPM(C)=FM(C):GOSUB459
643 FH(CE)=FH(CE)+AP:IFFH(CE)>F1(CE)THENFH(CE)=F1(CE)
644 IFCE<4THENPH(CE)=FH(CE):GOSUB459
645 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{green}";AP
646 GOSUB774
647 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
648 GOTO286
649 FM(C)=FM(C)-IE(M):IFC<4THENPM(C)=FM(C):GOSUB459
650 FS(CE)=FS(CE)/2:IFFS(CE)<5THENFS(CE)=5
651 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{green}SLOW";
652 GOSUB774
653 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
654 GOTO286
655 IFM=9THEN642
656 GOTO323
657 FORI=0TOHC-1:H(I)=0:NEXT
658 FORI=0TO3:PX(I)=0:NEXT
659 PA(0)=5:PD(0)=2:PA(1)=4:PD(1)=3:PA(2)=1:PD(2)=1:PA(3)=1:PD(3)=1:GOSUB570
660 FORI=0TO13:IN(I)=0:NEXT
661 IN(0)=18
662 ER(0)=3:EX(0)=12:EY(0)=5:ET(0)=4
663 ER(1)=6:EX(1)=16:EY(1)=5:ET(1)=3
664 ER(2)=8:EX(2)=14:EY(2)=3:ET(2)=8
665 ER(3)=9:EX(3)=1:EY(3)=4:ET(3)=5
666 ER(4)=13:EX(4)=18:EY(4)=4:ET(4)=16
667 ER(5)=13:EX(5)=18:EY(5)=6:ET(5)=17
668 ER(6)=13:EX(6)=15:EY(6)=2:ET(6)=13
669 ER(7)=14:EX(7)=10:EY(7)=2:ET(7)=15
670 ER(8)=12:EX(8)=1:EY(8)=2:ET(8)=11
671 ER(9)=15:EX(9)=16:EY(9)=4:ET(9)=7
672 ER(10)=0:EX(10)=8:EY(10)=5:ET(10)=9
673 ER(11)=6:EX(11)=3:EY(11)=5:ET(11)=15
674 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
675 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
676 PRINT"{$c6}{$c7:38}{up:3}";
677 POKE50151,72:POKE56295,9
678 X=8:Y=3:CR=0
679 P3(0)=1:P3(1)=0:P3(2)=0:P3(3)=0
680 P1(0)=15:P1(1)=17:P1(2)=9:P1(3)=9:P2(0)=0:P2(1)=0:P2(2)=7:P2(3)=23
681 FORI=0TO3:PH(I)=P1(I):PM(I)=P2(I):PX(I)=0:PW(I)=0:PR(I)=0:NEXT
682 PW(0)=1:PR(0)=2
683 GOSUB570
684 AA$="INTRO":GOSUB718
685 GOTO115
686 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
687 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
688 PRINT"{$c6}{$c7:38}{up:3}";
689 POKE50151,72:POKE56295,9
690 AA$="ENDE":GOSUB718
691 GOTO115
692 OPEN1,8,4,"PFSAVE":OPEN2,8,15:INPUT#2,ER,M$,T,SS
693 IFER<>0THENPRINT"{home}{white}{down}{right}ERROR LOADING    ";:CLOSE2:CLOSE1:GOTO657
694 FORI=0TO3:INPUT#1,PH(I):INPUT#1,PM(I):INPUT#1,PS(I):INPUT#1,P1(I)
695 INPUT#1,P2(I):INPUT#1,P3(I)
696 INPUT#1,PA(I):INPUT#1,PD(I):INPUT#1,PW(I):INPUT#1,PR(I):INPUT#1,PX(I)
697 INPUT#1,PL(I):NEXT
698 FORI=0TO13:INPUT#1,IN(I):NEXT:FORI=0TOHC-1:INPUT#1,H(I):NEXT
699 FORI=0TOEC-1:INPUT#1,ER(I):NEXT
700 INPUT#1,X:INPUT#1,Y:INPUT#1,CR
701 CLOSE1:CLOSE2:GOSUB570:RETURN
702 OPEN15,8,15,"S:PFSAVE":CLOSE15
703 OPEN1,8,4,"PFSAVE,W"
704 FORI=0TO3:PRINT#1,PH(I):PRINT#1,PM(I):PRINT#1,PS(I):PRINT#1,P1(I)
705 PRINT#1,P2(I):PRINT#1,P3(I)
706 PRINT#1,PA(I):PRINT#1,PD(I):PRINT#1,PW(I):PRINT#1,PR(I):PRINT#1,PX(I)
707 PRINT#1,PL(I)
708 NEXT
709 FORI=0TO13:PRINT#1,IN(I):NEXT:FORI=0TOHC-1:PRINT#1,H(I):NEXT
710 FORI=0TOEC-1:PRINT#1,ER(I):NEXT
711 PRINT#1,X:PRINT#1,Y:PRINT#1,CR
712 CLOSE1:RETURN
713 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
714 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
715 PRINT"{$c6}{$c7:38}{up:3}";
716 POKE50151,72:POKE56295,9
717 RETURN
718 POKE1020,Q7
719 POKE56322,224
720 IFAA$="INTRO"THENOPEN1,8,4,"TXT.INTRO,S,R"
721 IFAA$="ENDE"THENOPEN1,8,4,"TXT.ENDE,S,R"
722 IFAA$=""THENRETURN
723 GET#1,B$
724 IFB$="$"THENPRINT"{home}{down:2}{right:2}{white}";:GOTO723
725 IFB$="%"THENPRINT"{right:2}{white}";:GOTO723
726 IFB$="#"THENCLOSE1:POKE56322,255:GOSUB760:RETURN
727 PRINTB$;
728 IFB$<>" "THENPOKE162,0:WAIT162,4
729 GOTO723
730 T$=STR$(A)
731 D$=RIGHT$("000"+RIGHT$(T$,LEN(T$)-1),3):RETURN
732 A$=""
733 J=PEEK(56320)
734 IFJ=127THEN736
735 GOTO733
736 J=PEEK(56320)
737 IFJ=127ANDA$=CHR$(13)THENRETURN
738 IFJ=107THENA$="E":GOSUB765:RETURN
739 IFJ=103THENA$="E":GOSUB765:RETURN
740 IFJ=110THENA$="E":GOSUB765:RETURN
741 IFJ=109THENA$="E":GOSUB765:RETURN
742 IFJ=123THENA$="A":GOSUB765:RETURN
743 IFJ=119THENA$="D":GOSUB765:RETURN
744 IFJ=126THENA$="W":GOSUB765:RETURN
745 IFJ=125THENA$="S":GOSUB765:RETURN
746 IFJ=111THENA$=CHR$(13)
747 GOTO736
748 A$=""
749 J=PEEK(56320)
750 IFJ<>111ANDJ<>107ANDJ<>103ANDJ<>110ANDJ<>109THEN752
751 GOTO749
752 J=PEEK(56320)
753 IFJ=127ANDA$=CHR$(13)THENRETURN
754 IFJ=123THENA$="A":GOSUB766:RETURN
755 IFJ=119THENA$="D":GOSUB766:RETURN
756 IFJ=126THENA$="W":GOSUB766:RETURN
757 IFJ=125THENA$="S":GOSUB766:RETURN
758 IFJ=111THENA$=CHR$(13)
759 GOTO752
760 A$=""
761 J=PEEK(56320)
762 IFJ=127ANDA$=CHR$(13)THENRETURN
763 IFJ=111THENA$=CHR$(13)
764 GOTO761
765 POKE162,0:WAIT162,16:RETURN
766 POKE162,0:WAIT162,8:RETURN
767 POKE162,0:WAIT162,32:RETURN
768 POKE162,0:WAIT162,2:RETURN
769 POKE162,0:WAIT162,32
770 POKE162,0:WAIT162,32
771 POKE162,0:WAIT162,32
772 POKE162,0:WAIT162,32
773 RETURN
774 POKE162,0:WAIT162,32
775 RETURN
776 POKE162,0:WAIT162,4
777 RETURN
778 DATA"IMP",5,,8,"{lt. green}{$cd}{$ce}{down}{left:2}{$cf}{$d0}",4,1,,3
779 DATA"SNAKE",8,,12,"{gray3}{$d8}{$d9}{down}{left:2}{$da}{$db}",6,1,,5
780 DATA"VIPER",12,,18,"{gray1}{$d8}{$d9}{down}{left:2}{$da}{$db}",6,1,,9
781 DATA"NACHO",30,,22,"{lt. red}{$cd}{$ce}{down}{left:2}{$cb}{$cc}",15,5,,50
782 DATA"GOLEM",20,,4,"{gray2}{$af}{$b0}{down}{left:2}{$b1}{$b2}",20,8,,17
783 DATA"MOSS",30,,7,"{lt. green}{$af}{$b0}{down}{left:2}{$b1}{$b2}",26,9,,21
784 DATA"BONES",50,,20,"{brown}{$b3}{$b4}{down}{left:2}{$b5}{$b6}",18,8,,15
785 DATA"WISP",25,50,15,"{green}{$bb}{$bc}{down}{left:2}{lt. green}{$bd}{$be}",,8,,17
786 DATA"TROLL",100,,9,"{gray3}{$af}{$b0}{down}{left:2}{$b1}{$b2}",40,13,,100
787 DATA"SPARK",30,50,22,"{yellow}{$bb}{$bc}{down}{left:2}{gray3}{$bd}{$be}",,8,,17
788 DATA"BURNER",80,90,30,"{gray3}{$b3}{$b4}{down}{left:2}{$b5}{$b6}",25,13,,42
789 DATA"TBERRY",120,10,60,"{lt. green}{$b3}{$b4}{down}{left:2}{$b5}{$b6}",60,20,,150
790 DATA"DRACUL",250,60,50,"{gray2}{rvrs on}DE{down}{left:2}FG{rvrs off}",70,25,,500
791 DATA"GLADOS",500,70,60,"{gray2}{rvrs on}HI{down}{left:2}JK{rvrs off}",80,30,,1000
792 DATA"KRON",15,,10,"{lt. red}{$c9}{$ca}{down}{left:2}{$cb}{$cc}",4,3,,  
793 DATA"LENA",17,,10,"{gray2}{rvrs on}${rvrs off}{$ca}{down}{left:2}{$cb}{$cc}",3,3,,    
794 DATA"DOLM",9,7,8,"{lt. green}{$c9}{$ca}{down}{left:2}{$cb}{$cc}",1,1,, 
795 DATA"MIRA",9,23,7,"{orange}{rvrs on}${rvrs off}{$ca}{down}{left:2}{$cb}{$cc}",1,1,1,2 
796 DATA"{gray1}{rvrs off}{$c8}{rvrs off}{gray1}{$c9}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1}{$c8}{rvrs off}"
797 DATA"{gray1}{rvrs off}{$ca}{rvrs off}{gray1}{$cb}{down}{left:2}{rvrs off}{gray1}{$c8}{rvrs off}{gray1}{$c9}{rvrs off}"
798 DATA"{gray1}{rvrs off}{$cc}{rvrs off}{gray1}{$cd}{down}{left:2}{rvrs off}{gray1}{$c8}{rvrs off}{gray1}{$c9}{rvrs off}"
799 DATA"{orange}{rvrs off}{$ce}{rvrs off}{orange}{$ce}{down}{left:2}{rvrs off}{orange}{$ce}{rvrs off}{orange}{$ce}{rvrs off}"
800 DATA"{gray1}{rvrs off}{$c8}{rvrs off}{gray1}{$c9}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1}{$c8}{rvrs off}"
801 DATA"{gray1}{rvrs off}{$cf}{rvrs off}{gray1}{$d0}{down}{left:2}{rvrs off}{gray1}{$d1}{rvrs off}{gray1}{$d2}{rvrs off}"
802 DATA"{lt. green}{rvrs off}{$d3}{rvrs off}{lt. green}{$ce}{down}{left:2}{rvrs off}{lt. green}{$ce}{rvrs off}{lt. green}{$d4}{rvrs off}"
803 DATA"{gray1}{rvrs off}{$d5}{rvrs off}{gray1}{$d6}{down}{left:2}{rvrs off}{gray1}{$d7}{rvrs off}{gray1}{$d8}{rvrs off}"
804 DATA"{brown}{rvrs off}{$d9}{rvrs off}{brown}{$da}{down}{left:2}{rvrs off}{brown}{$db}{rvrs off}{brown}{$dc}{rvrs off}"
805 DATA"{lt. red}{rvrs off}{$dd}{rvrs off}{lt. red}{$dd}{down}{left:2}{rvrs off}{lt. red}{$dd}{rvrs off}{lt. red}{$dd}{rvrs off}"
806 DATA"{lt. red}{rvrs off}{$de}{rvrs off}{lt. red}{$df}{down}{left:2}{rvrs off}{lt. red}{$a1}{rvrs off}{lt. red}{$a2}{rvrs off}"
807 DATA"{lt. red}{rvrs off}{$a3}{rvrs off}{lt. red}{$a4}{down}{left:2}{rvrs off}{lt. red}{$a5}{rvrs off}{lt. red}{$a6}{rvrs off}"
808 DATA"{lt. green}{rvrs off}{$d3}{rvrs off}{lt. green}{$ce}{down}{left:2}{rvrs off}{lt. green}{$ce}{rvrs off}{lt. green}{$d4}{rvrs off}"
809 DATA"{brown}{rvrs off}{$a7}{rvrs off}{brown}{$a7}{down}{left:2}{rvrs off}{brown}{$a7}{rvrs off}{brown}{$a7}{rvrs off}"
810 DATA"{brown}{rvrs off}{cmdr pound}{rvrs off}{brown}{cmdr pound}{down}{left:2}{rvrs off}{brown}{cmdr pound}{rvrs off}{brown}{cmdr pound}{rvrs off}"
811 DATA"{gray2}{rvrs off}{shft pound}{rvrs off}{gray2}{$aa}{down}{left:2}{rvrs off}{gray2}{$ab}{rvrs off}{gray2}{$ac}{rvrs off}"
812 DATA"{gray1}{rvrs off}{$ad}{rvrs off}{gray1}{$ae}{down}{left:2}{rvrs off}{gray1}{$af}{rvrs off}{gray1}{$b0}{rvrs off}"
813 DATA"{lt. green}{rvrs off}{$b1}{rvrs off}{lt. green}{$b2}{down}{left:2}{rvrs off}{lt. green}{$b3}{rvrs off}{lt. green}{$b4}{rvrs off}"
814 DATA"{lt. green}{rvrs off}{$b5}{rvrs off}{lt. green}{$b6}{down}{left:2}{rvrs off}{lt. green}{$b5}{rvrs off}{lt. green}{$b6}{rvrs off}"
815 DATA"{lt. green}{rvrs off}{$b7}{rvrs off}{lt. green}{$b8}{down}{left:2}{rvrs off}{lt. green}{$b9}{rvrs off}{lt. green}{$ba}{rvrs off}"
816 DATA"{brown}{rvrs off}{$b1}{rvrs off}{brown}{$b2}{down}{left:2}{rvrs off}{brown}{$b3}{rvrs off}{brown}{$b4}{rvrs off}"
817 DATA"{brown}{rvrs off}{$b5}{rvrs off}{brown}{$b6}{down}{left:2}{rvrs off}{brown}{$b5}{rvrs off}{brown}{$b6}{rvrs off}"
818 DATA"{brown}{rvrs off}{$b7}{rvrs off}{brown}{$b8}{down}{left:2}{rvrs off}{brown}{$b9}{rvrs off}{brown}{$ba}{rvrs off}"
819 DATA"{brown}{rvrs off}{$d3}{rvrs off}{brown}{$ce}{down}{left:2}{rvrs off}{brown}{$ce}{rvrs off}{brown}{$d4}{rvrs off}"
820 DATA"{brown}{rvrs off}{$bb}{rvrs off}{brown}{$bb}{down}{left:2}{rvrs off}{brown}{$bc}{rvrs off}{brown}{$bc}{rvrs off}"
821 DATA"{gray1}{rvrs off}{$bd}{rvrs off}{gray1}{$be}{down}{left:2}{rvrs off}{gray1}{$bf}{rvrs on}{gray1}@{rvrs off}"
822 DATA"{brown}{rvrs off}{$bb}{rvrs on}{brown}A{down}{left:2}{rvrs off}{brown}{$bc}{rvrs off}{brown}{$bc}{rvrs off}"
823 DATA"{brown}{rvrs on}A{rvrs off}{brown}{$bb}{down}{left:2}{rvrs off}{brown}{$bc}{rvrs off}{brown}{$bc}{rvrs off}"
824 DATA"{gray1}{rvrs off}{$ce}{rvrs off}{gray1}{$ce}{down}{left:2}{rvrs off}{gray1}{$ce}{rvrs off}{gray1}{$ce}{rvrs off}"
825 DATA"{gray1}{rvrs on}B{rvrs off}{gray1}{$ce}{down}{left:2}{rvrs on}{gray1}B{rvrs off}{gray1}{$ce}{rvrs off}"
826 DATA"{gray1}{rvrs on}B{rvrs off}{gray1}{$ce}{down}{left:2}{rvrs on}{gray1}C{rvrs off}{gray1}{$ce}{rvrs off}"
827 DATA"{gray1}{rvrs on}D{rvrs on}{gray1}E{down}{left:2}{rvrs on}{gray1}F{rvrs on}{gray1}F{rvrs off}"
828 DATA"{gray1}{rvrs on}G{rvrs on}{gray1}G{down}{left:2}{rvrs on}{gray1}H{rvrs on}{gray1}H{rvrs off}"
829 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
830 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
831 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
832 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
833 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
834 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
835 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
836 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
837 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
838 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
839 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
840 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
841 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
842 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
843 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
844 DATA"{orange}{rvrs on}I{rvrs on}{orange}J{down}{left:2}{rvrs on}{orange}K{rvrs on}{orange}L{rvrs off}"
845 DATA"{gray1}{rvrs on}M{rvrs on}{gray1}J{down}{left:2}{rvrs on}{gray1}K{rvrs on}{gray1}L{rvrs off}"
846 DATA"{lt. green}{rvrs on}N{rvrs on}{lt. green}O{down}{left:2}{rvrs on}{lt. green}P{rvrs on}{lt. green}Q{rvrs off}"
847 DATA"{gray2}{rvrs on}R{rvrs on}{gray2}S{down}{left:2}{rvrs on}{gray2}T{rvrs on}{gray2}U{rvrs off}"
848 DATA"{gray1}{rvrs on}V{rvrs on}{gray1}W{down}{left:2}{rvrs on}{gray1}X{rvrs on}{gray1}Y{rvrs off}"
849 DATA"{gray1}{rvrs on}Z{rvrs on}{gray1}[{down}{left:2}{rvrs on}{gray1}{pound}{rvrs on}{gray1}]{rvrs off}"
850 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
851 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
852 DATA"{orange}{rvrs off} {rvrs off}{orange} {down}{left:2}{rvrs off}{orange} {rvrs off}{orange} {rvrs off}"
853 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
854 DATA"{lt. green}{rvrs off} {rvrs off}{lt. green} {down}{left:2}{rvrs off}{lt. green} {rvrs off}{lt. green} {rvrs off}"
855 DATA"{gray2}{rvrs off} {rvrs off}{gray2} {down}{left:2}{rvrs off}{gray2} {rvrs off}{gray2} {rvrs off}"
856 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
857 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
858 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
859 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
860 DATA"{lt. green}{rvrs on}{^}{rvrs on}{lt. green}{back arrow}{down}{left:2}{rvrs on}{lt. green}!{rvrs on}{lt. green}#{rvrs off}"
861 DATA"{gray3}{rvrs on}${rvrs on}{gray3}%{down}{left:2}{rvrs on}{gray3}&{rvrs on}{gray3}'{rvrs off}"
862 DATA"{gray1}{rvrs on}${rvrs on}{gray1}%{down}{left:2}{rvrs on}{gray1}&{rvrs on}{gray1}'{rvrs off}"
863 DATA"{lt. green}{rvrs on}({rvrs on}{lt. green}){down}{left:2}{rvrs on}{lt. green}*{rvrs on}{lt. green}+{rvrs off}"
864 DATA"{gray1}{rvrs on},{rvrs on}{gray1}-{down}{left:2}{rvrs on}{gray1}.{rvrs on}{gray1}/{rvrs off}"
865 DATA"{gray1}{rvrs on}0{rvrs on}{gray1}1{down}{left:2}{rvrs on}{gray1}2{rvrs on}{gray1}3{rvrs off}"
866 DATA"{gray1}{rvrs on}4{rvrs on}{gray1}5{down}{left:2}{rvrs on}{gray1}6{rvrs on}{gray1}7{rvrs off}"
867 DATA"{gray1}{rvrs on}8{rvrs on}{gray1}9{down}{left:2}{rvrs on}{gray1}:{rvrs on}{gray1};{rvrs off}"
868 DATA"{lt. green}{rvrs on}<{rvrs on}{lt. green}={down}{left:2}{rvrs on}{lt. green}>{rvrs on}{lt. green}?{rvrs off}"
869 DATA"{gray3}{rvrs on}8{rvrs on}{gray3}9{down}{left:2}{rvrs on}{gray3}:{rvrs on}{gray3};{rvrs off}"
870 DATA"{gray1}{rvrs on}4{rvrs on}{gray1}5{down}{left:2}{rvrs on}{gray1}6{rvrs on}{gray1}7{rvrs off}"
871 DATA"{lt. green}{rvrs on}4{rvrs on}{lt. green}5{down}{left:2}{rvrs on}{lt. green}6{rvrs on}{lt. green}7{rvrs off}"
872 DATA"{gray1}{rvrs on}{$c0}{rvrs on}{gray1}{$c1}{down}{left:2}{rvrs on}{gray1}{$c2}{rvrs on}{gray1}{$c3}{rvrs off}"
873 DATA"{gray1}{rvrs on}{$c4}{rvrs on}{gray1}{$c5}{down}{left:2}{rvrs on}{gray1}{$c6}{rvrs on}{gray1}{$c7}{rvrs off}"
874 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
875 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
876 DATA"        ",0    ,0    ,0    ,0    ,-1 
877 DATA"DOLCH   ",3    ,0    ,0    ,0    ,0  
878 DATA"HELM    ",0    ,2    ,-1   ,0    ,1  
879 DATA"KRAEUTER",0    ,0    ,-1   ,10   ,2  
880 DATA"FEUER   ",0    ,0    ,0    ,5    ,3  
881 DATA"EIS     ",0    ,0    ,0    ,8    ,3  
882 DATA"MUT     ",0    ,0    ,0    ,23   ,3  
883 DATA"BOMBE   ",0    ,0    ,0    ,50   ,3  
884 DATA"KIRSCHE ",0    ,0    ,-1   ,15   ,2  
885 DATA"HEILEN  ",0    ,0    ,0    ,4    ,3  
886 DATA"SKULL   ",0    ,0    ,0    ,0    ,4  
887 DATA"FLEGEL  ",7    ,0    ,-1   ,0    ,0  
888 DATA"LAHM    ",0    ,0    ,0    ,4    ,3  
889 DATA"STAB    ",12   ,0    ,0    ,0    ,0  
890 DATA"FEDER   ",0    ,10   ,0    ,0    ,1  
891 DATA"STOCK   ",23   ,0    ,0    ,0    ,0  
892 DATA"WESTE   ",0    ,14   ,0    ,0    ,1  
893 DATA"SCHILD  ",0    ,24   ,0    ,0    ,1  
894 DATA"ZURUECK ",0    ,0    ,0    ,0    ,9  
895 END
