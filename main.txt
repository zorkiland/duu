0 POKE55,240:POKE56,120:CLR
1 QO=53282
2 QP=53283
3 QQ=53281
4 QR=53280
5 QS=49152
6 V=53248
7 QT=30960
8 Q1=0
9 Q2=1
10 Q3=2
11 Q4=3
12 Q5=4
13 Q6=5
14 Q7=6
15 Q8=7
16 Q9=8
17 QA=9
18 QB=10
19 CB=11
20 QC=12
21 QD=13
22 QE=14
23 QF=15
24 QG=8
25 QH=9
26 QI=10
27 QJ=11
28 QK=12
29 QL=13
30 QM=14
31 QN=15
32 MQ=14
33 IC=19
34 HC=14
35 EC=12
36 IX=99
37 TM=80
38 RS=165
39 DD$="{home}{down:20}{white}"
40 CD$="{down:25}"
41 CT$="{home}{down}{white}{right}                                      "
42 DIMM$(MQ),MD$(MQ)
43 DIMMH(MQ),MM(MQ)
44 DIMMA(MQ),MD(MQ)
45 DIMMS(MQ)
46 DIMMB(MQ),MX(MQ)
47 DIMFH(11),FT(11),FC(11)
48 DIMFM(11),FS(11),FX(11),FY(11)
49 DIMFA(11),FD(11),F1(11),F2(11)
50 FORI=0TO3:FX(I)=37:FY(I)=5+3*I:NEXT
51 DIMI$(IC),IA(IC)
52 DIMID(IC),IS(IC)
53 DIMIE(IC),IT(IC)
54 DIMH(HC)
55 DIMER(EC),EX(EC)
56 DIMEY(EC),ET(EC)
57 DIMIN(IX)
58 DIMT$(TM)
59 DIMTN$(TM)
60 DIMTT(IX)
61 FORI=0TOMQ-1
62 READM$(I),MH(I),MM(I),MS(I)
63 READMA(I),MD(I),MB(I),MX(I)
64 NEXT
65 FORI=0TO3
66 READP$(I),PH(I),PM(I),PS(I)
67 READPA(I),PD(I),PW(I),PR(I)
68 NEXT
69 PM$="{rvrs on}{lt. red}UV{down}{left:2}WX{rvrs off}"
70 FORI=0TOTM-1
71 READT$(I)
72 NEXT
73 ZZ=48
74 FORI=0TO3
75 PD$(I)=T$(ZZ)
76 ZZ=ZZ+1
77 NEXTI
78 ZZ=64
79 FORI=0TOMQ-1
80 MD$(I)=T$(ZZ)
81 ZZ=ZZ+1
82 NEXTI
83 FORI=0TOIC-1
84 READI$(I),IA(I),ID(I),IS(I),IE(I),IT(I)
85 NEXT
86 POKEV+21,0
87 POKE648,192
88 POKE56576,0
89 POKE53272,8
90 POKE53270,PEEK(53270)OR16
91 POKEQO,QA
92 POKEQP,Q1
93 POKEQR,Q1
94 POKEQQ,Q7
95 GOTO638
96 POKE1020,Q7
97 POKE1021,Q7
98 SYS820
99 GOSUB433
100 GOSUB440
101 PRINT"{home}{down}{right}{white}                                      ";
102 POKE1020,Q7
103 GOSUB538
104 IFPEEK(2)=1THENCP=30960+RS*(CR-0)
105 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)PD$(0);
106 OX=ZX:OY=ZY
107 ZX=X:ZY=Y
108 GOSUB729
109 IFA$=CHR$(145)ORA$="W"THENPRINTCT$;:ZY=Y-1:GOTO115
110 IFA$=CHR$(157)ORA$="A"THENPRINTCT$;:ZX=X-1:GOTO115
111 IFA$=CHR$(29)ORA$="D"THENPRINTCT$;:ZX=X+1:GOTO115
112 IFA$=CHR$(17)ORA$="S"THENPRINTCT$;:ZY=Y+1:GOTO115
113 IFA$=CHR$(13)THEN172
114 GOTO108
115 AA=0
116 FORI=0TOEC
117 IFER(I)=CRANDEX(I)=ZXANDEY(I)=ZYTHENAA=1
118 NEXTI
119 IFZX=-1ORZX=20ORZY=-1ORZY=8THEN131
120 C=PEEK(CP+ZX+ZY*20)
121 IFC=1THEN152
122 IFC=3THEN145
123 IFC=7THEN158
124 IFC=8THEN145
125 IFC=11THEN145
126 IFC=13THEN171
127 IFC=18THEN158
128 IFC=25THEN145
129 IFC=99THEN162
130 IFC>9THEN106
131 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)T$(PEEK(CP+X+Y*20))
132 IFRND(1)*100>97ANDPEEK(CP+164)>0THENFF=0:GOTO248
133 X=ZX:Y=ZY
134 IFX=-1ANDPEEK(CP+160)=255THENX=0:GOTO105
135 IFX=-1THENCR=PEEK(CP+160):X=19:GOTO102
136 IFX=20ANDPEEK(CP+161)=255THENX=19:GOTO105
137 IFX=20THENCR=PEEK(CP+161):X=0:GOTO102
138 IFY=-1ANDPEEK(CP+162)=255THENY=0:GOTO105
139 IFY=-1THENCR=PEEK(CP+162):Y=7:GOTO102
140 IFY=8ANDPEEK(CP+163)=255THENY=7:GOTO105
141 IFY=8THENCR=PEEK(CP+163):Y=0:GOTO102
142 IFC=4THEN152
143 IFC=5THEN152
144 GOTO105
145 IFCR=99THENCR=99:X=0:Y=0:GOTO101
146 IFCR=99THENCR=99:X=0:Y=0:GOTO101
147 IFCR=99THENCR=99:X=0:Y=0:GOTO101
148 IFCR=99THENCR=99:X=0:Y=0:GOTO101
149 IFCR=99THENCR=99:X=0:Y=0:GOTO101
150 IFCR=99THENCR=99:X=0:Y=0:GOTO101
151 IFCR=99THEN667
152 IFC=1ANDCR=0ANDZX=16THENH(0)=1-H(0):GOSUB551:GOSUB567:GOTO102
153 IFC=1ANDCR=99ANDZX=0THENH(1)=1-H(1):GOSUB551:GOSUB567:GOTO106
154 IFC=4ANDCR=99ANDX=0ANDH(6)=0THENH(6)=1:GOSUB551:GOTO101
155 IFC=4ANDCR=99ANDX=0ANDH(7)=0THENH(7)=1:GOSUB551:GOTO101
156 IFC=5ANDCR=99ANDY=0ANDH(12)=0THENH(12)=1:GOSUB551:GOTO101
157 IFC=5ANDCR=99ANDY=0ANDH(13)=0THENH(13)=1:GOSUB551:GOTO101
158 IFC=7ANDAA=0THENGOTO106
159 IFC=7ANDAA=1THENPRINT"{home}{white}{down}{right}IN DER TRUHE IST WAS!":GOSUB750:GOSUB513:GOTO106
160 IFC=18ANDAA=0THENGOTO106
161 IFC=18ANDAA=1THENPRINT"{home}{white}{down}{right}DER BAUM SIEHT INTRESSANT AUS!":GOSUB750:GOSUB513:GOTO106
162 IFCR=99THENPRINT"{white}{home}{down}{right:2}EIN HEILZAUBER IST UNTER DEM BAUM":GOSUB750:GOSUB513:GOTO106
163 IFCR=99THENPRINT"{white}{home}{down}{right:2}EIN FEUERZAUBER IST IN DEN KATAKOMBEN":GOSUB750:GOSUB513:GOTO106
164 IFCR=99THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB750:H(2)=1:GOSUB551:P3(1)=1:GOTO96
165 IFCR=99THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB750:H(5)=1:GOSUB551:P3(2)=1:GOTO96
166 IFCR=99ANDX<=2THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB750:H(9)=1:GOSUB551:P3(3)=1:GOTO96
167 IFCR=99THENPRINT"{white}{home}{down}{right:2}DU KANNST HIER NICHT DURCH!":GOSUB750:FF=4:H(4)=1:GOSUB551:GOTO248
168 IFCR=99ANDX>3THENPRINT"{white}{home}{down}{right:2}ACH WIE SUESS!":GOSUB750:FF=9:H(3)=1:GOSUB551:GOTO248
169 IFCR=99THENPRINT"{white}{home}{down}{right:2}DU WILLST MICH BESIEGEN!":GOSUB750:FF=13:H(8)=1:GOSUB551:GOTO248
170 IFCR=99THENPRINT"{white}{home}{down}{right:2}DU HAST MEINE ERWARTUNG UEBERTROFFEN!":GOSUB750:FF=14:H(10)=1:GOSUB551:GOTO248
171 IFC=13THENPRINT"{home}{white}{down}{right}DU BIST GEHEILT!":GOSUB750:GOSUB428:GOSUB440:GOSUB513:GOTO106
172 PRINT"{home}{down}{white}{right}  UNTERSUCHE  EQUIPMENTS  INVENTAR"
173 M=0
174 POKEQS+42+M*12,35
175 GOSUB713
176 POKEQS+42+M*12,32
177 IFA$="A"THENM=M-1-3*(M=0):GOTO174
178 IFA$="D"THENM=M+1+3*(M=2):GOTO174
179 IFA$=CHR$(13)THENPOKEQS+42+M*7,32:GOTO181
180 GOTO174
181 PRINT"{home}{down}{white}{right}                                      ";
182 ONM+1GOTO236,196,183
183 POKE1020,Q1
184 GOSUB509
185 PRINT"{home}{down}{right}{white}INVENTAR: WAEHLE DEIN ITEM           ";
186 MT=2:MX=2:MY=4:MC=14:GOSUB451
187 IFIN(M)=9THEN101
188 IFIN(M)=0ORIT(IN(M))<>MTTHEN101
189 IFIN(M)=3THENGOSUB192:IN(M)=0:GOSUB440:GOTO101
190 IFIN(M)=8THENGOSUB194:IN(M)=0:GOSUB440:GOTO101
191 GOTO101
192 FORI=0TO3:PH(I)=PH(I)+IE(IN(M)):IFPH(I)>P1(I)THENPH(I)=P1(I)
193 FH(I)=PH(I):NEXT:RETURN
194 FORI=0TO3:PM(I)=PM(I)+IE(IN(M)):IFPM(I)>P2(I)THENPM(I)=P2(I)
195 FM(I)=PM(I):NEXT:RETURN
196 POKE1020,Q1
197 GOSUB515
198 PRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINEN SPIELER     ";
199 P=0
200 CP=QS+2+(4-8*(P>1))*40:IFP=1ORP=3THENCP=CP+20
201 POKECP,35
202 GOSUB713
203 POKECP,32
204 IFP=0ANDA$="D"ANDP3(1)=1THENP=1:GOTO200
205 IFP=0ANDA$="S"ANDP3(2)=1THENP=2:GOTO200
206 IFP=0ANDA$="D"ANDP3(1)=OANDP3(2)=0ANDP3(3)=1THENP=3:GOTO200
207 IFP=0ANDA$="S"ANDP3(1)=OANDP3(2)=0ANDP3(3)=1THENP=3:GOTO200
208 IFP=1ANDA$="A"ANDP3(0)=1THENP=0:GOTO200
209 IFP=1ANDA$="S"ANDP3(3)=1THENP=3:GOTO200
210 IFP=2ANDA$="D"ANDP3(3)=1THENP=3:GOTO200
211 IFP=2ANDA$="W"ANDP3(0)=1THENP=0:GOTO200
212 IFP=3ANDA$="A"ANDP3(2)=1THENP=2:GOTO200
213 IFP=3ANDA$="W"ANDP3(1)=1THENP=1:GOTO200
214 IFP=3ANDA$="A"ANDP3(2)=OANDP3(1)=0ANDP3(0)=1THENP=0:GOTO200
215 IFP=3ANDA$="W"ANDP3(2)=OANDP3(1)=0ANDP3(0)=1THENP=0:GOTO200
216 IFA$=CHR$(13)THEN218
217 GOTO201
218 SL=0:CP=CP+81
219 PRINT"{home}{down}{right}{white}EQUIPMENT: WAFFE ODER RUESTUNG       ";
220 POKECP+SL*40,35
221 GOSUB713
222 POKECP+SL*40,32:IFA$="W"ORA$="S"THENSL=1-SL:GOTO220
223 IFA$=CHR$(13)THEN225
224 GOTO220
225 MY=4:MX=23:MC=14:IFP=1ORP=3THENMX=3
226 IFSL=0THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE WAFFFE       ";
227 IFSL=1THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE RUESTUNG     ";
228 PRINT"{home}"LEFT$(CD$,MY-1)SPC(MX-3);"{brown}{$c1}{$c2:18}{$c3}";
229 FORI=0TO13:PRINT"{down}{left:20}{$c4}                  {$c5}";:NEXT
230 PRINT"{down}{left:20}{$c6}{$c7:18}{$c8}";
231 MT=SL:GOSUB451
232 IFIT(IN(M))=9THEN101
233 IFIN(M)<>0ANDIT(IN(M))<>SLTHEN101
234 IFSL=0THENQ=PW(P):PW(P)=IN(M):IN(M)=Q:GOTO101
235 IFSL=1THENQ=PR(P):PR(P)=IN(M):IN(M)=Q:GOTO101
236 GOSUB513
237 GOSUB755
238 EI=0
239 IFER(EI)<>CROREX(EI)<>OXOREY(EI)<>OYTHEN244
240 NI=ET(EI):GOSUB546
241 IFIS=IXTHENPRINT"{home}{white}{down}{right}DU KANNST NICHT MEHR TRAGEN!":GOSUB750:GOSUB513:GOTO106
242 ER(EI)=-1
243 PRINT"{home}{white}{down}{right}GEFUNDEN: ";I$(IN(IS));:GOSUB741:GOSUB513:GOTO106
244 EI=EI+1
245 IFEI=ECTHEN247
246 GOTO239
247 PRINT"{home}{down}{right}{white}NICHTS BESONDERES!":GOSUB750:GOSUB513:GOTO106
248 FORI=4TO11:FT(I)=-1:NEXT
249 PP=4:CC=4:WX=0
250 FORI=0TO3:FT(I)=-2:FH(I)=PH(I):FM(I)=PM(I):FS(I)=PS(I)
251 IFP3(I)=0THENFH(I)=-1
252 IFFH(I)<=0THENFT(I)=-1:PP=PP-1
253 FA(I)=PA(I)+IA(PW(I)):FD(I)=PD(I)+ID(PR(I)):F1(I)=P1(I):F2(I)=P2(I)
254 NEXT
255 PRINT"{brown}{home}{$c1}{$c2:38}{$c3}";
256 PRINT"{$c4}                                      {$c5}";
257 PRINT"{$c6}{$c7:38}{$c8}";
258 PRINT"{home}{down:3}{white}";:FORI=0TO15:PRINT"                                        ";:NEXT
259 PRINTDD$;"{brown}{up}{$c1}{$c2:7}{white}HP{brown}{$c2}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
260 GOSUB409
261 GOSUB440
262 GOSUB448
263 FORI=0TO11
264 IFFT(I)<>-1THENFC(I)=INT(RND(0)*FD(I))
265 NEXT
266 CC=CC-4
267 PRINT"{home}{down}{right}                       ";:GOSUB578
268 FORI=4TO11:IFFT(I)=-1THEN270
269 PRINT"{home}"LEFT$(CD$,FY(I));SPC(FX(I));MD$(FT(I))
270 NEXT
271 C=0
272 IFFT(C)<>-1ANDFC(C)>=100THENFC(C)=FC(C)-100:GOTO275
273 C=C+1:IFC<12THEN272
274 FORI=0TO11:FC(I)=FC(I)+FS(I):NEXT:GOTO271
275 IFFT(C)=-2THEN277
276 GOTO358
277 PRINT"{home}{white}{down}{right}"P$(C)": IST AM ZUG                     "
278 PRINTDD$;SPC(16)"{white} ANGRIFF {down}{left:9} INVENTAR";:M=2
279 PRINTDD$;SPC(16)"{down:2}         {down}{left:9}         ";
280 IFPM(C)>0THENPRINTDD$;SPC(16)"{down:2} MAGIE   ";:M=3
281 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(37)"  {down}{left:2}  ";
282 PRINT"{left:3}{up}"PD$(C);
283 MY=0:FZ=0
284 POKEQS+816+MY*40,35
285 GOSUB713
286 IFA$="S"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY+1:GOTO290
287 IFA$="W"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY-1:GOTO290
288 IFA$=CHR$(13)THENFZ=0:GOTO293
289 GOTO285
290 IFMY>=MTHENMY=0
291 IFMY<0THENMY=M-1
292 GOTO284
293 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(36)"{white}  {down}{left:2}  {left}{up}"PD$(C);
294 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
295 ONMY+1GOTO321,313
296 MT=3:MX=16:MY=20:MC=4:GOSUB451
297 IFIT(IN(M))=9THEN277
298 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         {down}{left:9}         ";
299 IFIN(M)=0ORIT(IN(M))<>3THEN277
300 M=IN(M)
301 IFIE(M)>FM(C)THENPRINT"{home}{down}{right}NICHT GENUG MANA                     ";:GOSUB750:GOTO277
302 GOSUB594
303 GOTO620
304 AP=IE(M)+2-INT(RND(0)*5):FM(C)=FM(C)-IE(M):PM(C)=FM(C):GOSUB440
305 AP=AP+PL(C)*3
306 FH(CE)=FH(CE)-AP:IFFH(CE)<0THENFH(CE)=0
307 IFCE<4THENPH(CE)=FH(CE)
308 IFFH(CE)>0THENGOTO343
309 IFFT(CE)<>-2THENCC=CC-1:WX=WX+MX(FT(CE))
310 IFFT(CE)=-2THENPP=PP-1
311 FT(CE)=-1
312 GOTO343
313 MT=2:MX=16:MY=20:MC=4:GOSUB451
314 IFIT(IN(M))=9THEN277
315 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
316 PRINT"{down}{left:9}         ";
317 IFIN(M)=0ORIT(IN(M))<>MTTHEN277
318 IFIN(M)=3THENGOSUB192:IN(M)=0:GOSUB440:GOTO267
319 IFIN(M)=8THENGOSUB194:IN(M)=0:GOSUB440:GOTO267
320 GOTO267
321 GOSUB594
322 AP=FA(C)-FD(CE)
323 IFAP<=0THENPRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{white}MISS";:GOSUB748:GOTO335
324 AP=INT(RND(0)*AP)+1
325 FH(CE)=FH(CE)-AP:IFFH(CE)<0THENFH(CE)=0
326 IFCE<4THENPH(CE)=FH(CE)
327 IFFH(CE)>0THENGOTO332
328 IFFT(CE)<>-2THENCC=CC-1:WX=WX+MX(FT(CE))
329 IFFT(CE)=-2THENPP=PP-1
330 FT(CE)=-1
331 GOTO332
332 POKEV,(FX(CE)*8+21)AND255:POKEV+1,FY(CE)*8+50:POKE50168,16:POKEV+28,0
333 POKEV+16,-(FX(CE)>30):POKEV+21,1
334 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{white}";AP
335 FORI=0TO5:POKEV,PEEK(V)+2:POKEV+1,PEEK(V+1)+2:GOSUB749:NEXTI
336 POKEV+21,0:IFCE<4THENGOSUB440
337 GOSUB748
338 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
339 IFFT(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE));"  {down}{left:2}  ";
340 IFCC=0THEN366
341 IFPP=0THEN394
342 GOTO267
343 POKEV,(FX(CE)*8+24)AND255:POKEV+1,FY(CE)*8+50
344 IFM=4THENPOKEV+37,8:POKEV+38,2:POKEV+39,7:POKEV+28,1
345 B=17:IFM=6THENB=19:POKEV+28,0
346 IFM=5THENB=21:POKEV+28,0
347 IFM=7THENB=23:POKEV+28,0
348 POKEV+16,-(FX(CE)>30):POKE50168,B:POKEV+21,1
349 PRINT"{white}";:IFM=9THENPRINT"{green}";
350 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}";AP
351 FORK=0TO3:FORI=0TO1:POKE50168,B+I:FORJ=1TO40:NEXTJ,I,K
352 POKEV+21,0:IFCE<4THENGOSUB440
353 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
354 IFFT(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE));"  {down}{left:2}  ";
355 IFCC=0THEN366
356 IFPP=0THEN394
357 GOTO267
358 GOSUB748
359 PRINT"{white}{home}{down}{right}"M$(FT(C))": GREIFT AN                    "
360 FC(C)=FC(C)-100
361 IFFT(C)=7THENIFFM(C)>=IE(9)THENM=9:GOSUB748:GOSUB403:GOTO623
362 IFFT(C)=9THENIFFM(C)>=IE(6)THENM=6:GOSUB748:GOSUB397:GOTO304
363 IFFT(C)=10THENIFFM(C)>=IE(4)THENM=4:GOSUB748:GOSUB397:GOTO304
364 IFFT(C)=11THENIFFM(C)>=IE(12)THENM=12:GOSUB748:GOSUB397:GOTO630
365 GOSUB748:GOSUB397:GOTO322
366 POKE1020,Q1
367 PRINTDD$;"{brown}{up}{$c1}{$c2:10}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}{white}";
368 PRINT"{home}{down}{right}DU HAST GEWONNEN                     ";:GOSUB578
369 GOSUB509
370 FORI=0TO3:IFP3(I)=0THEN374
371 PRINT"{home}{white}{right:3}";:IFPH(I)=0THENPRINT"{red}";
372 PRINTLEFT$(CD$,4+I*3)P$(I)
373 PRINT"{home}"LEFT$(CD$,5+I*3)"{right:3}LV";PL(I)+1;" EX";PX(I)
374 NEXT
375 FORI=0TO3:IFPH(I)=0ORP3(I)=0THEN385
376 TX=WX
377 PRINT"{home}{white}"LEFT$(CD$,5+I*3)SPC(20)"EXPERIENCE {left}"TX"{left} ";
378 IFTX=0THEN385
379 XS=1:IFTX>9THENXS=10
380 IFTX>99THENXS=100
381 TX=TX-XS
382 PX(I)=PX(I)+XS
383 LL=10+PL(I)*PL(I)*10:IFPX(I)>=LLTHENGOSUB387:GOTO383
384 PRINT"{home}{white}"LEFT$(CD$,5+I*3)"{right:3}LV";PL(I)+1;" EX";PX(I):GOTO377
385 NEXTI
386 GOTO392
387 PL(I)=PL(I)+1:PA(I)=PA(I)+1:PD(I)=PD(I)+1:PS(I)=PS(I)+1
388 P1(I)=P1(I)+PL(I)*2:IFP2(I)>0THENP2(I)=P2(I)+PL(I)*3
389 IFP1(I)>999THENP1(I)=999
390 IFP2(I)>999THENP2(I)=999
391 RETURN
392 GOSUB741
393 GOTO101
394 PRINT"{home}{down}{right}{white}DU HAST VERLOREN                     "
395 GOSUB741
396 GOTO638
397 RE=INT(RND(0)*4):CE=0
398 IFFT(CE)=-2ANDPH(CE)>0THEN401
399 CE=CE+1:IFCE=4THENCE=0
400 GOTO398
401 IFRE>0THENRE=RE-1:GOTO399
402 RETURN
403 RE=INT(RND(0)*8):CE=4
404 IFFT(CE)>=0THEN407
405 CE=CE+1:IFCE=12THENCE=4
406 GOTO404
407 IFRE>0THENRE=RE-1:GOTO405
408 RETURN
409 IFFF>0THENL=FF:GOTO411
410 L=PEEK(CP+164):IFL=0THENRETURN
411 IFL=3THENA=0:LC=INT(RND(0))*2+2:FORI=0TOLC:GOSUB572:NEXT:RETURN
412 IFL=1THENA=1:FORI=0TO2:GOSUB572:NEXT:RETURN
413 IFL=2THENA=1:FORI=0TO1:GOSUB572:NEXT:A=2:FORI=0TO1:GOSUB572:NEXT:RETURN
414 IFL=4THENA=3:GOSUB572:A=1:GOSUB572:GOSUB572:RETURN
415 IFL=5THENA=4:LC=INT(RND(0)*3)+1:FORI=0TOLC:GOSUB572:NEXT:RETURN
416 IFL=6THENONINT(RND(1)*2)+1GOTO426,427
417 IFL=7THENLC=INT(RND(0)*3)+1:A=6:FORI=0TOLC:GOSUB572:NEXT:RETURN
418 IFL=8THENA=7:GOSUB572:LC=INT(RND(0)*2)+1:A=6:GOSUB572:RETURN
419 IFL=9THENA=9:GOSUB572:A=8:GOSUB572:A=5:GOSUB572:RETURN
420 IFL=10THENA=6:LC=INT(RND(0)*2)+1:FORI=1TOLC:GOSUB572:NEXT:RETURN
421 IFL=11THENA=7:GOSUB572:A=10:GOSUB572:RETURN
422 IFL=12ANDINT(RND(1)*2)=1THENA=11:GOSUB572:RETURN
423 IFL=12THENA=5:GOSUB572:GOSUB572:A=9:GOSUB572:RETURN
424 IFL=13THENA=9:GOSUB572:A=12:GOSUB572:A=9:GOSUB572:RETURN
425 IFL=14THENA=9:GOSUB572:A=13:GOSUB572:A=7:GOSUB572:RETURN
426 A=5:LC=INT(RND(1)*2)+1:FORI=0TOLC:GOSUB572:NEXT:RETURN
427 A=9:LC=INT(RND(1)*2)+1:FORI=1TOLC:GOSUB572:NEXT:RETURN
428 FORI=0TO3
429 IFP3(I)=0THEN432
430 PH(I)=P1(I)
431 PM(I)=P2(I)
432 NEXT:RETURN
433 PRINT"{brown}{home}{$c1}{$c2:38}{$c3}";
434 PRINT"{$c4}                                      {$c5}";
435 PRINT"{$c6}{$c7:38}{$c8}";
436 PRINTDD$;"{brown}{up}{$c1}{$c2:10}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
437 FORI=0TO3:PRINT"{$c4}          {$c5}{$c4}            {$c5}{$c4}            {$c5}";:NEXT
438 PRINT"{$c6}{$c7:10}{$c8}{$c6}{$c7:12}{$c8}{$c6}{$c7:12}";
439 POKE50151,72:POKE56295,9:RETURN
440 PRINTDD$;
441 FORI=0TO3:IFP3(I)=0THENPRINTSPC(40);:GOTO447
442 PRINT"{white}";:IFPH(I)=0THENPRINT"{red}";
443 PRINTSPC(27)P$(I)" ";:A=PH(I):GOSUB711
444 PRINTD$;
445 A=PM(I):GOSUB711
446 PRINT" "D$"{right}";
447 NEXT:RETURN
448 FORI=0TO3:IFPH(I)<=0ORP3(I)=0THENNEXT:RETURN
449 PRINT"{home}"LEFT$(CD$,5+I*3);SPC(37);PD$(I):NEXT
450 RETURN
451 GOSUB486
452 MI=-1:M=0:MO=0
453 MI=MO-1:CY=MY:MM=0
454 IFMC=4THENGOSUB481
455 MI=MI+1
456 PRINT"{home}{white}"LEFT$(CD$,CY)SPC(MX)" ";
457 IFIT(IN(MI))<>MTTHENPRINT"{cyan}";
458 IFIT(IN(MI))=9THENPRINT"{white}";
459 IFIT(IN(MI))=-1THENPRINT"{cyan}";
460 PRINTI$(IN(MI));
461 CY=CY+1
462 MM=MM+1
463 IFMM=MCTHEN465
464 IFMI<13THEN455
465 CP=QS+MX+MY*40
466 POKECP+(M-MO)*40,35
467 GOSUB713
468 POKECP+(M-MO)*40,32
469 IFA$="W"THENM=M-1:GOTO473
470 IFA$="S"THENM=M+1:GOTO473
471 IFA$=CHR$(13)THENRETURN
472 GOTO466
473 IFM=-1THENM=14-1:MO=14-MC:GOTO453
474 IFM=14THENM=0:MO=0:GOTO453
475 M%=0
476 IFM<MOTHENMO=MO-MC:M%=1:GOTO476
477 IFM>=MO+MCTHENMO=MO+MC:M%=1
478 IFMO<0THENMO=0:M%=1
479 IFM%=1THEN453
480 GOTO466
481 FORI=0TO3
482 PRINT"{home}{white}"LEFT$(CD$,CY+I)SPC(MX)" ";
483 PRINT"        ";
484 NEXTI
485 RETURN
486 A=0
487 FORI=0TO99:TT(I)=0:NEXTI
488 FORI=0TO99:TT(I)=IN(I):IN(I)=0:NEXTI
489 IFMT=0THENGOSUB494:GOSUB497:A=50:GOSUB500:GOSUB503:GOSUB506
490 IFMT=1THENGOSUB494:GOSUB500:A=50:GOSUB497:GOSUB503:GOSUB506
491 IFMT=2THENGOSUB494:GOSUB503:A=50:GOSUB497:GOSUB500:GOSUB506
492 IFMT=3THENGOSUB494:GOSUB506:A=50:GOSUB497:GOSUB500:GOSUB503
493 RETURN
494 FORI=0TO99:IFIT(TT(I))=9THENIN(A)=TT(I):A=A+1
495 NEXTI
496 RETURN
497 FORI=0TO99:IFIT(TT(I))=0THENIN(A)=TT(I):A=A+1
498 NEXTI
499 RETURN
500 FORI=0TO99:IFIT(TT(I))=1THENIN(A)=TT(I):A=A+1
501 NEXTI
502 RETURN
503 FORI=0TO99:IFIT(TT(I))=2THENIN(A)=TT(I):A=A+1
504 NEXTI
505 RETURN
506 FORI=0TO99:IFIT(TT(I))=3THENIN(A)=TT(I):A=A+1
507 NEXTI
508 RETURN
509 PRINT"{brown}{home}{down:3}{$c1}{$c2:38}{$c3}";
510 FORI=0TO13
511 PRINT"{$c4}                                      {$c5}";:NEXT
512 PRINT"{$c6}{$c7:38}{$c8}";:RETURN
513 PRINT"{home}{down}{right}{white}                                      ";
514 RETURN
515 PRINT"{brown}{home}{down:3}{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
516 FORI=0TO5
517 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
518 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
519 PRINT"{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
520 FORI=0TO5
521 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
522 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
523 DX=3:DY=4:P=0:GOSUB526:DX=23:P=1:GOSUB526
524 DX=3:DY=12:P=2:GOSUB526:DX=23:P=3:GOSUB526
525 RETURN
526 IFP3(P)=0THENRETURN
527 PRINT"{home}{white}";:IFPH(P)=0THENPRINT"{red}";
528 PRINT"{home}"LEFT$(CD$,DY)SPC(DX-1)" "P$(P)"  LV"PL(P)+1
529 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX)" "I$(PW(P));
530 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX)" "I$(PR(P));
531 T1=PA(P)+IA(PW(P))
532 T2=PD(P)+ID(PR(P))
533 IFT1>999THENT1=999
534 IFT2>999THENT2=999
535 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX+9)"ATK"T1;
536 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX+9)"DEF"T2;
537 RETURN
538 IFPEEK(2)=1THENSP=30960+RS*(CR-0)
539 FORI=0TO7
540 PRINT"{home}{down:3}"LEFT$(CD$,I*2);
541 FORJ=0TO19
542 PRINTT$(PEEK(SP))"{up}";
543 SP=SP+1
544 NEXTJ,I
545 RETURN
546 IS=0
547 IFIN(IS)=0THENIN(IS)=NI:NI=-1:RETURN
548 IS=IS+1
549 IFIS=IXTHENRETURN
550 GOTO547
551 F=8-H(0)*8:C=30960+0*RS:POKEC+8+4*20,F+2
552 F=13-H(1)*13:C=30960+2*RS:POKEC+12+1*20,F:POKEC+12+2*20,F:POKEC+11+1*20,F
553 POKE30960+1*RS+6+6*20,14-H(2)*14
554 POKE30960+9*RS+6+4*20,14-H(3)*14
555 POKE30960+3*RS+10+6*20,14-H(4)*14
556 POKE30960+4*RS+14+6*20,14-H(5)*14
557 POKE30960+15*RS+9+4*20,14-H(8)*14
558 POKE30960+9*RS+1+2*20,14-H(9)*14
559 POKE30960+16*RS+8+2*20,14-H(10)*14
560 POKE30960+14*RS+3+5*20,16-H(6)*16
561 POKE30960+14*RS+8+2*20,11-H(7)*11
562 POKE30960+12*RS+15+2*20,6-H(12)*6
563 POKE30960+12*RS+15+6*20,6-H(13)*6
564 T=18:IFH(12)+H(13)=2THENT=0
565 POKE30960+12*RS+8+0,T
566 RETURN
567 IFCR=4ANDZX=6ANDZY=4ANDH(0)=0THENPRINT"{home}{white}{down}{right}DER SCHALTER IST AUS!";:GOSUB750:GOSUB513
568 IFCR=4ANDZX=6ANDZY=4ANDH(0)=1THENPRINT"{home}{white}{down}{right}DER SCHALTER IST EIN!";:GOSUB750:GOSUB513
569 IFCR=4ANDZX=14ANDZY=4ANDH(1)=0THENPRINT"{home}{white}{down}{right}DER SCHALTER IST AUS!";:GOSUB750:GOSUB513
570 IFCR=4ANDZX=14ANDZY=4ANDH(1)=1THENPRINT"{home}{white}{down}{right}DER SCHALTER IST EIN!";:GOSUB750:GOSUB513
571 RETURN
572 IFCC>=11THENRETURN
573 FH(CC)=MH(A):FM(CC)=MM(A):FT(CC)=A:FC(CC)=INT(RND(0)*MS(A)):FS(CC)=MS(A)
574 FX(CC)=2:FY(CC)=5+CC*3:FA(CC)=MA(A):FD(CC)=MD(A):FB(CC)=MB(A)
575 F1(CC)=MH(A):F2(CC)=MM(A)
576 IFCC>=4THENFX(CC)=5:FY(CC)=5+(CC-4)*3
577 CC=CC+1:RETURN
578 ZM=0
579 FORJ=4TO7
580 IFFT(J)=-1THENZM=ZM-1
581 ZM=ZM+1
582 NEXTJ
583 IFZM<>CMTHENPRINTDD$;"{right}          {down}{left:10}          {down}{left:10}          {down}{left:10}          ";
584 PRINTDD$;
585 CM=0
586 FORI=4TO7
587 IFFT(I)=-1THENNEXT:RETURN
588 A=FH(I):GOSUB711
589 PRINT"{home}{right}"LEFT$(CD$,20+CM)M$(FT(I));
590 PRINT"{home}{right}"LEFT$(CD$,20+CM);"{right:7}";D$
591 CM=CM+1
592 NEXT
593 RETURN
594 CE=4:PRINT"{home}{down}{right}{white}"P$(C)": WAHELE DEIN ZIEL               "
595 IFFT(CE)=-1THEN604
596 POKEQS-1+FX(CE)+FY(CE)*40,35
597 GOSUB713
598 IFA$=CHR$(13)THENPOKEQS-1+FX(CE)+FY(CE)*40,32:RETURN
599 IFA$="S"THENGOTO604
600 IFA$="W"THENGOTO612
601 IFA$="D"ANDCE>3THENPOKEQS-1+FX(CE)+FY(CE)*40,32:CE=0:GOTO595
602 IFA$="A"ANDCE<4THENPOKEQS-1+FX(CE)+FY(CE)*40,32:CE=4:GOTO595
603 GOTO597
604 CO=CE
605 POKEQS-1+FX(CE)+FY(CE)*40,32
606 CE=CE+1
607 IFCE>=12THENCE=0
608 IFCO<4ANDCE>3THENCE=CO
609 IFCO>3ANDCE<4THENCE=CO
610 IFFT(CE)<>-1THEN596
611 GOTO606
612 CO=CE
613 POKEQS-1+FX(CE)+FY(CE)*40,32
614 CE=CE-1
615 IFCE<0THENCE=11
616 IFCO<4ANDCE>3THENCE=CO
617 IFCO>3ANDCE<4THENCE=CO
618 IFFT(CE)<>-1THEN596
619 GOTO614
620 IFFT(C)=-2THEN636
621 IFFT(C)=7ANDFM(C)>=IE(9)THENM=9:GOTO623
622 GOTO304
623 AP=IE(M)*4+2-INT(RND(0)*5):FM(C)=FM(C)-IE(M):IFC<4THENPM(C)=FM(C):GOSUB440
624 FH(CE)=FH(CE)+AP:IFFH(CE)>F1(CE)THENFH(CE)=F1(CE)
625 IFCE<4THENPH(CE)=FH(CE):GOSUB440
626 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{green}";AP
627 GOSUB755
628 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
629 GOTO267
630 FM(C)=FM(C)-IE(M):IFC<4THENPM(C)=FM(C):GOSUB440
631 FS(CE)=FS(CE)/2:IFFS(CE)<5THENFS(CE)=5
632 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{green}SLOW";
633 GOSUB755
634 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
635 GOTO267
636 IFM=9THEN623
637 GOTO304
638 FORI=0TOHC-1:H(I)=0:NEXT
639 FORI=0TO3:PX(I)=0:NEXT
640 PA(0)=5:PD(0)=2:PA(1)=4:PD(1)=3:PA(2)=1:PD(2)=1:PA(3)=1:PD(3)=1:GOSUB551
641 FORI=0TO13:IN(I)=0:NEXT
642 IN(0)=18
643 ER(1)=0:EX(1)=16:EY(1)=5:ET(1)=3
644 ER(2)=0:EX(2)=3:EY(2)=5:ET(2)=15
645 ER(3)=99:EX(3)=0:EY(3)=0:ET(3)=4
646 ER(4)=99:EX(4)=0:EY(4)=0:ET(4)=8
647 ER(5)=99:EX(5)=0:EY(5)=0:ET(5)=5
648 ER(6)=99:EX(6)=0:EY(6)=0:ET(6)=16
649 ER(7)=99:EX(7)=0:EY(7)=0:ET(7)=17
650 ER(8)=99:EX(8)=0:EY(8)=0:ET(8)=13
651 ER(9)=99:EX(9)=0:EY(9)=0:ET(9)=15
652 ER(10)=99:EX(10)=0:EY(10)=0:ET(10)=11
653 ER(11)=99:EX(11)=0:EY(11)=0:ET(11)=7
654 ER(12)=99:EX(12)=0:EY(12)=0:ET(12)=9
655 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
656 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
657 PRINT"{$c6}{$c7:38}{up:3}";
658 POKE50151,72:POKE56295,9
659 X=13:Y=4:CR=0
660 P3(0)=1:P3(1)=0:P3(2)=0:P3(3)=0
661 P1(0)=15:P1(1)=17:P1(2)=9:P1(3)=9:P2(0)=0:P2(1)=0:P2(2)=7:P2(3)=23
662 FORI=0TO3:PH(I)=P1(I):PM(I)=P2(I):PX(I)=0:PW(I)=0:PR(I)=0:NEXT
663 PW(0)=1:PR(0)=2
664 GOSUB551
665 AA$="INTRO":GOSUB699
666 GOTO96
667 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
668 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
669 PRINT"{$c6}{$c7:38}{up:3}";
670 POKE50151,72:POKE56295,9
671 AA$="ENDE":GOSUB699
672 GOTO96
673 OPEN1,8,4,"PFSAVE":OPEN2,8,15:INPUT#2,ER,M$,T,SS
674 IFER<>0THENPRINT"{home}{white}{down}{right}ERROR LOADING    ";:CLOSE2:CLOSE1:GOTO638
675 FORI=0TO3:INPUT#1,PH(I):INPUT#1,PM(I):INPUT#1,PS(I):INPUT#1,P1(I)
676 INPUT#1,P2(I):INPUT#1,P3(I)
677 INPUT#1,PA(I):INPUT#1,PD(I):INPUT#1,PW(I):INPUT#1,PR(I):INPUT#1,PX(I)
678 INPUT#1,PL(I):NEXT
679 FORI=0TO13:INPUT#1,IN(I):NEXT:FORI=0TOHC-1:INPUT#1,H(I):NEXT
680 FORI=0TOEC-1:INPUT#1,ER(I):NEXT
681 INPUT#1,X:INPUT#1,Y:INPUT#1,CR
682 CLOSE1:CLOSE2:GOSUB551:RETURN
683 OPEN15,8,15,"S:PFSAVE":CLOSE15
684 OPEN1,8,4,"PFSAVE,W"
685 FORI=0TO3:PRINT#1,PH(I):PRINT#1,PM(I):PRINT#1,PS(I):PRINT#1,P1(I)
686 PRINT#1,P2(I):PRINT#1,P3(I)
687 PRINT#1,PA(I):PRINT#1,PD(I):PRINT#1,PW(I):PRINT#1,PR(I):PRINT#1,PX(I)
688 PRINT#1,PL(I)
689 NEXT
690 FORI=0TO13:PRINT#1,IN(I):NEXT:FORI=0TOHC-1:PRINT#1,H(I):NEXT
691 FORI=0TOEC-1:PRINT#1,ER(I):NEXT
692 PRINT#1,X:PRINT#1,Y:PRINT#1,CR
693 CLOSE1:RETURN
694 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
695 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
696 PRINT"{$c6}{$c7:38}{up:3}";
697 POKE50151,72:POKE56295,9
698 RETURN
699 POKE1020,Q7
700 POKE56322,224
701 IFAA$="INTRO"THENOPEN1,8,4,"TXT.INTRO,S,R"
702 IFAA$="ENDE"THENOPEN1,8,4,"TXT.ENDE,S,R"
703 IFAA$=""THENRETURN
704 GET#1,B$
705 IFB$="$"THENPRINT"{home}{down:2}{right:2}{white}";:GOTO704
706 IFB$="%"THENPRINT"{right:2}{white}";:GOTO704
707 IFB$="#"THENCLOSE1:POKE56322,255:GOSUB741:RETURN
708 PRINTB$;
709 IFB$<>" "THENPOKE162,0:WAIT162,4
710 GOTO704
711 T$=STR$(A)
712 D$=RIGHT$("000"+RIGHT$(T$,LEN(T$)-1),3):RETURN
713 A$=""
714 J=PEEK(56320)
715 IFJ=127THEN717
716 GOTO714
717 J=PEEK(56320)
718 IFJ=127ANDA$=CHR$(13)THENRETURN
719 IFJ=107THENA$="E":GOSUB746:RETURN
720 IFJ=103THENA$="E":GOSUB746:RETURN
721 IFJ=110THENA$="E":GOSUB746:RETURN
722 IFJ=109THENA$="E":GOSUB746:RETURN
723 IFJ=123THENA$="A":GOSUB746:RETURN
724 IFJ=119THENA$="D":GOSUB746:RETURN
725 IFJ=126THENA$="W":GOSUB746:RETURN
726 IFJ=125THENA$="S":GOSUB746:RETURN
727 IFJ=111THENA$=CHR$(13)
728 GOTO717
729 A$=""
730 J=PEEK(56320)
731 IFJ<>111ANDJ<>107ANDJ<>103ANDJ<>110ANDJ<>109THEN733
732 GOTO730
733 J=PEEK(56320)
734 IFJ=127ANDA$=CHR$(13)THENRETURN
735 IFJ=123THENA$="A":GOSUB747:RETURN
736 IFJ=119THENA$="D":GOSUB747:RETURN
737 IFJ=126THENA$="W":GOSUB747:RETURN
738 IFJ=125THENA$="S":GOSUB747:RETURN
739 IFJ=111THENA$=CHR$(13)
740 GOTO733
741 A$=""
742 J=PEEK(56320)
743 IFJ=127ANDA$=CHR$(13)THENRETURN
744 IFJ=111THENA$=CHR$(13)
745 GOTO742
746 POKE162,0:WAIT162,16:RETURN
747 POKE162,0:WAIT162,8:RETURN
748 POKE162,0:WAIT162,32:RETURN
749 POKE162,0:WAIT162,2:RETURN
750 POKE162,0:WAIT162,32
751 POKE162,0:WAIT162,32
752 POKE162,0:WAIT162,32
753 POKE162,0:WAIT162,32
754 RETURN
755 POKE162,0:WAIT162,32
756 RETURN
757 POKE162,0:WAIT162,4
758 RETURN
759 DATA"IMP"    ,5   ,0  ,8  ,4  ,1  ,0 ,3
760 DATA"SNAKE"  ,8   ,0  ,12 ,6  ,1  ,0 ,5
761 DATA"VIPER"  ,12  ,0  ,18 ,6  ,1  ,0 ,9
762 DATA"NACHO"  ,30  ,0  ,22 ,15 ,5  ,0 ,50
763 DATA"GOLEM"  ,20  ,0  ,4  ,20 ,8  ,0 ,17
764 DATA"MOSS"   ,30  ,0  ,7  ,26 ,9  ,0 ,21
765 DATA"BONES"  ,50  ,0  ,20 ,18 ,8  ,0 ,15
766 DATA"WISP"   ,25  ,50 ,15 ,0  ,8  ,0 ,17
767 DATA"TROLL"  ,100 ,0  ,9  ,40 ,13 ,0 ,100
768 DATA"SPARK"  ,30  ,50 ,22 ,0  ,8  ,0 ,17
769 DATA"BURNER" ,80  ,90 ,30 ,25 ,13 ,0 ,42
770 DATA"TBERRY" ,120 ,10 ,60 ,60 ,20 ,0 ,150
771 DATA"DRACUL" ,250 ,60 ,50 ,70 ,25 ,0 ,500
772 DATA"GLADOS" ,500 ,70 ,60 ,80 ,30 ,0 ,1000
773 DATA"KRON" ,15 ,0  ,10 ,4 ,3 ,0 ,0
774 DATA"LENA" ,17 ,0  ,10 ,3 ,3 ,0 ,0
775 DATA"DOLM" ,9  ,7  ,8  ,1 ,1 ,0 ,0
776 DATA"MIRA" ,9  ,23 ,7  ,1 ,1 ,1 ,2
777 DATA"{gray1}{rvrs off}{$c9}{rvrs off}{gray1}{$ca}{down}{left:2}{rvrs off}{gray1}{$ca}{rvrs off}{gray1}{$c9}{rvrs off}"
778 DATA"{gray1}{rvrs off}{$cb}{rvrs off}{gray1}{$cc}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1}{$ca}{rvrs off}"
779 DATA"{gray1}{rvrs off}{$cd}{rvrs off}{gray1}{$ce}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1}{$ca}{rvrs off}"
780 DATA"{orange}{rvrs off}{$cf}{rvrs off}{orange}{$cf}{down}{left:2}{rvrs off}{orange}{$cf}{rvrs off}{orange}{$cf}{rvrs off}"
781 DATA"{gray1}{rvrs off}{$c9}{rvrs off}{gray1}{$ca}{down}{left:2}{rvrs off}{gray1}{$ca}{rvrs off}{gray1}{$c9}{rvrs off}"
782 DATA"{gray1}{rvrs off}{$d0}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$d2}{rvrs off}{gray1}{$d3}{rvrs off}"
783 DATA"{lt. green}{rvrs off}{$d4}{rvrs off}{lt. green}{$cf}{down}{left:2}{rvrs off}{lt. green}{$cf}{rvrs off}{lt. green}{$d5}{rvrs off}"
784 DATA"{gray1}{rvrs off}{$d6}{rvrs off}{gray1}{$d7}{down}{left:2}{rvrs off}{gray1}{$d8}{rvrs off}{gray1}{$d9}{rvrs off}"
785 DATA"{brown}{rvrs off}{$da}{rvrs off}{brown}{$db}{down}{left:2}{rvrs off}{brown}{$dc}{rvrs off}{brown}{$dd}{rvrs off}"
786 DATA"{lt. red}{rvrs off}{$de}{rvrs off}{lt. red}{$de}{down}{left:2}{rvrs off}{lt. red}{$de}{rvrs off}{lt. red}{$de}{rvrs off}"
787 DATA"{lt. red}{rvrs off}{$df}{rvrs off}{lt. red}{shft space}{down}{left:2}{rvrs off}{lt. red}{$a2}{rvrs off}{lt. red}{$a3}{rvrs off}"
788 DATA"{lt. red}{rvrs off}{$a4}{rvrs off}{lt. red}{$a5}{down}{left:2}{rvrs off}{lt. red}{$a6}{rvrs off}{lt. red}{$a7}{rvrs off}"
789 DATA"{lt. green}{rvrs off}{$d4}{rvrs off}{lt. green}{$cf}{down}{left:2}{rvrs off}{lt. green}{$cf}{rvrs off}{lt. green}{$d5}{rvrs off}"
790 DATA"{brown}{rvrs off}{cmdr pound}{rvrs off}{brown}{cmdr pound}{down}{left:2}{rvrs off}{brown}{cmdr pound}{rvrs off}{brown}{cmdr pound}{rvrs off}"
791 DATA"{brown}{rvrs off}{shft pound}{rvrs off}{brown}{shft pound}{down}{left:2}{rvrs off}{brown}{shft pound}{rvrs off}{brown}{shft pound}{rvrs off}"
792 DATA"{gray2}{rvrs off}{$aa}{rvrs off}{gray2}{$ab}{down}{left:2}{rvrs off}{gray2}{$ac}{rvrs off}{gray2}{$ad}{rvrs off}"
793 DATA"{gray1}{rvrs off}{$ae}{rvrs off}{gray1}{$af}{down}{left:2}{rvrs off}{gray1}{$b0}{rvrs off}{gray1}{$b1}{rvrs off}"
794 DATA"{lt. green}{rvrs off}{$b2}{rvrs off}{lt. green}{$b3}{down}{left:2}{rvrs off}{lt. green}{$b4}{rvrs off}{lt. green}{$b5}{rvrs off}"
795 DATA"{lt. green}{rvrs off}{$b6}{rvrs off}{lt. green}{$b7}{down}{left:2}{rvrs off}{lt. green}{$b6}{rvrs off}{lt. green}{$b7}{rvrs off}"
796 DATA"{lt. green}{rvrs off}{$b8}{rvrs off}{lt. green}{$b9}{down}{left:2}{rvrs off}{lt. green}{$ba}{rvrs off}{lt. green}{$bb}{rvrs off}"
797 DATA"{brown}{rvrs off}{$b2}{rvrs off}{brown}{$b3}{down}{left:2}{rvrs off}{brown}{$b4}{rvrs off}{brown}{$b5}{rvrs off}"
798 DATA"{brown}{rvrs off}{$b6}{rvrs off}{brown}{$b7}{down}{left:2}{rvrs off}{brown}{$b6}{rvrs off}{brown}{$b7}{rvrs off}"
799 DATA"{brown}{rvrs off}{$b8}{rvrs off}{brown}{$b9}{down}{left:2}{rvrs off}{brown}{$ba}{rvrs off}{brown}{$bb}{rvrs off}"
800 DATA"{brown}{rvrs off}{$d4}{rvrs off}{brown}{$cf}{down}{left:2}{rvrs off}{brown}{$cf}{rvrs off}{brown}{$d5}{rvrs off}"
801 DATA"{brown}{rvrs off}{$bc}{rvrs off}{brown}{$bc}{down}{left:2}{rvrs off}{brown}{$bd}{rvrs off}{brown}{$bd}{rvrs off}"
802 DATA"{gray1}{rvrs off}{$be}{rvrs off}{gray1}{$bf}{down}{left:2}{rvrs on}{gray1}@{rvrs on}{gray1}A{rvrs off}"
803 DATA"{brown}{rvrs off}{$bc}{rvrs on}{brown}B{down}{left:2}{rvrs off}{brown}{$bd}{rvrs off}{brown}{$bd}{rvrs off}"
804 DATA"{brown}{rvrs on}B{rvrs off}{brown}{$bc}{down}{left:2}{rvrs off}{brown}{$bd}{rvrs off}{brown}{$bd}{rvrs off}"
805 DATA"{gray1}{rvrs off}{$cf}{rvrs off}{gray1}{$cf}{down}{left:2}{rvrs off}{gray1}{$cf}{rvrs off}{gray1}{$cf}{rvrs off}"
806 DATA"{gray1}{rvrs on}C{rvrs off}{gray1}{$cf}{down}{left:2}{rvrs on}{gray1}C{rvrs off}{gray1}{$cf}{rvrs off}"
807 DATA"{gray1}{rvrs on}C{rvrs off}{gray1}{$cf}{down}{left:2}{rvrs on}{gray1}D{rvrs off}{gray1}{$cf}{rvrs off}"
808 DATA"{gray1}{rvrs on}E{rvrs on}{gray1}F{down}{left:2}{rvrs on}{gray1}G{rvrs on}{gray1}G{rvrs off}"
809 DATA"{gray1}{rvrs on}H{rvrs on}{gray1}H{down}{left:2}{rvrs on}{gray1}I{rvrs on}{gray1}I{rvrs off}"
810 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
811 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
812 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
813 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
814 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
815 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
816 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
817 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
818 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
819 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
820 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
821 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
822 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
823 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
824 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
825 DATA"{orange}{rvrs on}J{rvrs on}{orange}K{down}{left:2}{rvrs on}{orange}L{rvrs on}{orange}M{rvrs off}"
826 DATA"{gray1}{rvrs on}N{rvrs on}{gray1}K{down}{left:2}{rvrs on}{gray1}L{rvrs on}{gray1}M{rvrs off}"
827 DATA"{lt. green}{rvrs on}O{rvrs on}{lt. green}P{down}{left:2}{rvrs on}{lt. green}Q{rvrs on}{lt. green}R{rvrs off}"
828 DATA"{gray2}{rvrs on}S{rvrs on}{gray2}T{down}{left:2}{rvrs on}{gray2}U{rvrs on}{gray2}V{rvrs off}"
829 DATA"{gray1}{rvrs on}W{rvrs on}{gray1}X{down}{left:2}{rvrs on}{gray1}Y{rvrs on}{gray1}Z{rvrs off}"
830 DATA"{gray1}{rvrs on}[{rvrs on}{gray1}{pound}{down}{left:2}{rvrs on}{gray1}]{rvrs on}{gray1}{^}{rvrs off}"
831 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
832 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
833 DATA"{orange}{rvrs off} {rvrs off}{orange} {down}{left:2}{rvrs off}{orange} {rvrs off}{orange} {rvrs off}"
834 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
835 DATA"{lt. green}{rvrs off} {rvrs off}{lt. green} {down}{left:2}{rvrs off}{lt. green} {rvrs off}{lt. green} {rvrs off}"
836 DATA"{gray2}{rvrs off} {rvrs off}{gray2} {down}{left:2}{rvrs off}{gray2} {rvrs off}{gray2} {rvrs off}"
837 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
838 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
839 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
840 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
841 DATA"{lt. green}{rvrs on}{back arrow}{rvrs on}{lt. green} {down}{left:2}{rvrs on}{lt. green}"$"
842 DATA"{gray3}{rvrs on}%{rvrs on}{gray3}&{down}{left:2}{rvrs on}{gray3}'{rvrs on}{gray3}({rvrs off}"
843 DATA"{gray1}{rvrs on}%{rvrs on}{gray1}&{down}{left:2}{rvrs on}{gray1}'{rvrs on}{gray1}({rvrs off}"
844 DATA"{lt. green}{rvrs on}){rvrs on}{lt. green}*{down}{left:2}{rvrs on}{lt. green}+{rvrs on}{lt. green},{rvrs off}"
845 DATA"{gray1}{rvrs on}-{rvrs on}{gray1}.{down}{left:2}{rvrs on}{gray1}/{rvrs on}{gray1}0{rvrs off}"
846 DATA"{gray1}{rvrs on}1{rvrs on}{gray1}2{down}{left:2}{rvrs on}{gray1}3{rvrs on}{gray1}4{rvrs off}"
847 DATA"{gray1}{rvrs on}5{rvrs on}{gray1}6{down}{left:2}{rvrs on}{gray1}7{rvrs on}{gray1}8{rvrs off}"
848 DATA"{gray1}{rvrs on}9{rvrs on}{gray1}:{down}{left:2}{rvrs on}{gray1};{rvrs on}{gray1}<{rvrs off}"
849 DATA"{lt. green}{rvrs on}={rvrs on}{lt. green}>{down}{left:2}{rvrs on}{lt. green}?{rvrs on}{lt. green}{$c0}{rvrs off}"
850 DATA"{gray3}{rvrs on}9{rvrs on}{gray3}:{down}{left:2}{rvrs on}{gray3};{rvrs on}{gray3}<{rvrs off}"
851 DATA"{gray1}{rvrs on}5{rvrs on}{gray1}6{down}{left:2}{rvrs on}{gray1}7{rvrs on}{gray1}8{rvrs off}"
852 DATA"{lt. green}{rvrs on}5{rvrs on}{lt. green}6{down}{left:2}{rvrs on}{lt. green}7{rvrs on}{lt. green}8{rvrs off}"
853 DATA"{gray1}{rvrs on}{$c1}{rvrs on}{gray1}{$c2}{down}{left:2}{rvrs on}{gray1}{$c3}{rvrs on}{gray1}{$c4}{rvrs off}"
854 DATA"{gray1}{rvrs on}{$c5}{rvrs on}{gray1}{$c6}{down}{left:2}{rvrs on}{gray1}{$c7}{rvrs on}{gray1}{$c8}{rvrs off}"
855 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
856 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
857 DATA"        ",0    ,0    ,0    ,0    ,-1 
858 DATA"DOLCH   ",3    ,0    ,0    ,0    ,0  
859 DATA"HELM    ",0    ,2    ,-1   ,0    ,1  
860 DATA"KRAEUTER",0    ,0    ,-1   ,10   ,2  
861 DATA"FEUER   ",0    ,0    ,0    ,5    ,3  
862 DATA"EIS     ",0    ,0    ,0    ,8    ,3  
863 DATA"MUT     ",0    ,0    ,0    ,23   ,3  
864 DATA"BOMBE   ",0    ,0    ,0    ,50   ,3  
865 DATA"KIRSCHE ",0    ,0    ,-1   ,15   ,2  
866 DATA"HEILEN  ",0    ,0    ,0    ,4    ,3  
867 DATA"SKULL   ",0    ,0    ,0    ,0    ,4  
868 DATA"FLEGEL  ",7    ,0    ,-1   ,0    ,0  
869 DATA"LAHM    ",0    ,0    ,0    ,4    ,3  
870 DATA"STAB    ",12   ,0    ,0    ,0    ,0  
871 DATA"FEDER   ",0    ,10   ,0    ,0    ,1  
872 DATA"STOCK   ",23   ,0    ,0    ,0    ,0  
873 DATA"WESTE   ",0    ,14   ,0    ,0    ,1  
874 DATA"SCHILD  ",0    ,24   ,0    ,0    ,1  
875 DATA"ZURUECK ",0    ,0    ,0    ,0    ,9  
876 PRINT"{white}"+CHR$(147)
877 Z=-1
878 FORA=0TO255
879 B=0
880 Z=Z+1
881 IFZ=16THENPRINT:Z=0
882 IFA>=0ANDA<=31THENC=A+64:PRINTCHR$(C);
883 IFA>=32ANDA<=63THENC=A+00:PRINTCHR$(C);
884 IFA>=64ANDA<=95THENC=A+32:PRINT"{gray1}"+CHR$(C);
885 IFA>=96ANDA<=127THENC=A+64:PRINT"{gray1}"+CHR$(C);
886 IFA=162THENPRINT" ";:GOTO891
887 IFA>=128ANDA<=159THENC=A-64:PRINT"{gray1}"+CHR$(18)+CHR$(C);
888 IFA>=160ANDA<=191THENC=A-128:PRINT"{gray1}"+CHR$(18)+CHR$(C);
889 IFA>=192ANDA<=254THENC=A:PRINT"{gray1}"+CHR$(18)+CHR$(C);
890 IFA=255THENC=A-64:PRINT"{gray1}"+CHR$(18)+CHR$(C);
891 NEXTA
892 GOSUB741
893 POKE1020,Q7
894 POKE1021,Q1
895 SYS820
896 PRINT"{white}"+CHR$(147)
897 FORI=0TO4
898 PRINT"{home}{down:3}"LEFT$(CD$,I*2);
899 FORJ=0TO15
900 PRINTT$(SP)"{up}";
901 SP=SP+1
902 NEXTJ,I
903 PRINTCHR$(5)
904 STOP
905 END
