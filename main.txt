0 POKE55,240:POKE56,120:CLR
1 QO=53282
2 QP=53283
3 QQ=53281
4 QR=53280
5 QS=49152
6 V=53248
7 Q1%=0
8 Q7%=6
9 QA%=9
10 RS=165
11 DD$="{home}{down:20}{white}"
12 CD$="{down:25}"
13 DIMM$(13),MD$(13),MH%(13),MM%(13),MA%(13),MD%(13),MS%(13),MB%(13),MX%(13)
14 DIMFH%(11),FT%(11),FC%(11),FM%(11),FS%(11),FX%(11),FY%(11),FA%(11),FD(11),F1%(11),F2%(11)
15 FORI=0TO3:FX%(I)=37:FY%(I)=5+3*I:NEXT
16 DIMI$(18),IA%(18),ID%(18),IS%(18),IE%(18),IT%(18)
17 DIMER%(12),EX%(12),EY%(12),ET%(12)
18 DIMIN%(99),TT%(99)
19 DIMT$(79)
20 DIMS0%(10),S1%(10),S2%(10),S3%(10),A0%(10),A1%(10),A2%(10)
21 DIMN0%(10),N1%(10),N2%(8),N3%(10)
22 FORI=0TO13
23 READM$(I),MH%(I),MM%(I),MS%(I)
24 READMA%(I),MD%(I),MB%(I),MX%(I)
25 NEXT
26 FORI=0TO3
27 READP$(I),PH%(I),PM%(I),PS%(I)
28 READPA%(I),PD%(I),PW%(I),PR%(I)
29 NEXT
30 FORI=0TO79
31 READT$(I)
32 NEXT
33 PD$(0)=T$(48)
34 PD$(1)=T$(50)
35 PD$(2)=T$(52)
36 PD$(3)=T$(51)
37 ZZ=64
38 FORI=0TO13
39 MD$(I)=T$(ZZ)
40 ZZ=ZZ+1
41 NEXTI
42 FORI=0TO18
43 READI$(I),IA%(I),ID%(I),IS%(I),IE%(I),IT%(I)
44 NEXT
45 POKEV+21,0
46 POKE648,192
47 POKE56576,0
48 POKE53272,8
49 POKE53270,PEEK(53270)OR16
50 POKEQO,QA%
51 POKEQP,Q1%
52 POKEQR,Q1%
53 POKEQQ,Q7%
54 POKE1020,Q7%
55 POKE1021,Q7%
56 SYS820
57 GOSUB148
58 GOTO621
59 GOSUB643
60 GOSUB513
61 GOSUB652
62 GOSUB444
63 GOSUB692
64 PRINT"{home}{down}{right}{white}                                      ";
65 POKE1020,Q7%
66 GOSUB542
67 IFPEEK(2)=1THENCP=30960+RS*(CR-0)
68 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)PD$(0);
69 OX=ZX:OY=ZY
70 ZX=X:ZY=Y
71 GOSUB732
72 IFA$=CHR$(145)ORA$="W"THENZY=Y-1:GOTO78
73 IFA$=CHR$(157)ORA$="A"THENZX=X-1:GOTO78
74 IFA$=CHR$(29)ORA$="D"THENZX=X+1:GOTO78
75 IFA$=CHR$(17)ORA$="S"THENZY=Y+1:GOTO78
76 IFA$=CHR$(13)THEN181
77 GOTO71
78 AA%=0
79 FORI=0TO12
80 IFER%(I)=CRANDEX%(I)=ZXANDEY%(I)=ZYTHENAA%=1
81 NEXTI
82 IFZX=-1ORZX=20ORZY=-1ORZY=8THEN94
83 C=PEEK(CP+ZX+ZY*20)
84 IFC=08THEN108
85 IFC=09THEN108
86 IFC=10THEN108
87 IFC=11THEN108
88 IFC=12THEN115
89 IFC=14THEN110
90 IFC=25THEN115
91 IFC=40THEN128
92 IFC>=48THEN119
93 IFC>7THEN69
94 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)T$(PEEK(CP+X+Y*20))
95 IFRND(1)*100>97ANDPEEK(CP+164)>0THENFF=0:GOTO259
96 X=ZX:Y=ZY
97 IFX=-1ANDPEEK(CP+160)=255THENX=0:GOTO68
98 IFX=-1THENCR=PEEK(CP+160):X=19:GOTO65
99 IFX=20ANDPEEK(CP+161)=255THENX=19:GOTO68
100 IFX=20THENCR=PEEK(CP+161):X=0:GOTO65
101 IFY=-1ANDPEEK(CP+162)=255THENY=0:GOTO68
102 IFY=-1THENCR=PEEK(CP+162):Y=7:GOTO65
103 IFY=8ANDPEEK(CP+163)=255THENY=7:GOTO68
104 IFY=8THENCR=PEEK(CP+163):Y=0:GOTO65
105 IFC=2THEN110
106 IFC=3THEN110
107 GOTO68
108 IFCR=16THENCR=16:X=0:Y=0:GOTO64
109 IFCR=16THEN637
110 IFC=14ANDCR=S0%(0)ANDZX=S1%(0)THENS3%(0)=1-S3%(0):GOSUB132:TX=A1%(0):TY=A2%(0):GOSUB129:GOSUB145:GOTO69
111 IFC=2ANDCR=S0%(1)ANDX=S1%(1)THENS3%(1)=1:GOSUB132:TX=A1%(1):TY=A2%(1):GOSUB129:GOTO68
112 IFC=2ANDCR=S0%(2)ANDX=S1%(2)THENS3%(2)=1:GOSUB132:TX=A1%(2):TY=A2%(2):GOSUB129:GOTO68
113 IFC=3ANDCR=S0%(3)ANDX=S1%(3)THENS3%(3)=1:GOSUB132:TX=A1%(3):TY=A2%(3):GOSUB129:GOTO68
114 IFC=3ANDCR=S0%(4)ANDX=S1%(4)THENS3%(4)=1:GOSUB132:TX=A1%(4):TY=A2%(4):GOSUB129:GOTO68
115 IFC=12ANDAA%=0THENGOTO69
116 IFC=12ANDAA%=1THENVA$="{white}= {cyan}IN DER TRUHE IST WAS":GOSUB686:GOTO69
117 IFC=25ANDAA%=0THENGOTO69
118 IFC=25ANDAA%=1THENVA$="{white}= {cyan}DER BAUM SIEHT INTRESSANT AUS":GOSUB686:GOTO69
119 IFC=49THENPRINT"{white}{home}{down}{right}EIN HEILZAUBER IST UNTER DEM BAUM":GOSUB753:GOSUB517:GOTO69
120 IFC=53THENPRINT"{white}{home}{down}{right}EIN FEUERZAUBER IST IN DEN KATAKOMBEN":GOSUB753:GOSUB517:GOTO69
121 IFC=50THENAA$="TXT.WELCOME.LENA":GOSUB661:N3%(0)=1:GOSUB132:P3%(1)=1:TX=12:TY=4:GOSUB129:GOSUB444:GOTO69
122 IFC=52THENAA$="TXT.WELCOME.DOLM":GOSUB661:N3%(1)=1:GOSUB132:P3%(2)=1:TX=7:TY=2:GOSUB129:GOSUB444:GOTO69
123 IFC=51THENAA$="TXT.WELCOME.MIRA":GOSUB661:N3%(2)=1:GOSUB132:P3%(3)=1:TX=3:TY=2:GOSUB129:GOSUB444:GOTO69
124 IFC=67THENPRINT"{white}{home}{down}{right}DU KANNST HIER NICHT DURCH!":GOSUB753:FF=4:N3%(3)=1:GOSUB132:GOTO259
125 IFC=72THENPRINT"{white}{home}{down}{right}ACH WIE SUESS!":GOSUB753:FF=9:N3%(4)=1:GOSUB132:GOTO259
126 IFC=76THENPRINT"{white}{home}{down}{right}DU WILLST MICH BESIEGEN!":GOSUB753:FF=13:N3%(5)=1:GOSUB132:GOTO259
127 IFC=77THENPRINT"{white}{home}{down}{right}DU HAST MEINE ERWARTUNG UEBERTROFFEN!":GOSUB753:FF=14:N3%(6)=1:GOSUB132:GOTO259
128 IFC=40THENVA$="{white}= {cyan}DU BIST GEHEILT":GOSUB439:GOSUB444:GOSUB686:GOTO69
129 SP=30960+(CR*RS)+(TX+TY*20)
130 PRINT"{home}{down:3}"LEFT$(CD$,TY*2)SPC(TX*2)T$(PEEK(SP));
131 RETURN
132 POKE30960+N0%(0)*RS+N1%(0)+N2%(0)*20,50-N3%(0)*50
133 POKE30960+N0%(1)*RS+N1%(1)+N2%(1)*20,52-N3%(1)*52
134 POKE30960+N0%(2)*RS+N1%(2)+N2%(2)*20,51-N3%(2)*51
135 POKE30960+N0%(3)*RS+N1%(3)+N2%(3)*20,67-N3%(3)*67
136 POKE30960+N0%(4)*RS+N1%(4)+N2%(4)*20,72-N3%(4)*72
137 POKE30960+N0%(5)*RS+N1%(5)+N2%(5)*20,76-N3%(5)*76
138 POKE30960+N0%(6)*RS+N1%(6)+N2%(6)*20,77-N3%(6)*77
139 POKE30960+A0%(0)*RS+A1%(0)+A2%(0)*20,15-S3%(0)*15+1
140 POKE30960+A0%(1)*RS+A1%(1)+A2%(1)*20,13-S3%(1)*13
141 POKE30960+A0%(2)*RS+A1%(2)+A2%(2)*20,13-S3%(2)*12
142 T=13:IFS3%(3)+S3%(4)=2THENT=0
143 POKE30960+A0%(3)*RS+A1%(3)+A2%(3)*20,T
144 RETURN
145 IFCR=S0%(0)ANDZX=S1%(0)ANDS3%(0)=0THENVA$="{white}= {cyan}DER SCHALTER IST AUS":GOSUB686
146 IFCR=S0%(0)ANDZX=S1%(0)ANDS3%(0)=1THENVA$="{white}= {cyan}SCHALTER IST EIN":GOSUB686
147 RETURN
148 S0%(0)=0:S1%(0)=11:S2%(0)=1:S3%(0)=0
149 S0%(1)=0:S1%(1)=16:S2%(1)=6:S3%(1)=0
150 S0%(2)=0:S1%(2)=18:S2%(2)=6:S3%(2)=0
151 S0%(3)=0:S1%(3)=1:S2%(3)=6:S3%(3)=0
152 S0%(4)=0:S1%(4)=5:S2%(4)=6:S3%(4)=0
153 A0%(0)=0:A1%(0)=14:A2%(0)=6
154 A0%(1)=0:A1%(1)=7:A2%(1)=4
155 A0%(2)=0:A1%(2)=9:A2%(2)=5
156 A0%(3)=0:A1%(3)=3:A2%(3)=4
157 A0%(4)=0:A1%(4)=3:A2%(4)=4
158 N0%(0)=0:N1%(0)=12:N2%(0)=4:N3%(0)=0
159 N0%(1)=0:N1%(1)=7:N2%(1)=2:N3%(1)=0
160 N0%(2)=0:N1%(2)=3:N2%(2)=2:N3%(2)=0
161 N0%(3)=16:N1%(3)=0:N2%(3)=0:N3%(3)=0
162 N0%(4)=16:N1%(4)=0:N2%(4)=0:N3%(4)=0
163 N0%(5)=16:N1%(5)=0:N2%(5)=0:N3%(5)=0
164 N0%(6)=16:N1%(6)=0:N2%(6)=0:N3%(6)=0
165 N0%(7)=16:N1%(7)=0:N2%(7)=0:N3%(7)=0
166 N0%(8)=16:N1%(8)=0:N2%(8)=0:N3%(8)=0
167 ER%(0)=16:EX%(0)=0:EY%(0)=0:ET%(1)=0
168 ER%(1)=0:EX%(1)=1:EY%(1)=2:ET%(1)=3
169 ER%(2)=0:EX%(2)=18:EY%(2)=2:ET%(2)=15
170 ER%(3)=16:EX%(3)=0:EY%(3)=0:ET%(3)=4
171 ER%(4)=16:EX%(4)=0:EY%(4)=0:ET%(4)=8
172 ER%(5)=16:EX%(5)=0:EY%(5)=0:ET%(5)=5
173 ER%(6)=16:EX%(6)=0:EY%(6)=0:ET%(6)=16
174 ER%(7)=16:EX%(7)=0:EY%(7)=0:ET%(7)=17
175 ER%(8)=16:EX%(8)=0:EY%(8)=0:ET%(8)=13
176 ER%(9)=16:EX%(9)=0:EY%(9)=0:ET%(9)=15
177 ER%(10)=16:EX%(10)=0:EY%(10)=0:ET%(10)=11
178 ER%(11)=16:EX%(11)=0:EY%(11)=0:ET%(11)=7
179 ER%(12)=16:EX%(12)=0:EY%(12)=0:ET%(12)=9
180 RETURN
181 PRINT"{home}{down}{white}{right}  UNTERSUCHE  EQUIPMENTS  INVENTAR"
182 M=0
183 POKEQS+42+M*12,35
184 GOSUB715
185 POKEQS+42+M*12,32
186 IFA$="A"THENM=M-1-3*(M=0):GOTO183
187 IFA$="D"THENM=M+1+3*(M=2):GOTO183
188 IFA$=CHR$(13)THENPOKEQS+42+M*7,32:GOTO190
189 GOTO183
190 PRINT"{home}{down}{white}{right}                                      ";
191 ONM+1GOTO245,205,192
192 POKE1020,Q1%
193 GOSUB657
194 PRINT"{home}{down}{right}{white}INVENTAR: WAEHLE DEIN ITEM           ";
195 MT=2:MX=2:MY=4:MC=14:GOSUB455
196 IFIN%(M)=9THEN64
197 IFIN%(M)=0ORIT%(IN%(M))<>MTTHEN64
198 IFIN%(M)=3THENGOSUB201:IN%(M)=0:GOSUB444:GOTO64
199 IFIN%(M)=8THENGOSUB203:IN%(M)=0:GOSUB444:GOTO64
200 GOTO64
201 FORI=0TO3:PH%(I)=PH%(I)+IE%(IN%(M)):IFPH%(I)>P1%(I)THENPH%(I)=P1%(I)
202 FH%(I)=PH%(I):NEXT:RETURN
203 FORI=0TO3:PM%(I)=PM%(I)+IE%(IN%(M)):IFPM%(I)>P2%(I)THENPM%(I)=P2%(I)
204 FM%(I)=PM%(I):NEXT:RETURN
205 POKE1020,Q1%
206 GOSUB519
207 PRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINEN SPIELER     ";
208 P=0
209 CP=QS+2+(4-8*(P>1))*40:IFP=1ORP=3THENCP=CP+20
210 POKECP,35
211 GOSUB715
212 POKECP,32
213 IFP=0ANDA$="D"ANDP3%(1)=1THENP=1:GOTO209
214 IFP=0ANDA$="S"ANDP3%(2)=1THENP=2:GOTO209
215 IFP=0ANDA$="D"ANDP3%(1)=OANDP3%(2)=0ANDP3%(3)=1THENP=3:GOTO209
216 IFP=0ANDA$="S"ANDP3%(1)=OANDP3%(2)=0ANDP3%(3)=1THENP=3:GOTO209
217 IFP=1ANDA$="A"ANDP3%(0)=1THENP=0:GOTO209
218 IFP=1ANDA$="S"ANDP3%(3)=1THENP=3:GOTO209
219 IFP=2ANDA$="D"ANDP3%(3)=1THENP=3:GOTO209
220 IFP=2ANDA$="W"ANDP3%(0)=1THENP=0:GOTO209
221 IFP=3ANDA$="A"ANDP3%(2)=1THENP=2:GOTO209
222 IFP=3ANDA$="W"ANDP3%(1)=1THENP=1:GOTO209
223 IFP=3ANDA$="A"ANDP3%(2)=OANDP3%(1)=0ANDP3%(0)=1THENP=0:GOTO209
224 IFP=3ANDA$="W"ANDP3%(2)=OANDP3%(1)=0ANDP3%(0)=1THENP=0:GOTO209
225 IFA$=CHR$(13)THEN227
226 GOTO210
227 SL=0:CP=CP+81
228 PRINT"{home}{down}{right}{white}EQUIPMENT: WAFFE ODER RUESTUNG       ";
229 POKECP+SL*40,35
230 GOSUB715
231 POKECP+SL*40,32:IFA$="W"ORA$="S"THENSL=1-SL:GOTO229
232 IFA$=CHR$(13)THEN234
233 GOTO229
234 MY=4:MX=23:MC=14:IFP=1ORP=3THENMX=3
235 IFSL=0THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE WAFFE        ";
236 IFSL=1THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE RUESTUNG     ";
237 PRINT"{home}"LEFT$(CD$,MY-1)SPC(MX-3);"{brown}{$c1}{$c2:18}{$c3}";
238 FORI=0TO13:PRINT"{down}{left:20}{$c4}                  {$c5}";:NEXT
239 PRINT"{down}{left:20}{$c6}{$c7:18}{$c8}";
240 MT=SL:GOSUB455
241 IFIT%(IN%(M))=9THEN64
242 IFIN%(M)<>0ANDIT%(IN%(M))<>SLTHEN64
243 IFSL=0THENQ=PW%(P):PW%(P)=IN%(M):IN%(M)=Q:GOSUB257:GOTO64
244 IFSL=1THENQ=PR%(P):PR%(P)=IN%(M):IN%(M)=Q:GOSUB258:GOTO64
245 GOSUB517
246 GOSUB758
247 EI=0
248 IFER%(EI)<>CROREX%(EI)<>OXOREY%(EI)<>OYTHEN253
249 NI%=ET%(EI):GOSUB550
250 IFIS=99THENVA$="{white}= {cyan}DU KANNST NICHT MEHR TRAGEN":GOSUB686:GOTO69
251 ER%(EI)=-1
252 VA$="{white}> {cyan}GEFUNDEN: "+I$(IN%(IS)):GOSUB686:GOTO69
253 EI=EI+1
254 IFEI=12THEN256
255 GOTO248
256 VA$="{white}= {cyan}NICHTS BESONDERES":GOSUB686:GOTO69
257 VA$="{white}= {cyan}"+P$(P)+" BENUTZT "+I$(PW%(P)):GOSUB686:RETURN
258 A$="{white}= {cyan}"+P$(P)+" BENUTZT "+I$(PR%(P)):GOSUB686:RETURN
259 FORI=4TO11:FT%(I)=-1:NEXT
260 PP=4:CC=4:WX=0
261 FORI=0TO3:FT%(I)=-2:FH%(I)=PH%(I):FM%(I)=PM%(I):FS%(I)=PS%(I)
262 IFP3%(I)=0THENFH%(I)=-1
263 IFFH%(I)<=0THENFT%(I)=-1:PP=PP-1
264 FA%(I)=PA%(I)+IA%(PW%(I)):FD(I)=PD%(I)+ID%(PR%(I)):F1%(I)=P1%(I):F2%(I)=P2%(I)
265 NEXT
266 PRINT"{home}{down:3}{white}";:FORI=0TO15:PRINT"                                        ";:NEXT
267 GOSUB647
268 GOSUB420
269 GOSUB444
270 GOSUB452
271 FORI=0TO11
272 IFFT%(I)<>-1THENFC%(I)=INT(RND(0)*FD%(I))
273 NEXT
274 CC=CC-4
275 PRINT"{home}{down}{right}                       ";:GOSUB561
276 FORI=4TO11:IFFT%(I)=-1THEN278
277 PRINT"{home}"LEFT$(CD$,FY%(I));SPC(FX%(I));MD$(FT%(I))
278 NEXT
279 C=0
280 IFFT%(C)<>-1ANDFC%(C)>=100THENFC%(C)=FC%(C)-100:GOTO283
281 C=C+1:IFC<12THEN280
282 FORI=0TO11:FC%(I)=FC%(I)+FS%(I):NEXT:GOTO279
283 IFFT%(C)=-2THEN285
284 GOTO369
285 PRINT"{home}{white}{down}{right}"P$(C)": IST AM ZUG                     "
286 PRINTDD$;SPC(16)"{white} ANGRIFF {down}{left:9} INVENTAR";:M=2
287 PRINTDD$;SPC(16)"{down:2}         {down}{left:9}         ";
288 IFPM%(C)>0THENPRINTDD$;SPC(16)"{down:2} MAGIE   ";:M=3
289 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(37)"  {down}{left:2}  ";
290 PRINT"{left:3}{up}"PD$(C);
291 MY=0:FZ=0
292 POKEQS+816+MY*40,35
293 GOSUB715
294 IFA$="S"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY+1:GOTO301
295 IFA$="W"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY-1:GOTO301
296 IFA$=CHR$(13)THENFZ=0:GOTO304
297 IFA$="FR"ANDCR=14ORCR=15ORCR=16THENPRINT"{home}{down}{right}{white}FLUCHT NICHT MOEGLICH                 ":GOSUB758:GOSUB517:GOSUB760:GOTO293
298 IFA$="FR"ANDFZ=3THENFZ=0:GOTO59
299 IFA$="FR"ANDFZ<=3THENFZ=FZ+1:PRINT"{home}{down}{right}{white}FLUCHT                                ":GOSUB758:GOSUB517:GOSUB760:GOTO292
300 GOTO293
301 IFMY>=MTHENMY=0
302 IFMY<0THENMY=M-1
303 GOTO292
304 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(36)"{white}  {down}{left:2}  {left}{up}"PD$(C);
305 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
306 ONMY+1GOTO332,324
307 MT=3:MX=16:MY=20:MC=4:GOSUB455
308 IFIT%(IN%(M))=9THEN285
309 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         {down}{left:9}         ";
310 IFIN%(M)=0ORIT%(IN%(M))<>3THEN285
311 M=IN%(M)
312 IFIE%(M)>FM%(C)THENPRINT"{home}{down}{right}NICHT GENUG MANA                     ";:GOSUB753:GOTO285
313 GOSUB577
314 GOTO603
315 AP=IE%(M)+2-INT(RND(0)*5):FM%(C)=FM%(C)-IE%(M):PM%(C)=FM%(C):GOSUB444
316 AP=AP+PL%(C)*3
317 FH%(CE)=FH%(CE)-AP:IFFH%(CE)<0THENFH%(CE)=0
318 IFCE<4THENPH%(CE)=FH%(CE)
319 IFFH%(CE)>0THENGOTO354
320 IFFT%(CE)<>-2THENCC=CC-1:WX=WX+MX%(FT%(CE))
321 IFFT%(CE)=-2THENPP=PP-1
322 FT%(CE)=-1
323 GOTO354
324 MT=2:MX=16:MY=20:MC=4:GOSUB455
325 IFIT%(IN%(M))=9THEN285
326 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
327 PRINT"{down}{left:9}         ";
328 IFIN%(M)=0ORIT%(IN%(M))<>MTTHEN285
329 IFIN%(M)=3THENGOSUB201:IN%(M)=0:GOSUB444:GOTO275
330 IFIN%(M)=8THENGOSUB203:IN%(M)=0:GOSUB444:GOTO275
331 GOTO275
332 GOSUB577
333 AP=FA%(C)-FD(CE)
334 IFAP<=0THENPRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{white}MISS";:GOSUB751:GOTO346
335 AP=INT(RND(0)*AP)+1
336 FH%(CE)=FH%(CE)-AP:IFFH%(CE)<0THENFH%(CE)=0
337 IFCE<4THENPH%(CE)=FH%(CE)
338 IFFH%(CE)>0THENGOTO343
339 IFFT%(CE)<>-2THENCC=CC-1:WX=WX+MX%(FT%(CE))
340 IFFT%(CE)=-2THENPP=PP-1
341 FT%(CE)=-1
342 GOTO343
343 POKEV,(FX%(CE)*8+21)AND255:POKEV+1,FY%(CE)*8+50:POKE50168,16:POKEV+28,0
344 POKEV+16,-(FX%(CE)>30):POKEV+21,1
345 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{white}";AP
346 FORI=0TO5:POKEV,PEEK(V)+2:POKEV+1,PEEK(V+1)+2:GOSUB752:NEXTI
347 POKEV+21,0:IFCE<4THENGOSUB444
348 GOSUB751
349 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
350 IFFT%(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE));"  {down}{left:2}  ";
351 IFCC=0THEN377
352 IFPP=0THEN405
353 GOTO275
354 POKEV,(FX%(CE)*8+24)AND255:POKEV+1,FY%(CE)*8+50
355 IFM=4THENPOKEV+37,8:POKEV+38,2:POKEV+39,7:POKEV+28,1
356 B=17:IFM=6THENB=19:POKEV+28,0
357 IFM=5THENB=21:POKEV+28,0
358 IFM=7THENB=23:POKEV+28,0
359 POKEV+16,-(FX%(CE)>30):POKE50168,B:POKEV+21,1
360 PRINT"{white}";:IFM=9THENPRINT"{green}";
361 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}";AP
362 FORK=0TO3:FORI=0TO1:POKE50168,B+I:FORJ=1TO40:NEXTJ,I,K
363 POKEV+21,0:IFCE<4THENGOSUB444
364 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
365 IFFT%(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE));"  {down}{left:2}  ";
366 IFCC=0THEN377
367 IFPP=0THEN405
368 GOTO275
369 GOSUB751
370 PRINT"{white}{home}{down}{right}"M$(FT%(C))": GREIFT AN                    "
371 FC%(C)=FC%(C)-100
372 IFFT%(C)=7THENIFFM%(C)>=IE%(9)THENM=9:GOSUB751:GOSUB414:GOTO606
373 IFFT%(C)=9THENIFFM%(C)>=IE%(6)THENM=6:GOSUB751:GOSUB408:GOTO315
374 IFFT%(C)=10THENIFFM%(C)>=IE%(4)THENM=4:GOSUB751:GOSUB408:GOTO315
375 IFFT%(C)=11THENIFFM%(C)>=IE%(12)THENM=12:GOSUB751:GOSUB408:GOTO613
376 GOSUB751:GOSUB408:GOTO333
377 POKE1020,Q1%
378 PRINTDD$;"{brown}{up}{$c1}{$c2:10}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}{white}";
379 PRINT"{home}{down}{right}DU HAST GEWONNEN                     ";:GOSUB561
380 GOSUB657
381 FORI=0TO3:IFP3%(I)=0THEN385
382 PRINT"{home}{white}{right:3}";:IFPH%(I)=0THENPRINT"{red}";
383 PRINTLEFT$(CD$,4+I*3)P$(I)
384 PRINT"{home}"LEFT$(CD$,5+I*3)"{right:3}LV";PL%(I)+1;" EX";PX%(I)
385 NEXT
386 FORI=0TO3:IFPH%(I)=0ORP3%(I)=0THEN396
387 TX=WX
388 PRINT"{home}{white}"LEFT$(CD$,5+I*3)SPC(20)"EXPERIENCE {left}"TX"{left} ";
389 IFTX=0THEN396
390 XS=1:IFTX>9THENXS=10
391 IFTX>99THENXS=100
392 TX=TX-XS
393 PX%(I)=PX%(I)+XS
394 LL=10+PL%(I)*PL%(I)*10:IFPX%(I)>=LLTHENGOSUB398:GOTO394
395 PRINT"{home}{white}"LEFT$(CD$,5+I*3)"{right:3}LV";PL%(I)+1;" EX";PX%(I):GOTO388
396 NEXTI
397 GOTO403
398 PL%(I)=PL%(I)+1:PA%(I)=PA%(I)+1:PD%(I)=PD%(I)+1:PS%(I)=PS%(I)+1
399 P1%(I)=P1%(I)+PL%(I)*2:IFP2%(I)>0THENP2%(I)=P2%(I)+PL%(I)*3
400 IFP1%(I)>999THENP1%(I)=999
401 IFP2%(I)>999THENP2%(I)=999
402 RETURN
403 GOSUB744
404 GOTO59
405 PRINT"{home}{down}{right}{white}DU HAST VERLOREN                     "
406 GOSUB744
407 GOTO621
408 RE=INT(RND(0)*4):CE=0
409 IFFT%(CE)=-2ANDPH%(CE)>0THEN412
410 CE=CE+1:IFCE=4THENCE=0
411 GOTO409
412 IFRE>0THENRE=RE-1:GOTO410
413 RETURN
414 RE=INT(RND(0)*8):CE=4
415 IFFT%(CE)>=0THEN418
416 CE=CE+1:IFCE=12THENCE=4
417 GOTO415
418 IFRE>0THENRE=RE-1:GOTO416
419 RETURN
420 IFFF>0THENL=FF:GOTO422
421 L=PEEK(CP+164):IFL=0THENRETURN
422 IFL=3THENA=0:LC=INT(RND(0))*2+2:FORI=0TOLC:GOSUB555:NEXT:RETURN
423 IFL=1THENA=1:FORI=0TO2:GOSUB555:NEXT:RETURN
424 IFL=2THENA=1:FORI=0TO1:GOSUB555:NEXT:A=2:FORI=0TO1:GOSUB555:NEXT:RETURN
425 IFL=4THENA=3:GOSUB555:A=1:GOSUB555:GOSUB555:RETURN
426 IFL=5THENA=4:LC=INT(RND(0)*3)+1:FORI=0TOLC:GOSUB555:NEXT:RETURN
427 IFL=6THENONINT(RND(1)*2)+1GOTO437,438
428 IFL=7THENLC=INT(RND(0)*3)+1:A=6:FORI=0TOLC:GOSUB555:NEXT:RETURN
429 IFL=8THENA=7:GOSUB555:LC=INT(RND(0)*2)+1:A=6:GOSUB555:RETURN
430 IFL=9THENA=9:GOSUB555:A=8:GOSUB555:A=5:GOSUB555:RETURN
431 IFL=10THENA=6:LC=INT(RND(0)*2)+1:FORI=1TOLC:GOSUB555:NEXT:RETURN
432 IFL=11THENA=7:GOSUB555:A=10:GOSUB555:RETURN
433 IFL=12ANDINT(RND(1)*2)=1THENA=11:GOSUB555:RETURN
434 IFL=12THENA=5:GOSUB555:GOSUB555:A=9:GOSUB555:RETURN
435 IFL=13THENA=9:GOSUB555:A=12:GOSUB555:A=9:GOSUB555:RETURN
436 IFL=14THENA=9:GOSUB555:A=13:GOSUB555:A=7:GOSUB555:RETURN
437 A=5:LC=INT(RND(1)*2)+1:FORI=0TOLC:GOSUB555:NEXT:RETURN
438 A=9:LC=INT(RND(1)*2)+1:FORI=1TOLC:GOSUB555:NEXT:RETURN
439 FORI=0TO3
440 IFP3%(I)=0THEN443
441 PH%(I)=P1%(I)
442 PM%(I)=P2%(I)
443 NEXT:RETURN
444 PRINTDD$;
445 FORI=0TO3:IFP3%(I)=0THENPRINTSPC(40);:GOTO451
446 PRINT"{white}";:IFPH%(I)=0THENPRINT"{red}";
447 PRINTSPC(27)P$(I)" ";:A=PH%(I):GOSUB713
448 PRINTD$;
449 A=PM%(I):GOSUB713
450 PRINT" "D$"{right}";
451 NEXT:RETURN
452 FORI=0TO3:IFPH%(I)<=0ORP3%(I)=0THENNEXT:RETURN
453 PRINT"{home}"LEFT$(CD$,5+I*3);SPC(37);PD$(I):NEXT
454 RETURN
455 GOSUB490
456 MI=-1:M=0:MO=0
457 MI=MO-1:CY=MY:MM=0
458 IFMC=4THENGOSUB485
459 MI=MI+1
460 PRINT"{home}{white}"LEFT$(CD$,CY)SPC(MX)" ";
461 IFIT%(IN%(MI))<>MTTHENPRINT"{cyan}";
462 IFIT%(IN%(MI))=9THENPRINT"{white}";
463 IFIT%(IN%(MI))=-1THENPRINT"{cyan}";
464 PRINTI$(IN%(MI));
465 CY=CY+1
466 MM=MM+1
467 IFMM=MCTHEN469
468 IFMI<13THEN459
469 CP=QS+MX+MY*40
470 POKECP+(M-MO)*40,35
471 GOSUB715
472 POKECP+(M-MO)*40,32
473 IFA$="W"THENM=M-1:GOTO477
474 IFA$="S"THENM=M+1:GOTO477
475 IFA$=CHR$(13)THENRETURN
476 GOTO470
477 IFM=-1THENM=14-1:MO=14-MC:GOTO457
478 IFM=14THENM=0:MO=0:GOTO457
479 M%=0
480 IFM<MOTHENMO=MO-MC:M%=1:GOTO480
481 IFM>=MO+MCTHENMO=MO+MC:M%=1
482 IFMO<0THENMO=0:M%=1
483 IFM%=1THEN457
484 GOTO470
485 FORI=0TO3
486 PRINT"{home}{white}"LEFT$(CD$,CY+I)SPC(MX)" ";
487 PRINT"        ";
488 NEXTI
489 RETURN
490 A=0
491 FORI=0TO99:TT%(I)=0:NEXTI
492 FORI=0TO99:TT%(I)=IN%(I):IN%(I)=0:NEXTI
493 IFMT=0THENGOSUB498:GOSUB501:A=50:GOSUB504:GOSUB507:GOSUB510
494 IFMT=1THENGOSUB498:GOSUB504:A=50:GOSUB501:GOSUB507:GOSUB510
495 IFMT=2THENGOSUB498:GOSUB507:A=50:GOSUB501:GOSUB504:GOSUB510
496 IFMT=3THENGOSUB498:GOSUB510:A=50:GOSUB501:GOSUB504:GOSUB507
497 RETURN
498 FORI=0TO99:IFIT%(TT%(I))=9THENIN%(A)=TT%(I):A=A+1
499 NEXTI
500 RETURN
501 FORI=0TO99:IFIT%(TT%(I))=0THENIN%(A)=TT%(I):A=A+1
502 NEXTI
503 RETURN
504 FORI=0TO99:IFIT%(TT%(I))=1THENIN%(A)=TT%(I):A=A+1
505 NEXTI
506 RETURN
507 FORI=0TO99:IFIT%(TT%(I))=2THENIN%(A)=TT%(I):A=A+1
508 NEXTI
509 RETURN
510 FORI=0TO99:IFIT%(TT%(I))=3THENIN%(A)=TT%(I):A=A+1
511 NEXTI
512 RETURN
513 PRINT"{brown}{home}{down:3}";
514 FORI=0TO15
515 PRINT"                                        ";:NEXT
516 RETURN
517 PRINT"{home}{down}{right}{white}                                      ";
518 RETURN
519 PRINT"{brown}{home}{down:3}{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
520 FORI=0TO5
521 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
522 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
523 PRINT"{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
524 FORI=0TO5
525 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
526 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
527 DX=3:DY=4:P=0:GOSUB530:DX=23:P=1:GOSUB530
528 DX=3:DY=12:P=2:GOSUB530:DX=23:P=3:GOSUB530
529 RETURN
530 IFP3%(P)=0THENRETURN
531 PRINT"{home}{white}";:IFPH%(P)=0THENPRINT"{red}";
532 PRINT"{home}"LEFT$(CD$,DY)SPC(DX-1)" "P$(P)"  LV"PL%(P)+1
533 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX)" "I$(PW%(P));
534 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX)" "I$(PR%(P));
535 T1%=PA%(P)+IA%(PW%(P))
536 T2%=PD%(P)+ID%(PR%(P))
537 IFT1%>999THENT1%=999
538 IFT2%>999THENT2%=999
539 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX+9)"ATK"T1%;
540 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX+9)"DEF"T2%;
541 RETURN
542 IFPEEK(2)=1THENSP=30960+RS*(CR-0)
543 FORI=0TO7
544 PRINT"{home}{down:3}"LEFT$(CD$,I*2);
545 FORJ=0TO19
546 PRINTT$(PEEK(SP))"{up}";
547 SP=SP+1
548 NEXTJ,I
549 RETURN
550 IS=0
551 IFIN%(IS)=0THENIN%(IS)=NI%:NI%=-1:RETURN
552 IS=IS+1
553 IFIS=99THENRETURN
554 GOTO551
555 IFCC>=11THENRETURN
556 FH%(CC)=MH%(A):FM%(CC)=MM%(A):FT%(CC)=A:FC%(CC)=INT(RND(0)*MS%(A)):FS%(CC)=MS%(A)
557 FX%(CC)=2:FY%(CC)=5+CC*3:FA%(CC)=MA%(A):FD(CC)=MD%(A):FB(CC)=MB%(A)
558 F1%(CC)=MH%(A):F2%(CC)=MM%(A)
559 IFCC>=4THENFX%(CC)=5:FY%(CC)=5+(CC-4)*3
560 CC=CC+1:RETURN
561 ZM=0
562 FORJ=4TO7
563 IFFT%(J)=-1THENZM=ZM-1
564 ZM=ZM+1
565 NEXTJ
566 IFZM<>CMTHENPRINTDD$;"{right}          {down}{left:10}          {down}{left:10}          {down}{left:10}          ";
567 PRINTDD$;
568 CM=0
569 FORI=4TO7
570 IFFT%(I)=-1THENNEXT:RETURN
571 A=FH%(I):GOSUB713
572 PRINT"{home}{right}"LEFT$(CD$,20+CM)M$(FT%(I));
573 PRINT"{home}{right}"LEFT$(CD$,20+CM);"{right:7}";D$
574 CM=CM+1
575 NEXT
576 RETURN
577 CE=4:PRINT"{home}{down}{right}{white}"P$(C)": WAHELE DEIN ZIEL               "
578 IFFT%(CE)=-1THEN587
579 POKEQS-2+FX%(CE)+FY%(CE)*40,35
580 GOSUB715
581 IFA$=CHR$(13)THENPOKEQS-2+FX%(CE)+FY%(CE)*40,32:RETURN
582 IFA$="S"THENGOTO587
583 IFA$="W"THENGOTO595
584 IFA$="D"ANDCE>3THENPOKEQS-2+FX%(CE)+FY%(CE)*40,32:CE=0:GOTO578
585 IFA$="A"ANDCE<4THENPOKEQS-2+FX%(CE)+FY%(CE)*40,32:CE=4:GOTO578
586 GOTO580
587 CO=CE
588 POKEQS-2+FX%(CE)+FY%(CE)*40,32
589 CE=CE+1
590 IFCE>=12THENCE=0
591 IFCO<4ANDCE>3THENCE=CO
592 IFCO>3ANDCE<4THENCE=CO
593 IFFT%(CE)<>-1THEN579
594 GOTO589
595 CO=CE
596 POKEQS-2+FX%(CE)+FY%(CE)*40,32
597 CE=CE-1
598 IFCE<0THENCE=11
599 IFCO<4ANDCE>3THENCE=CO
600 IFCO>3ANDCE<4THENCE=CO
601 IFFT%(CE)<>-1THEN579
602 GOTO597
603 IFFT%(C)=-2THEN619
604 IFFT%(C)=7ANDFM%(C)>=IE%(9)THENM=9:GOTO606
605 GOTO315
606 AP=IE%(M)*4+2-INT(RND(0)*5):FM%(C)=FM%(C)-IE%(M):IFC<4THENPM%(C)=FM%(C):GOSUB444
607 FH%(CE)=FH%(CE)+AP:IFFH%(CE)>F1%(CE)THENFH%(CE)=F1%(CE)
608 IFCE<4THENPH%(CE)=FH%(CE):GOSUB444
609 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{green}";AP
610 GOSUB758
611 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
612 GOTO275
613 FM%(C)=FM%(C)-IE%(M):IFC<4THENPM%(C)=FM%(C):GOSUB444
614 FS%(CE)=FS%(CE)/2:IFFS%(CE)<5THENFS%(CE)=5
615 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{green}SLOW";
616 GOSUB758
617 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
618 GOTO275
619 IFM=9THEN606
620 GOTO315
621 FORI=0TO3:PX%(I)=0:NEXT
622 PA%(0)=5:PD%(0)=2:PA%(1)=4:PD%(1)=3:PA%(2)=1:PD%(2)=1:PA%(3)=1:PD%(3)=1
623 FORI=0TO13:IN%(I)=0:NEXT
624 IN%(0)=18
625 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
626 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
627 PRINT"{$c6}{$c7:38}{up:3}";
628 POKE50151,72:POKE56295,9
629 X=15:Y=2:CR=0
630 P3%(0)=1:P3%(1)=0:P3%(2)=0:P3%(3)=0
631 P1%(0)=15:P1%(1)=17:P1%(2)=9:P1%(3)=9:P2%(0)=0:P2%(1)=0:P2%(2)=7:P2%(3)=23
632 FORI=0TO3:PH%(I)=P1%(I):PM%(I)=P2%(I):PX%(I)=0:PW%(I)=0:PR%(I)=0:NEXT
633 PW%(0)=1:PR%(0)=2
634 GOSUB132
635 AA$="INTRO":GOSUB701
636 GOTO59
637 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
638 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
639 PRINT"{$c6}{$c7:38}{up:3}";
640 POKE50151,72:POKE56295,9
641 AA$="ENDE":GOSUB701
642 GOTO59
643 PRINT"{brown}{home}{$c1}{$c2:38}{$c3}";
644 PRINT"{$c4}                                      {$c5}";
645 PRINT"{$c6}{$c7:38}{$c8}"
646 RETURN
647 PRINTDD$;"{brown}{up}{$c1}{$c2:7}{white}HP{brown}{$c2}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
648 FORI=0TO3:PRINT"{$c4}          {$c5}{$c4}            {$c5}{$c4}{right:12}{$c5}";:NEXT
649 PRINT"{$c6}{$c7:10}{$c8}{$c6}{$c7:12}{$c8}{$c6}{$c7:12}";
650 POKE50151,72:POKE56295,9
651 RETURN
652 PRINTDD$;"{brown}{up}{$c1}{$c2:24}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
653 FORI=0TO3:PRINT"{$c4}                        {$c5}{$c4}{right:12}{$c5}";:NEXT
654 PRINT"{$c6}{$c7:24}{$c8}{$c6}{$c7:12}";
655 POKE50151,72:POKE56295,9
656 RETURN
657 PRINT"{brown}{home}{down:3}{$c1}{$c2:38}{$c3}";
658 FORI=0TO13
659 PRINT"{$c4}                                      {$c5}";:NEXT
660 PRINT"{$c6}{$c7:38}{$c8}";:RETURN
661 POKE56322,224
662 IFAA$="TXT.WELCOME.LENA"THENOPEN1,8,4,"TXT.WELCOME.LENA,S,R":VA$="{white}> {cyan}LENA {cyan}IST IM TEAM"
663 IFAA$="TXT.WELCOME.DOLM"THENOPEN1,8,4,"TXT.WELCOME.DOLM,S,R":VA$="{white}> {cyan}DOLM {cyan}IST IM TEAM"
664 IFAA$="TXT.WELCOME.MIRA"THENOPEN1,8,4,"TXT.WELCOME.MIRA,S,R":VA$="{white}> {cyan}MIRA {cyan}IST IM TEAM"
665 IFAA$=""THENRETURN
666 I=0:SB$(0)="":SB$(1)="":SB$(2)=""
667 INPUT#1,SB$(I)
668 IFST=0THENI=I+1:GOTO667
669 CLOSE1
670 POKE56322,225
671 PRINT"{home}{right}{down:20}"+"                        "
672 PRINT"{home}{right}{down:21}"+"                        "
673 PRINT"{home}{right}{down:22}"+"                        "
674 PRINT"{home}{right}{down:23}"+"                        "
675 PRINT"{home}{right}{down:20}{white}"+SB$(0)
676 PRINT"{home}{right}{down:21}{white}"+SB$(1)
677 PRINT"{home}{right}{down:22}{white}"+SB$(2)
678 PRINT"{home}{right}{down:23}{white}"+SB$(3)
679 GOSUB744
680 PRINT"{home}{right}{down:20}"+"                        "
681 PRINT"{home}{right}{down:21}"+"                        "
682 PRINT"{home}{right}{down:22}"+"                        "
683 PRINT"{home}{right}{down:23}"+"                        "
684 GOSUB686
685 RETURN
686 IFIB$(0)=""THENIB$(0)=VA$:GOTO692
687 IFIB$(1)=""THENIB$(1)=VA$:GOTO692
688 IFIB$(2)=""THENIB$(2)=VA$:GOTO692
689 IB$(0)=IB$(1)
690 IB$(1)=IB$(2)
691 IB$(2)=VA$
692 PRINT"{home}{white}"
693 PRINT"{home}{right}{down:20}"+"                        "
694 PRINT"{home}{right}{down:21}"+"                        "
695 PRINT"{home}{right}{down:22}"+"                        "
696 PRINT"{home}{right}{down:23}"+"                        "
697 PRINT"{home}{right}{down:20}"+IB$(0)
698 PRINT"{home}{right}{down:21}"+IB$(1)
699 PRINT"{home}{right}{down:22}"+IB$(2)
700 RETURN
701 POKE1020,Q7%
702 POKE56322,224
703 IFAA$="INTRO"THENOPEN1,8,4,"TXT.INTRO,S,R"
704 IFAA$="ENDE"THENOPEN1,8,4,"TXT.ENDE,S,R"
705 IFAA$=""THENRETURN
706 GET#1,B$
707 IFB$="$"THENPRINT"{home}{down:2}{right:2}{white}";:GOTO706
708 IFB$="%"THENPRINT"{right:2}{white}";:GOTO706
709 IFB$="#"THENCLOSE1:POKE56322,255:GOSUB744:RETURN
710 PRINTB$;
711 IFB$<>" "THENPOKE162,0:WAIT162,4
712 GOTO706
713 T$=STR$(A)
714 D$=RIGHT$("000"+RIGHT$(T$,LEN(T$)-1),3):RETURN
715 A$=""
716 J=PEEK(56320)
717 IFJ=127THEN720
718 IFJ=107ORJ=103ORJ=110ORJ=109THEN720
719 GOTO716
720 J=PEEK(56320)
721 IFJ=127ANDA$=CHR$(13)THENRETURN
722 IFJ=107THENA$="FL":GOSUB749:RETURN
723 IFJ=103THENA$="FR":GOSUB749:RETURN
724 IFJ=110THENA$="FU":GOSUB749:RETURN
725 IFJ=109THENA$="FD":GOSUB749:RETURN
726 IFJ=123THENA$="A":GOSUB749:RETURN
727 IFJ=119THENA$="D":GOSUB749:RETURN
728 IFJ=126THENA$="W":GOSUB749:RETURN
729 IFJ=125THENA$="S":GOSUB749:RETURN
730 IFJ=111THENA$=CHR$(13)
731 GOTO720
732 A$=""
733 J=PEEK(56320)
734 IFJ<>111ANDJ<>107ANDJ<>103ANDJ<>110ANDJ<>109THEN736
735 GOTO733
736 J=PEEK(56320)
737 IFJ=127ANDA$=CHR$(13)THENRETURN
738 IFJ=123THENA$="A":GOSUB750:RETURN
739 IFJ=119THENA$="D":GOSUB750:RETURN
740 IFJ=126THENA$="W":GOSUB750:RETURN
741 IFJ=125THENA$="S":GOSUB750:RETURN
742 IFJ=111THENA$=CHR$(13)
743 GOTO736
744 A$=""
745 J=PEEK(56320)
746 IFJ=127ANDA$=CHR$(13)THENRETURN
747 IFJ=111THENA$=CHR$(13)
748 GOTO745
749 POKE162,0:WAIT162,16:RETURN
750 POKE162,0:WAIT162,8:RETURN
751 POKE162,0:WAIT162,32:RETURN
752 POKE162,0:WAIT162,2:RETURN
753 POKE162,0:WAIT162,32
754 POKE162,0:WAIT162,32
755 POKE162,0:WAIT162,32
756 POKE162,0:WAIT162,32
757 RETURN
758 POKE162,0:WAIT162,32
759 RETURN
760 POKE162,0:WAIT162,4
761 RETURN
762 DATA"IMP",5,0,8,4,1,0,3
763 DATA"SNAKE",8,0,12,6,1,0,5
764 DATA"VIPER",12,0,18,6,1,0,9
765 DATA"NACHO",30,0,22,15,5,0,50
766 DATA"GOLEM",20,0,4,20,8,0,17
767 DATA"MOSS",30,0,7,26,9,0,21
768 DATA"BONES",50,0,20,18,8,0,15
769 DATA"WISP",25,50,15,0,8,0,17
770 DATA"TROLL",100,0,9,40,13,0,100
771 DATA"SPARK",30,50,22,0,8,0,17
772 DATA"BURNER",80,90,30,25,13,0,42
773 DATA"TBERRY",120,10,60,60,20,0,150
774 DATA"DRACUL",250,60,50,70,25,0,500
775 DATA"GLADOS",500,70,60,80,30,0,1000
776 DATA"KRON",15 ,0  ,10 ,4 ,3 ,0 ,0
777 DATA"LENA",17 ,0  ,10 ,3 ,3 ,0 ,0
778 DATA"DOLM",9  ,7  ,8  ,1 ,1 ,0 ,0
779 DATA"MIRA",9  ,23 ,7  ,1 ,1 ,1 ,2
780 DATA"{gray1}{rvrs off}{$c9}{rvrs off}{gray1}{$ca}{down}{left:2}{rvrs off}{gray1}{$ca}{rvrs off}{gray1}{$c9}{rvrs off}"
781 DATA"{gray1}{rvrs off}{$cb}{rvrs off}{gray1}{$cc}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1}{$ca}{rvrs off}"
782 DATA"{gray1}{rvrs off}{$c9}{rvrs off}{gray1}{$ca}{down}{left:2}{rvrs off}{gray1}{$ca}{rvrs off}{gray1}{$c9}{rvrs off}"
783 DATA"{gray1}{rvrs off}{$cd}{rvrs off}{gray1}{$ce}{down}{left:2}{rvrs off}{gray1}{$cf}{rvrs off}{gray1}{$d0}{rvrs off}"
784 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
785 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
786 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
787 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
788 DATA"{orange}{rvrs off}{$d1}{rvrs off}{orange}{$d1}{down}{left:2}{rvrs off}{orange}{$d1}{rvrs off}{orange}{$d1}{rvrs off}"
789 DATA"{brown}{rvrs off}{$d2}{rvrs off}{brown}{$d3}{down}{left:2}{rvrs off}{brown}{$d4}{rvrs off}{brown}{$d5}{rvrs off}"
790 DATA"{lt. red}{rvrs off}{$d6}{rvrs off}{lt. red}{$d7}{down}{left:2}{rvrs off}{lt. red}{$d8}{rvrs off}{lt. red}{$d9}{rvrs off}"
791 DATA"{gray1}{rvrs off}{$da}{rvrs off}{gray1}{$db}{down}{left:2}{rvrs off}{gray1}{$dc}{rvrs off}{gray1}{$dd}{rvrs off}"
792 DATA"{gray1}{rvrs off}{$a2}{rvrs off}{gray1}{$a3}{down}{left:2}{rvrs off}{gray1}{$a4}{rvrs off}{gray1}{$a5}{rvrs off}"
793 DATA"{brown}{rvrs off}{$a1}{rvrs off}{brown}{$a1}{down}{left:2}{rvrs off}{brown}{$a1}{rvrs off}{brown}{$a1}{rvrs off}"
794 DATA"{gray1}{rvrs off}{$de}{rvrs off}{gray1}{$df}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1} {rvrs off}"
795 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
796 DATA"{lt. red}{rvrs off}{$a6}{rvrs off}{lt. red}{$a7}{down}{left:2}{rvrs off}{lt. red}{cmdr pound}{rvrs off}{lt. red}{shft pound}{rvrs off}"
797 DATA"{lt. red}{rvrs off}{$aa}{rvrs off}{lt. red}{$aa}{down}{left:2}{rvrs off}{lt. red}{$aa}{rvrs off}{lt. red}{$aa}{rvrs off}"
798 DATA"{brown}{rvrs off}{$ab}{rvrs off}{brown}{$ab}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
799 DATA"{brown}{rvrs off}{$ab}{rvrs off}{brown}{$ad}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
800 DATA"{brown}{rvrs off}{$ad}{rvrs off}{brown}{$ab}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
801 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
802 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
803 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
804 DATA"{lt. green}{rvrs off}{$ae}{rvrs off}{lt. green}{$d1}{down}{left:2}{rvrs off}{lt. green}{$d1}{rvrs off}{lt. green}{$af}{rvrs off}"
805 DATA"{lt. green}{rvrs off}{$b0}{rvrs off}{lt. green}{$b1}{down}{left:2}{rvrs off}{lt. green}{$b2}{rvrs off}{lt. green}{$b3}{rvrs off}"
806 DATA"{lt. green}{rvrs off}{$b4}{rvrs off}{lt. green}{$b5}{down}{left:2}{rvrs off}{lt. green}{$b4}{rvrs off}{lt. green}{$b5}{rvrs off}"
807 DATA"{lt. green}{rvrs off}{$b6}{rvrs off}{lt. green}{$b7}{down}{left:2}{rvrs off}{lt. green}{$b8}{rvrs off}{lt. green}{$b9}{rvrs off}"
808 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
809 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
810 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
811 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
812 DATA"{brown}{rvrs off}{$ae}{rvrs off}{brown}{$d1}{down}{left:2}{rvrs off}{brown}{$d1}{rvrs off}{brown}{$af}{rvrs off}"
813 DATA"{brown}{rvrs off}{$b0}{rvrs off}{brown}{$b1}{down}{left:2}{rvrs off}{brown}{$b2}{rvrs off}{brown}{$b3}{rvrs off}"
814 DATA"{brown}{rvrs off}{$b4}{rvrs off}{brown}{$b5}{down}{left:2}{rvrs off}{brown}{$b4}{rvrs off}{brown}{$b5}{rvrs off}"
815 DATA"{brown}{rvrs off}{$b6}{rvrs off}{brown}{$b7}{down}{left:2}{rvrs off}{brown}{$b8}{rvrs off}{brown}{$b9}{rvrs off}"
816 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
817 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
818 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
819 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
820 DATA"{gray1}{rvrs off}{$d1}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$d1}{rvrs off}{gray1}{$d1}{rvrs off}"
821 DATA"{gray1}{rvrs off}{$ba}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$ba}{rvrs off}{gray1}{$d1}{rvrs off}"
822 DATA"{gray1}{rvrs off}{$ba}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$bb}{rvrs off}{gray1}{$d1}{rvrs off}"
823 DATA"{gray1}{rvrs off}{$bc}{rvrs off}{gray1}{$bd}{down}{left:2}{rvrs off}{gray1}{$be}{rvrs off}{gray1}{$be}{rvrs off}"
824 DATA"{gray1}{rvrs off}{$bf}{rvrs off}{gray1}{$bf}{down}{left:2}{rvrs on}{gray1}@{rvrs on}{gray1}@{rvrs off}"
825 DATA"{gray2}{rvrs on}A{rvrs on}{gray2}B{down}{left:2}{rvrs on}{gray2}C{rvrs on}{gray2}D{rvrs off}"
826 DATA"{gray1}{rvrs on}E{rvrs on}{gray1}F{down}{left:2}{rvrs on}{gray1}G{rvrs on}{gray1}H{rvrs off}"
827 DATA"{brown}{rvrs on}I{rvrs on}{brown}I{down}{left:2}{rvrs on}{brown}I{rvrs on}{brown}I{rvrs off}"
828 DATA"{lt. red}{rvrs on}J{rvrs on}{lt. red}K{down}{left:2}{rvrs on}{lt. red}L{rvrs on}{lt. red}M{rvrs off}"
829 DATA"{gray1}{rvrs on}N{rvrs on}{gray1}K{down}{left:2}{rvrs on}{gray1}L{rvrs on}{gray1}M{rvrs off}"
830 DATA"{lt. green}{rvrs on}O{rvrs on}{lt. green}P{down}{left:2}{rvrs on}{lt. green}Q{rvrs on}{lt. green}R{rvrs off}"
831 DATA"{gray2}{rvrs on}S{rvrs on}{gray2}T{down}{left:2}{rvrs on}{gray2}U{rvrs on}{gray2}V{rvrs off}"
832 DATA"{gray1}{rvrs on}W{rvrs on}{gray1}X{down}{left:2}{rvrs on}{gray1}Y{rvrs on}{gray1}Z{rvrs off}"
833 DATA"{gray1}{rvrs on}[{rvrs on}{gray1}{pound}{down}{left:2}{rvrs on}{gray1}]{rvrs on}{gray1}{^}{rvrs off}"
834 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
835 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
836 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
837 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
838 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
839 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
840 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
841 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
842 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
843 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
844 DATA"{lt. green}{rvrs on}{back arrow}{rvrs on}{lt. green}!{down}{left:2}{rvrs on}{lt. green}#{rvrs on}{lt. green}${rvrs off}"
845 DATA"{gray3}{rvrs on}%{rvrs on}{gray3}&{down}{left:2}{rvrs on}{gray3}'{rvrs on}{gray3}({rvrs off}"
846 DATA"{gray1}{rvrs on}%{rvrs on}{gray1}&{down}{left:2}{rvrs on}{gray1}'{rvrs on}{gray1}({rvrs off}"
847 DATA"{lt. green}{rvrs on}){rvrs on}{lt. green}*{down}{left:2}{rvrs on}{lt. green}+{rvrs on}{lt. green},{rvrs off}"
848 DATA"{gray1}{rvrs on}-{rvrs on}{gray1}.{down}{left:2}{rvrs on}{gray1}/{rvrs on}{gray1}0{rvrs off}"
849 DATA"{gray1}{rvrs on}1{rvrs on}{gray1}2{down}{left:2}{rvrs on}{gray1}3{rvrs on}{gray1}4{rvrs off}"
850 DATA"{gray1}{rvrs on}5{rvrs on}{gray1}6{down}{left:2}{rvrs on}{gray1}7{rvrs on}{gray1}8{rvrs off}"
851 DATA"{gray1}{rvrs on}9{rvrs on}{gray1}:{down}{left:2}{rvrs on}{gray1};{rvrs on}{gray1}<{rvrs off}"
852 DATA"{gray1}{rvrs on}={rvrs on}{gray1}>{down}{left:2}{rvrs on}{gray1}?{rvrs on}{gray1}{$c0}{rvrs off}"
853 DATA"{lt. green}{rvrs on}9{rvrs on}{lt. green}:{down}{left:2}{rvrs on}{lt. green};{rvrs on}{lt. green}<{rvrs off}"
854 DATA"{lt. red}{rvrs on}5{rvrs on}{lt. red}6{down}{left:2}{rvrs on}{lt. red}7{rvrs on}{lt. red}8{rvrs off}"
855 DATA"{lt. green}{rvrs on}5{rvrs on}{lt. green}6{down}{left:2}{rvrs on}{lt. green}7{rvrs on}{lt. green}8{rvrs off}"
856 DATA"{gray1}{rvrs on}{$c1}{rvrs on}{gray1}{$c2}{down}{left:2}{rvrs on}{gray1}{$c3}{rvrs on}{gray1}{$c4}{rvrs off}"
857 DATA"{gray1}{rvrs on}{$c5}{rvrs on}{gray1}{$c6}{down}{left:2}{rvrs on}{gray1}{$c7}{rvrs on}{gray1}{$c8}{rvrs off}"
858 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
859 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
860 DATA"        ",00,00,00,00,-1 
861 DATA"DOLCH   ",03,00,00,00,00  
862 DATA"HELM    ",00,02,-1,00,01  
863 DATA"KRAEUTER",00,00,-1,10,02  
864 DATA"FEUER   ",00,00,00,05,03  
865 DATA"EIS     ",00,00,00,08,03  
866 DATA"MUT     ",00,00,00,23,03  
867 DATA"BOMBE   ",00,00,00,50,03  
868 DATA"KIRSCHE ",00,00,-1,15,02  
869 DATA"HEILEN  ",00,00,00,04,03  
870 DATA"SKULL   ",00,00,00,00,04  
871 DATA"FLEGEL  ",07,00,-1,00,00  
872 DATA"LAHM    ",00,00,00,04,03  
873 DATA"STAB    ",12,00,00,00,00  
874 DATA"FEDER   ",00,10,00,00,01  
875 DATA"STOCK   ",23,00,00,00,00  
876 DATA"WESTE   ",00,14,00,00,01  
877 DATA"SCHILD  ",00,24,00,00,01  
878 DATA"ZURUECK ",00,00,00,0 ,09  
879 END
