0 POKE55,240:POKE56,120:CLR
1 QO=53282
2 QP=53283
3 QQ=53281
4 QR=53280
5 QS=49152
6 V=53248
7 QT=30960
8 Q1=0
9 Q2=1
10 Q3=2
11 Q4=3
12 Q5=4
13 Q6=5
14 Q7=6
15 Q8=7
16 Q9=8
17 QA=9
18 QB=10
19 CB=11
20 QC=12
21 QD=13
22 QE=14
23 QF=15
24 QG=8
25 QH=9
26 QI=10
27 QJ=11
28 QK=12
29 QL=13
30 QM=14
31 QN=15
32 MQ=14
33 IC=19
34 HC=14
35 EC=12
36 IX=99
37 TM=80
38 RS=165
39 DD$="{home}{down:20}{white}"
40 CD$="{down:25}"
41 CT$="{home}{down}{white}{right}                                      "
42 DIMM$(MQ),MD$(MQ)
43 DIMMH(MQ),MM(MQ)
44 DIMMA(MQ),MD(MQ)
45 DIMMS(MQ)
46 DIMMB(MQ),MX(MQ)
47 DIMFH(11),FT(11),FC(11)
48 DIMFM(11),FS(11),FX(11),FY(11)
49 DIMFA(11),FD(11),F1(11),F2(11)
50 FORI=0TO3:FX(I)=37:FY(I)=5+3*I:NEXT
51 DIMI$(IC),IA(IC)
52 DIMID(IC),IS(IC)
53 DIMIE(IC),IT(IC)
54 DIMH(HC)
55 DIMER(EC),EX(EC)
56 DIMEY(EC),ET(EC)
57 DIMIN(IX)
58 DIMT$(TM)
59 DIMTN$(TM)
60 DIMTT(IX)
61 FORI=0TOMQ-1
62 READM$(I),MH(I),MM(I),MS(I)
63 READMA(I),MD(I),MB(I),MX(I)
64 NEXT
65 FORI=0TO3
66 READP$(I),PH(I),PM(I),PS(I)
67 READPA(I),PD(I),PW(I),PR(I)
68 NEXT
69 PM$="{rvrs on}{lt. red}UV{down}{left:2}WX{rvrs off}"
70 FORI=0TOTM-1
71 READT$(I)
72 NEXT
73 PD$(0)=T$(48)
74 PD$(1)=T$(49)
75 PD$(2)=T$(52)
76 PD$(3)=T$(51)
77 ZZ=64
78 FORI=0TOMQ-1
79 MD$(I)=T$(ZZ)
80 ZZ=ZZ+1
81 NEXTI
82 FORI=0TOIC-1
83 READI$(I),IA(I),ID(I),IS(I),IE(I),IT(I)
84 NEXT
85 POKEV+21,0
86 POKE648,192
87 POKE56576,0
88 POKE53272,8
89 POKE53270,PEEK(53270)OR16
90 POKEQO,QA
91 POKEQP,Q1
92 POKEQR,Q1
93 POKEQQ,Q7
94 GOTO637
95 POKE1020,Q7
96 POKE1021,Q7
97 SYS820
98 GOSUB432
99 GOSUB439
100 PRINT"{home}{down}{right}{white}                                      ";
101 POKE1020,Q7
102 GOSUB537
103 IFPEEK(2)=1THENCP=30960+RS*(CR-0)
104 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)PD$(0);
105 OX=ZX:OY=ZY
106 ZX=X:ZY=Y
107 GOSUB728
108 IFA$=CHR$(145)ORA$="W"THENPRINTCT$;:ZY=Y-1:GOTO114
109 IFA$=CHR$(157)ORA$="A"THENPRINTCT$;:ZX=X-1:GOTO114
110 IFA$=CHR$(29)ORA$="D"THENPRINTCT$;:ZX=X+1:GOTO114
111 IFA$=CHR$(17)ORA$="S"THENPRINTCT$;:ZY=Y+1:GOTO114
112 IFA$=CHR$(13)THEN171
113 GOTO107
114 AA=0
115 FORI=0TOEC
116 IFER(I)=CRANDEX(I)=ZXANDEY(I)=ZYTHENAA=1
117 NEXTI
118 IFZX=-1ORZX=20ORZY=-1ORZY=8THEN130
119 C=PEEK(CP+ZX+ZY*20)
120 IFC=08THEN144
121 IFC=09THEN144
122 IFC=10THEN144
123 IFC=11THEN144
124 IFC=12THEN157
125 IFC=14THEN151
126 IFC=25THEN157
127 IFC=47THEN170
128 IFC>=48THEN161
129 IFC>7THEN105
130 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)T$(PEEK(CP+X+Y*20))
131 IFRND(1)*100>97ANDPEEK(CP+164)>0THENFF=0:GOTO247
132 X=ZX:Y=ZY
133 IFX=-1ANDPEEK(CP+160)=255THENX=0:GOTO104
134 IFX=-1THENCR=PEEK(CP+160):X=19:GOTO101
135 IFX=20ANDPEEK(CP+161)=255THENX=19:GOTO104
136 IFX=20THENCR=PEEK(CP+161):X=0:GOTO101
137 IFY=-1ANDPEEK(CP+162)=255THENY=0:GOTO104
138 IFY=-1THENCR=PEEK(CP+162):Y=7:GOTO101
139 IFY=8ANDPEEK(CP+163)=255THENY=7:GOTO104
140 IFY=8THENCR=PEEK(CP+163):Y=0:GOTO101
141 IFC=2THEN151
142 IFC=3THEN151
143 GOTO104
144 IFCR=16THENCR=16:X=0:Y=0:GOTO100
145 IFCR=16THENCR=16:X=0:Y=0:GOTO100
146 IFCR=16THENCR=16:X=0:Y=0:GOTO100
147 IFCR=16THENCR=16:X=0:Y=0:GOTO100
148 IFCR=16THENCR=16:X=0:Y=0:GOTO100
149 IFCR=16THENCR=16:X=0:Y=0:GOTO100
150 IFCR=16THEN666
151 IFC=14ANDCR=0ANDZX=16THENH(0)=1-H(0):GOSUB550:GOSUB566:GOTO101
152 IFC=14ANDCR=16ANDZX=0THENH(1)=1-H(1):GOSUB550:GOSUB566:GOTO105
153 IFC=2ANDCR=16ANDX=0ANDH(6)=0THENH(6)=1:GOSUB550:GOTO100
154 IFC=2ANDCR=16ANDX=0ANDH(7)=0THENH(7)=1:GOSUB550:GOTO100
155 IFC=3ANDCR=16ANDY=0ANDH(12)=0THENH(12)=1:GOSUB550:GOTO100
156 IFC=3ANDCR=16ANDY=0ANDH(13)=0THENH(13)=1:GOSUB550:GOTO100
157 IFC=12ANDAA=0THENGOTO105
158 IFC=12ANDAA=1THENPRINT"{home}{white}{down}{right}IN DER TRUHE IST WAS!":GOSUB749:GOSUB512:GOTO105
159 IFC=25ANDAA=0THENGOTO105
160 IFC=25ANDAA=1THENPRINT"{home}{white}{down}{right}DER BAUM SIEHT INTRESSANT AUS!":GOSUB749:GOSUB512:GOTO105
161 IFCR=16THENPRINT"{white}{home}{down}{right:2}EIN HEILZAUBER IST UNTER DEM BAUM":GOSUB749:GOSUB512:GOTO105
162 IFCR=16THENPRINT"{white}{home}{down}{right:2}EIN FEUERZAUBER IST IN DEN KATAKOMBEN":GOSUB749:GOSUB512:GOTO105
163 IFC=49THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB749:H(2)=1:GOSUB550:P3(1)=1:GOTO95
164 IFC=52THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB749:H(5)=1:GOSUB550:P3(2)=1:GOTO95
165 IFC=51THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB749:H(9)=1:GOSUB550:P3(3)=1:GOTO95
166 IFC=47THENPRINT"{white}{home}{down}{right:2}DU KANNST HIER NICHT DURCH!":GOSUB749:FF=4:H(4)=1:GOSUB550:GOTO247
167 IFC=72THENPRINT"{white}{home}{down}{right:2}ACH WIE SUESS!":GOSUB749:FF=9:H(3)=1:GOSUB550:GOTO247
168 IFC=76THENPRINT"{white}{home}{down}{right:2}DU WILLST MICH BESIEGEN!":GOSUB749:FF=13:H(8)=1:GOSUB550:GOTO247
169 IFC=77THENPRINT"{white}{home}{down}{right:2}DU HAST MEINE ERWARTUNG UEBERTROFFEN!":GOSUB749:FF=14:H(10)=1:GOSUB550:GOTO247
170 IFC=47THENPRINT"{home}{white}{down}{right}DU BIST GEHEILT!":GOSUB749:GOSUB427:GOSUB439:GOSUB512:GOTO105
171 PRINT"{home}{down}{white}{right}  UNTERSUCHE  EQUIPMENTS  INVENTAR"
172 M=0
173 POKEQS+42+M*12,35
174 GOSUB712
175 POKEQS+42+M*12,32
176 IFA$="A"THENM=M-1-3*(M=0):GOTO173
177 IFA$="D"THENM=M+1+3*(M=2):GOTO173
178 IFA$=CHR$(13)THENPOKEQS+42+M*7,32:GOTO180
179 GOTO173
180 PRINT"{home}{down}{white}{right}                                      ";
181 ONM+1GOTO235,195,182
182 POKE1020,Q1
183 GOSUB508
184 PRINT"{home}{down}{right}{white}INVENTAR: WAEHLE DEIN ITEM           ";
185 MT=2:MX=2:MY=4:MC=14:GOSUB450
186 IFIN(M)=9THEN100
187 IFIN(M)=0ORIT(IN(M))<>MTTHEN100
188 IFIN(M)=3THENGOSUB191:IN(M)=0:GOSUB439:GOTO100
189 IFIN(M)=8THENGOSUB193:IN(M)=0:GOSUB439:GOTO100
190 GOTO100
191 FORI=0TO3:PH(I)=PH(I)+IE(IN(M)):IFPH(I)>P1(I)THENPH(I)=P1(I)
192 FH(I)=PH(I):NEXT:RETURN
193 FORI=0TO3:PM(I)=PM(I)+IE(IN(M)):IFPM(I)>P2(I)THENPM(I)=P2(I)
194 FM(I)=PM(I):NEXT:RETURN
195 POKE1020,Q1
196 GOSUB514
197 PRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINEN SPIELER     ";
198 P=0
199 CP=QS+2+(4-8*(P>1))*40:IFP=1ORP=3THENCP=CP+20
200 POKECP,35
201 GOSUB712
202 POKECP,32
203 IFP=0ANDA$="D"ANDP3(1)=1THENP=1:GOTO199
204 IFP=0ANDA$="S"ANDP3(2)=1THENP=2:GOTO199
205 IFP=0ANDA$="D"ANDP3(1)=OANDP3(2)=0ANDP3(3)=1THENP=3:GOTO199
206 IFP=0ANDA$="S"ANDP3(1)=OANDP3(2)=0ANDP3(3)=1THENP=3:GOTO199
207 IFP=1ANDA$="A"ANDP3(0)=1THENP=0:GOTO199
208 IFP=1ANDA$="S"ANDP3(3)=1THENP=3:GOTO199
209 IFP=2ANDA$="D"ANDP3(3)=1THENP=3:GOTO199
210 IFP=2ANDA$="W"ANDP3(0)=1THENP=0:GOTO199
211 IFP=3ANDA$="A"ANDP3(2)=1THENP=2:GOTO199
212 IFP=3ANDA$="W"ANDP3(1)=1THENP=1:GOTO199
213 IFP=3ANDA$="A"ANDP3(2)=OANDP3(1)=0ANDP3(0)=1THENP=0:GOTO199
214 IFP=3ANDA$="W"ANDP3(2)=OANDP3(1)=0ANDP3(0)=1THENP=0:GOTO199
215 IFA$=CHR$(13)THEN217
216 GOTO200
217 SL=0:CP=CP+81
218 PRINT"{home}{down}{right}{white}EQUIPMENT: WAFFE ODER RUESTUNG       ";
219 POKECP+SL*40,35
220 GOSUB712
221 POKECP+SL*40,32:IFA$="W"ORA$="S"THENSL=1-SL:GOTO219
222 IFA$=CHR$(13)THEN224
223 GOTO219
224 MY=4:MX=23:MC=14:IFP=1ORP=3THENMX=3
225 IFSL=0THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE WAFFFE       ";
226 IFSL=1THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE RUESTUNG     ";
227 PRINT"{home}"LEFT$(CD$,MY-1)SPC(MX-3);"{brown}{$c1}{$c2:18}{$c3}";
228 FORI=0TO13:PRINT"{down}{left:20}{$c4}                  {$c5}";:NEXT
229 PRINT"{down}{left:20}{$c6}{$c7:18}{$c8}";
230 MT=SL:GOSUB450
231 IFIT(IN(M))=9THEN100
232 IFIN(M)<>0ANDIT(IN(M))<>SLTHEN100
233 IFSL=0THENQ=PW(P):PW(P)=IN(M):IN(M)=Q:GOTO100
234 IFSL=1THENQ=PR(P):PR(P)=IN(M):IN(M)=Q:GOTO100
235 GOSUB512
236 GOSUB754
237 EI=0
238 IFER(EI)<>CROREX(EI)<>OXOREY(EI)<>OYTHEN243
239 NI=ET(EI):GOSUB545
240 IFIS=IXTHENPRINT"{home}{white}{down}{right}DU KANNST NICHT MEHR TRAGEN!":GOSUB749:GOSUB512:GOTO105
241 ER(EI)=-1
242 PRINT"{home}{white}{down}{right}GEFUNDEN: ";I$(IN(IS));:GOSUB740:GOSUB512:GOTO105
243 EI=EI+1
244 IFEI=ECTHEN246
245 GOTO238
246 PRINT"{home}{down}{right}{white}NICHTS BESONDERES!":GOSUB749:GOSUB512:GOTO105
247 FORI=4TO11:FT(I)=-1:NEXT
248 PP=4:CC=4:WX=0
249 FORI=0TO3:FT(I)=-2:FH(I)=PH(I):FM(I)=PM(I):FS(I)=PS(I)
250 IFP3(I)=0THENFH(I)=-1
251 IFFH(I)<=0THENFT(I)=-1:PP=PP-1
252 FA(I)=PA(I)+IA(PW(I)):FD(I)=PD(I)+ID(PR(I)):F1(I)=P1(I):F2(I)=P2(I)
253 NEXT
254 PRINT"{brown}{home}{$c1}{$c2:38}{$c3}";
255 PRINT"{$c4}                                      {$c5}";
256 PRINT"{$c6}{$c7:38}{$c8}";
257 PRINT"{home}{down:3}{white}";:FORI=0TO15:PRINT"                                        ";:NEXT
258 PRINTDD$;"{brown}{up}{$c1}{$c2:7}{white}HP{brown}{$c2}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
259 GOSUB408
260 GOSUB439
261 GOSUB447
262 FORI=0TO11
263 IFFT(I)<>-1THENFC(I)=INT(RND(0)*FD(I))
264 NEXT
265 CC=CC-4
266 PRINT"{home}{down}{right}                       ";:GOSUB577
267 FORI=4TO11:IFFT(I)=-1THEN269
268 PRINT"{home}"LEFT$(CD$,FY(I));SPC(FX(I));MD$(FT(I))
269 NEXT
270 C=0
271 IFFT(C)<>-1ANDFC(C)>=100THENFC(C)=FC(C)-100:GOTO274
272 C=C+1:IFC<12THEN271
273 FORI=0TO11:FC(I)=FC(I)+FS(I):NEXT:GOTO270
274 IFFT(C)=-2THEN276
275 GOTO357
276 PRINT"{home}{white}{down}{right}"P$(C)": IST AM ZUG                     "
277 PRINTDD$;SPC(16)"{white} ANGRIFF {down}{left:9} INVENTAR";:M=2
278 PRINTDD$;SPC(16)"{down:2}         {down}{left:9}         ";
279 IFPM(C)>0THENPRINTDD$;SPC(16)"{down:2} MAGIE   ";:M=3
280 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(37)"  {down}{left:2}  ";
281 PRINT"{left:3}{up}"PD$(C);
282 MY=0:FZ=0
283 POKEQS+816+MY*40,35
284 GOSUB712
285 IFA$="S"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY+1:GOTO289
286 IFA$="W"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY-1:GOTO289
287 IFA$=CHR$(13)THENFZ=0:GOTO292
288 GOTO284
289 IFMY>=MTHENMY=0
290 IFMY<0THENMY=M-1
291 GOTO283
292 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(36)"{white}  {down}{left:2}  {left}{up}"PD$(C);
293 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
294 ONMY+1GOTO320,312
295 MT=3:MX=16:MY=20:MC=4:GOSUB450
296 IFIT(IN(M))=9THEN276
297 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         {down}{left:9}         ";
298 IFIN(M)=0ORIT(IN(M))<>3THEN276
299 M=IN(M)
300 IFIE(M)>FM(C)THENPRINT"{home}{down}{right}NICHT GENUG MANA                     ";:GOSUB749:GOTO276
301 GOSUB593
302 GOTO619
303 AP=IE(M)+2-INT(RND(0)*5):FM(C)=FM(C)-IE(M):PM(C)=FM(C):GOSUB439
304 AP=AP+PL(C)*3
305 FH(CE)=FH(CE)-AP:IFFH(CE)<0THENFH(CE)=0
306 IFCE<4THENPH(CE)=FH(CE)
307 IFFH(CE)>0THENGOTO342
308 IFFT(CE)<>-2THENCC=CC-1:WX=WX+MX(FT(CE))
309 IFFT(CE)=-2THENPP=PP-1
310 FT(CE)=-1
311 GOTO342
312 MT=2:MX=16:MY=20:MC=4:GOSUB450
313 IFIT(IN(M))=9THEN276
314 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
315 PRINT"{down}{left:9}         ";
316 IFIN(M)=0ORIT(IN(M))<>MTTHEN276
317 IFIN(M)=3THENGOSUB191:IN(M)=0:GOSUB439:GOTO266
318 IFIN(M)=8THENGOSUB193:IN(M)=0:GOSUB439:GOTO266
319 GOTO266
320 GOSUB593
321 AP=FA(C)-FD(CE)
322 IFAP<=0THENPRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{white}MISS";:GOSUB747:GOTO334
323 AP=INT(RND(0)*AP)+1
324 FH(CE)=FH(CE)-AP:IFFH(CE)<0THENFH(CE)=0
325 IFCE<4THENPH(CE)=FH(CE)
326 IFFH(CE)>0THENGOTO331
327 IFFT(CE)<>-2THENCC=CC-1:WX=WX+MX(FT(CE))
328 IFFT(CE)=-2THENPP=PP-1
329 FT(CE)=-1
330 GOTO331
331 POKEV,(FX(CE)*8+21)AND255:POKEV+1,FY(CE)*8+50:POKE50168,16:POKEV+28,0
332 POKEV+16,-(FX(CE)>30):POKEV+21,1
333 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{white}";AP
334 FORI=0TO5:POKEV,PEEK(V)+2:POKEV+1,PEEK(V+1)+2:GOSUB748:NEXTI
335 POKEV+21,0:IFCE<4THENGOSUB439
336 GOSUB747
337 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
338 IFFT(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE));"  {down}{left:2}  ";
339 IFCC=0THEN365
340 IFPP=0THEN393
341 GOTO266
342 POKEV,(FX(CE)*8+24)AND255:POKEV+1,FY(CE)*8+50
343 IFM=4THENPOKEV+37,8:POKEV+38,2:POKEV+39,7:POKEV+28,1
344 B=17:IFM=6THENB=19:POKEV+28,0
345 IFM=5THENB=21:POKEV+28,0
346 IFM=7THENB=23:POKEV+28,0
347 POKEV+16,-(FX(CE)>30):POKE50168,B:POKEV+21,1
348 PRINT"{white}";:IFM=9THENPRINT"{green}";
349 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}";AP
350 FORK=0TO3:FORI=0TO1:POKE50168,B+I:FORJ=1TO40:NEXTJ,I,K
351 POKEV+21,0:IFCE<4THENGOSUB439
352 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
353 IFFT(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE));"  {down}{left:2}  ";
354 IFCC=0THEN365
355 IFPP=0THEN393
356 GOTO266
357 GOSUB747
358 PRINT"{white}{home}{down}{right}"M$(FT(C))": GREIFT AN                    "
359 FC(C)=FC(C)-100
360 IFFT(C)=7THENIFFM(C)>=IE(9)THENM=9:GOSUB747:GOSUB402:GOTO622
361 IFFT(C)=9THENIFFM(C)>=IE(6)THENM=6:GOSUB747:GOSUB396:GOTO303
362 IFFT(C)=10THENIFFM(C)>=IE(4)THENM=4:GOSUB747:GOSUB396:GOTO303
363 IFFT(C)=11THENIFFM(C)>=IE(12)THENM=12:GOSUB747:GOSUB396:GOTO629
364 GOSUB747:GOSUB396:GOTO321
365 POKE1020,Q1
366 PRINTDD$;"{brown}{up}{$c1}{$c2:10}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}{white}";
367 PRINT"{home}{down}{right}DU HAST GEWONNEN                     ";:GOSUB577
368 GOSUB508
369 FORI=0TO3:IFP3(I)=0THEN373
370 PRINT"{home}{white}{right:3}";:IFPH(I)=0THENPRINT"{red}";
371 PRINTLEFT$(CD$,4+I*3)P$(I)
372 PRINT"{home}"LEFT$(CD$,5+I*3)"{right:3}LV";PL(I)+1;" EX";PX(I)
373 NEXT
374 FORI=0TO3:IFPH(I)=0ORP3(I)=0THEN384
375 TX=WX
376 PRINT"{home}{white}"LEFT$(CD$,5+I*3)SPC(20)"EXPERIENCE {left}"TX"{left} ";
377 IFTX=0THEN384
378 XS=1:IFTX>9THENXS=10
379 IFTX>99THENXS=100
380 TX=TX-XS
381 PX(I)=PX(I)+XS
382 LL=10+PL(I)*PL(I)*10:IFPX(I)>=LLTHENGOSUB386:GOTO382
383 PRINT"{home}{white}"LEFT$(CD$,5+I*3)"{right:3}LV";PL(I)+1;" EX";PX(I):GOTO376
384 NEXTI
385 GOTO391
386 PL(I)=PL(I)+1:PA(I)=PA(I)+1:PD(I)=PD(I)+1:PS(I)=PS(I)+1
387 P1(I)=P1(I)+PL(I)*2:IFP2(I)>0THENP2(I)=P2(I)+PL(I)*3
388 IFP1(I)>999THENP1(I)=999
389 IFP2(I)>999THENP2(I)=999
390 RETURN
391 GOSUB740
392 GOTO100
393 PRINT"{home}{down}{right}{white}DU HAST VERLOREN                     "
394 GOSUB740
395 GOTO637
396 RE=INT(RND(0)*4):CE=0
397 IFFT(CE)=-2ANDPH(CE)>0THEN400
398 CE=CE+1:IFCE=4THENCE=0
399 GOTO397
400 IFRE>0THENRE=RE-1:GOTO398
401 RETURN
402 RE=INT(RND(0)*8):CE=4
403 IFFT(CE)>=0THEN406
404 CE=CE+1:IFCE=12THENCE=4
405 GOTO403
406 IFRE>0THENRE=RE-1:GOTO404
407 RETURN
408 IFFF>0THENL=FF:GOTO410
409 L=PEEK(CP+164):IFL=0THENRETURN
410 IFL=3THENA=0:LC=INT(RND(0))*2+2:FORI=0TOLC:GOSUB571:NEXT:RETURN
411 IFL=1THENA=1:FORI=0TO2:GOSUB571:NEXT:RETURN
412 IFL=2THENA=1:FORI=0TO1:GOSUB571:NEXT:A=2:FORI=0TO1:GOSUB571:NEXT:RETURN
413 IFL=4THENA=3:GOSUB571:A=1:GOSUB571:GOSUB571:RETURN
414 IFL=5THENA=4:LC=INT(RND(0)*3)+1:FORI=0TOLC:GOSUB571:NEXT:RETURN
415 IFL=6THENONINT(RND(1)*2)+1GOTO425,426
416 IFL=7THENLC=INT(RND(0)*3)+1:A=6:FORI=0TOLC:GOSUB571:NEXT:RETURN
417 IFL=8THENA=7:GOSUB571:LC=INT(RND(0)*2)+1:A=6:GOSUB571:RETURN
418 IFL=9THENA=9:GOSUB571:A=8:GOSUB571:A=5:GOSUB571:RETURN
419 IFL=10THENA=6:LC=INT(RND(0)*2)+1:FORI=1TOLC:GOSUB571:NEXT:RETURN
420 IFL=11THENA=7:GOSUB571:A=10:GOSUB571:RETURN
421 IFL=12ANDINT(RND(1)*2)=1THENA=11:GOSUB571:RETURN
422 IFL=12THENA=5:GOSUB571:GOSUB571:A=9:GOSUB571:RETURN
423 IFL=13THENA=9:GOSUB571:A=12:GOSUB571:A=9:GOSUB571:RETURN
424 IFL=14THENA=9:GOSUB571:A=13:GOSUB571:A=7:GOSUB571:RETURN
425 A=5:LC=INT(RND(1)*2)+1:FORI=0TOLC:GOSUB571:NEXT:RETURN
426 A=9:LC=INT(RND(1)*2)+1:FORI=1TOLC:GOSUB571:NEXT:RETURN
427 FORI=0TO3
428 IFP3(I)=0THEN431
429 PH(I)=P1(I)
430 PM(I)=P2(I)
431 NEXT:RETURN
432 PRINT"{brown}{home}{$c1}{$c2:38}{$c3}";
433 PRINT"{$c4}                                      {$c5}";
434 PRINT"{$c6}{$c7:38}{$c8}";
435 PRINTDD$;"{brown}{up}{$c1}{$c2:10}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
436 FORI=0TO3:PRINT"{$c4}          {$c5}{$c4}            {$c5}{$c4}            {$c5}";:NEXT
437 PRINT"{$c6}{$c7:10}{$c8}{$c6}{$c7:12}{$c8}{$c6}{$c7:12}";
438 POKE50151,72:POKE56295,9:RETURN
439 PRINTDD$;
440 FORI=0TO3:IFP3(I)=0THENPRINTSPC(40);:GOTO446
441 PRINT"{white}";:IFPH(I)=0THENPRINT"{red}";
442 PRINTSPC(27)P$(I)" ";:A=PH(I):GOSUB710
443 PRINTD$;
444 A=PM(I):GOSUB710
445 PRINT" "D$"{right}";
446 NEXT:RETURN
447 FORI=0TO3:IFPH(I)<=0ORP3(I)=0THENNEXT:RETURN
448 PRINT"{home}"LEFT$(CD$,5+I*3);SPC(37);PD$(I):NEXT
449 RETURN
450 GOSUB485
451 MI=-1:M=0:MO=0
452 MI=MO-1:CY=MY:MM=0
453 IFMC=4THENGOSUB480
454 MI=MI+1
455 PRINT"{home}{white}"LEFT$(CD$,CY)SPC(MX)" ";
456 IFIT(IN(MI))<>MTTHENPRINT"{cyan}";
457 IFIT(IN(MI))=9THENPRINT"{white}";
458 IFIT(IN(MI))=-1THENPRINT"{cyan}";
459 PRINTI$(IN(MI));
460 CY=CY+1
461 MM=MM+1
462 IFMM=MCTHEN464
463 IFMI<13THEN454
464 CP=QS+MX+MY*40
465 POKECP+(M-MO)*40,35
466 GOSUB712
467 POKECP+(M-MO)*40,32
468 IFA$="W"THENM=M-1:GOTO472
469 IFA$="S"THENM=M+1:GOTO472
470 IFA$=CHR$(13)THENRETURN
471 GOTO465
472 IFM=-1THENM=14-1:MO=14-MC:GOTO452
473 IFM=14THENM=0:MO=0:GOTO452
474 M%=0
475 IFM<MOTHENMO=MO-MC:M%=1:GOTO475
476 IFM>=MO+MCTHENMO=MO+MC:M%=1
477 IFMO<0THENMO=0:M%=1
478 IFM%=1THEN452
479 GOTO465
480 FORI=0TO3
481 PRINT"{home}{white}"LEFT$(CD$,CY+I)SPC(MX)" ";
482 PRINT"        ";
483 NEXTI
484 RETURN
485 A=0
486 FORI=0TO99:TT(I)=0:NEXTI
487 FORI=0TO99:TT(I)=IN(I):IN(I)=0:NEXTI
488 IFMT=0THENGOSUB493:GOSUB496:A=50:GOSUB499:GOSUB502:GOSUB505
489 IFMT=1THENGOSUB493:GOSUB499:A=50:GOSUB496:GOSUB502:GOSUB505
490 IFMT=2THENGOSUB493:GOSUB502:A=50:GOSUB496:GOSUB499:GOSUB505
491 IFMT=3THENGOSUB493:GOSUB505:A=50:GOSUB496:GOSUB499:GOSUB502
492 RETURN
493 FORI=0TO99:IFIT(TT(I))=9THENIN(A)=TT(I):A=A+1
494 NEXTI
495 RETURN
496 FORI=0TO99:IFIT(TT(I))=0THENIN(A)=TT(I):A=A+1
497 NEXTI
498 RETURN
499 FORI=0TO99:IFIT(TT(I))=1THENIN(A)=TT(I):A=A+1
500 NEXTI
501 RETURN
502 FORI=0TO99:IFIT(TT(I))=2THENIN(A)=TT(I):A=A+1
503 NEXTI
504 RETURN
505 FORI=0TO99:IFIT(TT(I))=3THENIN(A)=TT(I):A=A+1
506 NEXTI
507 RETURN
508 PRINT"{brown}{home}{down:3}{$c1}{$c2:38}{$c3}";
509 FORI=0TO13
510 PRINT"{$c4}                                      {$c5}";:NEXT
511 PRINT"{$c6}{$c7:38}{$c8}";:RETURN
512 PRINT"{home}{down}{right}{white}                                      ";
513 RETURN
514 PRINT"{brown}{home}{down:3}{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
515 FORI=0TO5
516 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
517 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
518 PRINT"{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
519 FORI=0TO5
520 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
521 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
522 DX=3:DY=4:P=0:GOSUB525:DX=23:P=1:GOSUB525
523 DX=3:DY=12:P=2:GOSUB525:DX=23:P=3:GOSUB525
524 RETURN
525 IFP3(P)=0THENRETURN
526 PRINT"{home}{white}";:IFPH(P)=0THENPRINT"{red}";
527 PRINT"{home}"LEFT$(CD$,DY)SPC(DX-1)" "P$(P)"  LV"PL(P)+1
528 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX)" "I$(PW(P));
529 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX)" "I$(PR(P));
530 T1=PA(P)+IA(PW(P))
531 T2=PD(P)+ID(PR(P))
532 IFT1>999THENT1=999
533 IFT2>999THENT2=999
534 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX+9)"ATK"T1;
535 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX+9)"DEF"T2;
536 RETURN
537 IFPEEK(2)=1THENSP=30960+RS*(CR-0)
538 FORI=0TO7
539 PRINT"{home}{down:3}"LEFT$(CD$,I*2);
540 FORJ=0TO19
541 PRINTT$(PEEK(SP))"{up}";
542 SP=SP+1
543 NEXTJ,I
544 RETURN
545 IS=0
546 IFIN(IS)=0THENIN(IS)=NI:NI=-1:RETURN
547 IS=IS+1
548 IFIS=IXTHENRETURN
549 GOTO546
550 F=15-H(0)*15:C=30960+0*RS:POKEC+8+4*20,F+1
551 F=13-H(1)*13:C=30960+16*RS:POKEC+12+1*20,F:POKEC+12+2*20,F:POKEC+11+1*20,F
552 POKE30960+00*RS+13+5*20,49-H(2)*49
553 POKE30960+00*RS+06+5*20,52-H(5)*52
554 POKE30960+00*RS+06+3*20,51-H(9)*51
555 POKE30960+16*RS+00+0*20,14-H(3)*47
556 POKE30960+16*RS+01+0*20,14-H(4)*72
557 POKE30960+16*RS+02+0*20,14-H(8)*76
558 POKE30960+16*RS+03+0*20,14-H(10)*77
559 POKE30960+16*RS+3+5*20,16-H(6)*16
560 POKE30960+16*RS+8+2*20,11-H(7)*11
561 POKE30960+16*RS+15+2*20,6-H(12)*6
562 POKE30960+16*RS+15+6*20,6-H(13)*6
563 T=13:IFH(12)+H(13)=2THENT=0
564 POKE30960+16*RS+08+0,T
565 RETURN
566 IFCR=4ANDZX=6ANDZY=4ANDH(0)=0THENPRINT"{home}{white}{down}{right}DER SCHALTER IST AUS!";:GOSUB749:GOSUB512
567 IFCR=4ANDZX=6ANDZY=4ANDH(0)=1THENPRINT"{home}{white}{down}{right}DER SCHALTER IST EIN!";:GOSUB749:GOSUB512
568 IFCR=4ANDZX=14ANDZY=4ANDH(1)=0THENPRINT"{home}{white}{down}{right}DER SCHALTER IST AUS!";:GOSUB749:GOSUB512
569 IFCR=4ANDZX=14ANDZY=4ANDH(1)=1THENPRINT"{home}{white}{down}{right}DER SCHALTER IST EIN!";:GOSUB749:GOSUB512
570 RETURN
571 IFCC>=11THENRETURN
572 FH(CC)=MH(A):FM(CC)=MM(A):FT(CC)=A:FC(CC)=INT(RND(0)*MS(A)):FS(CC)=MS(A)
573 FX(CC)=2:FY(CC)=5+CC*3:FA(CC)=MA(A):FD(CC)=MD(A):FB(CC)=MB(A)
574 F1(CC)=MH(A):F2(CC)=MM(A)
575 IFCC>=4THENFX(CC)=5:FY(CC)=5+(CC-4)*3
576 CC=CC+1:RETURN
577 ZM=0
578 FORJ=4TO7
579 IFFT(J)=-1THENZM=ZM-1
580 ZM=ZM+1
581 NEXTJ
582 IFZM<>CMTHENPRINTDD$;"{right}          {down}{left:10}          {down}{left:10}          {down}{left:10}          ";
583 PRINTDD$;
584 CM=0
585 FORI=4TO7
586 IFFT(I)=-1THENNEXT:RETURN
587 A=FH(I):GOSUB710
588 PRINT"{home}{right}"LEFT$(CD$,20+CM)M$(FT(I));
589 PRINT"{home}{right}"LEFT$(CD$,20+CM);"{right:7}";D$
590 CM=CM+1
591 NEXT
592 RETURN
593 CE=4:PRINT"{home}{down}{right}{white}"P$(C)": WAHELE DEIN ZIEL               "
594 IFFT(CE)=-1THEN603
595 POKEQS-1+FX(CE)+FY(CE)*40,35
596 GOSUB712
597 IFA$=CHR$(13)THENPOKEQS-1+FX(CE)+FY(CE)*40,32:RETURN
598 IFA$="S"THENGOTO603
599 IFA$="W"THENGOTO611
600 IFA$="D"ANDCE>3THENPOKEQS-1+FX(CE)+FY(CE)*40,32:CE=0:GOTO594
601 IFA$="A"ANDCE<4THENPOKEQS-1+FX(CE)+FY(CE)*40,32:CE=4:GOTO594
602 GOTO596
603 CO=CE
604 POKEQS-1+FX(CE)+FY(CE)*40,32
605 CE=CE+1
606 IFCE>=12THENCE=0
607 IFCO<4ANDCE>3THENCE=CO
608 IFCO>3ANDCE<4THENCE=CO
609 IFFT(CE)<>-1THEN595
610 GOTO605
611 CO=CE
612 POKEQS-1+FX(CE)+FY(CE)*40,32
613 CE=CE-1
614 IFCE<0THENCE=11
615 IFCO<4ANDCE>3THENCE=CO
616 IFCO>3ANDCE<4THENCE=CO
617 IFFT(CE)<>-1THEN595
618 GOTO613
619 IFFT(C)=-2THEN635
620 IFFT(C)=7ANDFM(C)>=IE(9)THENM=9:GOTO622
621 GOTO303
622 AP=IE(M)*4+2-INT(RND(0)*5):FM(C)=FM(C)-IE(M):IFC<4THENPM(C)=FM(C):GOSUB439
623 FH(CE)=FH(CE)+AP:IFFH(CE)>F1(CE)THENFH(CE)=F1(CE)
624 IFCE<4THENPH(CE)=FH(CE):GOSUB439
625 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{green}";AP
626 GOSUB754
627 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
628 GOTO266
629 FM(C)=FM(C)-IE(M):IFC<4THENPM(C)=FM(C):GOSUB439
630 FS(CE)=FS(CE)/2:IFFS(CE)<5THENFS(CE)=5
631 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{green}SLOW";
632 GOSUB754
633 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
634 GOTO266
635 IFM=9THEN622
636 GOTO303
637 FORI=0TOHC-1:H(I)=0:NEXT
638 FORI=0TO3:PX(I)=0:NEXT
639 PA(0)=5:PD(0)=2:PA(1)=4:PD(1)=3:PA(2)=1:PD(2)=1:PA(3)=1:PD(3)=1:GOSUB550
640 FORI=0TO13:IN(I)=0:NEXT
641 IN(0)=18
642 ER(1)=0:EX(1)=16:EY(1)=5:ET(1)=3
643 ER(2)=0:EX(2)=3:EY(2)=5:ET(2)=15
644 ER(3)=16:EX(3)=0:EY(3)=0:ET(3)=4
645 ER(4)=16:EX(4)=0:EY(4)=0:ET(4)=8
646 ER(5)=16:EX(5)=0:EY(5)=0:ET(5)=5
647 ER(6)=16:EX(6)=0:EY(6)=0:ET(6)=16
648 ER(7)=16:EX(7)=0:EY(7)=0:ET(7)=17
649 ER(8)=16:EX(8)=0:EY(8)=0:ET(8)=13
650 ER(9)=16:EX(9)=0:EY(9)=0:ET(9)=15
651 ER(10)=16:EX(10)=0:EY(10)=0:ET(10)=11
652 ER(11)=16:EX(11)=0:EY(11)=0:ET(11)=7
653 ER(12)=16:EX(12)=0:EY(12)=0:ET(12)=9
654 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
655 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
656 PRINT"{$c6}{$c7:38}{up:3}";
657 POKE50151,72:POKE56295,9
658 X=13:Y=3:CR=0
659 P3(0)=1:P3(1)=0:P3(2)=0:P3(3)=0
660 P1(0)=15:P1(1)=17:P1(2)=9:P1(3)=9:P2(0)=0:P2(1)=0:P2(2)=7:P2(3)=23
661 FORI=0TO3:PH(I)=P1(I):PM(I)=P2(I):PX(I)=0:PW(I)=0:PR(I)=0:NEXT
662 PW(0)=1:PR(0)=2
663 GOSUB550
664 AA$="INTRO":GOSUB698
665 GOTO95
666 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
667 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
668 PRINT"{$c6}{$c7:38}{up:3}";
669 POKE50151,72:POKE56295,9
670 AA$="ENDE":GOSUB698
671 GOTO95
672 OPEN1,8,4,"PFSAVE":OPEN2,8,15:INPUT#2,ER,M$,T,SS
673 IFER<>0THENPRINT"{home}{white}{down}{right}ERROR LOADING    ";:CLOSE2:CLOSE1:GOTO637
674 FORI=0TO3:INPUT#1,PH(I):INPUT#1,PM(I):INPUT#1,PS(I):INPUT#1,P1(I)
675 INPUT#1,P2(I):INPUT#1,P3(I)
676 INPUT#1,PA(I):INPUT#1,PD(I):INPUT#1,PW(I):INPUT#1,PR(I):INPUT#1,PX(I)
677 INPUT#1,PL(I):NEXT
678 FORI=0TO13:INPUT#1,IN(I):NEXT:FORI=0TOHC-1:INPUT#1,H(I):NEXT
679 FORI=0TOEC-1:INPUT#1,ER(I):NEXT
680 INPUT#1,X:INPUT#1,Y:INPUT#1,CR
681 CLOSE1:CLOSE2:GOSUB550:RETURN
682 OPEN15,8,15,"S:PFSAVE":CLOSE15
683 OPEN1,8,4,"PFSAVE,W"
684 FORI=0TO3:PRINT#1,PH(I):PRINT#1,PM(I):PRINT#1,PS(I):PRINT#1,P1(I)
685 PRINT#1,P2(I):PRINT#1,P3(I)
686 PRINT#1,PA(I):PRINT#1,PD(I):PRINT#1,PW(I):PRINT#1,PR(I):PRINT#1,PX(I)
687 PRINT#1,PL(I)
688 NEXT
689 FORI=0TO13:PRINT#1,IN(I):NEXT:FORI=0TOHC-1:PRINT#1,H(I):NEXT
690 FORI=0TOEC-1:PRINT#1,ER(I):NEXT
691 PRINT#1,X:PRINT#1,Y:PRINT#1,CR
692 CLOSE1:RETURN
693 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
694 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
695 PRINT"{$c6}{$c7:38}{up:3}";
696 POKE50151,72:POKE56295,9
697 RETURN
698 POKE1020,Q7
699 POKE56322,224
700 IFAA$="INTRO"THENOPEN1,8,4,"TXT.INTRO,S,R"
701 IFAA$="ENDE"THENOPEN1,8,4,"TXT.ENDE,S,R"
702 IFAA$=""THENRETURN
703 GET#1,B$
704 IFB$="$"THENPRINT"{home}{down:2}{right:2}{white}";:GOTO703
705 IFB$="%"THENPRINT"{right:2}{white}";:GOTO703
706 IFB$="#"THENCLOSE1:POKE56322,255:GOSUB740:RETURN
707 PRINTB$;
708 IFB$<>" "THENPOKE162,0:WAIT162,4
709 GOTO703
710 T$=STR$(A)
711 D$=RIGHT$("000"+RIGHT$(T$,LEN(T$)-1),3):RETURN
712 A$=""
713 J=PEEK(56320)
714 IFJ=127THEN716
715 GOTO713
716 J=PEEK(56320)
717 IFJ=127ANDA$=CHR$(13)THENRETURN
718 IFJ=107THENA$="E":GOSUB745:RETURN
719 IFJ=103THENA$="E":GOSUB745:RETURN
720 IFJ=110THENA$="E":GOSUB745:RETURN
721 IFJ=109THENA$="E":GOSUB745:RETURN
722 IFJ=123THENA$="A":GOSUB745:RETURN
723 IFJ=119THENA$="D":GOSUB745:RETURN
724 IFJ=126THENA$="W":GOSUB745:RETURN
725 IFJ=125THENA$="S":GOSUB745:RETURN
726 IFJ=111THENA$=CHR$(13)
727 GOTO716
728 A$=""
729 J=PEEK(56320)
730 IFJ<>111ANDJ<>107ANDJ<>103ANDJ<>110ANDJ<>109THEN732
731 GOTO729
732 J=PEEK(56320)
733 IFJ=127ANDA$=CHR$(13)THENRETURN
734 IFJ=123THENA$="A":GOSUB746:RETURN
735 IFJ=119THENA$="D":GOSUB746:RETURN
736 IFJ=126THENA$="W":GOSUB746:RETURN
737 IFJ=125THENA$="S":GOSUB746:RETURN
738 IFJ=111THENA$=CHR$(13)
739 GOTO732
740 A$=""
741 J=PEEK(56320)
742 IFJ=127ANDA$=CHR$(13)THENRETURN
743 IFJ=111THENA$=CHR$(13)
744 GOTO741
745 POKE162,0:WAIT162,16:RETURN
746 POKE162,0:WAIT162,8:RETURN
747 POKE162,0:WAIT162,32:RETURN
748 POKE162,0:WAIT162,2:RETURN
749 POKE162,0:WAIT162,32
750 POKE162,0:WAIT162,32
751 POKE162,0:WAIT162,32
752 POKE162,0:WAIT162,32
753 RETURN
754 POKE162,0:WAIT162,32
755 RETURN
756 POKE162,0:WAIT162,4
757 RETURN
758 DATA"IMP"    ,5   ,0  ,8  ,4  ,1  ,0 ,3
759 DATA"SNAKE"  ,8   ,0  ,12 ,6  ,1  ,0 ,5
760 DATA"VIPER"  ,12  ,0  ,18 ,6  ,1  ,0 ,9
761 DATA"NACHO"  ,30  ,0  ,22 ,15 ,5  ,0 ,50
762 DATA"GOLEM"  ,20  ,0  ,4  ,20 ,8  ,0 ,17
763 DATA"MOSS"   ,30  ,0  ,7  ,26 ,9  ,0 ,21
764 DATA"BONES"  ,50  ,0  ,20 ,18 ,8  ,0 ,15
765 DATA"WISP"   ,25  ,50 ,15 ,0  ,8  ,0 ,17
766 DATA"TROLL"  ,100 ,0  ,9  ,40 ,13 ,0 ,100
767 DATA"SPARK"  ,30  ,50 ,22 ,0  ,8  ,0 ,17
768 DATA"BURNER" ,80  ,90 ,30 ,25 ,13 ,0 ,42
769 DATA"TBERRY" ,120 ,10 ,60 ,60 ,20 ,0 ,150
770 DATA"DRACUL" ,250 ,60 ,50 ,70 ,25 ,0 ,500
771 DATA"GLADOS" ,500 ,70 ,60 ,80 ,30 ,0 ,1000
772 DATA"KRON",15 ,0  ,10 ,4 ,3 ,0 ,0
773 DATA"LENA",17 ,0  ,10 ,3 ,3 ,0 ,0
774 DATA"DOLM",9  ,7  ,8  ,1 ,1 ,0 ,0
775 DATA"MIRA",9  ,23 ,7  ,1 ,1 ,1 ,2
776 DATA"{gray1}{rvrs off}{$c9}{rvrs off}{gray1}{$ca}{down}{left:2}{rvrs off}{gray1}{$ca}{rvrs off}{gray1}{$c9}{rvrs off}"
777 DATA"{gray1}{rvrs off}{$cb}{rvrs off}{gray1}{$cc}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1}{$ca}{rvrs off}"
778 DATA"{gray1}{rvrs off}{$c9}{rvrs off}{gray1}{$ca}{down}{left:2}{rvrs off}{gray1}{$ca}{rvrs off}{gray1}{$c9}{rvrs off}"
779 DATA"{gray1}{rvrs off}{$cd}{rvrs off}{gray1}{$ce}{down}{left:2}{rvrs off}{gray1}{$cf}{rvrs off}{gray1}{$d0}{rvrs off}"
780 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
781 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
782 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
783 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
784 DATA"{orange}{rvrs off}{$d1}{rvrs off}{orange}{$d1}{down}{left:2}{rvrs off}{orange}{$d1}{rvrs off}{orange}{$d1}{rvrs off}"
785 DATA"{brown}{rvrs off}{$d2}{rvrs off}{brown}{$d3}{down}{left:2}{rvrs off}{brown}{$d4}{rvrs off}{brown}{$d5}{rvrs off}"
786 DATA"{lt. red}{rvrs off}{$d6}{rvrs off}{lt. red}{$d7}{down}{left:2}{rvrs off}{lt. red}{$d8}{rvrs off}{lt. red}{$d9}{rvrs off}"
787 DATA"{gray1}{rvrs off}{$da}{rvrs off}{gray1}{$db}{down}{left:2}{rvrs off}{gray1}{$dc}{rvrs off}{gray1}{$dd}{rvrs off}"
788 DATA"{gray1}{rvrs off}{$a2}{rvrs off}{gray1}{$a3}{down}{left:2}{rvrs off}{gray1}{$a4}{rvrs off}{gray1}{$a5}{rvrs off}"
789 DATA"{brown}{rvrs off}{$a1}{rvrs off}{brown}{$a1}{down}{left:2}{rvrs off}{brown}{$a1}{rvrs off}{brown}{$a1}{rvrs off}"
790 DATA"{gray1}{rvrs off}{$de}{rvrs off}{gray1}{$df}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1}{$ca}{rvrs off}"
791 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
792 DATA"{lt. red}{rvrs off}{$a6}{rvrs off}{lt. red}{$a7}{down}{left:2}{rvrs off}{lt. red}{cmdr pound}{rvrs off}{lt. red}{shft pound}{rvrs off}"
793 DATA"{lt. red}{rvrs off}{$aa}{rvrs off}{lt. red}{$aa}{down}{left:2}{rvrs off}{lt. red}{$aa}{rvrs off}{lt. red}{$aa}{rvrs off}"
794 DATA"{brown}{rvrs off}{$ab}{rvrs off}{brown}{$ab}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
795 DATA"{brown}{rvrs off}{$ab}{rvrs off}{brown}{$ad}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
796 DATA"{brown}{rvrs off}{$ad}{rvrs off}{brown}{$ab}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
797 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
798 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
799 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
800 DATA"{lt. green}{rvrs off}{$ae}{rvrs off}{lt. green}{$d1}{down}{left:2}{rvrs off}{lt. green}{$d1}{rvrs off}{lt. green}{$af}{rvrs off}"
801 DATA"{lt. green}{rvrs off}{$b0}{rvrs off}{lt. green}{$b1}{down}{left:2}{rvrs off}{lt. green}{$b2}{rvrs off}{lt. green}{$b3}{rvrs off}"
802 DATA"{lt. green}{rvrs off}{$b4}{rvrs off}{lt. green}{$b5}{down}{left:2}{rvrs off}{lt. green}{$b4}{rvrs off}{lt. green}{$b5}{rvrs off}"
803 DATA"{lt. green}{rvrs off}{$b6}{rvrs off}{lt. green}{$b7}{down}{left:2}{rvrs off}{lt. green}{$b8}{rvrs off}{lt. green}{$b9}{rvrs off}"
804 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
805 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
806 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
807 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
808 DATA"{brown}{rvrs off}{$ae}{rvrs off}{brown}{$d1}{down}{left:2}{rvrs off}{brown}{$d1}{rvrs off}{brown}{$af}{rvrs off}"
809 DATA"{brown}{rvrs off}{$b0}{rvrs off}{brown}{$b1}{down}{left:2}{rvrs off}{brown}{$b2}{rvrs off}{brown}{$b3}{rvrs off}"
810 DATA"{brown}{rvrs off}{$b4}{rvrs off}{brown}{$b5}{down}{left:2}{rvrs off}{brown}{$b4}{rvrs off}{brown}{$b5}{rvrs off}"
811 DATA"{brown}{rvrs off}{$b6}{rvrs off}{brown}{$b7}{down}{left:2}{rvrs off}{brown}{$b8}{rvrs off}{brown}{$b9}{rvrs off}"
812 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
813 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
814 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
815 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
816 DATA"{brown}{rvrs off}{$d1}{rvrs off}{brown}{$d1}{down}{left:2}{rvrs off}{brown}{$d1}{rvrs off}{brown}{$d1}{rvrs off}"
817 DATA"{brown}{rvrs off}{$ba}{rvrs off}{brown}{$d1}{down}{left:2}{rvrs off}{brown}{$ba}{rvrs off}{brown}{$d1}{rvrs off}"
818 DATA"{brown}{rvrs off}{$ba}{rvrs off}{brown}{$d1}{down}{left:2}{rvrs off}{brown}{$bb}{rvrs off}{brown}{$d1}{rvrs off}"
819 DATA"{brown}{rvrs off}{$bc}{rvrs off}{brown}{$bd}{down}{left:2}{rvrs off}{brown}{$be}{rvrs off}{brown}{$be}{rvrs off}"
820 DATA"{gray1}{rvrs off}{$bf}{rvrs off}{gray1}{$bf}{down}{left:2}{rvrs on}{gray1}@{rvrs on}{gray1}@{rvrs off}"
821 DATA"{gray1}{rvrs on}A{rvrs on}{gray1}B{down}{left:2}{rvrs on}{gray1}C{rvrs on}{gray1}D{rvrs off}"
822 DATA"{gray1}{rvrs on}E{rvrs on}{gray1}F{down}{left:2}{rvrs on}{gray1}G{rvrs on}{gray1}H{rvrs off}"
823 DATA"{gray1}{rvrs on}I{rvrs on}{gray1}I{down}{left:2}{rvrs on}{gray1}I{rvrs on}{gray1}I{rvrs off}"
824 DATA"{orange}{rvrs on}J{rvrs on}{orange}K{down}{left:2}{rvrs on}{orange}L{rvrs on}{orange}M{rvrs off}"
825 DATA"{gray1}{rvrs on}N{rvrs on}{gray1}K{down}{left:2}{rvrs on}{gray1}L{rvrs on}{gray1}M{rvrs off}"
826 DATA"{lt. green}{rvrs on}O{rvrs on}{lt. green}P{down}{left:2}{rvrs on}{lt. green}Q{rvrs on}{lt. green}R{rvrs off}"
827 DATA"{gray2}{rvrs on}S{rvrs on}{gray2}T{down}{left:2}{rvrs on}{gray2}U{rvrs on}{gray2}V{rvrs off}"
828 DATA"{gray1}{rvrs on}W{rvrs on}{gray1}X{down}{left:2}{rvrs on}{gray1}Y{rvrs on}{gray1}Z{rvrs off}"
829 DATA"{gray1}{rvrs on}[{rvrs on}{gray1}{pound}{down}{left:2}{rvrs on}{gray1}]{rvrs on}{gray1}{^}{rvrs off}"
830 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
831 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
832 DATA"{orange}{rvrs off} {rvrs off}{orange} {down}{left:2}{rvrs off}{orange} {rvrs off}{orange} {rvrs off}"
833 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
834 DATA"{lt. green}{rvrs off} {rvrs off}{lt. green} {down}{left:2}{rvrs off}{lt. green} {rvrs off}{lt. green} {rvrs off}"
835 DATA"{gray2}{rvrs off} {rvrs off}{gray2} {down}{left:2}{rvrs off}{gray2} {rvrs off}{gray2} {rvrs off}"
836 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
837 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
838 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
839 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
840 DATA"{lt. green}{rvrs on}{back arrow}{rvrs on}{lt. green}!{down}{left:2}{rvrs on}{lt. green}#{rvrs on}{lt. green}${rvrs off}"
841 DATA"{gray3}{rvrs on}%{rvrs on}{gray3}&{down}{left:2}{rvrs on}{gray3}'{rvrs on}{gray3}({rvrs off}"
842 DATA"{gray1}{rvrs on}%{rvrs on}{gray1}&{down}{left:2}{rvrs on}{gray1}'{rvrs on}{gray1}({rvrs off}"
843 DATA"{lt. green}{rvrs on}){rvrs on}{lt. green}*{down}{left:2}{rvrs on}{lt. green}+{rvrs on}{lt. green},{rvrs off}"
844 DATA"{gray1}{rvrs on}-{rvrs on}{gray1}.{down}{left:2}{rvrs on}{gray1}/{rvrs on}{gray1}0{rvrs off}"
845 DATA"{gray1}{rvrs on}1{rvrs on}{gray1}2{down}{left:2}{rvrs on}{gray1}3{rvrs on}{gray1}4{rvrs off}"
846 DATA"{gray1}{rvrs on}5{rvrs on}{gray1}6{down}{left:2}{rvrs on}{gray1}7{rvrs on}{gray1}8{rvrs off}"
847 DATA"{gray1}{rvrs on}9{rvrs on}{gray1}:{down}{left:2}{rvrs on}{gray1};{rvrs on}{gray1}<{rvrs off}"
848 DATA"{lt. green}{rvrs on}={rvrs on}{lt. green}>{down}{left:2}{rvrs on}{lt. green}?{rvrs on}{lt. green}{$c0}{rvrs off}"
849 DATA"{gray3}{rvrs on}9{rvrs on}{gray3}:{down}{left:2}{rvrs on}{gray3};{rvrs on}{gray3}<{rvrs off}"
850 DATA"{gray1}{rvrs on}5{rvrs on}{gray1}6{down}{left:2}{rvrs on}{gray1}7{rvrs on}{gray1}8{rvrs off}"
851 DATA"{lt. green}{rvrs on}5{rvrs on}{lt. green}6{down}{left:2}{rvrs on}{lt. green}7{rvrs on}{lt. green}8{rvrs off}"
852 DATA"{gray1}{rvrs on}{$c1}{rvrs on}{gray1}{$c2}{down}{left:2}{rvrs on}{gray1}{$c3}{rvrs on}{gray1}{$c4}{rvrs off}"
853 DATA"{gray1}{rvrs on}{$c5}{rvrs on}{gray1}{$c6}{down}{left:2}{rvrs on}{gray1}{$c7}{rvrs on}{gray1}{$c8}{rvrs off}"
854 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
855 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
856 DATA"        ",0    ,0    ,0    ,0    ,-1 
857 DATA"DOLCH   ",3    ,0    ,0    ,0    ,0  
858 DATA"HELM    ",0    ,2    ,-1   ,0    ,1  
859 DATA"KRAEUTER",0    ,0    ,-1   ,10   ,2  
860 DATA"FEUER   ",0    ,0    ,0    ,5    ,3  
861 DATA"EIS     ",0    ,0    ,0    ,8    ,3  
862 DATA"MUT     ",0    ,0    ,0    ,23   ,3  
863 DATA"BOMBE   ",0    ,0    ,0    ,50   ,3  
864 DATA"KIRSCHE ",0    ,0    ,-1   ,15   ,2  
865 DATA"HEILEN  ",0    ,0    ,0    ,4    ,3  
866 DATA"SKULL   ",0    ,0    ,0    ,0    ,4  
867 DATA"FLEGEL  ",7    ,0    ,-1   ,0    ,0  
868 DATA"LAHM    ",0    ,0    ,0    ,4    ,3  
869 DATA"STAB    ",12   ,0    ,0    ,0    ,0  
870 DATA"FEDER   ",0    ,10   ,0    ,0    ,1  
871 DATA"STOCK   ",23   ,0    ,0    ,0    ,0  
872 DATA"WESTE   ",0    ,14   ,0    ,0    ,1  
873 DATA"SCHILD  ",0    ,24   ,0    ,0    ,1  
874 DATA"ZURUECK ",0    ,0    ,0    ,0    ,9  
875 END
