0 POKE55,240:POKE56,120:CLR
1 QO=53282
2 QP=53283
3 QQ=53281
4 QR=53280
5 QS=49152
6 V=53248
7 Q1%=0
8 Q7%=6
9 QA%=9
10 RS=165
11 DD$="{home}{down:20}{white}"
12 CD$="{down:25}"
13 CT$="{home}{down}{white}{right}                                      "
14 DIMM$(13),MD$(13),MH%(13),MM%(13),MA%(13),MD%(13),MS%(13),MB%(13),MX%(13)
15 DIMFH%(11),FT%(11),FC%(11),FM%(11),FS%(11),FX%(11),FY%(11),FA%(11),FD(11),F1%(11),F2%(11)
16 FORI=0TO3:FX%(I)=37:FY%(I)=5+3*I:NEXT
17 DIMI$(18),IA%(18),ID%(18),IS%(18),IE%(18),IT%(18)
18 DIMER%(12),EX%(12),EY%(12),ET%(12)
19 DIMIN%(99),TT%(99)
20 DIMT$(79)
21 DIMS0%(10),S1%(10),S2%(10),S3%(10),A0%(10),A1%(10),A2%(10)
22 DIMN0%(10),N1%(10),N2%(8),N3%(10)
23 FORI=0TO13
24 READM$(I),MH%(I),MM%(I),MS%(I)
25 READMA%(I),MD%(I),MB%(I),MX%(I)
26 NEXT
27 FORI=0TO3
28 READP$(I),PH%(I),PM%(I),PS%(I)
29 READPA%(I),PD%(I),PW%(I),PR%(I)
30 NEXT
31 FORI=0TO79
32 READT$(I)
33 NEXT
34 PD$(0)=T$(48)
35 PD$(1)=T$(50)
36 PD$(2)=T$(52)
37 PD$(3)=T$(51)
38 ZZ=64
39 FORI=0TO13
40 MD$(I)=T$(ZZ)
41 ZZ=ZZ+1
42 NEXTI
43 FORI=0TO18
44 READI$(I),IA%(I),ID%(I),IS%(I),IE%(I),IT%(I)
45 NEXT
46 POKEV+21,0
47 POKE648,192
48 POKE56576,0
49 POKE53272,8
50 POKE53270,PEEK(53270)OR16
51 POKEQO,QA%
52 POKEQP,Q1%
53 POKEQR,Q1%
54 POKEQQ,Q7%
55 GOSUB146
56 GOTO624
57 POKE1020,Q7%
58 POKE1021,Q7%
59 SYS820
60 GOSUB440
61 GOSUB447
62 PRINT"{home}{down}{right}{white}                                      ";
63 POKE1020,Q7%
64 GOSUB545
65 IFPEEK(2)=1THENCP=30960+RS*(CR-0)
66 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)PD$(0);
67 OX=ZX:OY=ZY
68 ZX=X:ZY=Y
69 GOSUB676
70 IFA$=CHR$(145)ORA$="W"THENZY=Y-1:GOTO76
71 IFA$=CHR$(157)ORA$="A"THENZX=X-1:GOTO76
72 IFA$=CHR$(29)ORA$="D"THENZX=X+1:GOTO76
73 IFA$=CHR$(17)ORA$="S"THENZY=Y+1:GOTO76
74 IFA$=CHR$(13)THEN179
75 GOTO69
76 AA=0
77 FORI=0TO12
78 IFER%(I)=CRANDEX%(I)=ZXANDEY%(I)=ZYTHENAA=1
79 NEXTI
80 IFZX=-1ORZX=20ORZY=-1ORZY=8THEN92
81 C=PEEK(CP+ZX+ZY*20)
82 IFC=08THEN106
83 IFC=09THEN106
84 IFC=10THEN106
85 IFC=11THEN106
86 IFC=12THEN113
87 IFC=14THEN108
88 IFC=25THEN113
89 IFC=40THEN126
90 IFC>=48THEN117
91 IFC>7THEN67
92 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)T$(PEEK(CP+X+Y*20))
93 IFRND(1)*100>97ANDPEEK(CP+164)>0THENFF=0:GOTO255
94 X=ZX:Y=ZY
95 IFX=-1ANDPEEK(CP+160)=255THENX=0:GOTO66
96 IFX=-1THENCR=PEEK(CP+160):X=19:GOTO63
97 IFX=20ANDPEEK(CP+161)=255THENX=19:GOTO66
98 IFX=20THENCR=PEEK(CP+161):X=0:GOTO63
99 IFY=-1ANDPEEK(CP+162)=255THENY=0:GOTO66
100 IFY=-1THENCR=PEEK(CP+162):Y=7:GOTO63
101 IFY=8ANDPEEK(CP+163)=255THENY=7:GOTO66
102 IFY=8THENCR=PEEK(CP+163):Y=0:GOTO63
103 IFC=2THEN108
104 IFC=3THEN108
105 GOTO66
106 IFCR=16THENCR=16:X=0:Y=0:GOTO62
107 IFCR=16THEN640
108 IFC=14ANDCR=S0%(0)ANDZX=S1%(0)THENS3%(0)=1-S3%(0):GOSUB130:TX=A1%(0):TY=A2%(0):GOSUB127:GOSUB143:GOTO67
109 IFC=2ANDCR=S0%(1)ANDX=S1%(1)THENS3%(1)=1:GOSUB130:TX=A1%(1):TY=A2%(1):GOSUB127:GOTO66
110 IFC=2ANDCR=S0%(2)ANDX=S1%(2)THENS3%(2)=1:GOSUB130:TX=A1%(2):TY=A2%(2):GOSUB127:GOTO66
111 IFC=3ANDCR=S0%(3)ANDX=S1%(3)THENS3%(3)=1:GOSUB130:TX=A1%(3):TY=A2%(3):GOSUB127:GOTO66
112 IFC=3ANDCR=S0%(4)ANDX=S1%(4)THENS3%(4)=1:GOSUB130:TX=A1%(4):TY=A2%(4):GOSUB127:GOTO66
113 IFC=12ANDAA=0THENGOTO67
114 IFC=12ANDAA=1THENPRINT"{home}{white}{down}{right}IN DER TRUHE IST WAS!":GOSUB697:GOSUB520:GOTO67
115 IFC=25ANDAA=0THENGOTO67
116 IFC=25ANDAA=1THENPRINT"{home}{white}{down}{right}DER BAUM SIEHT INTRESSANT AUS!":GOSUB697:GOSUB520:GOTO67
117 IFC=49THENPRINT"{white}{home}{down}{right:2}EIN HEILZAUBER IST UNTER DEM BAUM":GOSUB697:GOSUB520:GOTO67
118 IFC=53THENPRINT"{white}{home}{down}{right:2}EIN FEUERZAUBER IST IN DEN KATAKOMBEN":GOSUB697:GOSUB520:GOTO67
119 IFC=50THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB697:GOSUB520:N3%(0)=1:GOSUB130:P3%(1)=1:TX=12:TY=4:GOSUB127:GOSUB447:GOTO67
120 IFC=52THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB697:GOSUB520:N3%(1)=1:GOSUB130:P3%(2)=1:TX=7:TY=2:GOSUB127:GOSUB447:GOTO67
121 IFC=51THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB697:GOSUB520:N3%(2)=1:GOSUB130:P3%(3)=1:TX=3:TY=2:GOSUB127:GOSUB447:GOTO67
122 IFC=67THENPRINT"{white}{home}{down}{right:2}DU KANNST HIER NICHT DURCH!":GOSUB697:FF=4:N3%(3)=1:GOSUB130:GOTO255
123 IFC=72THENPRINT"{white}{home}{down}{right:2}ACH WIE SUESS!":GOSUB697:FF=9:N3%(4)=1:GOSUB130:GOTO255
124 IFC=76THENPRINT"{white}{home}{down}{right:2}DU WILLST MICH BESIEGEN!":GOSUB697:FF=13:N3%(5)=1:GOSUB130:GOTO255
125 IFC=77THENPRINT"{white}{home}{down}{right:2}DU HAST MEINE ERWARTUNG UEBERTROFFEN!":GOSUB697:FF=14:N3%(6)=1:GOSUB130:GOTO255
126 IFC=40THENPRINT"{home}{white}{down}{right}DU BIST GEHEILT!":GOSUB435:GOSUB447:GOSUB697:GOSUB520:GOTO67
127 SP=30960+(CR*RS)+(TX+TY*20)
128 PRINT"{home}{down:3}"LEFT$(CD$,TY*2)SPC(TX*2)T$(PEEK(SP));
129 RETURN
130 POKE30960+N0%(0)*RS+N1%(0)+N2%(0)*20,50-N3%(0)*50
131 POKE30960+N0%(1)*RS+N1%(1)+N2%(1)*20,52-N3%(1)*52
132 POKE30960+N0%(2)*RS+N1%(2)+N2%(2)*20,51-N3%(2)*51
133 POKE30960+N0%(3)*RS+N1%(3)+N2%(3)*20,67-N3%(3)*67
134 POKE30960+N0%(4)*RS+N1%(4)+N2%(4)*20,72-N3%(4)*72
135 POKE30960+N0%(5)*RS+N1%(5)+N2%(5)*20,76-N3%(5)*76
136 POKE30960+N0%(6)*RS+N1%(6)+N2%(6)*20,77-N3%(6)*77
137 POKE30960+A0%(0)*RS+A1%(0)+A2%(0)*20,15-S3%(0)*15+1
138 POKE30960+A0%(1)*RS+A1%(1)+A2%(1)*20,13-S3%(1)*13
139 POKE30960+A0%(2)*RS+A1%(2)+A2%(2)*20,13-S3%(2)*12
140 T=13:IFS3%(3)+S3%(4)=2THENT=0
141 POKE30960+A0%(3)*RS+A1%(3)+A2%(3)*20,T
142 RETURN
143 IFCR=S0%(0)ANDZX=S1%(0)ANDS3%(0)=0THENPRINT"{home}{white}{down}{right}DER SCHALTER IST AUS!";:GOSUB697:GOSUB520
144 IFCR=S0%(0)ANDZX=S1%(0)ANDS3%(0)=1THENPRINT"{home}{white}{down}{right}DER SCHALTER IST EIN!";:GOSUB697:GOSUB520
145 RETURN
146 S0%(0)=0:S1%(0)=11:S2%(0)=1:S3%(0)=0
147 S0%(1)=0:S1%(1)=16:S2%(1)=6:S3%(1)=0
148 S0%(2)=0:S1%(2)=18:S2%(2)=6:S3%(2)=0
149 S0%(3)=0:S1%(3)=1:S2%(3)=6:S3%(3)=0
150 S0%(4)=0:S1%(4)=5:S2%(4)=6:S3%(4)=0
151 A0%(0)=0:A1%(0)=14:A2%(0)=6
152 A0%(1)=0:A1%(1)=7:A2%(1)=4
153 A0%(2)=0:A1%(2)=9:A2%(2)=5
154 A0%(3)=0:A1%(3)=3:A2%(3)=4
155 A0%(4)=0:A1%(4)=3:A2%(4)=4
156 N0%(0)=0:N1%(0)=12:N2%(0)=4:N3%(0)=0
157 N0%(1)=0:N1%(1)=7:N2%(1)=2:N3%(1)=0
158 N0%(2)=0:N1%(2)=3:N2%(2)=2:N3%(2)=0
159 N0%(3)=16:N1%(3)=0:N2%(3)=0:N3%(3)=0
160 N0%(4)=16:N1%(4)=0:N2%(4)=0:N3%(4)=0
161 N0%(5)=16:N1%(5)=0:N2%(5)=0:N3%(5)=0
162 N0%(6)=16:N1%(6)=0:N2%(6)=0:N3%(6)=0
163 N0%(7)=16:N1%(7)=0:N2%(7)=0:N3%(7)=0
164 N0%(8)=16:N1%(8)=0:N2%(8)=0:N3%(8)=0
165 ER%(0)=16:EX%(0)=0:EY%(0)=0:ET%(1)=0
166 ER%(1)=0:EX%(1)=1:EY%(1)=2:ET%(1)=3
167 ER%(2)=0:EX%(2)=18:EY%(2)=2:ET%(2)=15
168 ER%(3)=16:EX%(3)=0:EY%(3)=0:ET%(3)=4
169 ER%(4)=16:EX%(4)=0:EY%(4)=0:ET%(4)=8
170 ER%(5)=16:EX%(5)=0:EY%(5)=0:ET%(5)=5
171 ER%(6)=16:EX%(6)=0:EY%(6)=0:ET%(6)=16
172 ER%(7)=16:EX%(7)=0:EY%(7)=0:ET%(7)=17
173 ER%(8)=16:EX%(8)=0:EY%(8)=0:ET%(8)=13
174 ER%(9)=16:EX%(9)=0:EY%(9)=0:ET%(9)=15
175 ER%(10)=16:EX%(10)=0:EY%(10)=0:ET%(10)=11
176 ER%(11)=16:EX%(11)=0:EY%(11)=0:ET%(11)=7
177 ER%(12)=16:EX%(12)=0:EY%(12)=0:ET%(12)=9
178 RETURN
179 PRINT"{home}{down}{white}{right}  UNTERSUCHE  EQUIPMENTS  INVENTAR"
180 M=0
181 POKEQS+42+M*12,35
182 GOSUB660
183 POKEQS+42+M*12,32
184 IFA$="A"THENM=M-1-3*(M=0):GOTO181
185 IFA$="D"THENM=M+1+3*(M=2):GOTO181
186 IFA$=CHR$(13)THENPOKEQS+42+M*7,32:GOTO188
187 GOTO181
188 PRINT"{home}{down}{white}{right}                                      ";
189 ONM+1GOTO243,203,190
190 POKE1020,Q1%
191 GOSUB516
192 PRINT"{home}{down}{right}{white}INVENTAR: WAEHLE DEIN ITEM           ";
193 MT=2:MX=2:MY=4:MC=14:GOSUB458
194 IFIN%(M)=9THEN62
195 IFIN%(M)=0ORIT%(IN%(M))<>MTTHEN62
196 IFIN%(M)=3THENGOSUB199:IN%(M)=0:GOSUB447:GOTO62
197 IFIN%(M)=8THENGOSUB201:IN%(M)=0:GOSUB447:GOTO62
198 GOTO62
199 FORI=0TO3:PH%(I)=PH%(I)+IE%(IN%(M)):IFPH%(I)>P1%(I)THENPH%(I)=P1%(I)
200 FH%(I)=PH%(I):NEXT:RETURN
201 FORI=0TO3:PM%(I)=PM%(I)+IE%(IN%(M)):IFPM%(I)>P2%(I)THENPM%(I)=P2%(I)
202 FM%(I)=PM%(I):NEXT:RETURN
203 POKE1020,Q1%
204 GOSUB522
205 PRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINEN SPIELER     ";
206 P=0
207 CP=QS+2+(4-8*(P>1))*40:IFP=1ORP=3THENCP=CP+20
208 POKECP,35
209 GOSUB660
210 POKECP,32
211 IFP=0ANDA$="D"ANDP3%(1)=1THENP=1:GOTO207
212 IFP=0ANDA$="S"ANDP3%(2)=1THENP=2:GOTO207
213 IFP=0ANDA$="D"ANDP3%(1)=OANDP3%(2)=0ANDP3%(3)=1THENP=3:GOTO207
214 IFP=0ANDA$="S"ANDP3%(1)=OANDP3%(2)=0ANDP3%(3)=1THENP=3:GOTO207
215 IFP=1ANDA$="A"ANDP3%(0)=1THENP=0:GOTO207
216 IFP=1ANDA$="S"ANDP3%(3)=1THENP=3:GOTO207
217 IFP=2ANDA$="D"ANDP3%(3)=1THENP=3:GOTO207
218 IFP=2ANDA$="W"ANDP3%(0)=1THENP=0:GOTO207
219 IFP=3ANDA$="A"ANDP3%(2)=1THENP=2:GOTO207
220 IFP=3ANDA$="W"ANDP3%(1)=1THENP=1:GOTO207
221 IFP=3ANDA$="A"ANDP3%(2)=OANDP3%(1)=0ANDP3%(0)=1THENP=0:GOTO207
222 IFP=3ANDA$="W"ANDP3%(2)=OANDP3%(1)=0ANDP3%(0)=1THENP=0:GOTO207
223 IFA$=CHR$(13)THEN225
224 GOTO208
225 SL=0:CP=CP+81
226 PRINT"{home}{down}{right}{white}EQUIPMENT: WAFFE ODER RUESTUNG       ";
227 POKECP+SL*40,35
228 GOSUB660
229 POKECP+SL*40,32:IFA$="W"ORA$="S"THENSL=1-SL:GOTO227
230 IFA$=CHR$(13)THEN232
231 GOTO227
232 MY=4:MX=23:MC=14:IFP=1ORP=3THENMX=3
233 IFSL=0THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE WAFFE        ";
234 IFSL=1THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE RUESTUNG     ";
235 PRINT"{home}"LEFT$(CD$,MY-1)SPC(MX-3);"{brown}{$c1}{$c2:18}{$c3}";
236 FORI=0TO13:PRINT"{down}{left:20}{$c4}                  {$c5}";:NEXT
237 PRINT"{down}{left:20}{$c6}{$c7:18}{$c8}";
238 MT=SL:GOSUB458
239 IFIT%(IN%(M))=9THEN62
240 IFIN%(M)<>0ANDIT%(IN%(M))<>SLTHEN62
241 IFSL=0THENQ=PW%(P):PW%(P)=IN%(M):IN%(M)=Q:GOTO62
242 IFSL=1THENQ=PR%(P):PR%(P)=IN%(M):IN%(M)=Q:GOTO62
243 GOSUB520
244 GOSUB702
245 EI=0
246 IFER%(EI)<>CROREX%(EI)<>OXOREY%(EI)<>OYTHEN251
247 NI%=ET%(EI):GOSUB553
248 IFIS=99THENPRINT"{home}{white}{down}{right}DU KANNST NICHT MEHR TRAGEN!":GOSUB697:GOSUB520:GOTO67
249 ER%(EI)=-1
250 PRINT"{home}{white}{down}{right}GEFUNDEN: ";I$(IN%(IS));:GOSUB697:GOSUB520:GOTO67
251 EI=EI+1
252 IFEI=12THEN254
253 GOTO246
254 PRINT"{home}{down}{right}{white}NICHTS BESONDERES!":GOSUB697:GOSUB520:GOTO67
255 FORI=4TO11:FT%(I)=-1:NEXT
256 PP=4:CC=4:WX=0
257 FORI=0TO3:FT%(I)=-2:FH%(I)=PH%(I):FM%(I)=PM%(I):FS%(I)=PS%(I)
258 IFP3%(I)=0THENFH%(I)=-1
259 IFFH%(I)<=0THENFT%(I)=-1:PP=PP-1
260 FA%(I)=PA%(I)+IA%(PW%(I)):FD(I)=PD%(I)+ID%(PR%(I)):F1%(I)=P1%(I):F2%(I)=P2%(I)
261 NEXT
262 PRINT"{brown}{home}{$c1}{$c2:38}{$c3}";
263 PRINT"{$c4}                                      {$c5}";
264 PRINT"{$c6}{$c7:38}{$c8}";
265 PRINT"{home}{down:3}{white}";:FORI=0TO15:PRINT"                                        ";:NEXT
266 PRINTDD$;"{brown}{up}{$c1}{$c2:7}{white}HP{brown}{$c2}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
267 GOSUB416
268 GOSUB447
269 GOSUB455
270 FORI=0TO11
271 IFFT%(I)<>-1THENFC%(I)=INT(RND(0)*FD%(I))
272 NEXT
273 CC=CC-4
274 PRINT"{home}{down}{right}                       ";:GOSUB564
275 FORI=4TO11:IFFT%(I)=-1THEN277
276 PRINT"{home}"LEFT$(CD$,FY%(I));SPC(FX%(I));MD$(FT%(I))
277 NEXT
278 C=0
279 IFFT%(C)<>-1ANDFC%(C)>=100THENFC%(C)=FC%(C)-100:GOTO282
280 C=C+1:IFC<12THEN279
281 FORI=0TO11:FC%(I)=FC%(I)+FS%(I):NEXT:GOTO278
282 IFFT%(C)=-2THEN284
283 GOTO365
284 PRINT"{home}{white}{down}{right}"P$(C)": IST AM ZUG                     "
285 PRINTDD$;SPC(16)"{white} ANGRIFF {down}{left:9} INVENTAR";:M=2
286 PRINTDD$;SPC(16)"{down:2}         {down}{left:9}         ";
287 IFPM%(C)>0THENPRINTDD$;SPC(16)"{down:2} MAGIE   ";:M=3
288 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(37)"  {down}{left:2}  ";
289 PRINT"{left:3}{up}"PD$(C);
290 MY=0:FZ=0
291 POKEQS+816+MY*40,35
292 GOSUB660
293 IFA$="S"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY+1:GOTO297
294 IFA$="W"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY-1:GOTO297
295 IFA$=CHR$(13)THENFZ=0:GOTO300
296 GOTO292
297 IFMY>=MTHENMY=0
298 IFMY<0THENMY=M-1
299 GOTO291
300 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(36)"{white}  {down}{left:2}  {left}{up}"PD$(C);
301 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
302 ONMY+1GOTO328,320
303 MT=3:MX=16:MY=20:MC=4:GOSUB458
304 IFIT%(IN%(M))=9THEN284
305 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         {down}{left:9}         ";
306 IFIN%(M)=0ORIT%(IN%(M))<>3THEN284
307 M=IN%(M)
308 IFIE%(M)>FM%(C)THENPRINT"{home}{down}{right}NICHT GENUG MANA                     ";:GOSUB697:GOTO284
309 GOSUB580
310 GOTO606
311 AP=IE%(M)+2-INT(RND(0)*5):FM%(C)=FM%(C)-IE%(M):PM%(C)=FM%(C):GOSUB447
312 AP=AP+PL%(C)*3
313 FH%(CE)=FH%(CE)-AP:IFFH%(CE)<0THENFH%(CE)=0
314 IFCE<4THENPH%(CE)=FH%(CE)
315 IFFH%(CE)>0THENGOTO350
316 IFFT%(CE)<>-2THENCC=CC-1:WX=WX+MX%(FT%(CE))
317 IFFT%(CE)=-2THENPP=PP-1
318 FT%(CE)=-1
319 GOTO350
320 MT=2:MX=16:MY=20:MC=4:GOSUB458
321 IFIT%(IN%(M))=9THEN284
322 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
323 PRINT"{down}{left:9}         ";
324 IFIN%(M)=0ORIT%(IN%(M))<>MTTHEN284
325 IFIN%(M)=3THENGOSUB199:IN%(M)=0:GOSUB447:GOTO274
326 IFIN%(M)=8THENGOSUB201:IN%(M)=0:GOSUB447:GOTO274
327 GOTO274
328 GOSUB580
329 AP=FA%(C)-FD(CE)
330 IFAP<=0THENPRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{white}MISS";:GOSUB695:GOTO342
331 AP=INT(RND(0)*AP)+1
332 FH%(CE)=FH%(CE)-AP:IFFH%(CE)<0THENFH%(CE)=0
333 IFCE<4THENPH%(CE)=FH%(CE)
334 IFFH%(CE)>0THENGOTO339
335 IFFT%(CE)<>-2THENCC=CC-1:WX=WX+MX%(FT%(CE))
336 IFFT%(CE)=-2THENPP=PP-1
337 FT%(CE)=-1
338 GOTO339
339 POKEV,(FX%(CE)*8+21)AND255:POKEV+1,FY%(CE)*8+50:POKE50168,16:POKEV+28,0
340 POKEV+16,-(FX%(CE)>30):POKEV+21,1
341 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{white}";AP
342 FORI=0TO5:POKEV,PEEK(V)+2:POKEV+1,PEEK(V+1)+2:GOSUB696:NEXTI
343 POKEV+21,0:IFCE<4THENGOSUB447
344 GOSUB695
345 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
346 IFFT%(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE));"  {down}{left:2}  ";
347 IFCC=0THEN373
348 IFPP=0THEN401
349 GOTO274
350 POKEV,(FX%(CE)*8+24)AND255:POKEV+1,FY%(CE)*8+50
351 IFM=4THENPOKEV+37,8:POKEV+38,2:POKEV+39,7:POKEV+28,1
352 B=17:IFM=6THENB=19:POKEV+28,0
353 IFM=5THENB=21:POKEV+28,0
354 IFM=7THENB=23:POKEV+28,0
355 POKEV+16,-(FX%(CE)>30):POKE50168,B:POKEV+21,1
356 PRINT"{white}";:IFM=9THENPRINT"{green}";
357 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}";AP
358 FORK=0TO3:FORI=0TO1:POKE50168,B+I:FORJ=1TO40:NEXTJ,I,K
359 POKEV+21,0:IFCE<4THENGOSUB447
360 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
361 IFFT%(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE));"  {down}{left:2}  ";
362 IFCC=0THEN373
363 IFPP=0THEN401
364 GOTO274
365 GOSUB695
366 PRINT"{white}{home}{down}{right}"M$(FT%(C))": GREIFT AN                    "
367 FC%(C)=FC%(C)-100
368 IFFT%(C)=7THENIFFM%(C)>=IE%(9)THENM=9:GOSUB695:GOSUB410:GOTO609
369 IFFT%(C)=9THENIFFM%(C)>=IE%(6)THENM=6:GOSUB695:GOSUB404:GOTO311
370 IFFT%(C)=10THENIFFM%(C)>=IE%(4)THENM=4:GOSUB695:GOSUB404:GOTO311
371 IFFT%(C)=11THENIFFM%(C)>=IE%(12)THENM=12:GOSUB695:GOSUB404:GOTO616
372 GOSUB695:GOSUB404:GOTO329
373 POKE1020,Q1%
374 PRINTDD$;"{brown}{up}{$c1}{$c2:10}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}{white}";
375 PRINT"{home}{down}{right}DU HAST GEWONNEN                     ";:GOSUB564
376 GOSUB516
377 FORI=0TO3:IFP3%(I)=0THEN381
378 PRINT"{home}{white}{right:3}";:IFPH%(I)=0THENPRINT"{red}";
379 PRINTLEFT$(CD$,4+I*3)P$(I)
380 PRINT"{home}"LEFT$(CD$,5+I*3)"{right:3}LV";PL%(I)+1;" EX";PX%(I)
381 NEXT
382 FORI=0TO3:IFPH%(I)=0ORP3%(I)=0THEN392
383 TX=WX
384 PRINT"{home}{white}"LEFT$(CD$,5+I*3)SPC(20)"EXPERIENCE {left}"TX"{left} ";
385 IFTX=0THEN392
386 XS=1:IFTX>9THENXS=10
387 IFTX>99THENXS=100
388 TX=TX-XS
389 PX%(I)=PX%(I)+XS
390 LL=10+PL%(I)*PL%(I)*10:IFPX%(I)>=LLTHENGOSUB394:GOTO390
391 PRINT"{home}{white}"LEFT$(CD$,5+I*3)"{right:3}LV";PL%(I)+1;" EX";PX%(I):GOTO384
392 NEXTI
393 GOTO399
394 PL%(I)=PL%(I)+1:PA%(I)=PA%(I)+1:PD%(I)=PD%(I)+1:PS%(I)=PS%(I)+1
395 P1%(I)=P1%(I)+PL%(I)*2:IFP2%(I)>0THENP2%(I)=P2%(I)+PL%(I)*3
396 IFP1%(I)>999THENP1%(I)=999
397 IFP2%(I)>999THENP2%(I)=999
398 RETURN
399 GOSUB688
400 GOTO62
401 PRINT"{home}{down}{right}{white}DU HAST VERLOREN                     "
402 GOSUB688
403 GOTO624
404 RE=INT(RND(0)*4):CE=0
405 IFFT%(CE)=-2ANDPH%(CE)>0THEN408
406 CE=CE+1:IFCE=4THENCE=0
407 GOTO405
408 IFRE>0THENRE=RE-1:GOTO406
409 RETURN
410 RE=INT(RND(0)*8):CE=4
411 IFFT%(CE)>=0THEN414
412 CE=CE+1:IFCE=12THENCE=4
413 GOTO411
414 IFRE>0THENRE=RE-1:GOTO412
415 RETURN
416 IFFF>0THENL=FF:GOTO418
417 L=PEEK(CP+164):IFL=0THENRETURN
418 IFL=3THENA=0:LC=INT(RND(0))*2+2:FORI=0TOLC:GOSUB558:NEXT:RETURN
419 IFL=1THENA=1:FORI=0TO2:GOSUB558:NEXT:RETURN
420 IFL=2THENA=1:FORI=0TO1:GOSUB558:NEXT:A=2:FORI=0TO1:GOSUB558:NEXT:RETURN
421 IFL=4THENA=3:GOSUB558:A=1:GOSUB558:GOSUB558:RETURN
422 IFL=5THENA=4:LC=INT(RND(0)*3)+1:FORI=0TOLC:GOSUB558:NEXT:RETURN
423 IFL=6THENONINT(RND(1)*2)+1GOTO433,434
424 IFL=7THENLC=INT(RND(0)*3)+1:A=6:FORI=0TOLC:GOSUB558:NEXT:RETURN
425 IFL=8THENA=7:GOSUB558:LC=INT(RND(0)*2)+1:A=6:GOSUB558:RETURN
426 IFL=9THENA=9:GOSUB558:A=8:GOSUB558:A=5:GOSUB558:RETURN
427 IFL=10THENA=6:LC=INT(RND(0)*2)+1:FORI=1TOLC:GOSUB558:NEXT:RETURN
428 IFL=11THENA=7:GOSUB558:A=10:GOSUB558:RETURN
429 IFL=12ANDINT(RND(1)*2)=1THENA=11:GOSUB558:RETURN
430 IFL=12THENA=5:GOSUB558:GOSUB558:A=9:GOSUB558:RETURN
431 IFL=13THENA=9:GOSUB558:A=12:GOSUB558:A=9:GOSUB558:RETURN
432 IFL=14THENA=9:GOSUB558:A=13:GOSUB558:A=7:GOSUB558:RETURN
433 A=5:LC=INT(RND(1)*2)+1:FORI=0TOLC:GOSUB558:NEXT:RETURN
434 A=9:LC=INT(RND(1)*2)+1:FORI=1TOLC:GOSUB558:NEXT:RETURN
435 FORI=0TO3
436 IFP3%(I)=0THEN439
437 PH%(I)=P1%(I)
438 PM%(I)=P2%(I)
439 NEXT:RETURN
440 PRINT"{brown}{home}{$c1}{$c2:38}{$c3}";
441 PRINT"{$c4}                                      {$c5}";
442 PRINT"{$c6}{$c7:38}{$c8}";
443 PRINTDD$;"{brown}{up}{$c1}{$c2:10}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
444 FORI=0TO3:PRINT"{$c4}          {$c5}{$c4}            {$c5}{$c4}            {$c5}";:NEXT
445 PRINT"{$c6}{$c7:10}{$c8}{$c6}{$c7:12}{$c8}{$c6}{$c7:12}";
446 POKE50151,72:POKE56295,9:RETURN
447 PRINTDD$;
448 FORI=0TO3:IFP3%(I)=0THENPRINTSPC(40);:GOTO454
449 PRINT"{white}";:IFPH%(I)=0THENPRINT"{red}";
450 PRINTSPC(27)P$(I)" ";:A=PH%(I):GOSUB658
451 PRINTD$;
452 A=PM%(I):GOSUB658
453 PRINT" "D$"{right}";
454 NEXT:RETURN
455 FORI=0TO3:IFPH%(I)<=0ORP3%(I)=0THENNEXT:RETURN
456 PRINT"{home}"LEFT$(CD$,5+I*3);SPC(37);PD$(I):NEXT
457 RETURN
458 GOSUB493
459 MI=-1:M=0:MO=0
460 MI=MO-1:CY=MY:MM=0
461 IFMC=4THENGOSUB488
462 MI=MI+1
463 PRINT"{home}{white}"LEFT$(CD$,CY)SPC(MX)" ";
464 IFIT%(IN%(MI))<>MTTHENPRINT"{cyan}";
465 IFIT%(IN%(MI))=9THENPRINT"{white}";
466 IFIT%(IN%(MI))=-1THENPRINT"{cyan}";
467 PRINTI$(IN%(MI));
468 CY=CY+1
469 MM=MM+1
470 IFMM=MCTHEN472
471 IFMI<13THEN462
472 CP=QS+MX+MY*40
473 POKECP+(M-MO)*40,35
474 GOSUB660
475 POKECP+(M-MO)*40,32
476 IFA$="W"THENM=M-1:GOTO480
477 IFA$="S"THENM=M+1:GOTO480
478 IFA$=CHR$(13)THENRETURN
479 GOTO473
480 IFM=-1THENM=14-1:MO=14-MC:GOTO460
481 IFM=14THENM=0:MO=0:GOTO460
482 M%=0
483 IFM<MOTHENMO=MO-MC:M%=1:GOTO483
484 IFM>=MO+MCTHENMO=MO+MC:M%=1
485 IFMO<0THENMO=0:M%=1
486 IFM%=1THEN460
487 GOTO473
488 FORI=0TO3
489 PRINT"{home}{white}"LEFT$(CD$,CY+I)SPC(MX)" ";
490 PRINT"        ";
491 NEXTI
492 RETURN
493 A=0
494 FORI=0TO99:TT%(I)=0:NEXTI
495 FORI=0TO99:TT%(I)=IN%(I):IN%(I)=0:NEXTI
496 IFMT=0THENGOSUB501:GOSUB504:A=50:GOSUB507:GOSUB510:GOSUB513
497 IFMT=1THENGOSUB501:GOSUB507:A=50:GOSUB504:GOSUB510:GOSUB513
498 IFMT=2THENGOSUB501:GOSUB510:A=50:GOSUB504:GOSUB507:GOSUB513
499 IFMT=3THENGOSUB501:GOSUB513:A=50:GOSUB504:GOSUB507:GOSUB510
500 RETURN
501 FORI=0TO99:IFIT%(TT%(I))=9THENIN%(A)=TT%(I):A=A+1
502 NEXTI
503 RETURN
504 FORI=0TO99:IFIT%(TT%(I))=0THENIN%(A)=TT%(I):A=A+1
505 NEXTI
506 RETURN
507 FORI=0TO99:IFIT%(TT%(I))=1THENIN%(A)=TT%(I):A=A+1
508 NEXTI
509 RETURN
510 FORI=0TO99:IFIT%(TT%(I))=2THENIN%(A)=TT%(I):A=A+1
511 NEXTI
512 RETURN
513 FORI=0TO99:IFIT%(TT%(I))=3THENIN%(A)=TT%(I):A=A+1
514 NEXTI
515 RETURN
516 PRINT"{brown}{home}{down:3}{$c1}{$c2:38}{$c3}";
517 FORI=0TO13
518 PRINT"{$c4}                                      {$c5}";:NEXT
519 PRINT"{$c6}{$c7:38}{$c8}";:RETURN
520 PRINT"{home}{down}{right}{white}                                      ";
521 RETURN
522 PRINT"{brown}{home}{down:3}{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
523 FORI=0TO5
524 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
525 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
526 PRINT"{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
527 FORI=0TO5
528 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
529 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
530 DX=3:DY=4:P=0:GOSUB533:DX=23:P=1:GOSUB533
531 DX=3:DY=12:P=2:GOSUB533:DX=23:P=3:GOSUB533
532 RETURN
533 IFP3%(P)=0THENRETURN
534 PRINT"{home}{white}";:IFPH%(P)=0THENPRINT"{red}";
535 PRINT"{home}"LEFT$(CD$,DY)SPC(DX-1)" "P$(P)"  LV"PL%(P)+1
536 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX)" "I$(PW%(P));
537 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX)" "I$(PR%(P));
538 T1%=PA%(P)+IA%(PW%(P))
539 T2%=PD%(P)+ID%(PR%(P))
540 IFT1%>999THENT1%=999
541 IFT2%>999THENT2%=999
542 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX+9)"ATK"T1%;
543 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX+9)"DEF"T2%;
544 RETURN
545 IFPEEK(2)=1THENSP=30960+RS*(CR-0)
546 FORI=0TO7
547 PRINT"{home}{down:3}"LEFT$(CD$,I*2);
548 FORJ=0TO19
549 PRINTT$(PEEK(SP))"{up}";
550 SP=SP+1
551 NEXTJ,I
552 RETURN
553 IS=0
554 IFIN%(IS)=0THENIN%(IS)=NI%:NI%=-1:RETURN
555 IS=IS+1
556 IFIS=99THENRETURN
557 GOTO554
558 IFCC>=11THENRETURN
559 FH%(CC)=MH%(A):FM%(CC)=MM%(A):FT%(CC)=A:FC%(CC)=INT(RND(0)*MS%(A)):FS%(CC)=MS%(A)
560 FX%(CC)=2:FY%(CC)=5+CC*3:FA%(CC)=MA%(A):FD(CC)=MD%(A):FB(CC)=MB%(A)
561 F1%(CC)=MH%(A):F2%(CC)=MM%(A)
562 IFCC>=4THENFX%(CC)=5:FY%(CC)=5+(CC-4)*3
563 CC=CC+1:RETURN
564 ZM=0
565 FORJ=4TO7
566 IFFT%(J)=-1THENZM=ZM-1
567 ZM=ZM+1
568 NEXTJ
569 IFZM<>CMTHENPRINTDD$;"{right}          {down}{left:10}          {down}{left:10}          {down}{left:10}          ";
570 PRINTDD$;
571 CM=0
572 FORI=4TO7
573 IFFT%(I)=-1THENNEXT:RETURN
574 A=FH%(I):GOSUB658
575 PRINT"{home}{right}"LEFT$(CD$,20+CM)M$(FT%(I));
576 PRINT"{home}{right}"LEFT$(CD$,20+CM);"{right:7}";D$
577 CM=CM+1
578 NEXT
579 RETURN
580 CE=4:PRINT"{home}{down}{right}{white}"P$(C)": WAHELE DEIN ZIEL               "
581 IFFT%(CE)=-1THEN590
582 POKEQS-1+FX%(CE)+FY%(CE)*40,35
583 GOSUB660
584 IFA$=CHR$(13)THENPOKEQS-1+FX%(CE)+FY%(CE)*40,32:RETURN
585 IFA$="S"THENGOTO590
586 IFA$="W"THENGOTO598
587 IFA$="D"ANDCE>3THENPOKEQS-1+FX%(CE)+FY%(CE)*40,32:CE=0:GOTO581
588 IFA$="A"ANDCE<4THENPOKEQS-1+FX%(CE)+FY%(CE)*40,32:CE=4:GOTO581
589 GOTO583
590 CO=CE
591 POKEQS-1+FX%(CE)+FY%(CE)*40,32
592 CE=CE+1
593 IFCE>=12THENCE=0
594 IFCO<4ANDCE>3THENCE=CO
595 IFCO>3ANDCE<4THENCE=CO
596 IFFT%(CE)<>-1THEN582
597 GOTO592
598 CO=CE
599 POKEQS-1+FX%(CE)+FY%(CE)*40,32
600 CE=CE-1
601 IFCE<0THENCE=11
602 IFCO<4ANDCE>3THENCE=CO
603 IFCO>3ANDCE<4THENCE=CO
604 IFFT%(CE)<>-1THEN582
605 GOTO600
606 IFFT%(C)=-2THEN622
607 IFFT%(C)=7ANDFM%(C)>=IE%(9)THENM=9:GOTO609
608 GOTO311
609 AP=IE%(M)*4+2-INT(RND(0)*5):FM%(C)=FM%(C)-IE%(M):IFC<4THENPM%(C)=FM%(C):GOSUB447
610 FH%(CE)=FH%(CE)+AP:IFFH%(CE)>F1%(CE)THENFH%(CE)=F1%(CE)
611 IFCE<4THENPH%(CE)=FH%(CE):GOSUB447
612 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{green}";AP
613 GOSUB702
614 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
615 GOTO274
616 FM%(C)=FM%(C)-IE%(M):IFC<4THENPM%(C)=FM%(C):GOSUB447
617 FS%(CE)=FS%(CE)/2:IFFS%(CE)<5THENFS%(CE)=5
618 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{green}SLOW";
619 GOSUB702
620 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
621 GOTO274
622 IFM=9THEN609
623 GOTO311
624 FORI=0TO3:PX%(I)=0:NEXT
625 PA%(0)=5:PD%(0)=2:PA%(1)=4:PD%(1)=3:PA%(2)=1:PD%(2)=1:PA%(3)=1:PD%(3)=1
626 FORI=0TO13:IN%(I)=0:NEXT
627 IN%(0)=18
628 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
629 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
630 PRINT"{$c6}{$c7:38}{up:3}";
631 POKE50151,72:POKE56295,9
632 X=15:Y=2:CR=0
633 P3%(0)=1:P3%(1)=0:P3%(2)=0:P3%(3)=0
634 P1%(0)=15:P1%(1)=17:P1%(2)=9:P1%(3)=9:P2%(0)=0:P2%(1)=0:P2%(2)=7:P2%(3)=23
635 FORI=0TO3:PH%(I)=P1%(I):PM%(I)=P2%(I):PX%(I)=0:PW%(I)=0:PR%(I)=0:NEXT
636 PW%(0)=1:PR%(0)=2
637 GOSUB130
638 AA$="INTRO":GOSUB646
639 GOTO57
640 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
641 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
642 PRINT"{$c6}{$c7:38}{up:3}";
643 POKE50151,72:POKE56295,9
644 AA$="ENDE":GOSUB646
645 GOTO57
646 POKE1020,Q7%
647 POKE56322,224
648 IFAA$="INTRO"THENOPEN1,8,4,"TXT.INTRO,S,R"
649 IFAA$="ENDE"THENOPEN1,8,4,"TXT.ENDE,S,R"
650 IFAA$=""THENRETURN
651 GET#1,B$
652 IFB$="$"THENPRINT"{home}{down:2}{right:2}{white}";:GOTO651
653 IFB$="%"THENPRINT"{right:2}{white}";:GOTO651
654 IFB$="#"THENCLOSE1:POKE56322,255:GOSUB688:RETURN
655 PRINTB$;
656 IFB$<>" "THENPOKE162,0:WAIT162,4
657 GOTO651
658 T$=STR$(A)
659 D$=RIGHT$("000"+RIGHT$(T$,LEN(T$)-1),3):RETURN
660 A$=""
661 J=PEEK(56320)
662 IFJ=127THEN664
663 GOTO661
664 J=PEEK(56320)
665 IFJ=127ANDA$=CHR$(13)THENRETURN
666 IFJ=107THENA$="E":GOSUB693:RETURN
667 IFJ=103THENA$="E":GOSUB693:RETURN
668 IFJ=110THENA$="E":GOSUB693:RETURN
669 IFJ=109THENA$="E":GOSUB693:RETURN
670 IFJ=123THENA$="A":GOSUB693:RETURN
671 IFJ=119THENA$="D":GOSUB693:RETURN
672 IFJ=126THENA$="W":GOSUB693:RETURN
673 IFJ=125THENA$="S":GOSUB693:RETURN
674 IFJ=111THENA$=CHR$(13)
675 GOTO664
676 A$=""
677 J=PEEK(56320)
678 IFJ<>111ANDJ<>107ANDJ<>103ANDJ<>110ANDJ<>109THEN680
679 GOTO677
680 J=PEEK(56320)
681 IFJ=127ANDA$=CHR$(13)THENRETURN
682 IFJ=123THENA$="A":GOSUB694:RETURN
683 IFJ=119THENA$="D":GOSUB694:RETURN
684 IFJ=126THENA$="W":GOSUB694:RETURN
685 IFJ=125THENA$="S":GOSUB694:RETURN
686 IFJ=111THENA$=CHR$(13)
687 GOTO680
688 A$=""
689 J=PEEK(56320)
690 IFJ=127ANDA$=CHR$(13)THENRETURN
691 IFJ=111THENA$=CHR$(13)
692 GOTO689
693 POKE162,0:WAIT162,16:RETURN
694 POKE162,0:WAIT162,8:RETURN
695 POKE162,0:WAIT162,32:RETURN
696 POKE162,0:WAIT162,2:RETURN
697 POKE162,0:WAIT162,32
698 POKE162,0:WAIT162,32
699 POKE162,0:WAIT162,32
700 POKE162,0:WAIT162,32
701 RETURN
702 POKE162,0:WAIT162,32
703 RETURN
704 POKE162,0:WAIT162,4
705 RETURN
706 DATA"IMP",5,0,8,4,1,0,3
707 DATA"SNAKE",8,0,12,6,1,0,5
708 DATA"VIPER",12,0,18,6,1,0,9
709 DATA"NACHO",30,0,22,15,5,0,50
710 DATA"GOLEM",20,0,4,20,8,0,17
711 DATA"MOSS",30,0,7,26,9,0,21
712 DATA"BONES",50,0,20,18,8,0,15
713 DATA"WISP",25,50,15,0,8,0,17
714 DATA"TROLL",100,0,9,40,13,0,100
715 DATA"SPARK",30,50,22,0,8,0,17
716 DATA"BURNER",80,90,30,25,13,0,42
717 DATA"TBERRY",120,10,60,60,20,0,150
718 DATA"DRACUL",250,60,50,70,25,0,500
719 DATA"GLADOS",500,70,60,80,30,0,1000
720 DATA"KRON",15 ,0  ,10 ,4 ,3 ,0 ,0
721 DATA"LENA",17 ,0  ,10 ,3 ,3 ,0 ,0
722 DATA"DOLM",9  ,7  ,8  ,1 ,1 ,0 ,0
723 DATA"MIRA",9  ,23 ,7  ,1 ,1 ,1 ,2
724 DATA"{gray1}{rvrs off}{$c9}{rvrs off}{gray1}{$ca}{down}{left:2}{rvrs off}{gray1}{$ca}{rvrs off}{gray1}{$c9}{rvrs off}"
725 DATA"{gray1}{rvrs off}{$cb}{rvrs off}{gray1}{$cc}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1}{$ca}{rvrs off}"
726 DATA"{gray1}{rvrs off}{$c9}{rvrs off}{gray1}{$ca}{down}{left:2}{rvrs off}{gray1}{$ca}{rvrs off}{gray1}{$c9}{rvrs off}"
727 DATA"{gray1}{rvrs off}{$cd}{rvrs off}{gray1}{$ce}{down}{left:2}{rvrs off}{gray1}{$cf}{rvrs off}{gray1}{$d0}{rvrs off}"
728 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
729 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
730 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
731 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
732 DATA"{orange}{rvrs off}{$d1}{rvrs off}{orange}{$d1}{down}{left:2}{rvrs off}{orange}{$d1}{rvrs off}{orange}{$d1}{rvrs off}"
733 DATA"{brown}{rvrs off}{$d2}{rvrs off}{brown}{$d3}{down}{left:2}{rvrs off}{brown}{$d4}{rvrs off}{brown}{$d5}{rvrs off}"
734 DATA"{lt. red}{rvrs off}{$d6}{rvrs off}{lt. red}{$d7}{down}{left:2}{rvrs off}{lt. red}{$d8}{rvrs off}{lt. red}{$d9}{rvrs off}"
735 DATA"{gray1}{rvrs off}{$da}{rvrs off}{gray1}{$db}{down}{left:2}{rvrs off}{gray1}{$dc}{rvrs off}{gray1}{$dd}{rvrs off}"
736 DATA"{gray1}{rvrs off}{$a2}{rvrs off}{gray1}{$a3}{down}{left:2}{rvrs off}{gray1}{$a4}{rvrs off}{gray1}{$a5}{rvrs off}"
737 DATA"{brown}{rvrs off}{$a1}{rvrs off}{brown}{$a1}{down}{left:2}{rvrs off}{brown}{$a1}{rvrs off}{brown}{$a1}{rvrs off}"
738 DATA"{gray1}{rvrs off}{$de}{rvrs off}{gray1}{$df}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1} {rvrs off}"
739 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
740 DATA"{lt. red}{rvrs off}{$a6}{rvrs off}{lt. red}{$a7}{down}{left:2}{rvrs off}{lt. red}{cmdr pound}{rvrs off}{lt. red}{shft pound}{rvrs off}"
741 DATA"{lt. red}{rvrs off}{$aa}{rvrs off}{lt. red}{$aa}{down}{left:2}{rvrs off}{lt. red}{$aa}{rvrs off}{lt. red}{$aa}{rvrs off}"
742 DATA"{brown}{rvrs off}{$ab}{rvrs off}{brown}{$ab}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
743 DATA"{brown}{rvrs off}{$ab}{rvrs off}{brown}{$ad}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
744 DATA"{brown}{rvrs off}{$ad}{rvrs off}{brown}{$ab}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
745 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
746 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
747 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
748 DATA"{lt. green}{rvrs off}{$ae}{rvrs off}{lt. green}{$d1}{down}{left:2}{rvrs off}{lt. green}{$d1}{rvrs off}{lt. green}{$af}{rvrs off}"
749 DATA"{lt. green}{rvrs off}{$b0}{rvrs off}{lt. green}{$b1}{down}{left:2}{rvrs off}{lt. green}{$b2}{rvrs off}{lt. green}{$b3}{rvrs off}"
750 DATA"{lt. green}{rvrs off}{$b4}{rvrs off}{lt. green}{$b5}{down}{left:2}{rvrs off}{lt. green}{$b4}{rvrs off}{lt. green}{$b5}{rvrs off}"
751 DATA"{lt. green}{rvrs off}{$b6}{rvrs off}{lt. green}{$b7}{down}{left:2}{rvrs off}{lt. green}{$b8}{rvrs off}{lt. green}{$b9}{rvrs off}"
752 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
753 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
754 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
755 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
756 DATA"{brown}{rvrs off}{$ae}{rvrs off}{brown}{$d1}{down}{left:2}{rvrs off}{brown}{$d1}{rvrs off}{brown}{$af}{rvrs off}"
757 DATA"{brown}{rvrs off}{$b0}{rvrs off}{brown}{$b1}{down}{left:2}{rvrs off}{brown}{$b2}{rvrs off}{brown}{$b3}{rvrs off}"
758 DATA"{brown}{rvrs off}{$b4}{rvrs off}{brown}{$b5}{down}{left:2}{rvrs off}{brown}{$b4}{rvrs off}{brown}{$b5}{rvrs off}"
759 DATA"{brown}{rvrs off}{$b6}{rvrs off}{brown}{$b7}{down}{left:2}{rvrs off}{brown}{$b8}{rvrs off}{brown}{$b9}{rvrs off}"
760 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
761 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
762 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
763 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
764 DATA"{gray1}{rvrs off}{$d1}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$d1}{rvrs off}{gray1}{$d1}{rvrs off}"
765 DATA"{gray1}{rvrs off}{$ba}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$ba}{rvrs off}{gray1}{$d1}{rvrs off}"
766 DATA"{gray1}{rvrs off}{$ba}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$bb}{rvrs off}{gray1}{$d1}{rvrs off}"
767 DATA"{gray1}{rvrs off}{$bc}{rvrs off}{gray1}{$bd}{down}{left:2}{rvrs off}{gray1}{$be}{rvrs off}{gray1}{$be}{rvrs off}"
768 DATA"{gray1}{rvrs off}{$bf}{rvrs off}{gray1}{$bf}{down}{left:2}{rvrs on}{gray1}@{rvrs on}{gray1}@{rvrs off}"
769 DATA"{gray2}{rvrs on}A{rvrs on}{gray2}B{down}{left:2}{rvrs on}{gray2}C{rvrs on}{gray2}D{rvrs off}"
770 DATA"{gray1}{rvrs on}E{rvrs on}{gray1}F{down}{left:2}{rvrs on}{gray1}G{rvrs on}{gray1}H{rvrs off}"
771 DATA"{brown}{rvrs on}I{rvrs on}{brown}I{down}{left:2}{rvrs on}{brown}I{rvrs on}{brown}I{rvrs off}"
772 DATA"{lt. red}{rvrs on}J{rvrs on}{lt. red}K{down}{left:2}{rvrs on}{lt. red}L{rvrs on}{lt. red}M{rvrs off}"
773 DATA"{gray1}{rvrs on}N{rvrs on}{gray1}K{down}{left:2}{rvrs on}{gray1}L{rvrs on}{gray1}M{rvrs off}"
774 DATA"{lt. green}{rvrs on}O{rvrs on}{lt. green}P{down}{left:2}{rvrs on}{lt. green}Q{rvrs on}{lt. green}R{rvrs off}"
775 DATA"{gray2}{rvrs on}S{rvrs on}{gray2}T{down}{left:2}{rvrs on}{gray2}U{rvrs on}{gray2}V{rvrs off}"
776 DATA"{gray1}{rvrs on}W{rvrs on}{gray1}X{down}{left:2}{rvrs on}{gray1}Y{rvrs on}{gray1}Z{rvrs off}"
777 DATA"{gray1}{rvrs on}[{rvrs on}{gray1}{pound}{down}{left:2}{rvrs on}{gray1}]{rvrs on}{gray1}{^}{rvrs off}"
778 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
779 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
780 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
781 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
782 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
783 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
784 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
785 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
786 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
787 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
788 DATA"{lt. green}{rvrs on}{back arrow}{rvrs on}{lt. green}!{down}{left:2}{rvrs on}{lt. green}#{rvrs on}{lt. green}${rvrs off}"
789 DATA"{gray3}{rvrs on}%{rvrs on}{gray3}&{down}{left:2}{rvrs on}{gray3}'{rvrs on}{gray3}({rvrs off}"
790 DATA"{gray1}{rvrs on}%{rvrs on}{gray1}&{down}{left:2}{rvrs on}{gray1}'{rvrs on}{gray1}({rvrs off}"
791 DATA"{lt. green}{rvrs on}){rvrs on}{lt. green}*{down}{left:2}{rvrs on}{lt. green}+{rvrs on}{lt. green},{rvrs off}"
792 DATA"{gray1}{rvrs on}-{rvrs on}{gray1}.{down}{left:2}{rvrs on}{gray1}/{rvrs on}{gray1}0{rvrs off}"
793 DATA"{gray1}{rvrs on}1{rvrs on}{gray1}2{down}{left:2}{rvrs on}{gray1}3{rvrs on}{gray1}4{rvrs off}"
794 DATA"{gray1}{rvrs on}5{rvrs on}{gray1}6{down}{left:2}{rvrs on}{gray1}7{rvrs on}{gray1}8{rvrs off}"
795 DATA"{gray1}{rvrs on}9{rvrs on}{gray1}:{down}{left:2}{rvrs on}{gray1};{rvrs on}{gray1}<{rvrs off}"
796 DATA"{gray1}{rvrs on}={rvrs on}{gray1}>{down}{left:2}{rvrs on}{gray1}?{rvrs on}{gray1}{$c0}{rvrs off}"
797 DATA"{lt. green}{rvrs on}9{rvrs on}{lt. green}:{down}{left:2}{rvrs on}{lt. green};{rvrs on}{lt. green}<{rvrs off}"
798 DATA"{lt. red}{rvrs on}5{rvrs on}{lt. red}6{down}{left:2}{rvrs on}{lt. red}7{rvrs on}{lt. red}8{rvrs off}"
799 DATA"{lt. green}{rvrs on}5{rvrs on}{lt. green}6{down}{left:2}{rvrs on}{lt. green}7{rvrs on}{lt. green}8{rvrs off}"
800 DATA"{gray1}{rvrs on}{$c1}{rvrs on}{gray1}{$c2}{down}{left:2}{rvrs on}{gray1}{$c3}{rvrs on}{gray1}{$c4}{rvrs off}"
801 DATA"{gray1}{rvrs on}{$c5}{rvrs on}{gray1}{$c6}{down}{left:2}{rvrs on}{gray1}{$c7}{rvrs on}{gray1}{$c8}{rvrs off}"
802 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
803 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
804 DATA"        ",00,00,00,00,-1 
805 DATA"DOLCH   ",03,00,00,00,00  
806 DATA"HELM    ",00,02,-1,00,01  
807 DATA"KRAEUTER",00,00,-1,10,02  
808 DATA"FEUER   ",00,00,00,05,03  
809 DATA"EIS     ",00,00,00,08,03  
810 DATA"MUT     ",00,00,00,23,03  
811 DATA"BOMBE   ",00,00,00,50,03  
812 DATA"KIRSCHE ",00,00,-1,15,02  
813 DATA"HEILEN  ",00,00,00,04,03  
814 DATA"SKULL   ",00,00,00,00,04  
815 DATA"FLEGEL  ",07,00,-1,00,00  
816 DATA"LAHM    ",00,00,00,04,03  
817 DATA"STAB    ",12,00,00,00,00  
818 DATA"FEDER   ",00,10,00,00,01  
819 DATA"STOCK   ",23,00,00,00,00  
820 DATA"WESTE   ",00,14,00,00,01  
821 DATA"SCHILD  ",00,24,00,00,01  
822 DATA"ZURUECK ",00,00,00,0 ,09  
823 END
