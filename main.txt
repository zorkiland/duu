0 POKE55,240:POKE56,120:CLR
1 QO=53282
2 QP=53283
3 QQ=53281
4 QR=53280
5 QS=49152
6 V=53248
7 Q1%=0
8 Q7%=6
9 QA%=9
10 RS=165
11 DD$="{home}{down:20}{white}"
12 CD$="{down:25}"
13 DIMM$(13),MD$(13),MH%(13),MM%(13),MA%(13),MD%(13),MS%(13),MB%(13),MX%(13)
14 DIMFH%(11),FT%(11),FC%(11),FM%(11),FS%(11),FX%(11),FY%(11),FA%(11),FD(11),F1%(11),F2%(11)
15 FORI=0TO3:FX%(I)=37:FY%(I)=5+3*I:NEXT
16 DIMI$(18),IA%(18),ID%(18),IS%(18),IE%(18),IT%(18)
17 DIMER%(12),EX%(12),EY%(12),ET%(12)
18 DIMIN%(99),TT%(99)
19 DIMT$(79)
20 DIMS0%(10),S1%(10),S2%(10),S3%(10),A0%(10),A1%(10),A2%(10)
21 DIMN0%(10),N1%(10),N2%(8),N3%(10)
22 FORI=0TO13
23 READM$(I),MH%(I),MM%(I),MS%(I)
24 READMA%(I),MD%(I),MB%(I),MX%(I)
25 NEXT
26 FORI=0TO3
27 READP$(I),PH%(I),PM%(I),PS%(I)
28 READPA%(I),PD%(I),PW%(I),PR%(I)
29 NEXT
30 FORI=0TO79
31 READT$(I)
32 NEXT
33 PD$(0)=T$(48)
34 PD$(1)=T$(50)
35 PD$(2)=T$(52)
36 PD$(3)=T$(51)
37 ZZ=64
38 FORI=0TO13
39 MD$(I)=T$(ZZ)
40 ZZ=ZZ+1
41 NEXTI
42 FORI=0TO18
43 READI$(I),IA%(I),ID%(I),IS%(I),IE%(I),IT%(I)
44 NEXT
45 POKEV+21,0
46 POKE648,192
47 POKE56576,0
48 POKE53272,8
49 POKE53270,PEEK(53270)OR16
50 POKEQO,QA%
51 POKEQP,Q1%
52 POKEQR,Q1%
53 POKEQQ,Q7%
54 POKE1020,Q7%
55 POKE1021,Q7%
56 SYS820
57 GOSUB149
58 GOTO620
59 GOSUB642
60 GOSUB512
61 GOSUB651
62 GOSUB443
63 PRINT"{home}{down}{right}{white}                                      ";
64 POKE1020,Q7%
65 GOSUB541
66 IFPEEK(2)=1THENCP=30960+RS*(CR-0)
67 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)PD$(0);
68 OX=ZX:OY=ZY
69 ZX=X:ZY=Y
70 GOSUB719
71 IFA$=CHR$(145)ORA$="W"THENZY=Y-1:GOTO77
72 IFA$=CHR$(157)ORA$="A"THENZX=X-1:GOTO77
73 IFA$=CHR$(29)ORA$="D"THENZX=X+1:GOTO77
74 IFA$=CHR$(17)ORA$="S"THENZY=Y+1:GOTO77
75 IFA$=CHR$(13)THEN182
76 GOTO70
77 AA%=0
78 FORI=0TO12
79 IFER%(I)=CRANDEX%(I)=ZXANDEY%(I)=ZYTHENAA%=1
80 NEXTI
81 IFZX=-1ORZX=20ORZY=-1ORZY=8THEN93
82 C=PEEK(CP+ZX+ZY*20)
83 IFC=08THEN109
84 IFC=09THEN109
85 IFC=10THEN109
86 IFC=11THEN109
87 IFC=12THEN116
88 IFC=14THEN111
89 IFC=25THEN116
90 IFC=40THEN129
91 IFC>=48THEN120
92 IFC>7THEN68
93 VA=0
94 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)T$(PEEK(CP+X+Y*20))
95 GOSUB516
96 IFRND(1)*100>97ANDPEEK(CP+164)>0THENFF=0:GOTO258
97 X=ZX:Y=ZY
98 IFX=-1ANDPEEK(CP+160)=255THENX=0:GOTO67
99 IFX=-1THENCR=PEEK(CP+160):X=19:GOTO64
100 IFX=20ANDPEEK(CP+161)=255THENX=19:GOTO67
101 IFX=20THENCR=PEEK(CP+161):X=0:GOTO64
102 IFY=-1ANDPEEK(CP+162)=255THENY=0:GOTO67
103 IFY=-1THENCR=PEEK(CP+162):Y=7:GOTO64
104 IFY=8ANDPEEK(CP+163)=255THENY=7:GOTO67
105 IFY=8THENCR=PEEK(CP+163):Y=0:GOTO64
106 IFC=2THEN111
107 IFC=3THEN111
108 GOTO67
109 IFCR=16THENCR=16:X=0:Y=0:GOTO63
110 IFCR=16THEN636
111 IFC=14ANDCR=S0%(0)ANDZX=S1%(0)THENS3%(0)=1-S3%(0):GOSUB133:TX=A1%(0):TY=A2%(0):GOSUB130:GOSUB146:GOTO68
112 IFC=2ANDCR=S0%(1)ANDX=S1%(1)THENS3%(1)=1:GOSUB133:TX=A1%(1):TY=A2%(1):GOSUB130:GOTO67
113 IFC=2ANDCR=S0%(2)ANDX=S1%(2)THENS3%(2)=1:GOSUB133:TX=A1%(2):TY=A2%(2):GOSUB130:GOTO67
114 IFC=3ANDCR=S0%(3)ANDX=S1%(3)THENS3%(3)=1:GOSUB133:TX=A1%(3):TY=A2%(3):GOSUB130:GOTO67
115 IFC=3ANDCR=S0%(4)ANDX=S1%(4)THENS3%(4)=1:GOSUB133:TX=A1%(4):TY=A2%(4):GOSUB130:GOTO67
116 IFC=12ANDAA%=0THENGOTO68
117 IFC=12ANDAA%=1THENVA$="IN DER TRUHE IST WAS":GOSUB684:GOTO68
118 IFC=25ANDAA%=0THENGOTO68
119 IFC=25ANDAA%=1THENVA$="DER BAUM SIEHT INTRESSANT AUS":GOSUB684:GOTO68
120 IFC=49THENPRINT"{white}{home}{down}{right}EIN HEILZAUBER IST UNTER DEM BAUM":GOSUB741:GOSUB516:GOTO68
121 IFC=53THENPRINT"{white}{home}{down}{right}EIN FEUERZAUBER IST IN DEN KATAKOMBEN":GOSUB741:GOSUB516:GOTO68
122 IFC=50THENAA$="TXT.WELCOME.LENA":GOSUB660:N3%(0)=1:GOSUB133:P3%(1)=1:TX=12:TY=4:GOSUB130:GOSUB443:GOTO68
123 IFC=52THENAA$="TXT.WELCOME.DOLM":GOSUB660:N3%(1)=1:GOSUB133:P3%(2)=1:TX=7:TY=2:GOSUB130:GOSUB443:GOTO68
124 IFC=51THENAA$="TXT.WELCOME.MIRA":GOSUB660:N3%(2)=1:GOSUB133:P3%(3)=1:TX=3:TY=2:GOSUB130:GOSUB443:GOTO68
125 IFC=67THENPRINT"{white}{home}{down}{right}DU KANNST HIER NICHT DURCH!":GOSUB741:FF=4:N3%(3)=1:GOSUB133:GOSUB516:GOTO258
126 IFC=72THENPRINT"{white}{home}{down}{right}ACH WIE SUESS!":GOSUB741:FF=9:N3%(4)=1:GOSUB133:GOSUB516:GOTO258
127 IFC=76THENPRINT"{white}{home}{down}{right}DU WILLST MICH BESIEGEN!":GOSUB741:FF=13:N3%(5)=1:GOSUB133:GOSUB516:GOTO258
128 IFC=77THENPRINT"{white}{home}{down}{right}DU HAST MEINE ERWARTUNG UEBERTROFFEN!":GOSUB741:FF=14:N3%(6)=1:GOSUB133:GOSUB516:GOTO258
129 IFC=40THENVA$="DU BIST GEHEILT":GOSUB438:GOSUB443:GOSUB684:GOTO68
130 SP=30960+(CR*RS)+(TX+TY*20)
131 PRINT"{home}{down:3}"LEFT$(CD$,TY*2)SPC(TX*2)T$(PEEK(SP));
132 RETURN
133 POKE30960+N0%(0)*RS+N1%(0)+N2%(0)*20,50-N3%(0)*50
134 POKE30960+N0%(1)*RS+N1%(1)+N2%(1)*20,52-N3%(1)*52
135 POKE30960+N0%(2)*RS+N1%(2)+N2%(2)*20,51-N3%(2)*51
136 POKE30960+N0%(3)*RS+N1%(3)+N2%(3)*20,67-N3%(3)*67
137 POKE30960+N0%(4)*RS+N1%(4)+N2%(4)*20,72-N3%(4)*72
138 POKE30960+N0%(5)*RS+N1%(5)+N2%(5)*20,76-N3%(5)*76
139 POKE30960+N0%(6)*RS+N1%(6)+N2%(6)*20,77-N3%(6)*77
140 POKE30960+A0%(0)*RS+A1%(0)+A2%(0)*20,15-S3%(0)*15+1
141 POKE30960+A0%(1)*RS+A1%(1)+A2%(1)*20,13-S3%(1)*13
142 POKE30960+A0%(2)*RS+A1%(2)+A2%(2)*20,13-S3%(2)*12
143 T=13:IFS3%(3)+S3%(4)=2THENT=0
144 POKE30960+A0%(3)*RS+A1%(3)+A2%(3)*20,T
145 RETURN
146 IFCR=S0%(0)ANDZX=S1%(0)ANDS3%(0)=0THENVA$="DER SCHALTER IST AUS":GOSUB684
147 IFCR=S0%(0)ANDZX=S1%(0)ANDS3%(0)=1THENVA$="SCHALTER IST EIN":GOSUB684
148 RETURN
149 S0%(0)=0:S1%(0)=11:S2%(0)=1:S3%(0)=0
150 S0%(1)=0:S1%(1)=16:S2%(1)=6:S3%(1)=0
151 S0%(2)=0:S1%(2)=18:S2%(2)=6:S3%(2)=0
152 S0%(3)=0:S1%(3)=1:S2%(3)=6:S3%(3)=0
153 S0%(4)=0:S1%(4)=5:S2%(4)=6:S3%(4)=0
154 A0%(0)=0:A1%(0)=14:A2%(0)=6
155 A0%(1)=0:A1%(1)=7:A2%(1)=4
156 A0%(2)=0:A1%(2)=9:A2%(2)=5
157 A0%(3)=0:A1%(3)=3:A2%(3)=4
158 A0%(4)=0:A1%(4)=3:A2%(4)=4
159 N0%(0)=0:N1%(0)=12:N2%(0)=4:N3%(0)=0
160 N0%(1)=0:N1%(1)=7:N2%(1)=2:N3%(1)=0
161 N0%(2)=0:N1%(2)=3:N2%(2)=2:N3%(2)=0
162 N0%(3)=16:N1%(3)=0:N2%(3)=0:N3%(3)=0
163 N0%(4)=16:N1%(4)=0:N2%(4)=0:N3%(4)=0
164 N0%(5)=16:N1%(5)=0:N2%(5)=0:N3%(5)=0
165 N0%(6)=16:N1%(6)=0:N2%(6)=0:N3%(6)=0
166 N0%(7)=16:N1%(7)=0:N2%(7)=0:N3%(7)=0
167 N0%(8)=16:N1%(8)=0:N2%(8)=0:N3%(8)=0
168 ER%(0)=16:EX%(0)=0:EY%(0)=0:ET%(1)=0
169 ER%(1)=0:EX%(1)=1:EY%(1)=2:ET%(1)=4
170 ER%(2)=0:EX%(2)=18:EY%(2)=2:ET%(2)=15
171 ER%(3)=16:EX%(3)=0:EY%(3)=0:ET%(3)=3
172 ER%(4)=16:EX%(4)=0:EY%(4)=0:ET%(4)=8
173 ER%(5)=16:EX%(5)=0:EY%(5)=0:ET%(5)=5
174 ER%(6)=16:EX%(6)=0:EY%(6)=0:ET%(6)=16
175 ER%(7)=16:EX%(7)=0:EY%(7)=0:ET%(7)=17
176 ER%(8)=16:EX%(8)=0:EY%(8)=0:ET%(8)=13
177 ER%(9)=16:EX%(9)=0:EY%(9)=0:ET%(9)=15
178 ER%(10)=16:EX%(10)=0:EY%(10)=0:ET%(10)=11
179 ER%(11)=16:EX%(11)=0:EY%(11)=0:ET%(11)=7
180 ER%(12)=16:EX%(12)=0:EY%(12)=0:ET%(12)=9
181 RETURN
182 PRINT"{home}{down}{white}{right}  UNTERSUCHE  EQUIPMENTS  INVENTAR"
183 M=0
184 POKEQS+42+M*12,35
185 GOSUB702
186 POKEQS+42+M*12,32
187 IFA$="A"THENM=M-1-3*(M=0):GOTO184
188 IFA$="D"THENM=M+1+3*(M=2):GOTO184
189 IFA$=CHR$(13)THENPOKEQS+42+M*7,32:GOTO191
190 GOTO184
191 PRINT"{home}{down}{white}{right}                                      ";
192 ONM+1GOTO246,206,193
193 POKE1020,Q1%
194 GOSUB656
195 PRINT"{home}{down}{right}{white}INVENTAR: WAEHLE DEIN ITEM           ";
196 MT=2:MX=2:MY=4:MC=14:GOSUB454
197 IFIN%(M)=9THEN63
198 IFIN%(M)=0ORIT%(IN%(M))<>MTTHEN63
199 IFIN%(M)=3THENGOSUB202:IN%(M)=0:GOSUB443:GOTO63
200 IFIN%(M)=8THENGOSUB204:IN%(M)=0:GOSUB443:GOTO63
201 GOTO63
202 FORI=0TO3:PH%(I)=PH%(I)+IE%(IN%(M)):IFPH%(I)>P1%(I)THENPH%(I)=P1%(I)
203 FH%(I)=PH%(I):NEXT:RETURN
204 FORI=0TO3:PM%(I)=PM%(I)+IE%(IN%(M)):IFPM%(I)>P2%(I)THENPM%(I)=P2%(I)
205 FM%(I)=PM%(I):NEXT:RETURN
206 POKE1020,Q1%
207 GOSUB518
208 PRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINEN SPIELER     ";
209 P=0
210 CP=QS+2+(4-8*(P>1))*40:IFP=1ORP=3THENCP=CP+20
211 POKECP,35
212 GOSUB702
213 POKECP,32
214 IFP=0ANDA$="D"ANDP3%(1)=1THENP=1:GOTO210
215 IFP=0ANDA$="S"ANDP3%(2)=1THENP=2:GOTO210
216 IFP=0ANDA$="D"ANDP3%(1)=OANDP3%(2)=0ANDP3%(3)=1THENP=3:GOTO210
217 IFP=0ANDA$="S"ANDP3%(1)=OANDP3%(2)=0ANDP3%(3)=1THENP=3:GOTO210
218 IFP=1ANDA$="A"ANDP3%(0)=1THENP=0:GOTO210
219 IFP=1ANDA$="S"ANDP3%(3)=1THENP=3:GOTO210
220 IFP=2ANDA$="D"ANDP3%(3)=1THENP=3:GOTO210
221 IFP=2ANDA$="W"ANDP3%(0)=1THENP=0:GOTO210
222 IFP=3ANDA$="A"ANDP3%(2)=1THENP=2:GOTO210
223 IFP=3ANDA$="W"ANDP3%(1)=1THENP=1:GOTO210
224 IFP=3ANDA$="A"ANDP3%(2)=OANDP3%(1)=0ANDP3%(0)=1THENP=0:GOTO210
225 IFP=3ANDA$="W"ANDP3%(2)=OANDP3%(1)=0ANDP3%(0)=1THENP=0:GOTO210
226 IFA$=CHR$(13)THEN228
227 GOTO211
228 SL=0:CP=CP+81
229 PRINT"{home}{down}{right}{white}EQUIPMENT: WAFFE ODER RUESTUNG       ";
230 POKECP+SL*40,35
231 GOSUB702
232 POKECP+SL*40,32:IFA$="W"ORA$="S"THENSL=1-SL:GOTO230
233 IFA$=CHR$(13)THEN235
234 GOTO230
235 MY=4:MX=23:MC=14:IFP=1ORP=3THENMX=3
236 IFSL=0THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE WAFFE        ";
237 IFSL=1THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE RUESTUNG     ";
238 PRINT"{home}"LEFT$(CD$,MY-1)SPC(MX-3);"{brown}{$c1}{$c2:18}{$c3}";
239 FORI=0TO13:PRINT"{down}{left:20}{$c4}                  {$c5}";:NEXT
240 PRINT"{down}{left:20}{$c6}{$c7:18}{$c8}";
241 MT=SL:GOSUB454
242 IFIT%(IN%(M))=9THEN63
243 IFIN%(M)<>0ANDIT%(IN%(M))<>SLTHEN63
244 IFSL=0THENQ=PW%(P):PW%(P)=IN%(M):IN%(M)=Q:GOTO63
245 IFSL=1THENQ=PR%(P):PR%(P)=IN%(M):IN%(M)=Q:GOTO63
246 GOSUB516
247 GOSUB746
248 EI=0
249 IFER%(EI)<>CROREX%(EI)<>OXOREY%(EI)<>OYTHEN254
250 NI%=ET%(EI):GOSUB549
251 IFIS=99THENVA$="DU KANNST NICHT MEHR TRAGEN":GOSUB684:GOTO68
252 ER%(EI)=-1
253 VA$="GEFUNDEN: "+I$(IN%(IS)):GOSUB684:GOTO68
254 EI=EI+1
255 IFEI=12THEN257
256 GOTO249
257 VA$="NICHTS BESONDERES":GOSUB684:GOTO68
258 FORI=4TO11:FT%(I)=-1:NEXT
259 PP=4:CC=4:WX=0
260 FORI=0TO3:FT%(I)=-2:FH%(I)=PH%(I):FM%(I)=PM%(I):FS%(I)=PS%(I)
261 IFP3%(I)=0THENFH%(I)=-1
262 IFFH%(I)<=0THENFT%(I)=-1:PP=PP-1
263 FA%(I)=PA%(I)+IA%(PW%(I)):FD(I)=PD%(I)+ID%(PR%(I)):F1%(I)=P1%(I):F2%(I)=P2%(I)
264 NEXT
265 PRINT"{home}{down:3}{white}";:FORI=0TO15:PRINT"                                        ";:NEXT
266 GOSUB646
267 GOSUB419
268 GOSUB443
269 GOSUB451
270 FORI=0TO11
271 IFFT%(I)<>-1THENFC%(I)=INT(RND(0)*FD%(I))
272 NEXT
273 CC=CC-4
274 PRINT"{home}{down}{right}                       ";:GOSUB560
275 FORI=4TO11:IFFT%(I)=-1THEN277
276 PRINT"{home}"LEFT$(CD$,FY%(I));SPC(FX%(I));MD$(FT%(I))
277 NEXT
278 C=0
279 IFFT%(C)<>-1ANDFC%(C)>=100THENFC%(C)=FC%(C)-100:GOTO282
280 C=C+1:IFC<12THEN279
281 FORI=0TO11:FC%(I)=FC%(I)+FS%(I):NEXT:GOTO278
282 IFFT%(C)=-2THEN284
283 GOTO368
284 PRINT"{home}{white}{down}{right}"P$(C)": IST AM ZUG                     "
285 PRINTDD$;SPC(16)"{white} ANGRIFF {down}{left:9} INVENTAR";:M=2
286 PRINTDD$;SPC(16)"{down:2}         {down}{left:9}         ";
287 IFPM%(C)>0THENPRINTDD$;SPC(16)"{down:2} MAGIE   ";:M=3
288 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(37)"  {down}{left:2}  ";
289 PRINT"{left:3}{up}"PD$(C);
290 MY=0:FZ=0
291 POKEQS+816+MY*40,35
292 GOSUB702
293 IFA$="S"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY+1:GOTO300
294 IFA$="W"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY-1:GOTO300
295 IFA$=CHR$(13)THENFZ=0:GOTO303
296 IFA$="FR"ANDCR=14ORCR=15ORCR=16THENPRINT"{home}{down}{right}{white}FLUCHT NICHT MOEGLICH                 ":GOSUB746:GOSUB516:GOSUB748:GOTO292
297 IFA$="FR"ANDFZ=3THENFZ=0:GOTO59
298 IFA$="FR"ANDFZ<=3THENFZ=FZ+1:PRINT"{home}{down}{right}{white}FLUCHT                                ":GOSUB746:GOSUB516:GOSUB748:GOTO291
299 GOTO292
300 IFMY>=MTHENMY=0
301 IFMY<0THENMY=M-1
302 GOTO291
303 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(36)"{white}  {down}{left:2}  {left}{up}"PD$(C);
304 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
305 ONMY+1GOTO331,323
306 MT=3:MX=16:MY=20:MC=4:GOSUB454
307 IFIT%(IN%(M))=9THEN284
308 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         {down}{left:9}         ";
309 IFIN%(M)=0ORIT%(IN%(M))<>3THEN284
310 M=IN%(M)
311 IFIE%(M)>FM%(C)THENPRINT"{home}{down}{right}NICHT GENUG MANA                     ";:GOSUB741:GOTO284
312 GOSUB576
313 GOTO602
314 AP=IE%(M)+2-INT(RND(0)*5):FM%(C)=FM%(C)-IE%(M):PM%(C)=FM%(C):GOSUB443
315 AP=AP+PL%(C)*3
316 FH%(CE)=FH%(CE)-AP:IFFH%(CE)<0THENFH%(CE)=0
317 IFCE<4THENPH%(CE)=FH%(CE)
318 IFFH%(CE)>0THENGOTO353
319 IFFT%(CE)<>-2THENCC=CC-1:WX=WX+MX%(FT%(CE))
320 IFFT%(CE)=-2THENPP=PP-1
321 FT%(CE)=-1
322 GOTO353
323 MT=2:MX=16:MY=20:MC=4:GOSUB454
324 IFIT%(IN%(M))=9THEN284
325 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
326 PRINT"{down}{left:9}         ";
327 IFIN%(M)=0ORIT%(IN%(M))<>MTTHEN284
328 IFIN%(M)=3THENGOSUB202:IN%(M)=0:GOSUB443:GOTO274
329 IFIN%(M)=8THENGOSUB204:IN%(M)=0:GOSUB443:GOTO274
330 GOTO274
331 GOSUB576
332 AP=FA%(C)-FD(CE)
333 IFAP<=0THENPRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{white}MISS";:GOSUB739:GOTO345
334 AP=INT(RND(0)*AP)+1
335 FH%(CE)=FH%(CE)-AP:IFFH%(CE)<0THENFH%(CE)=0
336 IFCE<4THENPH%(CE)=FH%(CE)
337 IFFH%(CE)>0THENGOTO342
338 IFFT%(CE)<>-2THENCC=CC-1:WX=WX+MX%(FT%(CE))
339 IFFT%(CE)=-2THENPP=PP-1
340 FT%(CE)=-1
341 GOTO342
342 POKEV,(FX%(CE)*8+21)AND255:POKEV+1,FY%(CE)*8+50:POKE50168,16:POKEV+28,0
343 POKEV+16,-(FX%(CE)>30):POKEV+21,1
344 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{white}";AP
345 FORI=0TO5:POKEV,PEEK(V)+2:POKEV+1,PEEK(V+1)+2:GOSUB740:NEXTI
346 POKEV+21,0:IFCE<4THENGOSUB443
347 GOSUB739
348 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
349 IFFT%(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE));"  {down}{left:2}  ";
350 IFCC=0THEN376
351 IFPP=0THEN404
352 GOTO274
353 POKEV,(FX%(CE)*8+24)AND255:POKEV+1,FY%(CE)*8+50
354 IFM=4THENPOKEV+37,8:POKEV+38,2:POKEV+39,7:POKEV+28,1
355 B=17:IFM=6THENB=19:POKEV+28,0
356 IFM=5THENB=21:POKEV+28,0
357 IFM=7THENB=23:POKEV+28,0
358 POKEV+16,-(FX%(CE)>30):POKE50168,B:POKEV+21,1
359 PRINT"{white}";:IFM=9THENPRINT"{green}";
360 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}";AP
361 FORK=0TO3:FORI=0TO1:POKE50168,B+I:FORJ=1TO40:NEXTJ,I,K
362 POKEV+21,0:IFCE<4THENGOSUB443
363 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
364 IFFT%(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE));"  {down}{left:2}  ";
365 IFCC=0THEN376
366 IFPP=0THEN404
367 GOTO274
368 GOSUB739
369 PRINT"{white}{home}{down}{right}"M$(FT%(C))": GREIFT AN                    "
370 FC%(C)=FC%(C)-100
371 IFFT%(C)=7THENIFFM%(C)>=IE%(9)THENM=9:GOSUB739:GOSUB413:GOTO605
372 IFFT%(C)=9THENIFFM%(C)>=IE%(6)THENM=6:GOSUB739:GOSUB407:GOTO314
373 IFFT%(C)=10THENIFFM%(C)>=IE%(4)THENM=4:GOSUB739:GOSUB407:GOTO314
374 IFFT%(C)=11THENIFFM%(C)>=IE%(12)THENM=12:GOSUB739:GOSUB407:GOTO612
375 GOSUB739:GOSUB407:GOTO332
376 POKE1020,Q1%
377 PRINTDD$;"{brown}{up}{$c1}{$c2:10}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}{white}";
378 PRINT"{home}{down}{right}DU HAST GEWONNEN                     ";:GOSUB560
379 GOSUB656
380 FORI=0TO3:IFP3%(I)=0THEN384
381 PRINT"{home}{white}{right:3}";:IFPH%(I)=0THENPRINT"{red}";
382 PRINTLEFT$(CD$,4+I*3)P$(I)
383 PRINT"{home}"LEFT$(CD$,5+I*3)"{right:3}LV";PL%(I)+1;" EX";PX%(I)
384 NEXT
385 FORI=0TO3:IFPH%(I)=0ORP3%(I)=0THEN395
386 TX=WX
387 PRINT"{home}{white}"LEFT$(CD$,5+I*3)SPC(20)"EXPERIENCE {left}"TX"{left} ";
388 IFTX=0THEN395
389 XS=1:IFTX>9THENXS=10
390 IFTX>99THENXS=100
391 TX=TX-XS
392 PX%(I)=PX%(I)+XS
393 LL=10+PL%(I)*PL%(I)*10:IFPX%(I)>=LLTHENGOSUB397:GOTO393
394 PRINT"{home}{white}"LEFT$(CD$,5+I*3)"{right:3}LV";PL%(I)+1;" EX";PX%(I):GOTO387
395 NEXTI
396 GOTO402
397 PL%(I)=PL%(I)+1:PA%(I)=PA%(I)+1:PD%(I)=PD%(I)+1:PS%(I)=PS%(I)+1
398 P1%(I)=P1%(I)+PL%(I)*2:IFP2%(I)>0THENP2%(I)=P2%(I)+PL%(I)*3
399 IFP1%(I)>999THENP1%(I)=999
400 IFP2%(I)>999THENP2%(I)=999
401 RETURN
402 GOSUB732
403 GOTO59
404 PRINT"{home}{down}{right}{white}DU HAST VERLOREN                     "
405 GOSUB732
406 GOTO57
407 RE=INT(RND(0)*4):CE=0
408 IFFT%(CE)=-2ANDPH%(CE)>0THEN411
409 CE=CE+1:IFCE=4THENCE=0
410 GOTO408
411 IFRE>0THENRE=RE-1:GOTO409
412 RETURN
413 RE=INT(RND(0)*8):CE=4
414 IFFT%(CE)>=0THEN417
415 CE=CE+1:IFCE=12THENCE=4
416 GOTO414
417 IFRE>0THENRE=RE-1:GOTO415
418 RETURN
419 IFFF>0THENL=FF:GOTO421
420 L=PEEK(CP+164):IFL=0THENRETURN
421 IFL=3THENA=0:LC=INT(RND(0))*2+2:FORI=0TOLC:GOSUB554:NEXT:RETURN
422 IFL=1THENA=1:FORI=0TO2:GOSUB554:NEXT:RETURN
423 IFL=2THENA=1:FORI=0TO1:GOSUB554:NEXT:A=2:FORI=0TO1:GOSUB554:NEXT:RETURN
424 IFL=4THENA=3:GOSUB554:A=1:GOSUB554:GOSUB554:RETURN
425 IFL=5THENA=4:LC=INT(RND(0)*3)+1:FORI=0TOLC:GOSUB554:NEXT:RETURN
426 IFL=6THENONINT(RND(1)*2)+1GOTO436,437
427 IFL=7THENLC=INT(RND(0)*3)+1:A=6:FORI=0TOLC:GOSUB554:NEXT:RETURN
428 IFL=8THENA=7:GOSUB554:LC=INT(RND(0)*2)+1:A=6:GOSUB554:RETURN
429 IFL=9THENA=9:GOSUB554:A=8:GOSUB554:A=5:GOSUB554:RETURN
430 IFL=10THENA=6:LC=INT(RND(0)*2)+1:FORI=1TOLC:GOSUB554:NEXT:RETURN
431 IFL=11THENA=7:GOSUB554:A=10:GOSUB554:RETURN
432 IFL=12ANDINT(RND(1)*2)=1THENA=11:GOSUB554:RETURN
433 IFL=12THENA=5:GOSUB554:GOSUB554:A=9:GOSUB554:RETURN
434 IFL=13THENA=9:GOSUB554:A=12:GOSUB554:A=9:GOSUB554:RETURN
435 IFL=14THENA=9:GOSUB554:A=13:GOSUB554:A=7:GOSUB554:RETURN
436 A=5:LC=INT(RND(1)*2)+1:FORI=0TOLC:GOSUB554:NEXT:RETURN
437 A=9:LC=INT(RND(1)*2)+1:FORI=1TOLC:GOSUB554:NEXT:RETURN
438 FORI=0TO3
439 IFP3%(I)=0THEN442
440 PH%(I)=P1%(I)
441 PM%(I)=P2%(I)
442 NEXT:RETURN
443 PRINTDD$;
444 FORI=0TO3:IFP3%(I)=0THENPRINTSPC(40);:GOTO450
445 PRINT"{white}";:IFPH%(I)=0THENPRINT"{red}";
446 PRINTSPC(27)P$(I)" ";:A=PH%(I):GOSUB700
447 PRINTD$;
448 A=PM%(I):GOSUB700
449 PRINT" "D$"{right}";
450 NEXT:RETURN
451 FORI=0TO3:IFPH%(I)<=0ORP3%(I)=0THENNEXT:RETURN
452 PRINT"{home}"LEFT$(CD$,5+I*3);SPC(37);PD$(I):NEXT
453 RETURN
454 GOSUB489
455 MI=-1:M=0:MO=0
456 MI=MO-1:CY=MY:MM=0
457 IFMC=4THENGOSUB484
458 MI=MI+1
459 PRINT"{home}{white}"LEFT$(CD$,CY)SPC(MX)" ";
460 IFIT%(IN%(MI))<>MTTHENPRINT"{cyan}";
461 IFIT%(IN%(MI))=9THENPRINT"{white}";
462 IFIT%(IN%(MI))=-1THENPRINT"{cyan}";
463 PRINTI$(IN%(MI));
464 CY=CY+1
465 MM=MM+1
466 IFMM=MCTHEN468
467 IFMI<13THEN458
468 CP=QS+MX+MY*40
469 POKECP+(M-MO)*40,35
470 GOSUB702
471 POKECP+(M-MO)*40,32
472 IFA$="W"THENM=M-1:GOTO476
473 IFA$="S"THENM=M+1:GOTO476
474 IFA$=CHR$(13)THENRETURN
475 GOTO469
476 IFM=-1THENM=14-1:MO=14-MC:GOTO456
477 IFM=14THENM=0:MO=0:GOTO456
478 M%=0
479 IFM<MOTHENMO=MO-MC:M%=1:GOTO479
480 IFM>=MO+MCTHENMO=MO+MC:M%=1
481 IFMO<0THENMO=0:M%=1
482 IFM%=1THEN456
483 GOTO469
484 FORI=0TO3
485 PRINT"{home}{white}"LEFT$(CD$,CY+I)SPC(MX)" ";
486 PRINT"        ";
487 NEXTI
488 RETURN
489 A=0
490 FORI=0TO99:TT%(I)=0:NEXTI
491 FORI=0TO99:TT%(I)=IN%(I):IN%(I)=0:NEXTI
492 IFMT=0THENGOSUB497:GOSUB500:A=50:GOSUB503:GOSUB506:GOSUB509
493 IFMT=1THENGOSUB497:GOSUB503:A=50:GOSUB500:GOSUB506:GOSUB509
494 IFMT=2THENGOSUB497:GOSUB506:A=50:GOSUB500:GOSUB503:GOSUB509
495 IFMT=3THENGOSUB497:GOSUB509:A=50:GOSUB500:GOSUB503:GOSUB506
496 RETURN
497 FORI=0TO99:IFIT%(TT%(I))=9THENIN%(A)=TT%(I):A=A+1
498 NEXTI
499 RETURN
500 FORI=0TO99:IFIT%(TT%(I))=0THENIN%(A)=TT%(I):A=A+1
501 NEXTI
502 RETURN
503 FORI=0TO99:IFIT%(TT%(I))=1THENIN%(A)=TT%(I):A=A+1
504 NEXTI
505 RETURN
506 FORI=0TO99:IFIT%(TT%(I))=2THENIN%(A)=TT%(I):A=A+1
507 NEXTI
508 RETURN
509 FORI=0TO99:IFIT%(TT%(I))=3THENIN%(A)=TT%(I):A=A+1
510 NEXTI
511 RETURN
512 PRINT"{brown}{home}{down:3}";
513 FORI=0TO15
514 PRINT"                                        ";:NEXT
515 RETURN
516 PRINT"{home}{down}{right}{white}                                      ";
517 RETURN
518 PRINT"{brown}{home}{down:3}{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
519 FORI=0TO5
520 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
521 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
522 PRINT"{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
523 FORI=0TO5
524 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
525 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
526 DX=3:DY=4:P=0:GOSUB529:DX=23:P=1:GOSUB529
527 DX=3:DY=12:P=2:GOSUB529:DX=23:P=3:GOSUB529
528 RETURN
529 IFP3%(P)=0THENRETURN
530 PRINT"{home}{white}";:IFPH%(P)=0THENPRINT"{red}";
531 PRINT"{home}"LEFT$(CD$,DY)SPC(DX-1)" "P$(P)"  LV"PL%(P)+1
532 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX)" "I$(PW%(P));
533 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX)" "I$(PR%(P));
534 T1%=PA%(P)+IA%(PW%(P))
535 T2%=PD%(P)+ID%(PR%(P))
536 IFT1%>999THENT1%=999
537 IFT2%>999THENT2%=999
538 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX+9)"ATK"T1%;
539 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX+9)"DEF"T2%;
540 RETURN
541 IFPEEK(2)=1THENSP=30960+RS*(CR-0)
542 FORI=0TO7
543 PRINT"{home}{down:3}"LEFT$(CD$,I*2);
544 FORJ=0TO19
545 PRINTT$(PEEK(SP))"{up}";
546 SP=SP+1
547 NEXTJ,I
548 RETURN
549 IS=0
550 IFIN%(IS)=0THENIN%(IS)=NI%:NI%=-1:RETURN
551 IS=IS+1
552 IFIS=99THENRETURN
553 GOTO550
554 IFCC>=11THENRETURN
555 FH%(CC)=MH%(A):FM%(CC)=MM%(A):FT%(CC)=A:FC%(CC)=INT(RND(0)*MS%(A)):FS%(CC)=MS%(A)
556 FX%(CC)=2:FY%(CC)=5+CC*3:FA%(CC)=MA%(A):FD(CC)=MD%(A):FB(CC)=MB%(A)
557 F1%(CC)=MH%(A):F2%(CC)=MM%(A)
558 IFCC>=4THENFX%(CC)=5:FY%(CC)=5+(CC-4)*3
559 CC=CC+1:RETURN
560 ZM=0
561 FORJ=4TO7
562 IFFT%(J)=-1THENZM=ZM-1
563 ZM=ZM+1
564 NEXTJ
565 IFZM<>CMTHENPRINTDD$;"{right}          {down}{left:10}          {down}{left:10}          {down}{left:10}          ";
566 PRINTDD$;
567 CM=0
568 FORI=4TO7
569 IFFT%(I)=-1THENNEXT:RETURN
570 A=FH%(I):GOSUB700
571 PRINT"{home}{right}"LEFT$(CD$,20+CM)M$(FT%(I));
572 PRINT"{home}{right}"LEFT$(CD$,20+CM);"{right:7}";D$
573 CM=CM+1
574 NEXT
575 RETURN
576 CE=4:PRINT"{home}{down}{right}{white}"P$(C)": WAHELE DEIN ZIEL               "
577 IFFT%(CE)=-1THEN586
578 POKEQS-2+FX%(CE)+FY%(CE)*40,35
579 GOSUB702
580 IFA$=CHR$(13)THENPOKEQS-2+FX%(CE)+FY%(CE)*40,32:RETURN
581 IFA$="S"THENGOTO586
582 IFA$="W"THENGOTO594
583 IFA$="D"ANDCE>3THENPOKEQS-2+FX%(CE)+FY%(CE)*40,32:CE=0:GOTO577
584 IFA$="A"ANDCE<4THENPOKEQS-2+FX%(CE)+FY%(CE)*40,32:CE=4:GOTO577
585 GOTO579
586 CO=CE
587 POKEQS-2+FX%(CE)+FY%(CE)*40,32
588 CE=CE+1
589 IFCE>=12THENCE=0
590 IFCO<4ANDCE>3THENCE=CO
591 IFCO>3ANDCE<4THENCE=CO
592 IFFT%(CE)<>-1THEN578
593 GOTO588
594 CO=CE
595 POKEQS-2+FX%(CE)+FY%(CE)*40,32
596 CE=CE-1
597 IFCE<0THENCE=11
598 IFCO<4ANDCE>3THENCE=CO
599 IFCO>3ANDCE<4THENCE=CO
600 IFFT%(CE)<>-1THEN578
601 GOTO596
602 IFFT%(C)=-2THEN618
603 IFFT%(C)=7ANDFM%(C)>=IE%(9)THENM=9:GOTO605
604 GOTO314
605 AP=IE%(M)*4+2-INT(RND(0)*5):FM%(C)=FM%(C)-IE%(M):IFC<4THENPM%(C)=FM%(C):GOSUB443
606 FH%(CE)=FH%(CE)+AP:IFFH%(CE)>F1%(CE)THENFH%(CE)=F1%(CE)
607 IFCE<4THENPH%(CE)=FH%(CE):GOSUB443
608 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{green}";AP
609 GOSUB746
610 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
611 GOTO274
612 FM%(C)=FM%(C)-IE%(M):IFC<4THENPM%(C)=FM%(C):GOSUB443
613 FS%(CE)=FS%(CE)/2:IFFS%(CE)<5THENFS%(CE)=5
614 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{green}SLOW";
615 GOSUB746
616 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
617 GOTO274
618 IFM=9THEN605
619 GOTO314
620 FORI=0TO3:PX%(I)=0:NEXT
621 PA%(0)=5:PD%(0)=2:PA%(1)=4:PD%(1)=3:PA%(2)=1:PD%(2)=1:PA%(3)=1:PD%(3)=1
622 FORI=0TO13:IN%(I)=0:NEXT
623 IN%(0)=18
624 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
625 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
626 PRINT"{$c6}{$c7:38}{up:3}";
627 POKE50151,72:POKE56295,9
628 X=15:Y=2:CR=0
629 P3%(0)=1:P3%(1)=0:P3%(2)=0:P3%(3)=0
630 P1%(0)=15:P1%(1)=17:P1%(2)=9:P1%(3)=9:P2%(0)=0:P2%(1)=0:P2%(2)=7:P2%(3)=23
631 FORI=0TO3:PH%(I)=P1%(I):PM%(I)=P2%(I):PX%(I)=0:PW%(I)=0:PR%(I)=0:NEXT
632 PW%(0)=1:PR%(0)=2
633 GOSUB133
634 AA$="INTRO":GOSUB688
635 GOTO59
636 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
637 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
638 PRINT"{$c6}{$c7:38}{up:3}";
639 POKE50151,72:POKE56295,9
640 AA$="ENDE":GOSUB688
641 GOTO59
642 PRINT"{brown}{home}{$c1}{$c2:38}{$c3}";
643 PRINT"{$c4}                                      {$c5}";
644 PRINT"{$c6}{$c7:38}{$c8}"
645 RETURN
646 PRINTDD$;"{brown}{up}{$c1}{$c2:7}{white}HP{brown}{$c2}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
647 FORI=0TO3:PRINT"{$c4}          {$c5}{$c4}            {$c5}{$c4}{right:12}{$c5}";:NEXT
648 PRINT"{$c6}{$c7:10}{$c8}{$c6}{$c7:12}{$c8}{$c6}{$c7:12}";
649 POKE50151,72:POKE56295,9
650 RETURN
651 PRINTDD$;"{brown}{up}{$c1}{$c2:24}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
652 FORI=0TO3:PRINT"{$c4}                        {$c5}{$c4}{right:12}{$c5}";:NEXT
653 PRINT"{$c6}{$c7:24}{$c8}{$c6}{$c7:12}";
654 POKE50151,72:POKE56295,9
655 RETURN
656 PRINT"{brown}{home}{down:3}{$c1}{$c2:38}{$c3}";
657 FORI=0TO13
658 PRINT"{$c4}                                      {$c5}";:NEXT
659 PRINT"{$c6}{$c7:38}{$c8}";:RETURN
660 POKE56322,224
661 IFAA$="TXT.WELCOME.LENA"THENOPEN1,8,4,"TXT.WELCOME.LENA,S,R"
662 IFAA$="TXT.WELCOME.DOLM"THENOPEN1,8,4,"TXT.WELCOME.DOLM,S,R"
663 IFAA$="TXT.WELCOME.MIRA"THENOPEN1,8,4,"TXT.WELCOME.MIRA,S,R"
664 IFAA$=""THENRETURN
665 I=0:SB$(0)="":SB$(1)="":SB$(2)=""
666 INPUT#1,SB$(I)
667 IFST=0THENI=I+1:GOTO666
668 CLOSE1
669 POKE56322,225
670 PRINT"{home}{right}{down:20}"+"                        "
671 PRINT"{home}{right}{down:21}"+"                        "
672 PRINT"{home}{right}{down:22}"+"                        "
673 PRINT"{home}{right}{down:23}"+"                        "
674 PRINT"{home}{right}{down:20}{white}"+SB$(0)
675 PRINT"{home}{right}{down:21}{white}"+SB$(1)
676 PRINT"{home}{right}{down:22}{white}"+SB$(2)
677 PRINT"{home}{right}{down:23}{white}"+SB$(3)
678 GOSUB732
679 PRINT"{home}{right}{down:20}"+"                        "
680 PRINT"{home}{right}{down:21}"+"                        "
681 PRINT"{home}{right}{down:22}"+"                        "
682 PRINT"{home}{right}{down:23}"+"                        "
683 RETURN
684 VA=1
685 GOSUB516
686 PRINT"{home}{down}{white}{right}";VA$
687 RETURN
688 POKE1020,Q7%
689 POKE56322,224
690 IFAA$="INTRO"THENOPEN1,8,4,"TXT.INTRO,S,R"
691 IFAA$="ENDE"THENOPEN1,8,4,"TXT.ENDE,S,R"
692 IFAA$=""THENRETURN
693 GET#1,B$
694 IFB$="$"THENPRINT"{home}{down:2}{right:2}{white}";:GOTO693
695 IFB$="%"THENPRINT"{right:2}{white}";:GOTO693
696 IFB$="#"THENCLOSE1:POKE56322,255:GOSUB732:RETURN
697 PRINTB$;
698 IFB$<>" "THENPOKE162,0:WAIT162,4
699 GOTO693
700 T$=STR$(A)
701 D$=RIGHT$("000"+RIGHT$(T$,LEN(T$)-1),3):RETURN
702 A$=""
703 J=PEEK(56320)
704 IFJ=127THEN707
705 IFJ=107ORJ=103ORJ=110ORJ=109THEN707
706 GOTO703
707 J=PEEK(56320)
708 IFJ=127ANDA$=CHR$(13)THENRETURN
709 IFJ=107THENA$="FL":GOSUB737:RETURN
710 IFJ=103THENA$="FR":GOSUB737:RETURN
711 IFJ=110THENA$="FU":GOSUB737:RETURN
712 IFJ=109THENA$="FD":GOSUB737:RETURN
713 IFJ=123THENA$="A":GOSUB737:RETURN
714 IFJ=119THENA$="D":GOSUB737:RETURN
715 IFJ=126THENA$="W":GOSUB737:RETURN
716 IFJ=125THENA$="S":GOSUB737:RETURN
717 IFJ=111THENA$=CHR$(13)
718 GOTO707
719 A$=""
720 J=PEEK(56320)
721 IFJ<>111ANDJ<>107ANDJ<>103ANDJ<>110ANDJ<>109THEN723
722 GOTO720
723 J=PEEK(56320)
724 IFJ=127ANDA$=CHR$(13)ANDVA=1THENVA=0:GOSUB516:A$="":RETURN
725 IFJ=127ANDA$=CHR$(13)THENRETURN
726 IFJ=123THENA$="A":GOSUB738:RETURN
727 IFJ=119THENA$="D":GOSUB738:RETURN
728 IFJ=126THENA$="W":GOSUB738:RETURN
729 IFJ=125THENA$="S":GOSUB738:RETURN
730 IFJ=111THENA$=CHR$(13)
731 GOTO723
732 A$=""
733 J=PEEK(56320)
734 IFJ=127ANDA$=CHR$(13)THENRETURN
735 IFJ=111THENA$=CHR$(13)
736 GOTO733
737 POKE162,0:WAIT162,16:RETURN
738 POKE162,0:WAIT162,8:RETURN
739 POKE162,0:WAIT162,32:RETURN
740 POKE162,0:WAIT162,2:RETURN
741 POKE162,0:WAIT162,32
742 POKE162,0:WAIT162,32
743 POKE162,0:WAIT162,32
744 POKE162,0:WAIT162,32
745 RETURN
746 POKE162,0:WAIT162,32
747 RETURN
748 POKE162,0:WAIT162,4
749 RETURN
750 DATA"IMP",5,0,8,4,1,0,3
751 DATA"SNAKE",8,0,12,6,1,0,5
752 DATA"VIPER",12,0,18,6,1,0,9
753 DATA"NACHO",30,0,22,15,5,0,50
754 DATA"GOLEM",20,0,4,20,8,0,17
755 DATA"MOSS",30,0,7,26,9,0,21
756 DATA"BONES",50,0,20,18,8,0,15
757 DATA"WISP",25,50,15,0,8,0,17
758 DATA"TROLL",100,0,9,40,13,0,100
759 DATA"SPARK",30,50,22,0,8,0,17
760 DATA"BURNER",80,90,30,25,13,0,42
761 DATA"TBERRY",120,10,60,60,20,0,150
762 DATA"DRACUL",250,60,50,70,25,0,500
763 DATA"GLADOS",500,70,60,80,30,0,1000
764 DATA"KRON",15 ,0  ,10 ,4 ,3 ,0 ,0
765 DATA"LENA",17 ,0  ,10 ,3 ,3 ,0 ,0
766 DATA"DOLM",9  ,7  ,8  ,1 ,1 ,0 ,0
767 DATA"MIRA",9  ,23 ,7  ,1 ,1 ,1 ,2
768 DATA"{gray1}{rvrs off}{$c9}{rvrs off}{gray1}{$ca}{down}{left:2}{rvrs off}{gray1}{$ca}{rvrs off}{gray1}{$c9}{rvrs off}"
769 DATA"{gray1}{rvrs off}{$cb}{rvrs off}{gray1}{$cc}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1}{$ca}{rvrs off}"
770 DATA"{gray1}{rvrs off}{$c9}{rvrs off}{gray1}{$ca}{down}{left:2}{rvrs off}{gray1}{$ca}{rvrs off}{gray1}{$c9}{rvrs off}"
771 DATA"{gray1}{rvrs off}{$cd}{rvrs off}{gray1}{$ce}{down}{left:2}{rvrs off}{gray1}{$cf}{rvrs off}{gray1}{$d0}{rvrs off}"
772 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
773 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
774 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
775 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
776 DATA"{orange}{rvrs off}{$d1}{rvrs off}{orange}{$d1}{down}{left:2}{rvrs off}{orange}{$d1}{rvrs off}{orange}{$d1}{rvrs off}"
777 DATA"{brown}{rvrs off}{$d2}{rvrs off}{brown}{$d3}{down}{left:2}{rvrs off}{brown}{$d4}{rvrs off}{brown}{$d5}{rvrs off}"
778 DATA"{lt. red}{rvrs off}{$d6}{rvrs off}{lt. red}{$d7}{down}{left:2}{rvrs off}{lt. red}{$d8}{rvrs off}{lt. red}{$d9}{rvrs off}"
779 DATA"{gray1}{rvrs off}{$da}{rvrs off}{gray1}{$db}{down}{left:2}{rvrs off}{gray1}{$dc}{rvrs off}{gray1}{$dd}{rvrs off}"
780 DATA"{gray1}{rvrs off}{$a2}{rvrs off}{gray1}{$a3}{down}{left:2}{rvrs off}{gray1}{$a4}{rvrs off}{gray1}{$a5}{rvrs off}"
781 DATA"{brown}{rvrs off}{$a1}{rvrs off}{brown}{$a1}{down}{left:2}{rvrs off}{brown}{$a1}{rvrs off}{brown}{$a1}{rvrs off}"
782 DATA"{gray1}{rvrs off}{$de}{rvrs off}{gray1}{$df}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1}{$c0}{rvrs off}"
783 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
784 DATA"{lt. red}{rvrs off}{$a6}{rvrs off}{lt. red}{$a7}{down}{left:2}{rvrs off}{lt. red}{cmdr pound}{rvrs off}{lt. red}{shft pound}{rvrs off}"
785 DATA"{lt. red}{rvrs off}{$aa}{rvrs off}{lt. red}{$aa}{down}{left:2}{rvrs off}{lt. red}{$aa}{rvrs off}{lt. red}{$aa}{rvrs off}"
786 DATA"{brown}{rvrs off}{$ab}{rvrs off}{brown}{$ab}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
787 DATA"{brown}{rvrs off}{$ab}{rvrs off}{brown}{$ad}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
788 DATA"{brown}{rvrs off}{$ad}{rvrs off}{brown}{$ab}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
789 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
790 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
791 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
792 DATA"{lt. green}{rvrs off}{$ae}{rvrs off}{lt. green}{$d1}{down}{left:2}{rvrs off}{lt. green}{$d1}{rvrs off}{lt. green}{$af}{rvrs off}"
793 DATA"{lt. green}{rvrs off}{$b0}{rvrs off}{lt. green}{$b1}{down}{left:2}{rvrs off}{lt. green}{$b2}{rvrs off}{lt. green}{$b3}{rvrs off}"
794 DATA"{lt. green}{rvrs off}{$b4}{rvrs off}{lt. green}{$b5}{down}{left:2}{rvrs off}{lt. green}{$b4}{rvrs off}{lt. green}{$b5}{rvrs off}"
795 DATA"{lt. green}{rvrs off}{$b6}{rvrs off}{lt. green}{$b7}{down}{left:2}{rvrs off}{lt. green}{$b8}{rvrs off}{lt. green}{$b9}{rvrs off}"
796 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
797 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
798 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
799 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
800 DATA"{brown}{rvrs off}{$ae}{rvrs off}{brown}{$d1}{down}{left:2}{rvrs off}{brown}{$d1}{rvrs off}{brown}{$af}{rvrs off}"
801 DATA"{brown}{rvrs off}{$b0}{rvrs off}{brown}{$b1}{down}{left:2}{rvrs off}{brown}{$b2}{rvrs off}{brown}{$b3}{rvrs off}"
802 DATA"{brown}{rvrs off}{$b4}{rvrs off}{brown}{$b5}{down}{left:2}{rvrs off}{brown}{$b4}{rvrs off}{brown}{$b5}{rvrs off}"
803 DATA"{brown}{rvrs off}{$b6}{rvrs off}{brown}{$b7}{down}{left:2}{rvrs off}{brown}{$b8}{rvrs off}{brown}{$b9}{rvrs off}"
804 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
805 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
806 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
807 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
808 DATA"{gray1}{rvrs off}{$d1}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$d1}{rvrs off}{gray1}{$d1}{rvrs off}"
809 DATA"{gray1}{rvrs off}{$ba}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$ba}{rvrs off}{gray1}{$d1}{rvrs off}"
810 DATA"{gray1}{rvrs off}{$ba}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$bb}{rvrs off}{gray1}{$d1}{rvrs off}"
811 DATA"{gray1}{rvrs off}{$bc}{rvrs off}{gray1}{$bd}{down}{left:2}{rvrs off}{gray1}{$be}{rvrs off}{gray1}{$be}{rvrs off}"
812 DATA"{gray1}{rvrs off}{$bf}{rvrs off}{gray1}{$bf}{down}{left:2}{rvrs on}{gray1}@{rvrs on}{gray1}@{rvrs off}"
813 DATA"{gray2}{rvrs on}A{rvrs on}{gray2}B{down}{left:2}{rvrs on}{gray2}C{rvrs on}{gray2}D{rvrs off}"
814 DATA"{gray1}{rvrs on}E{rvrs on}{gray1}F{down}{left:2}{rvrs on}{gray1}G{rvrs on}{gray1}H{rvrs off}"
815 DATA"{brown}{rvrs on}I{rvrs on}{brown}I{down}{left:2}{rvrs on}{brown}I{rvrs on}{brown}I{rvrs off}"
816 DATA"{lt. red}{rvrs on}J{rvrs on}{lt. red}K{down}{left:2}{rvrs on}{lt. red}L{rvrs on}{lt. red}M{rvrs off}"
817 DATA"{gray1}{rvrs on}N{rvrs on}{gray1}K{down}{left:2}{rvrs on}{gray1}L{rvrs on}{gray1}M{rvrs off}"
818 DATA"{lt. green}{rvrs on}O{rvrs on}{lt. green}P{down}{left:2}{rvrs on}{lt. green}Q{rvrs on}{lt. green}R{rvrs off}"
819 DATA"{gray2}{rvrs on}S{rvrs on}{gray2}T{down}{left:2}{rvrs on}{gray2}U{rvrs on}{gray2}V{rvrs off}"
820 DATA"{gray1}{rvrs on}W{rvrs on}{gray1}X{down}{left:2}{rvrs on}{gray1}Y{rvrs on}{gray1}Z{rvrs off}"
821 DATA"{gray1}{rvrs on}[{rvrs on}{gray1}{pound}{down}{left:2}{rvrs on}{gray1}]{rvrs on}{gray1}{^}{rvrs off}"
822 DATA"{lt. red}{rvrs off} {rvrs off}{lt. red} {down}{left:2}{rvrs off}{lt. red} {rvrs off}{lt. red} {rvrs off}"
823 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
824 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
825 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
826 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
827 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
828 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
829 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
830 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
831 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
832 DATA"{lt. green}{rvrs on}{back arrow}{rvrs on}{lt. green}!{down}{left:2}{rvrs on}{lt. green}#{rvrs on}{lt. green}${rvrs off}"
833 DATA"{gray3}{rvrs on}%{rvrs on}{gray3}&{down}{left:2}{rvrs on}{gray3}'{rvrs on}{gray3}({rvrs off}"
834 DATA"{gray1}{rvrs on}%{rvrs on}{gray1}&{down}{left:2}{rvrs on}{gray1}'{rvrs on}{gray1}({rvrs off}"
835 DATA"{orange}{rvrs on}{back arrow}{rvrs on}{orange}!{down}{left:2}{rvrs on}{orange}#{rvrs on}{orange}${rvrs off}"
836 DATA"{gray1}{rvrs on}-{rvrs on}{gray1}.{down}{left:2}{rvrs on}{gray1}/{rvrs on}{gray1}0{rvrs off}"
837 DATA"{gray1}{rvrs on}1{rvrs on}{gray1}2{down}{left:2}{rvrs on}{gray1}3{rvrs on}{gray1}4{rvrs off}"
838 DATA"{gray1}{rvrs on}5{rvrs on}{gray1}6{down}{left:2}{rvrs on}{gray1}7{rvrs on}{gray1}8{rvrs off}"
839 DATA"{gray1}{rvrs on}9{rvrs on}{gray1}:{down}{left:2}{rvrs on}{gray1};{rvrs on}{gray1}<{rvrs off}"
840 DATA"{gray1}{rvrs on}={rvrs on}{gray1}>{down}{left:2}{rvrs on}{gray1}?{rvrs on}{gray1}{$c0}{rvrs off}"
841 DATA"{lt. green}{rvrs on}9{rvrs on}{lt. green}:{down}{left:2}{rvrs on}{lt. green};{rvrs on}{lt. green}<{rvrs off}"
842 DATA"{lt. red}{rvrs on}5{rvrs on}{lt. red}6{down}{left:2}{rvrs on}{lt. red}7{rvrs on}{lt. red}8{rvrs off}"
843 DATA"{lt. green}{rvrs on}5{rvrs on}{lt. green}6{down}{left:2}{rvrs on}{lt. green}7{rvrs on}{lt. green}8{rvrs off}"
844 DATA"{gray1}{rvrs on}{$c1}{rvrs on}{gray1}{$c2}{down}{left:2}{rvrs on}{gray1}{$c3}{rvrs on}{gray1}{$c4}{rvrs off}"
845 DATA"{gray1}{rvrs on}{$c5}{rvrs on}{gray1}{$c6}{down}{left:2}{rvrs on}{gray1}{$c7}{rvrs on}{gray1}{$c8}{rvrs off}"
846 DATA"{lt. green}{rvrs on}){rvrs on}{lt. green}*{down}{left:2}{rvrs on}{lt. green}+{rvrs on}{lt. green},{rvrs off}"
847 DATA"{gray1}{rvrs off}{$c0}{rvrs off}{gray1}{$c0}{down}{left:2}{rvrs off}{gray1}{$c0}{rvrs off}{gray1}{$c0}{rvrs off}"
848 DATA"        ",00,00,00,00,-1 
849 DATA"DOLCH   ",03,00,00,00,00  
850 DATA"HELM    ",00,02,-1,00,01  
851 DATA"KRAEUTER",00,00,-1,10,02  
852 DATA"FEUER   ",00,00,00,05,03  
853 DATA"EIS     ",00,00,00,08,03  
854 DATA"MUT     ",00,00,00,23,03  
855 DATA"BOMBE   ",00,00,00,50,03  
856 DATA"KIRSCHE ",00,00,-1,15,02  
857 DATA"HEILEN  ",00,00,00,04,03  
858 DATA"SKULL   ",00,00,00,00,04  
859 DATA"FLEGEL  ",07,00,-1,00,00  
860 DATA"LAHM    ",00,00,00,04,03  
861 DATA"STAB    ",12,00,00,00,00  
862 DATA"FEDER   ",00,10,00,00,01  
863 DATA"STOCK   ",23,00,00,00,00  
864 DATA"WESTE   ",00,14,00,00,01  
865 DATA"SCHILD  ",00,24,00,00,01  
866 DATA"ZURUECK ",00,00,00,0 ,09  
867 END
