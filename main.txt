0 POKE55,240:POKE56,120:CLR
1 QO=53282
2 QP=53283
3 QQ=53281
4 QR=53280
5 QS=49152
6 V=53248
7 Q1%=0
8 Q7%=6
9 QA%=9
10 RS=165
11 DD$="{home}{down:20}{white}"
12 CD$="{down:25}"
13 CT$="{home}{down}{white}{right}                                      "
14 DIMM$(13),MD$(13),MH%(13),MM%(13),MA%(13),MD%(13),MS%(13),MB%(13),MX%(13)
15 DIMFH%(11),FT%(11),FC%(11),FM%(11),FS%(11),FX%(11),FY%(11),FA%(11),FD(11),F1%(11),F2%(11)
16 FORI=0TO3:FX%(I)=37:FY%(I)=5+3*I:NEXT
17 DIMI$(18),IA%(18),ID%(18),IS%(18),IE%(18),IT%(18)
18 DIMER%(12),EX%(12),EY%(12),ET%(12)
19 DIMIN%(99),TT%(99)
20 DIMT$(79)
21 DIMS0%(10),S1%(10),S2%(10),S3%(10),A0%(10),A1%(10),A2%(10)
22 DIMN0%(10),N1%(10),N2%(10),N3%(8),N4%(10)
23 FORI=0TO13
24 READM$(I),MH%(I),MM%(I),MS%(I)
25 READMA%(I),MD%(I),MB%(I),MX%(I)
26 NEXT
27 FORI=0TO3
28 READP$(I),PH%(I),PM%(I),PS%(I)
29 READPA%(I),PD%(I),PW%(I),PR%(I)
30 NEXT
31 FORI=0TO79
32 READT$(I)
33 NEXT
34 PD$(0)=T$(48)
35 PD$(1)=T$(50)
36 PD$(2)=T$(52)
37 PD$(3)=T$(51)
38 ZZ=64
39 FORI=0TO13
40 MD$(I)=T$(ZZ)
41 ZZ=ZZ+1
42 NEXTI
43 FORI=0TO18
44 READI$(I),IA%(I),ID%(I),IS%(I),IE%(I),IT%(I)
45 NEXT
46 POKEV+21,0
47 POKE648,192
48 POKE56576,0
49 POKE53272,8
50 POKE53270,PEEK(53270)OR16
51 POKEQO,QA%
52 POKEQP,Q1%
53 POKEQR,Q1%
54 POKEQQ,Q7%
55 GOSUB146
56 GOTO606
57 POKE1020,Q7%
58 POKE1021,Q7%
59 SYS820
60 GOSUB422
61 GOSUB429
62 PRINT"{home}{down}{right}{white}                                      ";
63 POKE1020,Q7%
64 GOSUB527
65 IFPEEK(2)=1THENCP=30960+RS*(CR-0)
66 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)PD$(0);
67 OX=ZX:OY=ZY
68 ZX=X:ZY=Y
69 GOSUB671
70 IFA$=CHR$(145)ORA$="W"THENPRINTCT$;:ZY=Y-1:GOTO76
71 IFA$=CHR$(157)ORA$="A"THENPRINTCT$;:ZX=X-1:GOTO76
72 IFA$=CHR$(29)ORA$="D"THENPRINTCT$;:ZX=X+1:GOTO76
73 IFA$=CHR$(17)ORA$="S"THENPRINTCT$;:ZY=Y+1:GOTO76
74 IFA$=CHR$(13)THEN161
75 GOTO69
76 AA=0
77 FORI=0TO12
78 IFER%(I)=CRANDEX%(I)=ZXANDEY%(I)=ZYTHENAA=1
79 NEXTI
80 IFZX=-1ORZX=20ORZY=-1ORZY=8THEN92
81 C=PEEK(CP+ZX+ZY*20)
82 IFC=08THEN106
83 IFC=09THEN106
84 IFC=10THEN106
85 IFC=11THEN106
86 IFC=12THEN113
87 IFC=14THEN108
88 IFC=25THEN113
89 IFC=47THEN126
90 IFC>=48THEN117
91 IFC>7THEN67
92 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)T$(PEEK(CP+X+Y*20))
93 IFRND(1)*100>97ANDPEEK(CP+164)>0THENFF=0:GOTO237
94 X=ZX:Y=ZY
95 IFX=-1ANDPEEK(CP+160)=255THENX=0:GOTO66
96 IFX=-1THENCR=PEEK(CP+160):X=19:GOTO63
97 IFX=20ANDPEEK(CP+161)=255THENX=19:GOTO66
98 IFX=20THENCR=PEEK(CP+161):X=0:GOTO63
99 IFY=-1ANDPEEK(CP+162)=255THENY=0:GOTO66
100 IFY=-1THENCR=PEEK(CP+162):Y=7:GOTO63
101 IFY=8ANDPEEK(CP+163)=255THENY=7:GOTO66
102 IFY=8THENCR=PEEK(CP+163):Y=0:GOTO63
103 IFC=2THEN108
104 IFC=3THEN108
105 GOTO66
106 IFCR=16THENCR=16:X=0:Y=0:GOTO62
107 IFCR=16THEN635
108 IFC=14ANDCR=S0%(0)ANDZX=S1%(0)THENS3%(0)=1-S3%(0):GOSUB127:TX=A1%(0):TY=A2%(0):GOSUB143:GOSUB140:GOTO67
109 IFC=2ANDCR=S0%(1)ANDX=S1%(1)THENS3%(1)=1:GOSUB127:TX=A1%(1):TY=A2%(1):GOSUB143:GOTO66
110 IFC=2ANDCR=S0%(2)ANDX=S1%(2)THENS3%(2)=1:GOSUB127:TX=A1%(2):TY=A2%(2):GOSUB143:GOTO66
111 IFC=3ANDCR=S0%(3)ANDX=S1%(3)THENS3%(3)=1:GOSUB127:TX=A1%(3):TY=A2%(3):GOSUB143:GOTO66
112 IFC=3ANDCR=S0%(4)ANDX=S1%(4)THENS3%(4)=1:GOSUB127:TX=A1%(4):TY=A2%(4):GOSUB143:GOTO66
113 IFC=12ANDAA=0THENGOTO67
114 IFC=12ANDAA=1THENPRINT"{home}{white}{down}{right}IN DER TRUHE IST WAS!":GOSUB692:GOSUB502:GOTO67
115 IFC=25ANDAA=0THENGOTO67
116 IFC=25ANDAA=1THENPRINT"{home}{white}{down}{right}DER BAUM SIEHT INTRESSANT AUS!":GOSUB692:GOSUB502:GOTO67
117 IFC=N0%(7)THENPRINT"{white}{home}{down}{right:2}EIN HEILZAUBER IST UNTER DEM BAUM":GOSUB692:GOSUB502:GOTO67
118 IFC=N0%(8)THENPRINT"{white}{home}{down}{right:2}EIN FEUERZAUBER IST IN DEN KATAKOMBEN":GOSUB692:GOSUB502:GOTO67
119 IFC=N0%(0)THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB692:GOSUB502:N4%(0)=1:GOSUB127:P3%(1)=1:TX=12:TY=4:GOSUB143:GOSUB429:GOTO67
120 IFC=N0%(1)THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB692:GOSUB502:N4%(1)=1:GOSUB127:P3%(2)=1:TX=7:TY=2:GOSUB143:GOSUB429:GOTO67
121 IFC=N0%(2)THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB692:GOSUB502:N4%(2)=1:GOSUB127:P3%(3)=1:TX=3:TY=2:GOSUB143:GOSUB429:GOTO67
122 IFC=N0%(3)THENPRINT"{white}{home}{down}{right:2}DU KANNST HIER NICHT DURCH!":GOSUB692:FF=4:N4%(3)=1:GOSUB127:GOTO237
123 IFC=N0%(4)THENPRINT"{white}{home}{down}{right:2}ACH WIE SUESS!":GOSUB692:FF=9:N4%(4)=1:GOSUB127:GOTO237
124 IFC=N0%(5)THENPRINT"{white}{home}{down}{right:2}DU WILLST MICH BESIEGEN!":GOSUB692:FF=13:N4%(5)=1:GOSUB127:GOTO237
125 IFC=N0%(6)THENPRINT"{white}{home}{down}{right:2}DU HAST MEINE ERWARTUNG UEBERTROFFEN!":GOSUB692:FF=14:N4%(6)=1:GOSUB127:GOTO237
126 IFC=47THENPRINT"{home}{white}{down}{right}DU BIST GEHEILT!":GOSUB692:GOSUB417:GOSUB429:GOSUB502:GOTO67
127 POKE30960+A0%(0)*RS+A1%(0)+A2%(0)*20,15-S3%(0)*15+1
128 POKE30960+N1%(0)*RS+N2%(0)+N3%(0)*20,N0%(0)-N4%(0)*N0%(0)
129 POKE30960+N1%(1)*RS+N2%(1)+N3%(1)*20,N0%(1)-N4%(1)*N0%(1)
130 POKE30960+N1%(2)*RS+N2%(2)+N3%(2)*20,N0%(2)-N4%(2)*N0%(2)
131 POKE30960+N1%(3)*RS+N2%(3)+N3%(3)*20,N0%(3)-N4%(3)*N0%(3)
132 POKE30960+N1%(4)*RS+N2%(4)+N3%(4)*20,N0%(4)-N4%(4)*N0%(4)
133 POKE30960+N1%(5)*RS+N2%(5)+N3%(5)*20,N0%(5)-N4%(5)*N0%(5)
134 POKE30960+N1%(6)*RS+N2%(6)+N3%(6)*20,N0%(6)-N4%(6)*N0%(6)
135 POKE30960+A0%(1)*RS+A1%(1)+A2%(1)*20,13-S3%(1)*13
136 POKE30960+A0%(2)*RS+A1%(2)+A2%(2)*20,13-S3%(2)*12
137 T=13:IFS3%(3)+S3%(4)=2THENT=0
138 POKE30960+A0%(3)*RS+A1%(3)+A2%(3)*20,T
139 RETURN
140 IFCR=S0%(0)ANDZX=S1%(0)ANDS3%(0)=0THENPRINT"{home}{white}{down}{right}DER SCHALTER IST AUS!";:GOSUB692:GOSUB502
141 IFCR=S0%(0)ANDZX=S1%(0)ANDS3%(0)=1THENPRINT"{home}{white}{down}{right}DER SCHALTER IST EIN!";:GOSUB692:GOSUB502
142 RETURN
143 SP=30960+(CR*RS)+(TX+TY*20)
144 PRINT"{home}{down:3}"LEFT$(CD$,TY*2)SPC(TX*2)T$(PEEK(SP));
145 RETURN
146 S0%(0)=0:S1%(0)=11:S2%(0)=1:S3%(0)=0:A0%(0)=0:A1%(0)=14:A2%(0)=6
147 S0%(1)=0:S1%(1)=16:S2%(1)=6:S3%(1)=0:A0%(1)=0:A1%(1)=7:A2%(1)=4
148 S0%(2)=0:S1%(2)=18:S2%(2)=6:S3%(2)=0:A0%(2)=0:A1%(2)=9:A2%(2)=5
149 S0%(3)=0:S1%(3)=1:S2%(3)=6:S3%(3)=0:A0%(3)=0:A1%(3)=3:A2%(3)=4
150 S0%(4)=0:S1%(4)=5:S2%(4)=6:S3%(4)=0:A0%(4)=0:A1%(4)=3:A2%(4)=4
151 N0%(0)=50:N1%(0)=0:N2%(0)=12:N3%(0)=4:N4%(0)=0
152 N0%(1)=52:N1%(1)=0:N2%(1)=7:N3%(1)=2:N4%(1)=0
153 N0%(2)=51:N1%(2)=0:N2%(2)=3:N3%(2)=2:N4%(2)=0
154 N0%(3)=67:N1%(3)=16:N2%(3)=0:N3%(3)=0:N4%(3)=0
155 N0%(4)=72:N1%(4)=16:N2%(4)=0:N3%(4)=0:N4%(4)=0
156 N0%(5)=76:N1%(5)=16:N2%(5)=0:N3%(5)=0:N4%(5)=0
157 N0%(6)=77:N1%(6)=16:N2%(6)=0:N3%(6)=0:N4%(6)=0
158 N0%(7)=49:N1%(7)=16:N2%(7)=0:N3%(7)=0:N4%(7)=0
159 N0%(8)=53:N1%(8)=16:N2%(8)=0:N3%(8)=0:N4%(8)=0
160 RETURN
161 PRINT"{home}{down}{white}{right}  UNTERSUCHE  EQUIPMENTS  INVENTAR"
162 M=0
163 POKEQS+42+M*12,35
164 GOSUB655
165 POKEQS+42+M*12,32
166 IFA$="A"THENM=M-1-3*(M=0):GOTO163
167 IFA$="D"THENM=M+1+3*(M=2):GOTO163
168 IFA$=CHR$(13)THENPOKEQS+42+M*7,32:GOTO170
169 GOTO163
170 PRINT"{home}{down}{white}{right}                                      ";
171 ONM+1GOTO225,185,172
172 POKE1020,Q1%
173 GOSUB498
174 PRINT"{home}{down}{right}{white}INVENTAR: WAEHLE DEIN ITEM           ";
175 MT=2:MX=2:MY=4:MC=14:GOSUB440
176 IFIN%(M)=9THEN62
177 IFIN%(M)=0ORIT%(IN%(M))<>MTTHEN62
178 IFIN%(M)=3THENGOSUB181:IN%(M)=0:GOSUB429:GOTO62
179 IFIN%(M)=8THENGOSUB183:IN%(M)=0:GOSUB429:GOTO62
180 GOTO62
181 FORI=0TO3:PH%(I)=PH%(I)+IE%(IN%(M)):IFPH%(I)>P1%(I)THENPH%(I)=P1%(I)
182 FH%(I)=PH%(I):NEXT:RETURN
183 FORI=0TO3:PM%(I)=PM%(I)+IE%(IN%(M)):IFPM%(I)>P2%(I)THENPM%(I)=P2%(I)
184 FM%(I)=PM%(I):NEXT:RETURN
185 POKE1020,Q1%
186 GOSUB504
187 PRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINEN SPIELER     ";
188 P=0
189 CP=QS+2+(4-8*(P>1))*40:IFP=1ORP=3THENCP=CP+20
190 POKECP,35
191 GOSUB655
192 POKECP,32
193 IFP=0ANDA$="D"ANDP3%(1)=1THENP=1:GOTO189
194 IFP=0ANDA$="S"ANDP3%(2)=1THENP=2:GOTO189
195 IFP=0ANDA$="D"ANDP3%(1)=OANDP3%(2)=0ANDP3%(3)=1THENP=3:GOTO189
196 IFP=0ANDA$="S"ANDP3%(1)=OANDP3%(2)=0ANDP3%(3)=1THENP=3:GOTO189
197 IFP=1ANDA$="A"ANDP3%(0)=1THENP=0:GOTO189
198 IFP=1ANDA$="S"ANDP3%(3)=1THENP=3:GOTO189
199 IFP=2ANDA$="D"ANDP3%(3)=1THENP=3:GOTO189
200 IFP=2ANDA$="W"ANDP3%(0)=1THENP=0:GOTO189
201 IFP=3ANDA$="A"ANDP3%(2)=1THENP=2:GOTO189
202 IFP=3ANDA$="W"ANDP3%(1)=1THENP=1:GOTO189
203 IFP=3ANDA$="A"ANDP3%(2)=OANDP3%(1)=0ANDP3%(0)=1THENP=0:GOTO189
204 IFP=3ANDA$="W"ANDP3%(2)=OANDP3%(1)=0ANDP3%(0)=1THENP=0:GOTO189
205 IFA$=CHR$(13)THEN207
206 GOTO190
207 SL=0:CP=CP+81
208 PRINT"{home}{down}{right}{white}EQUIPMENT: WAFFE ODER RUESTUNG       ";
209 POKECP+SL*40,35
210 GOSUB655
211 POKECP+SL*40,32:IFA$="W"ORA$="S"THENSL=1-SL:GOTO209
212 IFA$=CHR$(13)THEN214
213 GOTO209
214 MY=4:MX=23:MC=14:IFP=1ORP=3THENMX=3
215 IFSL=0THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE WAFFE        ";
216 IFSL=1THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE RUESTUNG     ";
217 PRINT"{home}"LEFT$(CD$,MY-1)SPC(MX-3);"{brown}{$c1}{$c2:18}{$c3}";
218 FORI=0TO13:PRINT"{down}{left:20}{$c4}                  {$c5}";:NEXT
219 PRINT"{down}{left:20}{$c6}{$c7:18}{$c8}";
220 MT=SL:GOSUB440
221 IFIT%(IN%(M))=9THEN62
222 IFIN%(M)<>0ANDIT%(IN%(M))<>SLTHEN62
223 IFSL=0THENQ=PW%(P):PW%(P)=IN%(M):IN%(M)=Q:GOTO62
224 IFSL=1THENQ=PR%(P):PR%(P)=IN%(M):IN%(M)=Q:GOTO62
225 GOSUB502
226 GOSUB697
227 EI=0
228 IFER%(EI)<>CROREX%(EI)<>OXOREY%(EI)<>OYTHEN233
229 NI%=ET%(EI):GOSUB535
230 IFIS=99THENPRINT"{home}{white}{down}{right}DU KANNST NICHT MEHR TRAGEN!":GOSUB692:GOSUB502:GOTO67
231 ER%(EI)=-1
232 PRINT"{home}{white}{down}{right}GEFUNDEN: ";I$(IN%(IS));:GOSUB692:GOSUB502:GOTO67
233 EI=EI+1
234 IFEI=12THEN236
235 GOTO228
236 PRINT"{home}{down}{right}{white}NICHTS BESONDERES!":GOSUB692:GOSUB502:GOTO67
237 FORI=4TO11:FT%(I)=-1:NEXT
238 PP=4:CC=4:WX=0
239 FORI=0TO3:FT%(I)=-2:FH%(I)=PH%(I):FM%(I)=PM%(I):FS%(I)=PS%(I)
240 IFP3%(I)=0THENFH%(I)=-1
241 IFFH%(I)<=0THENFT%(I)=-1:PP=PP-1
242 FA%(I)=PA%(I)+IA%(PW%(I)):FD(I)=PD%(I)+ID%(PR%(I)):F1%(I)=P1%(I):F2%(I)=P2%(I)
243 NEXT
244 PRINT"{brown}{home}{$c1}{$c2:38}{$c3}";
245 PRINT"{$c4}                                      {$c5}";
246 PRINT"{$c6}{$c7:38}{$c8}";
247 PRINT"{home}{down:3}{white}";:FORI=0TO15:PRINT"                                        ";:NEXT
248 PRINTDD$;"{brown}{up}{$c1}{$c2:7}{white}HP{brown}{$c2}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
249 GOSUB398
250 GOSUB429
251 GOSUB437
252 FORI=0TO11
253 IFFT%(I)<>-1THENFC%(I)=INT(RND(0)*FD%(I))
254 NEXT
255 CC=CC-4
256 PRINT"{home}{down}{right}                       ";:GOSUB546
257 FORI=4TO11:IFFT%(I)=-1THEN259
258 PRINT"{home}"LEFT$(CD$,FY%(I));SPC(FX%(I));MD$(FT%(I))
259 NEXT
260 C=0
261 IFFT%(C)<>-1ANDFC%(C)>=100THENFC%(C)=FC%(C)-100:GOTO264
262 C=C+1:IFC<12THEN261
263 FORI=0TO11:FC%(I)=FC%(I)+FS%(I):NEXT:GOTO260
264 IFFT%(C)=-2THEN266
265 GOTO347
266 PRINT"{home}{white}{down}{right}"P$(C)": IST AM ZUG                     "
267 PRINTDD$;SPC(16)"{white} ANGRIFF {down}{left:9} INVENTAR";:M=2
268 PRINTDD$;SPC(16)"{down:2}         {down}{left:9}         ";
269 IFPM%(C)>0THENPRINTDD$;SPC(16)"{down:2} MAGIE   ";:M=3
270 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(37)"  {down}{left:2}  ";
271 PRINT"{left:3}{up}"PD$(C);
272 MY=0:FZ=0
273 POKEQS+816+MY*40,35
274 GOSUB655
275 IFA$="S"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY+1:GOTO279
276 IFA$="W"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY-1:GOTO279
277 IFA$=CHR$(13)THENFZ=0:GOTO282
278 GOTO274
279 IFMY>=MTHENMY=0
280 IFMY<0THENMY=M-1
281 GOTO273
282 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(36)"{white}  {down}{left:2}  {left}{up}"PD$(C);
283 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
284 ONMY+1GOTO310,302
285 MT=3:MX=16:MY=20:MC=4:GOSUB440
286 IFIT%(IN%(M))=9THEN266
287 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         {down}{left:9}         ";
288 IFIN%(M)=0ORIT%(IN%(M))<>3THEN266
289 M=IN%(M)
290 IFIE%(M)>FM%(C)THENPRINT"{home}{down}{right}NICHT GENUG MANA                     ";:GOSUB692:GOTO266
291 GOSUB562
292 GOTO588
293 AP=IE%(M)+2-INT(RND(0)*5):FM%(C)=FM%(C)-IE%(M):PM%(C)=FM%(C):GOSUB429
294 AP=AP+PL%(C)*3
295 FH%(CE)=FH%(CE)-AP:IFFH%(CE)<0THENFH%(CE)=0
296 IFCE<4THENPH%(CE)=FH%(CE)
297 IFFH%(CE)>0THENGOTO332
298 IFFT%(CE)<>-2THENCC=CC-1:WX=WX+MX%(FT%(CE))
299 IFFT%(CE)=-2THENPP=PP-1
300 FT%(CE)=-1
301 GOTO332
302 MT=2:MX=16:MY=20:MC=4:GOSUB440
303 IFIT%(IN%(M))=9THEN266
304 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
305 PRINT"{down}{left:9}         ";
306 IFIN%(M)=0ORIT%(IN%(M))<>MTTHEN266
307 IFIN%(M)=3THENGOSUB181:IN%(M)=0:GOSUB429:GOTO256
308 IFIN%(M)=8THENGOSUB183:IN%(M)=0:GOSUB429:GOTO256
309 GOTO256
310 GOSUB562
311 AP=FA%(C)-FD(CE)
312 IFAP<=0THENPRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{white}MISS";:GOSUB690:GOTO324
313 AP=INT(RND(0)*AP)+1
314 FH%(CE)=FH%(CE)-AP:IFFH%(CE)<0THENFH%(CE)=0
315 IFCE<4THENPH%(CE)=FH%(CE)
316 IFFH%(CE)>0THENGOTO321
317 IFFT%(CE)<>-2THENCC=CC-1:WX=WX+MX%(FT%(CE))
318 IFFT%(CE)=-2THENPP=PP-1
319 FT%(CE)=-1
320 GOTO321
321 POKEV,(FX%(CE)*8+21)AND255:POKEV+1,FY%(CE)*8+50:POKE50168,16:POKEV+28,0
322 POKEV+16,-(FX%(CE)>30):POKEV+21,1
323 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{white}";AP
324 FORI=0TO5:POKEV,PEEK(V)+2:POKEV+1,PEEK(V+1)+2:GOSUB691:NEXTI
325 POKEV+21,0:IFCE<4THENGOSUB429
326 GOSUB690
327 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
328 IFFT%(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE));"  {down}{left:2}  ";
329 IFCC=0THEN355
330 IFPP=0THEN383
331 GOTO256
332 POKEV,(FX%(CE)*8+24)AND255:POKEV+1,FY%(CE)*8+50
333 IFM=4THENPOKEV+37,8:POKEV+38,2:POKEV+39,7:POKEV+28,1
334 B=17:IFM=6THENB=19:POKEV+28,0
335 IFM=5THENB=21:POKEV+28,0
336 IFM=7THENB=23:POKEV+28,0
337 POKEV+16,-(FX%(CE)>30):POKE50168,B:POKEV+21,1
338 PRINT"{white}";:IFM=9THENPRINT"{green}";
339 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}";AP
340 FORK=0TO3:FORI=0TO1:POKE50168,B+I:FORJ=1TO40:NEXTJ,I,K
341 POKEV+21,0:IFCE<4THENGOSUB429
342 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
343 IFFT%(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE));"  {down}{left:2}  ";
344 IFCC=0THEN355
345 IFPP=0THEN383
346 GOTO256
347 GOSUB690
348 PRINT"{white}{home}{down}{right}"M$(FT%(C))": GREIFT AN                    "
349 FC%(C)=FC%(C)-100
350 IFFT%(C)=7THENIFFM%(C)>=IE%(9)THENM=9:GOSUB690:GOSUB392:GOTO591
351 IFFT%(C)=9THENIFFM%(C)>=IE%(6)THENM=6:GOSUB690:GOSUB386:GOTO293
352 IFFT%(C)=10THENIFFM%(C)>=IE%(4)THENM=4:GOSUB690:GOSUB386:GOTO293
353 IFFT%(C)=11THENIFFM%(C)>=IE%(12)THENM=12:GOSUB690:GOSUB386:GOTO598
354 GOSUB690:GOSUB386:GOTO311
355 POKE1020,Q1%
356 PRINTDD$;"{brown}{up}{$c1}{$c2:10}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}{white}";
357 PRINT"{home}{down}{right}DU HAST GEWONNEN                     ";:GOSUB546
358 GOSUB498
359 FORI=0TO3:IFP3%(I)=0THEN363
360 PRINT"{home}{white}{right:3}";:IFPH%(I)=0THENPRINT"{red}";
361 PRINTLEFT$(CD$,4+I*3)P$(I)
362 PRINT"{home}"LEFT$(CD$,5+I*3)"{right:3}LV";PL%(I)+1;" EX";PX%(I)
363 NEXT
364 FORI=0TO3:IFPH%(I)=0ORP3%(I)=0THEN374
365 TX=WX
366 PRINT"{home}{white}"LEFT$(CD$,5+I*3)SPC(20)"EXPERIENCE {left}"TX"{left} ";
367 IFTX=0THEN374
368 XS=1:IFTX>9THENXS=10
369 IFTX>99THENXS=100
370 TX=TX-XS
371 PX%(I)=PX%(I)+XS
372 LL=10+PL%(I)*PL%(I)*10:IFPX%(I)>=LLTHENGOSUB376:GOTO372
373 PRINT"{home}{white}"LEFT$(CD$,5+I*3)"{right:3}LV";PL%(I)+1;" EX";PX%(I):GOTO366
374 NEXTI
375 GOTO381
376 PL%(I)=PL%(I)+1:PA%(I)=PA%(I)+1:PD%(I)=PD%(I)+1:PS%(I)=PS%(I)+1
377 P1%(I)=P1%(I)+PL%(I)*2:IFP2%(I)>0THENP2%(I)=P2%(I)+PL%(I)*3
378 IFP1%(I)>999THENP1%(I)=999
379 IFP2%(I)>999THENP2%(I)=999
380 RETURN
381 GOSUB683
382 GOTO62
383 PRINT"{home}{down}{right}{white}DU HAST VERLOREN                     "
384 GOSUB683
385 GOTO606
386 RE=INT(RND(0)*4):CE=0
387 IFFT%(CE)=-2ANDPH%(CE)>0THEN390
388 CE=CE+1:IFCE=4THENCE=0
389 GOTO387
390 IFRE>0THENRE=RE-1:GOTO388
391 RETURN
392 RE=INT(RND(0)*8):CE=4
393 IFFT%(CE)>=0THEN396
394 CE=CE+1:IFCE=12THENCE=4
395 GOTO393
396 IFRE>0THENRE=RE-1:GOTO394
397 RETURN
398 IFFF>0THENL=FF:GOTO400
399 L=PEEK(CP+164):IFL=0THENRETURN
400 IFL=3THENA=0:LC=INT(RND(0))*2+2:FORI=0TOLC:GOSUB540:NEXT:RETURN
401 IFL=1THENA=1:FORI=0TO2:GOSUB540:NEXT:RETURN
402 IFL=2THENA=1:FORI=0TO1:GOSUB540:NEXT:A=2:FORI=0TO1:GOSUB540:NEXT:RETURN
403 IFL=4THENA=3:GOSUB540:A=1:GOSUB540:GOSUB540:RETURN
404 IFL=5THENA=4:LC=INT(RND(0)*3)+1:FORI=0TOLC:GOSUB540:NEXT:RETURN
405 IFL=6THENONINT(RND(1)*2)+1GOTO415,416
406 IFL=7THENLC=INT(RND(0)*3)+1:A=6:FORI=0TOLC:GOSUB540:NEXT:RETURN
407 IFL=8THENA=7:GOSUB540:LC=INT(RND(0)*2)+1:A=6:GOSUB540:RETURN
408 IFL=9THENA=9:GOSUB540:A=8:GOSUB540:A=5:GOSUB540:RETURN
409 IFL=10THENA=6:LC=INT(RND(0)*2)+1:FORI=1TOLC:GOSUB540:NEXT:RETURN
410 IFL=11THENA=7:GOSUB540:A=10:GOSUB540:RETURN
411 IFL=12ANDINT(RND(1)*2)=1THENA=11:GOSUB540:RETURN
412 IFL=12THENA=5:GOSUB540:GOSUB540:A=9:GOSUB540:RETURN
413 IFL=13THENA=9:GOSUB540:A=12:GOSUB540:A=9:GOSUB540:RETURN
414 IFL=14THENA=9:GOSUB540:A=13:GOSUB540:A=7:GOSUB540:RETURN
415 A=5:LC=INT(RND(1)*2)+1:FORI=0TOLC:GOSUB540:NEXT:RETURN
416 A=9:LC=INT(RND(1)*2)+1:FORI=1TOLC:GOSUB540:NEXT:RETURN
417 FORI=0TO3
418 IFP3%(I)=0THEN421
419 PH%(I)=P1%(I)
420 PM%(I)=P2%(I)
421 NEXT:RETURN
422 PRINT"{brown}{home}{$c1}{$c2:38}{$c3}";
423 PRINT"{$c4}                                      {$c5}";
424 PRINT"{$c6}{$c7:38}{$c8}";
425 PRINTDD$;"{brown}{up}{$c1}{$c2:10}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
426 FORI=0TO3:PRINT"{$c4}          {$c5}{$c4}            {$c5}{$c4}            {$c5}";:NEXT
427 PRINT"{$c6}{$c7:10}{$c8}{$c6}{$c7:12}{$c8}{$c6}{$c7:12}";
428 POKE50151,72:POKE56295,9:RETURN
429 PRINTDD$;
430 FORI=0TO3:IFP3%(I)=0THENPRINTSPC(40);:GOTO436
431 PRINT"{white}";:IFPH%(I)=0THENPRINT"{red}";
432 PRINTSPC(27)P$(I)" ";:A=PH%(I):GOSUB653
433 PRINTD$;
434 A=PM%(I):GOSUB653
435 PRINT" "D$"{right}";
436 NEXT:RETURN
437 FORI=0TO3:IFPH%(I)<=0ORP3%(I)=0THENNEXT:RETURN
438 PRINT"{home}"LEFT$(CD$,5+I*3);SPC(37);PD$(I):NEXT
439 RETURN
440 GOSUB475
441 MI=-1:M=0:MO=0
442 MI=MO-1:CY=MY:MM=0
443 IFMC=4THENGOSUB470
444 MI=MI+1
445 PRINT"{home}{white}"LEFT$(CD$,CY)SPC(MX)" ";
446 IFIT%(IN%(MI))<>MTTHENPRINT"{cyan}";
447 IFIT%(IN%(MI))=9THENPRINT"{white}";
448 IFIT%(IN%(MI))=-1THENPRINT"{cyan}";
449 PRINTI$(IN%(MI));
450 CY=CY+1
451 MM=MM+1
452 IFMM=MCTHEN454
453 IFMI<13THEN444
454 CP=QS+MX+MY*40
455 POKECP+(M-MO)*40,35
456 GOSUB655
457 POKECP+(M-MO)*40,32
458 IFA$="W"THENM=M-1:GOTO462
459 IFA$="S"THENM=M+1:GOTO462
460 IFA$=CHR$(13)THENRETURN
461 GOTO455
462 IFM=-1THENM=14-1:MO=14-MC:GOTO442
463 IFM=14THENM=0:MO=0:GOTO442
464 M%=0
465 IFM<MOTHENMO=MO-MC:M%=1:GOTO465
466 IFM>=MO+MCTHENMO=MO+MC:M%=1
467 IFMO<0THENMO=0:M%=1
468 IFM%=1THEN442
469 GOTO455
470 FORI=0TO3
471 PRINT"{home}{white}"LEFT$(CD$,CY+I)SPC(MX)" ";
472 PRINT"        ";
473 NEXTI
474 RETURN
475 A=0
476 FORI=0TO99:TT%(I)=0:NEXTI
477 FORI=0TO99:TT%(I)=IN%(I):IN%(I)=0:NEXTI
478 IFMT=0THENGOSUB483:GOSUB486:A=50:GOSUB489:GOSUB492:GOSUB495
479 IFMT=1THENGOSUB483:GOSUB489:A=50:GOSUB486:GOSUB492:GOSUB495
480 IFMT=2THENGOSUB483:GOSUB492:A=50:GOSUB486:GOSUB489:GOSUB495
481 IFMT=3THENGOSUB483:GOSUB495:A=50:GOSUB486:GOSUB489:GOSUB492
482 RETURN
483 FORI=0TO99:IFIT%(TT%(I))=9THENIN%(A)=TT%(I):A=A+1
484 NEXTI
485 RETURN
486 FORI=0TO99:IFIT%(TT%(I))=0THENIN%(A)=TT%(I):A=A+1
487 NEXTI
488 RETURN
489 FORI=0TO99:IFIT%(TT%(I))=1THENIN%(A)=TT%(I):A=A+1
490 NEXTI
491 RETURN
492 FORI=0TO99:IFIT%(TT%(I))=2THENIN%(A)=TT%(I):A=A+1
493 NEXTI
494 RETURN
495 FORI=0TO99:IFIT%(TT%(I))=3THENIN%(A)=TT%(I):A=A+1
496 NEXTI
497 RETURN
498 PRINT"{brown}{home}{down:3}{$c1}{$c2:38}{$c3}";
499 FORI=0TO13
500 PRINT"{$c4}                                      {$c5}";:NEXT
501 PRINT"{$c6}{$c7:38}{$c8}";:RETURN
502 PRINT"{home}{down}{right}{white}                                      ";
503 RETURN
504 PRINT"{brown}{home}{down:3}{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
505 FORI=0TO5
506 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
507 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
508 PRINT"{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
509 FORI=0TO5
510 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
511 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
512 DX=3:DY=4:P=0:GOSUB515:DX=23:P=1:GOSUB515
513 DX=3:DY=12:P=2:GOSUB515:DX=23:P=3:GOSUB515
514 RETURN
515 IFP3%(P)=0THENRETURN
516 PRINT"{home}{white}";:IFPH%(P)=0THENPRINT"{red}";
517 PRINT"{home}"LEFT$(CD$,DY)SPC(DX-1)" "P$(P)"  LV"PL%(P)+1
518 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX)" "I$(PW%(P));
519 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX)" "I$(PR%(P));
520 T1%=PA%(P)+IA%(PW%(P))
521 T2%=PD%(P)+ID%(PR%(P))
522 IFT1%>999THENT1%=999
523 IFT2%>999THENT2%=999
524 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX+9)"ATK"T1%;
525 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX+9)"DEF"T2%;
526 RETURN
527 IFPEEK(2)=1THENSP=30960+RS*(CR-0)
528 FORI=0TO7
529 PRINT"{home}{down:3}"LEFT$(CD$,I*2);
530 FORJ=0TO19
531 PRINTT$(PEEK(SP))"{up}";
532 SP=SP+1
533 NEXTJ,I
534 RETURN
535 IS=0
536 IFIN%(IS)=0THENIN%(IS)=NI%:NI%=-1:RETURN
537 IS=IS+1
538 IFIS=99THENRETURN
539 GOTO536
540 IFCC>=11THENRETURN
541 FH%(CC)=MH%(A):FM%(CC)=MM%(A):FT%(CC)=A:FC%(CC)=INT(RND(0)*MS%(A)):FS%(CC)=MS%(A)
542 FX%(CC)=2:FY%(CC)=5+CC*3:FA%(CC)=MA%(A):FD(CC)=MD%(A):FB(CC)=MB%(A)
543 F1%(CC)=MH%(A):F2%(CC)=MM%(A)
544 IFCC>=4THENFX%(CC)=5:FY%(CC)=5+(CC-4)*3
545 CC=CC+1:RETURN
546 ZM=0
547 FORJ=4TO7
548 IFFT%(J)=-1THENZM=ZM-1
549 ZM=ZM+1
550 NEXTJ
551 IFZM<>CMTHENPRINTDD$;"{right}          {down}{left:10}          {down}{left:10}          {down}{left:10}          ";
552 PRINTDD$;
553 CM=0
554 FORI=4TO7
555 IFFT%(I)=-1THENNEXT:RETURN
556 A=FH%(I):GOSUB653
557 PRINT"{home}{right}"LEFT$(CD$,20+CM)M$(FT%(I));
558 PRINT"{home}{right}"LEFT$(CD$,20+CM);"{right:7}";D$
559 CM=CM+1
560 NEXT
561 RETURN
562 CE=4:PRINT"{home}{down}{right}{white}"P$(C)": WAHELE DEIN ZIEL               "
563 IFFT%(CE)=-1THEN572
564 POKEQS-1+FX%(CE)+FY%(CE)*40,35
565 GOSUB655
566 IFA$=CHR$(13)THENPOKEQS-1+FX%(CE)+FY%(CE)*40,32:RETURN
567 IFA$="S"THENGOTO572
568 IFA$="W"THENGOTO580
569 IFA$="D"ANDCE>3THENPOKEQS-1+FX%(CE)+FY%(CE)*40,32:CE=0:GOTO563
570 IFA$="A"ANDCE<4THENPOKEQS-1+FX%(CE)+FY%(CE)*40,32:CE=4:GOTO563
571 GOTO565
572 CO=CE
573 POKEQS-1+FX%(CE)+FY%(CE)*40,32
574 CE=CE+1
575 IFCE>=12THENCE=0
576 IFCO<4ANDCE>3THENCE=CO
577 IFCO>3ANDCE<4THENCE=CO
578 IFFT%(CE)<>-1THEN564
579 GOTO574
580 CO=CE
581 POKEQS-1+FX%(CE)+FY%(CE)*40,32
582 CE=CE-1
583 IFCE<0THENCE=11
584 IFCO<4ANDCE>3THENCE=CO
585 IFCO>3ANDCE<4THENCE=CO
586 IFFT%(CE)<>-1THEN564
587 GOTO582
588 IFFT%(C)=-2THEN604
589 IFFT%(C)=7ANDFM%(C)>=IE%(9)THENM=9:GOTO591
590 GOTO293
591 AP=IE%(M)*4+2-INT(RND(0)*5):FM%(C)=FM%(C)-IE%(M):IFC<4THENPM%(C)=FM%(C):GOSUB429
592 FH%(CE)=FH%(CE)+AP:IFFH%(CE)>F1%(CE)THENFH%(CE)=F1%(CE)
593 IFCE<4THENPH%(CE)=FH%(CE):GOSUB429
594 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{green}";AP
595 GOSUB697
596 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
597 GOTO256
598 FM%(C)=FM%(C)-IE%(M):IFC<4THENPM%(C)=FM%(C):GOSUB429
599 FS%(CE)=FS%(CE)/2:IFFS%(CE)<5THENFS%(CE)=5
600 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{green}SLOW";
601 GOSUB697
602 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
603 GOTO256
604 IFM=9THEN591
605 GOTO293
606 FORI=0TO3:PX%(I)=0:NEXT
607 PA%(0)=5:PD%(0)=2:PA%(1)=4:PD%(1)=3:PA%(2)=1:PD%(2)=1:PA%(3)=1:PD%(3)=1
608 FORI=0TO13:IN%(I)=0:NEXT
609 IN%(0)=18
610 ER%(0)=16:EX%(0)=0:EY%(0)=0:ET%(1)=0
611 ER%(1)=0:EX%(1)=1:EY%(1)=2:ET%(1)=3
612 ER%(2)=0:EX%(2)=18:EY%(2)=2:ET%(2)=15
613 ER%(3)=16:EX%(3)=0:EY%(3)=0:ET%(3)=4
614 ER%(4)=16:EX%(4)=0:EY%(4)=0:ET%(4)=8
615 ER%(5)=16:EX%(5)=0:EY%(5)=0:ET%(5)=5
616 ER%(6)=16:EX%(6)=0:EY%(6)=0:ET%(6)=16
617 ER%(7)=16:EX%(7)=0:EY%(7)=0:ET%(7)=17
618 ER%(8)=16:EX%(8)=0:EY%(8)=0:ET%(8)=13
619 ER%(9)=16:EX%(9)=0:EY%(9)=0:ET%(9)=15
620 ER%(10)=16:EX%(10)=0:EY%(10)=0:ET%(10)=11
621 ER%(11)=16:EX%(11)=0:EY%(11)=0:ET%(11)=7
622 ER%(12)=16:EX%(12)=0:EY%(12)=0:ET%(12)=9
623 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
624 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
625 PRINT"{$c6}{$c7:38}{up:3}";
626 POKE50151,72:POKE56295,9
627 X=15:Y=2:CR=0
628 P3%(0)=1:P3%(1)=0:P3%(2)=0:P3%(3)=0
629 P1%(0)=15:P1%(1)=17:P1%(2)=9:P1%(3)=9:P2%(0)=0:P2%(1)=0:P2%(2)=7:P2%(3)=23
630 FORI=0TO3:PH%(I)=P1%(I):PM%(I)=P2%(I):PX%(I)=0:PW%(I)=0:PR%(I)=0:NEXT
631 PW%(0)=1:PR%(0)=2
632 GOSUB127
633 AA$="INTRO":GOSUB641
634 GOTO57
635 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
636 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
637 PRINT"{$c6}{$c7:38}{up:3}";
638 POKE50151,72:POKE56295,9
639 AA$="ENDE":GOSUB641
640 GOTO57
641 POKE1020,Q7%
642 POKE56322,224
643 IFAA$="INTRO"THENOPEN1,8,4,"TXT.INTRO,S,R"
644 IFAA$="ENDE"THENOPEN1,8,4,"TXT.ENDE,S,R"
645 IFAA$=""THENRETURN
646 GET#1,B$
647 IFB$="$"THENPRINT"{home}{down:2}{right:2}{white}";:GOTO646
648 IFB$="%"THENPRINT"{right:2}{white}";:GOTO646
649 IFB$="#"THENCLOSE1:POKE56322,255:GOSUB683:RETURN
650 PRINTB$;
651 IFB$<>" "THENPOKE162,0:WAIT162,4
652 GOTO646
653 T$=STR$(A)
654 D$=RIGHT$("000"+RIGHT$(T$,LEN(T$)-1),3):RETURN
655 A$=""
656 J=PEEK(56320)
657 IFJ=127THEN659
658 GOTO656
659 J=PEEK(56320)
660 IFJ=127ANDA$=CHR$(13)THENRETURN
661 IFJ=107THENA$="E":GOSUB688:RETURN
662 IFJ=103THENA$="E":GOSUB688:RETURN
663 IFJ=110THENA$="E":GOSUB688:RETURN
664 IFJ=109THENA$="E":GOSUB688:RETURN
665 IFJ=123THENA$="A":GOSUB688:RETURN
666 IFJ=119THENA$="D":GOSUB688:RETURN
667 IFJ=126THENA$="W":GOSUB688:RETURN
668 IFJ=125THENA$="S":GOSUB688:RETURN
669 IFJ=111THENA$=CHR$(13)
670 GOTO659
671 A$=""
672 J=PEEK(56320)
673 IFJ<>111ANDJ<>107ANDJ<>103ANDJ<>110ANDJ<>109THEN675
674 GOTO672
675 J=PEEK(56320)
676 IFJ=127ANDA$=CHR$(13)THENRETURN
677 IFJ=123THENA$="A":GOSUB689:RETURN
678 IFJ=119THENA$="D":GOSUB689:RETURN
679 IFJ=126THENA$="W":GOSUB689:RETURN
680 IFJ=125THENA$="S":GOSUB689:RETURN
681 IFJ=111THENA$=CHR$(13)
682 GOTO675
683 A$=""
684 J=PEEK(56320)
685 IFJ=127ANDA$=CHR$(13)THENRETURN
686 IFJ=111THENA$=CHR$(13)
687 GOTO684
688 POKE162,0:WAIT162,16:RETURN
689 POKE162,0:WAIT162,8:RETURN
690 POKE162,0:WAIT162,32:RETURN
691 POKE162,0:WAIT162,2:RETURN
692 POKE162,0:WAIT162,32
693 POKE162,0:WAIT162,32
694 POKE162,0:WAIT162,32
695 POKE162,0:WAIT162,32
696 RETURN
697 POKE162,0:WAIT162,32
698 RETURN
699 POKE162,0:WAIT162,4
700 RETURN
701 DATA"IMP",5,0,8,4,1,0,3
702 DATA"SNAKE",8,0,12,6,1,0,5
703 DATA"VIPER",12,0,18,6,1,0,9
704 DATA"NACHO",30,0,22,15,5,0,50
705 DATA"GOLEM",20,0,4,20,8,0,17
706 DATA"MOSS",30,0,7,26,9,0,21
707 DATA"BONES",50,0,20,18,8,0,15
708 DATA"WISP",25,50,15,0,8,0,17
709 DATA"TROLL",100,0,9,40,13,0,100
710 DATA"SPARK",30,50,22,0,8,0,17
711 DATA"BURNER",80,90,30,25,13,0,42
712 DATA"TBERRY",120,10,60,60,20,0,150
713 DATA"DRACUL",250,60,50,70,25,0,500
714 DATA"GLADOS",500,70,60,80,30,0,1000
715 DATA"KRON",15 ,0  ,10 ,4 ,3 ,0 ,0
716 DATA"LENA",17 ,0  ,10 ,3 ,3 ,0 ,0
717 DATA"DOLM",9  ,7  ,8  ,1 ,1 ,0 ,0
718 DATA"MIRA",9  ,23 ,7  ,1 ,1 ,1 ,2
719 DATA"{gray1}{rvrs off}{$c9}{rvrs off}{gray1}{$ca}{down}{left:2}{rvrs off}{gray1}{$ca}{rvrs off}{gray1}{$c9}{rvrs off}"
720 DATA"{gray1}{rvrs off}{$cb}{rvrs off}{gray1}{$cc}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1}{$ca}{rvrs off}"
721 DATA"{gray1}{rvrs off}{$c9}{rvrs off}{gray1}{$ca}{down}{left:2}{rvrs off}{gray1}{$ca}{rvrs off}{gray1}{$c9}{rvrs off}"
722 DATA"{gray1}{rvrs off}{$cd}{rvrs off}{gray1}{$ce}{down}{left:2}{rvrs off}{gray1}{$cf}{rvrs off}{gray1}{$d0}{rvrs off}"
723 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
724 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
725 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
726 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
727 DATA"{orange}{rvrs off}{$d1}{rvrs off}{orange}{$d1}{down}{left:2}{rvrs off}{orange}{$d1}{rvrs off}{orange}{$d1}{rvrs off}"
728 DATA"{brown}{rvrs off}{$d2}{rvrs off}{brown}{$d3}{down}{left:2}{rvrs off}{brown}{$d4}{rvrs off}{brown}{$d5}{rvrs off}"
729 DATA"{lt. red}{rvrs off}{$d6}{rvrs off}{lt. red}{$d7}{down}{left:2}{rvrs off}{lt. red}{$d8}{rvrs off}{lt. red}{$d9}{rvrs off}"
730 DATA"{gray1}{rvrs off}{$da}{rvrs off}{gray1}{$db}{down}{left:2}{rvrs off}{gray1}{$dc}{rvrs off}{gray1}{$dd}{rvrs off}"
731 DATA"{gray1}{rvrs off}{$a2}{rvrs off}{gray1}{$a3}{down}{left:2}{rvrs off}{gray1}{$a4}{rvrs off}{gray1}{$a5}{rvrs off}"
732 DATA"{brown}{rvrs off}{$a1}{rvrs off}{brown}{$a1}{down}{left:2}{rvrs off}{brown}{$a1}{rvrs off}{brown}{$a1}{rvrs off}"
733 DATA"{gray1}{rvrs off}{$de}{rvrs off}{gray1}{$df}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1} {rvrs off}"
734 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
735 DATA"{lt. red}{rvrs off}{$a6}{rvrs off}{lt. red}{$a7}{down}{left:2}{rvrs off}{lt. red}{cmdr pound}{rvrs off}{lt. red}{shft pound}{rvrs off}"
736 DATA"{lt. red}{rvrs off}{$aa}{rvrs off}{lt. red}{$aa}{down}{left:2}{rvrs off}{lt. red}{$aa}{rvrs off}{lt. red}{$aa}{rvrs off}"
737 DATA"{brown}{rvrs off}{$ab}{rvrs off}{brown}{$ab}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
738 DATA"{brown}{rvrs off}{$ab}{rvrs off}{brown}{$ad}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
739 DATA"{brown}{rvrs off}{$ad}{rvrs off}{brown}{$ab}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
740 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
741 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
742 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
743 DATA"{lt. green}{rvrs off}{$ae}{rvrs off}{lt. green}{$d1}{down}{left:2}{rvrs off}{lt. green}{$d1}{rvrs off}{lt. green}{$af}{rvrs off}"
744 DATA"{lt. green}{rvrs off}{$b0}{rvrs off}{lt. green}{$b1}{down}{left:2}{rvrs off}{lt. green}{$b2}{rvrs off}{lt. green}{$b3}{rvrs off}"
745 DATA"{lt. green}{rvrs off}{$b4}{rvrs off}{lt. green}{$b5}{down}{left:2}{rvrs off}{lt. green}{$b4}{rvrs off}{lt. green}{$b5}{rvrs off}"
746 DATA"{lt. green}{rvrs off}{$b6}{rvrs off}{lt. green}{$b7}{down}{left:2}{rvrs off}{lt. green}{$b8}{rvrs off}{lt. green}{$b9}{rvrs off}"
747 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
748 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
749 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
750 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
751 DATA"{brown}{rvrs off}{$ae}{rvrs off}{brown}{$d1}{down}{left:2}{rvrs off}{brown}{$d1}{rvrs off}{brown}{$af}{rvrs off}"
752 DATA"{brown}{rvrs off}{$b0}{rvrs off}{brown}{$b1}{down}{left:2}{rvrs off}{brown}{$b2}{rvrs off}{brown}{$b3}{rvrs off}"
753 DATA"{brown}{rvrs off}{$b4}{rvrs off}{brown}{$b5}{down}{left:2}{rvrs off}{brown}{$b4}{rvrs off}{brown}{$b5}{rvrs off}"
754 DATA"{brown}{rvrs off}{$b6}{rvrs off}{brown}{$b7}{down}{left:2}{rvrs off}{brown}{$b8}{rvrs off}{brown}{$b9}{rvrs off}"
755 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
756 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
757 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
758 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
759 DATA"{gray1}{rvrs off}{$d1}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$d1}{rvrs off}{gray1}{$d1}{rvrs off}"
760 DATA"{gray1}{rvrs off}{$ba}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$ba}{rvrs off}{gray1}{$d1}{rvrs off}"
761 DATA"{gray1}{rvrs off}{$ba}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$bb}{rvrs off}{gray1}{$d1}{rvrs off}"
762 DATA"{gray1}{rvrs off}{$bc}{rvrs off}{gray1}{$bd}{down}{left:2}{rvrs off}{gray1}{$be}{rvrs off}{gray1}{$be}{rvrs off}"
763 DATA"{gray1}{rvrs off}{$bf}{rvrs off}{gray1}{$bf}{down}{left:2}{rvrs on}{gray1}@{rvrs on}{gray1}@{rvrs off}"
764 DATA"{gray2}{rvrs on}A{rvrs on}{gray2}B{down}{left:2}{rvrs on}{gray2}C{rvrs on}{gray2}D{rvrs off}"
765 DATA"{gray1}{rvrs on}E{rvrs on}{gray1}F{down}{left:2}{rvrs on}{gray1}G{rvrs on}{gray1}H{rvrs off}"
766 DATA"{brown}{rvrs on}I{rvrs on}{brown}I{down}{left:2}{rvrs on}{brown}I{rvrs on}{brown}I{rvrs off}"
767 DATA"{lt. red}{rvrs on}J{rvrs on}{lt. red}K{down}{left:2}{rvrs on}{lt. red}L{rvrs on}{lt. red}M{rvrs off}"
768 DATA"{gray1}{rvrs on}N{rvrs on}{gray1}K{down}{left:2}{rvrs on}{gray1}L{rvrs on}{gray1}M{rvrs off}"
769 DATA"{lt. green}{rvrs on}O{rvrs on}{lt. green}P{down}{left:2}{rvrs on}{lt. green}Q{rvrs on}{lt. green}R{rvrs off}"
770 DATA"{gray2}{rvrs on}S{rvrs on}{gray2}T{down}{left:2}{rvrs on}{gray2}U{rvrs on}{gray2}V{rvrs off}"
771 DATA"{gray1}{rvrs on}W{rvrs on}{gray1}X{down}{left:2}{rvrs on}{gray1}Y{rvrs on}{gray1}Z{rvrs off}"
772 DATA"{gray1}{rvrs on}[{rvrs on}{gray1}{pound}{down}{left:2}{rvrs on}{gray1}]{rvrs on}{gray1}{^}{rvrs off}"
773 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
774 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
775 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
776 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
777 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
778 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
779 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
780 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
781 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
782 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
783 DATA"{lt. green}{rvrs on}{back arrow}{rvrs on}{lt. green}!{down}{left:2}{rvrs on}{lt. green}#{rvrs on}{lt. green}${rvrs off}"
784 DATA"{gray3}{rvrs on}%{rvrs on}{gray3}&{down}{left:2}{rvrs on}{gray3}'{rvrs on}{gray3}({rvrs off}"
785 DATA"{gray1}{rvrs on}%{rvrs on}{gray1}&{down}{left:2}{rvrs on}{gray1}'{rvrs on}{gray1}({rvrs off}"
786 DATA"{lt. green}{rvrs on}){rvrs on}{lt. green}*{down}{left:2}{rvrs on}{lt. green}+{rvrs on}{lt. green},{rvrs off}"
787 DATA"{gray1}{rvrs on}-{rvrs on}{gray1}.{down}{left:2}{rvrs on}{gray1}/{rvrs on}{gray1}0{rvrs off}"
788 DATA"{gray1}{rvrs on}1{rvrs on}{gray1}2{down}{left:2}{rvrs on}{gray1}3{rvrs on}{gray1}4{rvrs off}"
789 DATA"{gray1}{rvrs on}5{rvrs on}{gray1}6{down}{left:2}{rvrs on}{gray1}7{rvrs on}{gray1}8{rvrs off}"
790 DATA"{gray1}{rvrs on}9{rvrs on}{gray1}:{down}{left:2}{rvrs on}{gray1};{rvrs on}{gray1}<{rvrs off}"
791 DATA"{gray1}{rvrs on}={rvrs on}{gray1}>{down}{left:2}{rvrs on}{gray1}?{rvrs on}{gray1}{$c0}{rvrs off}"
792 DATA"{lt. green}{rvrs on}9{rvrs on}{lt. green}:{down}{left:2}{rvrs on}{lt. green};{rvrs on}{lt. green}<{rvrs off}"
793 DATA"{lt. red}{rvrs on}5{rvrs on}{lt. red}6{down}{left:2}{rvrs on}{lt. red}7{rvrs on}{lt. red}8{rvrs off}"
794 DATA"{lt. green}{rvrs on}5{rvrs on}{lt. green}6{down}{left:2}{rvrs on}{lt. green}7{rvrs on}{lt. green}8{rvrs off}"
795 DATA"{gray1}{rvrs on}{$c1}{rvrs on}{gray1}{$c2}{down}{left:2}{rvrs on}{gray1}{$c3}{rvrs on}{gray1}{$c4}{rvrs off}"
796 DATA"{gray1}{rvrs on}{$c5}{rvrs on}{gray1}{$c6}{down}{left:2}{rvrs on}{gray1}{$c7}{rvrs on}{gray1}{$c8}{rvrs off}"
797 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
798 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
799 DATA"        ",00,00,00,00,-1 
800 DATA"DOLCH   ",03,00,00,00,00  
801 DATA"HELM    ",00,02,-1,00,01  
802 DATA"KRAEUTER",00,00,-1,10,02  
803 DATA"FEUER   ",00,00,00,05,03  
804 DATA"EIS     ",00,00,00,08,03  
805 DATA"MUT     ",00,00,00,23,03  
806 DATA"BOMBE   ",00,00,00,50,03  
807 DATA"KIRSCHE ",00,00,-1,15,02  
808 DATA"HEILEN  ",00,00,00,04,03  
809 DATA"SKULL   ",00,00,00,00,04  
810 DATA"FLEGEL  ",07,00,-1,00,00  
811 DATA"LAHM    ",00,00,00,04,03  
812 DATA"STAB    ",12,00,00,00,00  
813 DATA"FEDER   ",00,10,00,00,01  
814 DATA"STOCK   ",23,00,00,00,00  
815 DATA"WESTE   ",00,14,00,00,01  
816 DATA"SCHILD  ",00,24,00,00,01  
817 DATA"ZURUECK ",00,00,00,0 ,09  
818 END
