0 POKE55,240:POKE56,120:CLR
1 QO=53282
2 QP=53283
3 QQ=53281
4 QR=53280
5 QS=49152
6 V=53248
7 Q1%=0
8 Q7%=6
9 QA%=9
10 RS=165
11 DD$="{home}{down:20}{white}"
12 CD$="{down:25}"
13 DIMM$(13),MD$(13),MH%(13),MM%(13),MA%(13),MD%(13),MS%(13),MB%(13),MX%(13)
14 DIMFH%(11),FT%(11),FC%(11),FM%(11),FS%(11),FX%(11),FY%(11),FA%(11),FD(11),F1%(11),F2%(11)
15 FORI=0TO3:FX%(I)=37:FY%(I)=5+3*I:NEXT
16 DIMI$(18),IA%(18),ID%(18),IS%(18),IE%(18),IT%(18)
17 DIMER%(12),EX%(12),EY%(12),ET%(12)
18 DIMIN%(99),TT%(99)
19 DIMT$(79)
20 DIMS0%(10),S1%(10),S2%(10),S3%(10),A0%(10),A1%(10),A2%(10)
21 DIMN0%(10),N1%(10),N2%(8),N3%(10)
22 FORI=0TO13
23 READM$(I),MH%(I),MM%(I),MS%(I)
24 READMA%(I),MD%(I),MB%(I),MX%(I)
25 NEXT
26 FORI=0TO3
27 READP$(I),PH%(I),PM%(I),PS%(I)
28 READPA%(I),PD%(I),PW%(I),PR%(I)
29 NEXT
30 FORI=0TO79
31 READT$(I)
32 NEXT
33 PD$(0)=T$(48)
34 PD$(1)=T$(50)
35 PD$(2)=T$(52)
36 PD$(3)=T$(51)
37 ZZ=64
38 FORI=0TO13
39 MD$(I)=T$(ZZ)
40 ZZ=ZZ+1
41 NEXTI
42 FORI=0TO18
43 READI$(I),IA%(I),ID%(I),IS%(I),IE%(I),IT%(I)
44 NEXT
45 POKEV+21,0
46 POKE648,192
47 POKE56576,0
48 POKE53272,8
49 POKE53270,PEEK(53270)OR16
50 POKEQO,QA%
51 POKEQP,Q1%
52 POKEQR,Q1%
53 POKEQQ,Q7%
54 GOSUB145
55 GOTO626
56 POKE1020,Q7%
57 POKE1021,Q7%
58 SYS820
59 GOSUB442
60 GOSUB449
61 PRINT"{home}{down}{right}{white}                                      ";
62 POKE1020,Q7%
63 GOSUB547
64 IFPEEK(2)=1THENCP=30960+RS*(CR-0)
65 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)PD$(0);
66 OX=ZX:OY=ZY
67 ZX=X:ZY=Y
68 GOSUB679
69 IFA$=CHR$(145)ORA$="W"THENZY=Y-1:GOTO75
70 IFA$=CHR$(157)ORA$="A"THENZX=X-1:GOTO75
71 IFA$=CHR$(29)ORA$="D"THENZX=X+1:GOTO75
72 IFA$=CHR$(17)ORA$="S"THENZY=Y+1:GOTO75
73 IFA$=CHR$(13)THEN178
74 GOTO68
75 AA%=0
76 FORI=0TO12
77 IFER%(I)=CRANDEX%(I)=ZXANDEY%(I)=ZYTHENAA%=1
78 NEXTI
79 IFZX=-1ORZX=20ORZY=-1ORZY=8THEN91
80 C=PEEK(CP+ZX+ZY*20)
81 IFC=08THEN105
82 IFC=09THEN105
83 IFC=10THEN105
84 IFC=11THEN105
85 IFC=12THEN112
86 IFC=14THEN107
87 IFC=25THEN112
88 IFC=40THEN125
89 IFC>=48THEN116
90 IFC>7THEN66
91 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)T$(PEEK(CP+X+Y*20))
92 IFRND(1)*100>97ANDPEEK(CP+164)>0THENFF=0:GOTO254
93 X=ZX:Y=ZY
94 IFX=-1ANDPEEK(CP+160)=255THENX=0:GOTO65
95 IFX=-1THENCR=PEEK(CP+160):X=19:GOTO62
96 IFX=20ANDPEEK(CP+161)=255THENX=19:GOTO65
97 IFX=20THENCR=PEEK(CP+161):X=0:GOTO62
98 IFY=-1ANDPEEK(CP+162)=255THENY=0:GOTO65
99 IFY=-1THENCR=PEEK(CP+162):Y=7:GOTO62
100 IFY=8ANDPEEK(CP+163)=255THENY=7:GOTO65
101 IFY=8THENCR=PEEK(CP+163):Y=0:GOTO62
102 IFC=2THEN107
103 IFC=3THEN107
104 GOTO65
105 IFCR=16THENCR=16:X=0:Y=0:GOTO61
106 IFCR=16THEN642
107 IFC=14ANDCR=S0%(0)ANDZX=S1%(0)THENS3%(0)=1-S3%(0):GOSUB129:TX=A1%(0):TY=A2%(0):GOSUB126:GOSUB142:GOTO66
108 IFC=2ANDCR=S0%(1)ANDX=S1%(1)THENS3%(1)=1:GOSUB129:TX=A1%(1):TY=A2%(1):GOSUB126:GOTO65
109 IFC=2ANDCR=S0%(2)ANDX=S1%(2)THENS3%(2)=1:GOSUB129:TX=A1%(2):TY=A2%(2):GOSUB126:GOTO65
110 IFC=3ANDCR=S0%(3)ANDX=S1%(3)THENS3%(3)=1:GOSUB129:TX=A1%(3):TY=A2%(3):GOSUB126:GOTO65
111 IFC=3ANDCR=S0%(4)ANDX=S1%(4)THENS3%(4)=1:GOSUB129:TX=A1%(4):TY=A2%(4):GOSUB126:GOTO65
112 IFC=12ANDAA%=0THENGOTO66
113 IFC=12ANDAA%=1THENPRINT"{home}{white}{down}{right}IN DER TRUHE IST WAS!":GOSUB700:GOSUB522:GOTO66
114 IFC=25ANDAA%=0THENGOTO66
115 IFC=25ANDAA%=1THENPRINT"{home}{white}{down}{right}DER BAUM SIEHT INTRESSANT AUS!":GOSUB700:GOSUB522:GOTO66
116 IFC=49THENPRINT"{white}{home}{down}{right:2}EIN HEILZAUBER IST UNTER DEM BAUM":GOSUB700:GOSUB522:GOTO66
117 IFC=53THENPRINT"{white}{home}{down}{right:2}EIN FEUERZAUBER IST IN DEN KATAKOMBEN":GOSUB700:GOSUB522:GOTO66
118 IFC=50THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB700:GOSUB522:N3%(0)=1:GOSUB129:P3%(1)=1:TX=12:TY=4:GOSUB126:GOSUB449:GOTO66
119 IFC=52THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB700:GOSUB522:N3%(1)=1:GOSUB129:P3%(2)=1:TX=7:TY=2:GOSUB126:GOSUB449:GOTO66
120 IFC=51THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB700:GOSUB522:N3%(2)=1:GOSUB129:P3%(3)=1:TX=3:TY=2:GOSUB126:GOSUB449:GOTO66
121 IFC=67THENPRINT"{white}{home}{down}{right:2}DU KANNST HIER NICHT DURCH!":GOSUB700:FF=4:N3%(3)=1:GOSUB129:GOTO254
122 IFC=72THENPRINT"{white}{home}{down}{right:2}ACH WIE SUESS!":GOSUB700:FF=9:N3%(4)=1:GOSUB129:GOTO254
123 IFC=76THENPRINT"{white}{home}{down}{right:2}DU WILLST MICH BESIEGEN!":GOSUB700:FF=13:N3%(5)=1:GOSUB129:GOTO254
124 IFC=77THENPRINT"{white}{home}{down}{right:2}DU HAST MEINE ERWARTUNG UEBERTROFFEN!":GOSUB700:FF=14:N3%(6)=1:GOSUB129:GOTO254
125 IFC=40THENPRINT"{home}{white}{down}{right}DU BIST GEHEILT!":GOSUB437:GOSUB449:GOSUB700:GOSUB522:GOTO66
126 SP=30960+(CR*RS)+(TX+TY*20)
127 PRINT"{home}{down:3}"LEFT$(CD$,TY*2)SPC(TX*2)T$(PEEK(SP));
128 RETURN
129 POKE30960+N0%(0)*RS+N1%(0)+N2%(0)*20,50-N3%(0)*50
130 POKE30960+N0%(1)*RS+N1%(1)+N2%(1)*20,52-N3%(1)*52
131 POKE30960+N0%(2)*RS+N1%(2)+N2%(2)*20,51-N3%(2)*51
132 POKE30960+N0%(3)*RS+N1%(3)+N2%(3)*20,67-N3%(3)*67
133 POKE30960+N0%(4)*RS+N1%(4)+N2%(4)*20,72-N3%(4)*72
134 POKE30960+N0%(5)*RS+N1%(5)+N2%(5)*20,76-N3%(5)*76
135 POKE30960+N0%(6)*RS+N1%(6)+N2%(6)*20,77-N3%(6)*77
136 POKE30960+A0%(0)*RS+A1%(0)+A2%(0)*20,15-S3%(0)*15+1
137 POKE30960+A0%(1)*RS+A1%(1)+A2%(1)*20,13-S3%(1)*13
138 POKE30960+A0%(2)*RS+A1%(2)+A2%(2)*20,13-S3%(2)*12
139 T=13:IFS3%(3)+S3%(4)=2THENT=0
140 POKE30960+A0%(3)*RS+A1%(3)+A2%(3)*20,T
141 RETURN
142 IFCR=S0%(0)ANDZX=S1%(0)ANDS3%(0)=0THENPRINT"{home}{white}{down}{right}DER SCHALTER IST AUS!";:GOSUB700:GOSUB522
143 IFCR=S0%(0)ANDZX=S1%(0)ANDS3%(0)=1THENPRINT"{home}{white}{down}{right}DER SCHALTER IST EIN!";:GOSUB700:GOSUB522
144 RETURN
145 S0%(0)=0:S1%(0)=11:S2%(0)=1:S3%(0)=0
146 S0%(1)=0:S1%(1)=16:S2%(1)=6:S3%(1)=0
147 S0%(2)=0:S1%(2)=18:S2%(2)=6:S3%(2)=0
148 S0%(3)=0:S1%(3)=1:S2%(3)=6:S3%(3)=0
149 S0%(4)=0:S1%(4)=5:S2%(4)=6:S3%(4)=0
150 A0%(0)=0:A1%(0)=14:A2%(0)=6
151 A0%(1)=0:A1%(1)=7:A2%(1)=4
152 A0%(2)=0:A1%(2)=9:A2%(2)=5
153 A0%(3)=0:A1%(3)=3:A2%(3)=4
154 A0%(4)=0:A1%(4)=3:A2%(4)=4
155 N0%(0)=0:N1%(0)=12:N2%(0)=4:N3%(0)=0
156 N0%(1)=0:N1%(1)=7:N2%(1)=2:N3%(1)=0
157 N0%(2)=0:N1%(2)=3:N2%(2)=2:N3%(2)=0
158 N0%(3)=16:N1%(3)=0:N2%(3)=0:N3%(3)=0
159 N0%(4)=16:N1%(4)=0:N2%(4)=0:N3%(4)=0
160 N0%(5)=16:N1%(5)=0:N2%(5)=0:N3%(5)=0
161 N0%(6)=16:N1%(6)=0:N2%(6)=0:N3%(6)=0
162 N0%(7)=16:N1%(7)=0:N2%(7)=0:N3%(7)=0
163 N0%(8)=16:N1%(8)=0:N2%(8)=0:N3%(8)=0
164 ER%(0)=16:EX%(0)=0:EY%(0)=0:ET%(1)=0
165 ER%(1)=0:EX%(1)=1:EY%(1)=2:ET%(1)=3
166 ER%(2)=0:EX%(2)=18:EY%(2)=2:ET%(2)=15
167 ER%(3)=16:EX%(3)=0:EY%(3)=0:ET%(3)=4
168 ER%(4)=16:EX%(4)=0:EY%(4)=0:ET%(4)=8
169 ER%(5)=16:EX%(5)=0:EY%(5)=0:ET%(5)=5
170 ER%(6)=16:EX%(6)=0:EY%(6)=0:ET%(6)=16
171 ER%(7)=16:EX%(7)=0:EY%(7)=0:ET%(7)=17
172 ER%(8)=16:EX%(8)=0:EY%(8)=0:ET%(8)=13
173 ER%(9)=16:EX%(9)=0:EY%(9)=0:ET%(9)=15
174 ER%(10)=16:EX%(10)=0:EY%(10)=0:ET%(10)=11
175 ER%(11)=16:EX%(11)=0:EY%(11)=0:ET%(11)=7
176 ER%(12)=16:EX%(12)=0:EY%(12)=0:ET%(12)=9
177 RETURN
178 PRINT"{home}{down}{white}{right}  UNTERSUCHE  EQUIPMENTS  INVENTAR"
179 M=0
180 POKEQS+42+M*12,35
181 GOSUB662
182 POKEQS+42+M*12,32
183 IFA$="A"THENM=M-1-3*(M=0):GOTO180
184 IFA$="D"THENM=M+1+3*(M=2):GOTO180
185 IFA$=CHR$(13)THENPOKEQS+42+M*7,32:GOTO187
186 GOTO180
187 PRINT"{home}{down}{white}{right}                                      ";
188 ONM+1GOTO242,202,189
189 POKE1020,Q1%
190 GOSUB518
191 PRINT"{home}{down}{right}{white}INVENTAR: WAEHLE DEIN ITEM           ";
192 MT=2:MX=2:MY=4:MC=14:GOSUB460
193 IFIN%(M)=9THEN61
194 IFIN%(M)=0ORIT%(IN%(M))<>MTTHEN61
195 IFIN%(M)=3THENGOSUB198:IN%(M)=0:GOSUB449:GOTO61
196 IFIN%(M)=8THENGOSUB200:IN%(M)=0:GOSUB449:GOTO61
197 GOTO61
198 FORI=0TO3:PH%(I)=PH%(I)+IE%(IN%(M)):IFPH%(I)>P1%(I)THENPH%(I)=P1%(I)
199 FH%(I)=PH%(I):NEXT:RETURN
200 FORI=0TO3:PM%(I)=PM%(I)+IE%(IN%(M)):IFPM%(I)>P2%(I)THENPM%(I)=P2%(I)
201 FM%(I)=PM%(I):NEXT:RETURN
202 POKE1020,Q1%
203 GOSUB524
204 PRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINEN SPIELER     ";
205 P=0
206 CP=QS+2+(4-8*(P>1))*40:IFP=1ORP=3THENCP=CP+20
207 POKECP,35
208 GOSUB662
209 POKECP,32
210 IFP=0ANDA$="D"ANDP3%(1)=1THENP=1:GOTO206
211 IFP=0ANDA$="S"ANDP3%(2)=1THENP=2:GOTO206
212 IFP=0ANDA$="D"ANDP3%(1)=OANDP3%(2)=0ANDP3%(3)=1THENP=3:GOTO206
213 IFP=0ANDA$="S"ANDP3%(1)=OANDP3%(2)=0ANDP3%(3)=1THENP=3:GOTO206
214 IFP=1ANDA$="A"ANDP3%(0)=1THENP=0:GOTO206
215 IFP=1ANDA$="S"ANDP3%(3)=1THENP=3:GOTO206
216 IFP=2ANDA$="D"ANDP3%(3)=1THENP=3:GOTO206
217 IFP=2ANDA$="W"ANDP3%(0)=1THENP=0:GOTO206
218 IFP=3ANDA$="A"ANDP3%(2)=1THENP=2:GOTO206
219 IFP=3ANDA$="W"ANDP3%(1)=1THENP=1:GOTO206
220 IFP=3ANDA$="A"ANDP3%(2)=OANDP3%(1)=0ANDP3%(0)=1THENP=0:GOTO206
221 IFP=3ANDA$="W"ANDP3%(2)=OANDP3%(1)=0ANDP3%(0)=1THENP=0:GOTO206
222 IFA$=CHR$(13)THEN224
223 GOTO207
224 SL=0:CP=CP+81
225 PRINT"{home}{down}{right}{white}EQUIPMENT: WAFFE ODER RUESTUNG       ";
226 POKECP+SL*40,35
227 GOSUB662
228 POKECP+SL*40,32:IFA$="W"ORA$="S"THENSL=1-SL:GOTO226
229 IFA$=CHR$(13)THEN231
230 GOTO226
231 MY=4:MX=23:MC=14:IFP=1ORP=3THENMX=3
232 IFSL=0THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE WAFFE        ";
233 IFSL=1THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE RUESTUNG     ";
234 PRINT"{home}"LEFT$(CD$,MY-1)SPC(MX-3);"{brown}{$c1}{$c2:18}{$c3}";
235 FORI=0TO13:PRINT"{down}{left:20}{$c4}                  {$c5}";:NEXT
236 PRINT"{down}{left:20}{$c6}{$c7:18}{$c8}";
237 MT=SL:GOSUB460
238 IFIT%(IN%(M))=9THEN61
239 IFIN%(M)<>0ANDIT%(IN%(M))<>SLTHEN61
240 IFSL=0THENQ=PW%(P):PW%(P)=IN%(M):IN%(M)=Q:GOTO61
241 IFSL=1THENQ=PR%(P):PR%(P)=IN%(M):IN%(M)=Q:GOTO61
242 GOSUB522
243 GOSUB705
244 EI=0
245 IFER%(EI)<>CROREX%(EI)<>OXOREY%(EI)<>OYTHEN250
246 NI%=ET%(EI):GOSUB555
247 IFIS=99THENPRINT"{home}{white}{down}{right}DU KANNST NICHT MEHR TRAGEN!":GOSUB700:GOSUB522:GOTO66
248 ER%(EI)=-1
249 PRINT"{home}{white}{down}{right}GEFUNDEN: ";I$(IN%(IS));:GOSUB700:GOSUB522:GOTO66
250 EI=EI+1
251 IFEI=12THEN253
252 GOTO245
253 PRINT"{home}{down}{right}{white}NICHTS BESONDERES!":GOSUB700:GOSUB522:GOTO66
254 FORI=4TO11:FT%(I)=-1:NEXT
255 PP=4:CC=4:WX=0
256 FORI=0TO3:FT%(I)=-2:FH%(I)=PH%(I):FM%(I)=PM%(I):FS%(I)=PS%(I)
257 IFP3%(I)=0THENFH%(I)=-1
258 IFFH%(I)<=0THENFT%(I)=-1:PP=PP-1
259 FA%(I)=PA%(I)+IA%(PW%(I)):FD(I)=PD%(I)+ID%(PR%(I)):F1%(I)=P1%(I):F2%(I)=P2%(I)
260 NEXT
261 PRINT"{brown}{home}{$c1}{$c2:38}{$c3}";
262 PRINT"{$c4}                                      {$c5}";
263 PRINT"{$c6}{$c7:38}{$c8}";
264 PRINT"{home}{down:3}{white}";:FORI=0TO15:PRINT"                                        ";:NEXT
265 PRINTDD$;"{brown}{up}{$c1}{$c2:7}{white}HP{brown}{$c2}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
266 GOSUB418
267 GOSUB449
268 GOSUB457
269 FORI=0TO11
270 IFFT%(I)<>-1THENFC%(I)=INT(RND(0)*FD%(I))
271 NEXT
272 CC=CC-4
273 PRINT"{home}{down}{right}                       ";:GOSUB566
274 FORI=4TO11:IFFT%(I)=-1THEN276
275 PRINT"{home}"LEFT$(CD$,FY%(I));SPC(FX%(I));MD$(FT%(I))
276 NEXT
277 C=0
278 IFFT%(C)<>-1ANDFC%(C)>=100THENFC%(C)=FC%(C)-100:GOTO281
279 C=C+1:IFC<12THEN278
280 FORI=0TO11:FC%(I)=FC%(I)+FS%(I):NEXT:GOTO277
281 IFFT%(C)=-2THEN283
282 GOTO367
283 PRINT"{home}{white}{down}{right}"P$(C)": IST AM ZUG                     "
284 PRINTDD$;SPC(16)"{white} ANGRIFF {down}{left:9} INVENTAR";:M=2
285 PRINTDD$;SPC(16)"{down:2}         {down}{left:9}         ";
286 IFPM%(C)>0THENPRINTDD$;SPC(16)"{down:2} MAGIE   ";:M=3
287 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(37)"  {down}{left:2}  ";
288 PRINT"{left:3}{up}"PD$(C);
289 MY=0:FZ=0
290 POKEQS+816+MY*40,35
291 GOSUB662
292 IFA$="S"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY+1:GOTO299
293 IFA$="W"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY-1:GOTO299
294 IFA$=CHR$(13)THENFZ=0:GOTO302
295 IFA$="E"ANDCR=14ORCR=15ORCR=16THENPRINT"{home}{down}{right}{white}FLUCHT NICHT MOEGLICH                 ":GOSUB705:GOSUB522:GOSUB707:GOTO291
296 IFA$="E"ANDFZ=3THENFZ=0:POKEQS+816+MY*40,32:GOTO56
297 IFA$="E"ANDFZ<=3THENFZ=FZ+1:PRINT"{home}{down}{right}{white}FLUCHT                                ":GOSUB705:GOSUB522:GOSUB707:GOTO290
298 GOTO291
299 IFMY>=MTHENMY=0
300 IFMY<0THENMY=M-1
301 GOTO290
302 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(36)"{white}  {down}{left:2}  {left}{up}"PD$(C);
303 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
304 ONMY+1GOTO330,322
305 MT=3:MX=16:MY=20:MC=4:GOSUB460
306 IFIT%(IN%(M))=9THEN283
307 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         {down}{left:9}         ";
308 IFIN%(M)=0ORIT%(IN%(M))<>3THEN283
309 M=IN%(M)
310 IFIE%(M)>FM%(C)THENPRINT"{home}{down}{right}NICHT GENUG MANA                     ";:GOSUB700:GOTO283
311 GOSUB582
312 GOTO608
313 AP=IE%(M)+2-INT(RND(0)*5):FM%(C)=FM%(C)-IE%(M):PM%(C)=FM%(C):GOSUB449
314 AP=AP+PL%(C)*3
315 FH%(CE)=FH%(CE)-AP:IFFH%(CE)<0THENFH%(CE)=0
316 IFCE<4THENPH%(CE)=FH%(CE)
317 IFFH%(CE)>0THENGOTO352
318 IFFT%(CE)<>-2THENCC=CC-1:WX=WX+MX%(FT%(CE))
319 IFFT%(CE)=-2THENPP=PP-1
320 FT%(CE)=-1
321 GOTO352
322 MT=2:MX=16:MY=20:MC=4:GOSUB460
323 IFIT%(IN%(M))=9THEN283
324 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
325 PRINT"{down}{left:9}         ";
326 IFIN%(M)=0ORIT%(IN%(M))<>MTTHEN283
327 IFIN%(M)=3THENGOSUB198:IN%(M)=0:GOSUB449:GOTO273
328 IFIN%(M)=8THENGOSUB200:IN%(M)=0:GOSUB449:GOTO273
329 GOTO273
330 GOSUB582
331 AP=FA%(C)-FD(CE)
332 IFAP<=0THENPRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{white}MISS";:GOSUB698:GOTO344
333 AP=INT(RND(0)*AP)+1
334 FH%(CE)=FH%(CE)-AP:IFFH%(CE)<0THENFH%(CE)=0
335 IFCE<4THENPH%(CE)=FH%(CE)
336 IFFH%(CE)>0THENGOTO341
337 IFFT%(CE)<>-2THENCC=CC-1:WX=WX+MX%(FT%(CE))
338 IFFT%(CE)=-2THENPP=PP-1
339 FT%(CE)=-1
340 GOTO341
341 POKEV,(FX%(CE)*8+21)AND255:POKEV+1,FY%(CE)*8+50:POKE50168,16:POKEV+28,0
342 POKEV+16,-(FX%(CE)>30):POKEV+21,1
343 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{white}";AP
344 FORI=0TO5:POKEV,PEEK(V)+2:POKEV+1,PEEK(V+1)+2:GOSUB699:NEXTI
345 POKEV+21,0:IFCE<4THENGOSUB449
346 GOSUB698
347 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
348 IFFT%(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE));"  {down}{left:2}  ";
349 IFCC=0THEN375
350 IFPP=0THEN403
351 GOTO273
352 POKEV,(FX%(CE)*8+24)AND255:POKEV+1,FY%(CE)*8+50
353 IFM=4THENPOKEV+37,8:POKEV+38,2:POKEV+39,7:POKEV+28,1
354 B=17:IFM=6THENB=19:POKEV+28,0
355 IFM=5THENB=21:POKEV+28,0
356 IFM=7THENB=23:POKEV+28,0
357 POKEV+16,-(FX%(CE)>30):POKE50168,B:POKEV+21,1
358 PRINT"{white}";:IFM=9THENPRINT"{green}";
359 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}";AP
360 FORK=0TO3:FORI=0TO1:POKE50168,B+I:FORJ=1TO40:NEXTJ,I,K
361 POKEV+21,0:IFCE<4THENGOSUB449
362 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
363 IFFT%(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE));"  {down}{left:2}  ";
364 IFCC=0THEN375
365 IFPP=0THEN403
366 GOTO273
367 GOSUB698
368 PRINT"{white}{home}{down}{right}"M$(FT%(C))": GREIFT AN                    "
369 FC%(C)=FC%(C)-100
370 IFFT%(C)=7THENIFFM%(C)>=IE%(9)THENM=9:GOSUB698:GOSUB412:GOTO611
371 IFFT%(C)=9THENIFFM%(C)>=IE%(6)THENM=6:GOSUB698:GOSUB406:GOTO313
372 IFFT%(C)=10THENIFFM%(C)>=IE%(4)THENM=4:GOSUB698:GOSUB406:GOTO313
373 IFFT%(C)=11THENIFFM%(C)>=IE%(12)THENM=12:GOSUB698:GOSUB406:GOTO618
374 GOSUB698:GOSUB406:GOTO331
375 POKE1020,Q1%
376 PRINTDD$;"{brown}{up}{$c1}{$c2:10}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}{white}";
377 PRINT"{home}{down}{right}DU HAST GEWONNEN                     ";:GOSUB566
378 GOSUB518
379 FORI=0TO3:IFP3%(I)=0THEN383
380 PRINT"{home}{white}{right:3}";:IFPH%(I)=0THENPRINT"{red}";
381 PRINTLEFT$(CD$,4+I*3)P$(I)
382 PRINT"{home}"LEFT$(CD$,5+I*3)"{right:3}LV";PL%(I)+1;" EX";PX%(I)
383 NEXT
384 FORI=0TO3:IFPH%(I)=0ORP3%(I)=0THEN394
385 TX=WX
386 PRINT"{home}{white}"LEFT$(CD$,5+I*3)SPC(20)"EXPERIENCE {left}"TX"{left} ";
387 IFTX=0THEN394
388 XS=1:IFTX>9THENXS=10
389 IFTX>99THENXS=100
390 TX=TX-XS
391 PX%(I)=PX%(I)+XS
392 LL=10+PL%(I)*PL%(I)*10:IFPX%(I)>=LLTHENGOSUB396:GOTO392
393 PRINT"{home}{white}"LEFT$(CD$,5+I*3)"{right:3}LV";PL%(I)+1;" EX";PX%(I):GOTO386
394 NEXTI
395 GOTO401
396 PL%(I)=PL%(I)+1:PA%(I)=PA%(I)+1:PD%(I)=PD%(I)+1:PS%(I)=PS%(I)+1
397 P1%(I)=P1%(I)+PL%(I)*2:IFP2%(I)>0THENP2%(I)=P2%(I)+PL%(I)*3
398 IFP1%(I)>999THENP1%(I)=999
399 IFP2%(I)>999THENP2%(I)=999
400 RETURN
401 GOSUB691
402 GOTO61
403 PRINT"{home}{down}{right}{white}DU HAST VERLOREN                     "
404 GOSUB691
405 GOTO626
406 RE=INT(RND(0)*4):CE=0
407 IFFT%(CE)=-2ANDPH%(CE)>0THEN410
408 CE=CE+1:IFCE=4THENCE=0
409 GOTO407
410 IFRE>0THENRE=RE-1:GOTO408
411 RETURN
412 RE=INT(RND(0)*8):CE=4
413 IFFT%(CE)>=0THEN416
414 CE=CE+1:IFCE=12THENCE=4
415 GOTO413
416 IFRE>0THENRE=RE-1:GOTO414
417 RETURN
418 IFFF>0THENL=FF:GOTO420
419 L=PEEK(CP+164):IFL=0THENRETURN
420 IFL=3THENA=0:LC=INT(RND(0))*2+2:FORI=0TOLC:GOSUB560:NEXT:RETURN
421 IFL=1THENA=1:FORI=0TO2:GOSUB560:NEXT:RETURN
422 IFL=2THENA=1:FORI=0TO1:GOSUB560:NEXT:A=2:FORI=0TO1:GOSUB560:NEXT:RETURN
423 IFL=4THENA=3:GOSUB560:A=1:GOSUB560:GOSUB560:RETURN
424 IFL=5THENA=4:LC=INT(RND(0)*3)+1:FORI=0TOLC:GOSUB560:NEXT:RETURN
425 IFL=6THENONINT(RND(1)*2)+1GOTO435,436
426 IFL=7THENLC=INT(RND(0)*3)+1:A=6:FORI=0TOLC:GOSUB560:NEXT:RETURN
427 IFL=8THENA=7:GOSUB560:LC=INT(RND(0)*2)+1:A=6:GOSUB560:RETURN
428 IFL=9THENA=9:GOSUB560:A=8:GOSUB560:A=5:GOSUB560:RETURN
429 IFL=10THENA=6:LC=INT(RND(0)*2)+1:FORI=1TOLC:GOSUB560:NEXT:RETURN
430 IFL=11THENA=7:GOSUB560:A=10:GOSUB560:RETURN
431 IFL=12ANDINT(RND(1)*2)=1THENA=11:GOSUB560:RETURN
432 IFL=12THENA=5:GOSUB560:GOSUB560:A=9:GOSUB560:RETURN
433 IFL=13THENA=9:GOSUB560:A=12:GOSUB560:A=9:GOSUB560:RETURN
434 IFL=14THENA=9:GOSUB560:A=13:GOSUB560:A=7:GOSUB560:RETURN
435 A=5:LC=INT(RND(1)*2)+1:FORI=0TOLC:GOSUB560:NEXT:RETURN
436 A=9:LC=INT(RND(1)*2)+1:FORI=1TOLC:GOSUB560:NEXT:RETURN
437 FORI=0TO3
438 IFP3%(I)=0THEN441
439 PH%(I)=P1%(I)
440 PM%(I)=P2%(I)
441 NEXT:RETURN
442 PRINT"{brown}{home}{$c1}{$c2:38}{$c3}";
443 PRINT"{$c4}                                      {$c5}";
444 PRINT"{$c6}{$c7:38}{$c8}";
445 PRINTDD$;"{brown}{up}{$c1}{$c2:10}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
446 FORI=0TO3:PRINT"{$c4}          {$c5}{$c4}            {$c5}{$c4}            {$c5}";:NEXT
447 PRINT"{$c6}{$c7:10}{$c8}{$c6}{$c7:12}{$c8}{$c6}{$c7:12}";
448 POKE50151,72:POKE56295,9:RETURN
449 PRINTDD$;
450 FORI=0TO3:IFP3%(I)=0THENPRINTSPC(40);:GOTO456
451 PRINT"{white}";:IFPH%(I)=0THENPRINT"{red}";
452 PRINTSPC(27)P$(I)" ";:A=PH%(I):GOSUB660
453 PRINTD$;
454 A=PM%(I):GOSUB660
455 PRINT" "D$"{right}";
456 NEXT:RETURN
457 FORI=0TO3:IFPH%(I)<=0ORP3%(I)=0THENNEXT:RETURN
458 PRINT"{home}"LEFT$(CD$,5+I*3);SPC(37);PD$(I):NEXT
459 RETURN
460 GOSUB495
461 MI=-1:M=0:MO=0
462 MI=MO-1:CY=MY:MM=0
463 IFMC=4THENGOSUB490
464 MI=MI+1
465 PRINT"{home}{white}"LEFT$(CD$,CY)SPC(MX)" ";
466 IFIT%(IN%(MI))<>MTTHENPRINT"{cyan}";
467 IFIT%(IN%(MI))=9THENPRINT"{white}";
468 IFIT%(IN%(MI))=-1THENPRINT"{cyan}";
469 PRINTI$(IN%(MI));
470 CY=CY+1
471 MM=MM+1
472 IFMM=MCTHEN474
473 IFMI<13THEN464
474 CP=QS+MX+MY*40
475 POKECP+(M-MO)*40,35
476 GOSUB662
477 POKECP+(M-MO)*40,32
478 IFA$="W"THENM=M-1:GOTO482
479 IFA$="S"THENM=M+1:GOTO482
480 IFA$=CHR$(13)THENRETURN
481 GOTO475
482 IFM=-1THENM=14-1:MO=14-MC:GOTO462
483 IFM=14THENM=0:MO=0:GOTO462
484 M%=0
485 IFM<MOTHENMO=MO-MC:M%=1:GOTO485
486 IFM>=MO+MCTHENMO=MO+MC:M%=1
487 IFMO<0THENMO=0:M%=1
488 IFM%=1THEN462
489 GOTO475
490 FORI=0TO3
491 PRINT"{home}{white}"LEFT$(CD$,CY+I)SPC(MX)" ";
492 PRINT"        ";
493 NEXTI
494 RETURN
495 A=0
496 FORI=0TO99:TT%(I)=0:NEXTI
497 FORI=0TO99:TT%(I)=IN%(I):IN%(I)=0:NEXTI
498 IFMT=0THENGOSUB503:GOSUB506:A=50:GOSUB509:GOSUB512:GOSUB515
499 IFMT=1THENGOSUB503:GOSUB509:A=50:GOSUB506:GOSUB512:GOSUB515
500 IFMT=2THENGOSUB503:GOSUB512:A=50:GOSUB506:GOSUB509:GOSUB515
501 IFMT=3THENGOSUB503:GOSUB515:A=50:GOSUB506:GOSUB509:GOSUB512
502 RETURN
503 FORI=0TO99:IFIT%(TT%(I))=9THENIN%(A)=TT%(I):A=A+1
504 NEXTI
505 RETURN
506 FORI=0TO99:IFIT%(TT%(I))=0THENIN%(A)=TT%(I):A=A+1
507 NEXTI
508 RETURN
509 FORI=0TO99:IFIT%(TT%(I))=1THENIN%(A)=TT%(I):A=A+1
510 NEXTI
511 RETURN
512 FORI=0TO99:IFIT%(TT%(I))=2THENIN%(A)=TT%(I):A=A+1
513 NEXTI
514 RETURN
515 FORI=0TO99:IFIT%(TT%(I))=3THENIN%(A)=TT%(I):A=A+1
516 NEXTI
517 RETURN
518 PRINT"{brown}{home}{down:3}{$c1}{$c2:38}{$c3}";
519 FORI=0TO13
520 PRINT"{$c4}                                      {$c5}";:NEXT
521 PRINT"{$c6}{$c7:38}{$c8}";:RETURN
522 PRINT"{home}{down}{right}{white}                                      ";
523 RETURN
524 PRINT"{brown}{home}{down:3}{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
525 FORI=0TO5
526 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
527 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
528 PRINT"{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
529 FORI=0TO5
530 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
531 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
532 DX=3:DY=4:P=0:GOSUB535:DX=23:P=1:GOSUB535
533 DX=3:DY=12:P=2:GOSUB535:DX=23:P=3:GOSUB535
534 RETURN
535 IFP3%(P)=0THENRETURN
536 PRINT"{home}{white}";:IFPH%(P)=0THENPRINT"{red}";
537 PRINT"{home}"LEFT$(CD$,DY)SPC(DX-1)" "P$(P)"  LV"PL%(P)+1
538 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX)" "I$(PW%(P));
539 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX)" "I$(PR%(P));
540 T1%=PA%(P)+IA%(PW%(P))
541 T2%=PD%(P)+ID%(PR%(P))
542 IFT1%>999THENT1%=999
543 IFT2%>999THENT2%=999
544 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX+9)"ATK"T1%;
545 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX+9)"DEF"T2%;
546 RETURN
547 IFPEEK(2)=1THENSP=30960+RS*(CR-0)
548 FORI=0TO7
549 PRINT"{home}{down:3}"LEFT$(CD$,I*2);
550 FORJ=0TO19
551 PRINTT$(PEEK(SP))"{up}";
552 SP=SP+1
553 NEXTJ,I
554 RETURN
555 IS=0
556 IFIN%(IS)=0THENIN%(IS)=NI%:NI%=-1:RETURN
557 IS=IS+1
558 IFIS=99THENRETURN
559 GOTO556
560 IFCC>=11THENRETURN
561 FH%(CC)=MH%(A):FM%(CC)=MM%(A):FT%(CC)=A:FC%(CC)=INT(RND(0)*MS%(A)):FS%(CC)=MS%(A)
562 FX%(CC)=2:FY%(CC)=5+CC*3:FA%(CC)=MA%(A):FD(CC)=MD%(A):FB(CC)=MB%(A)
563 F1%(CC)=MH%(A):F2%(CC)=MM%(A)
564 IFCC>=4THENFX%(CC)=5:FY%(CC)=5+(CC-4)*3
565 CC=CC+1:RETURN
566 ZM=0
567 FORJ=4TO7
568 IFFT%(J)=-1THENZM=ZM-1
569 ZM=ZM+1
570 NEXTJ
571 IFZM<>CMTHENPRINTDD$;"{right}          {down}{left:10}          {down}{left:10}          {down}{left:10}          ";
572 PRINTDD$;
573 CM=0
574 FORI=4TO7
575 IFFT%(I)=-1THENNEXT:RETURN
576 A=FH%(I):GOSUB660
577 PRINT"{home}{right}"LEFT$(CD$,20+CM)M$(FT%(I));
578 PRINT"{home}{right}"LEFT$(CD$,20+CM);"{right:7}";D$
579 CM=CM+1
580 NEXT
581 RETURN
582 CE=4:PRINT"{home}{down}{right}{white}"P$(C)": WAHELE DEIN ZIEL               "
583 IFFT%(CE)=-1THEN592
584 POKEQS-1+FX%(CE)+FY%(CE)*40,35
585 GOSUB662
586 IFA$=CHR$(13)THENPOKEQS-1+FX%(CE)+FY%(CE)*40,32:RETURN
587 IFA$="S"THENGOTO592
588 IFA$="W"THENGOTO600
589 IFA$="D"ANDCE>3THENPOKEQS-1+FX%(CE)+FY%(CE)*40,32:CE=0:GOTO583
590 IFA$="A"ANDCE<4THENPOKEQS-1+FX%(CE)+FY%(CE)*40,32:CE=4:GOTO583
591 GOTO585
592 CO=CE
593 POKEQS-1+FX%(CE)+FY%(CE)*40,32
594 CE=CE+1
595 IFCE>=12THENCE=0
596 IFCO<4ANDCE>3THENCE=CO
597 IFCO>3ANDCE<4THENCE=CO
598 IFFT%(CE)<>-1THEN584
599 GOTO594
600 CO=CE
601 POKEQS-1+FX%(CE)+FY%(CE)*40,32
602 CE=CE-1
603 IFCE<0THENCE=11
604 IFCO<4ANDCE>3THENCE=CO
605 IFCO>3ANDCE<4THENCE=CO
606 IFFT%(CE)<>-1THEN584
607 GOTO602
608 IFFT%(C)=-2THEN624
609 IFFT%(C)=7ANDFM%(C)>=IE%(9)THENM=9:GOTO611
610 GOTO313
611 AP=IE%(M)*4+2-INT(RND(0)*5):FM%(C)=FM%(C)-IE%(M):IFC<4THENPM%(C)=FM%(C):GOSUB449
612 FH%(CE)=FH%(CE)+AP:IFFH%(CE)>F1%(CE)THENFH%(CE)=F1%(CE)
613 IFCE<4THENPH%(CE)=FH%(CE):GOSUB449
614 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{green}";AP
615 GOSUB705
616 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
617 GOTO273
618 FM%(C)=FM%(C)-IE%(M):IFC<4THENPM%(C)=FM%(C):GOSUB449
619 FS%(CE)=FS%(CE)/2:IFFS%(CE)<5THENFS%(CE)=5
620 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{green}SLOW";
621 GOSUB705
622 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
623 GOTO273
624 IFM=9THEN611
625 GOTO313
626 FORI=0TO3:PX%(I)=0:NEXT
627 PA%(0)=5:PD%(0)=2:PA%(1)=4:PD%(1)=3:PA%(2)=1:PD%(2)=1:PA%(3)=1:PD%(3)=1
628 FORI=0TO13:IN%(I)=0:NEXT
629 IN%(0)=18
630 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
631 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
632 PRINT"{$c6}{$c7:38}{up:3}";
633 POKE50151,72:POKE56295,9
634 X=15:Y=2:CR=0
635 P3%(0)=1:P3%(1)=0:P3%(2)=0:P3%(3)=0
636 P1%(0)=15:P1%(1)=17:P1%(2)=9:P1%(3)=9:P2%(0)=0:P2%(1)=0:P2%(2)=7:P2%(3)=23
637 FORI=0TO3:PH%(I)=P1%(I):PM%(I)=P2%(I):PX%(I)=0:PW%(I)=0:PR%(I)=0:NEXT
638 PW%(0)=1:PR%(0)=2
639 GOSUB129
640 AA$="INTRO":GOSUB648
641 GOTO56
642 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
643 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
644 PRINT"{$c6}{$c7:38}{up:3}";
645 POKE50151,72:POKE56295,9
646 AA$="ENDE":GOSUB648
647 GOTO56
648 POKE1020,Q7%
649 POKE56322,224
650 IFAA$="INTRO"THENOPEN1,8,4,"TXT.INTRO,S,R"
651 IFAA$="ENDE"THENOPEN1,8,4,"TXT.ENDE,S,R"
652 IFAA$=""THENRETURN
653 GET#1,B$
654 IFB$="$"THENPRINT"{home}{down:2}{right:2}{white}";:GOTO653
655 IFB$="%"THENPRINT"{right:2}{white}";:GOTO653
656 IFB$="#"THENCLOSE1:POKE56322,255:GOSUB691:RETURN
657 PRINTB$;
658 IFB$<>" "THENPOKE162,0:WAIT162,4
659 GOTO653
660 T$=STR$(A)
661 D$=RIGHT$("000"+RIGHT$(T$,LEN(T$)-1),3):RETURN
662 A$=""
663 J=PEEK(56320)
664 IFJ=103THEN667
665 IFJ=127THEN667
666 GOTO663
667 J=PEEK(56320)
668 IFJ=127ANDA$=CHR$(13)THENRETURN
669 IFJ=107THENA$="E":GOSUB696:RETURN
670 IFJ=103THENA$="E":GOSUB696:RETURN
671 IFJ=110THENA$="E":GOSUB696:RETURN
672 IFJ=109THENA$="E":GOSUB696:RETURN
673 IFJ=123THENA$="A":GOSUB696:RETURN
674 IFJ=119THENA$="D":GOSUB696:RETURN
675 IFJ=126THENA$="W":GOSUB696:RETURN
676 IFJ=125THENA$="S":GOSUB696:RETURN
677 IFJ=111THENA$=CHR$(13)
678 GOTO667
679 A$=""
680 J=PEEK(56320)
681 IFJ<>111ANDJ<>107ANDJ<>103ANDJ<>110ANDJ<>109THEN683
682 GOTO680
683 J=PEEK(56320)
684 IFJ=127ANDA$=CHR$(13)THENRETURN
685 IFJ=123THENA$="A":GOSUB697:RETURN
686 IFJ=119THENA$="D":GOSUB697:RETURN
687 IFJ=126THENA$="W":GOSUB697:RETURN
688 IFJ=125THENA$="S":GOSUB697:RETURN
689 IFJ=111THENA$=CHR$(13)
690 GOTO683
691 A$=""
692 J=PEEK(56320)
693 IFJ=127ANDA$=CHR$(13)THENRETURN
694 IFJ=111THENA$=CHR$(13)
695 GOTO692
696 POKE162,0:WAIT162,16:RETURN
697 POKE162,0:WAIT162,8:RETURN
698 POKE162,0:WAIT162,32:RETURN
699 POKE162,0:WAIT162,2:RETURN
700 POKE162,0:WAIT162,32
701 POKE162,0:WAIT162,32
702 POKE162,0:WAIT162,32
703 POKE162,0:WAIT162,32
704 RETURN
705 POKE162,0:WAIT162,32
706 RETURN
707 POKE162,0:WAIT162,4
708 RETURN
709 DATA"IMP",5,0,8,4,1,0,3
710 DATA"SNAKE",8,0,12,6,1,0,5
711 DATA"VIPER",12,0,18,6,1,0,9
712 DATA"NACHO",30,0,22,15,5,0,50
713 DATA"GOLEM",20,0,4,20,8,0,17
714 DATA"MOSS",30,0,7,26,9,0,21
715 DATA"BONES",50,0,20,18,8,0,15
716 DATA"WISP",25,50,15,0,8,0,17
717 DATA"TROLL",100,0,9,40,13,0,100
718 DATA"SPARK",30,50,22,0,8,0,17
719 DATA"BURNER",80,90,30,25,13,0,42
720 DATA"TBERRY",120,10,60,60,20,0,150
721 DATA"DRACUL",250,60,50,70,25,0,500
722 DATA"GLADOS",500,70,60,80,30,0,1000
723 DATA"KRON",15 ,0  ,10 ,4 ,3 ,0 ,0
724 DATA"LENA",17 ,0  ,10 ,3 ,3 ,0 ,0
725 DATA"DOLM",9  ,7  ,8  ,1 ,1 ,0 ,0
726 DATA"MIRA",9  ,23 ,7  ,1 ,1 ,1 ,2
727 DATA"{gray1}{rvrs off}{$c9}{rvrs off}{gray1}{$ca}{down}{left:2}{rvrs off}{gray1}{$ca}{rvrs off}{gray1}{$c9}{rvrs off}"
728 DATA"{gray1}{rvrs off}{$cb}{rvrs off}{gray1}{$cc}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1}{$ca}{rvrs off}"
729 DATA"{gray1}{rvrs off}{$c9}{rvrs off}{gray1}{$ca}{down}{left:2}{rvrs off}{gray1}{$ca}{rvrs off}{gray1}{$c9}{rvrs off}"
730 DATA"{gray1}{rvrs off}{$cd}{rvrs off}{gray1}{$ce}{down}{left:2}{rvrs off}{gray1}{$cf}{rvrs off}{gray1}{$d0}{rvrs off}"
731 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
732 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
733 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
734 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
735 DATA"{orange}{rvrs off}{$d1}{rvrs off}{orange}{$d1}{down}{left:2}{rvrs off}{orange}{$d1}{rvrs off}{orange}{$d1}{rvrs off}"
736 DATA"{brown}{rvrs off}{$d2}{rvrs off}{brown}{$d3}{down}{left:2}{rvrs off}{brown}{$d4}{rvrs off}{brown}{$d5}{rvrs off}"
737 DATA"{lt. red}{rvrs off}{$d6}{rvrs off}{lt. red}{$d7}{down}{left:2}{rvrs off}{lt. red}{$d8}{rvrs off}{lt. red}{$d9}{rvrs off}"
738 DATA"{gray1}{rvrs off}{$da}{rvrs off}{gray1}{$db}{down}{left:2}{rvrs off}{gray1}{$dc}{rvrs off}{gray1}{$dd}{rvrs off}"
739 DATA"{gray1}{rvrs off}{$a2}{rvrs off}{gray1}{$a3}{down}{left:2}{rvrs off}{gray1}{$a4}{rvrs off}{gray1}{$a5}{rvrs off}"
740 DATA"{brown}{rvrs off}{$a1}{rvrs off}{brown}{$a1}{down}{left:2}{rvrs off}{brown}{$a1}{rvrs off}{brown}{$a1}{rvrs off}"
741 DATA"{gray1}{rvrs off}{$de}{rvrs off}{gray1}{$df}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1} {rvrs off}"
742 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
743 DATA"{lt. red}{rvrs off}{$a6}{rvrs off}{lt. red}{$a7}{down}{left:2}{rvrs off}{lt. red}{cmdr pound}{rvrs off}{lt. red}{shft pound}{rvrs off}"
744 DATA"{lt. red}{rvrs off}{$aa}{rvrs off}{lt. red}{$aa}{down}{left:2}{rvrs off}{lt. red}{$aa}{rvrs off}{lt. red}{$aa}{rvrs off}"
745 DATA"{brown}{rvrs off}{$ab}{rvrs off}{brown}{$ab}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
746 DATA"{brown}{rvrs off}{$ab}{rvrs off}{brown}{$ad}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
747 DATA"{brown}{rvrs off}{$ad}{rvrs off}{brown}{$ab}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
748 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
749 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
750 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
751 DATA"{lt. green}{rvrs off}{$ae}{rvrs off}{lt. green}{$d1}{down}{left:2}{rvrs off}{lt. green}{$d1}{rvrs off}{lt. green}{$af}{rvrs off}"
752 DATA"{lt. green}{rvrs off}{$b0}{rvrs off}{lt. green}{$b1}{down}{left:2}{rvrs off}{lt. green}{$b2}{rvrs off}{lt. green}{$b3}{rvrs off}"
753 DATA"{lt. green}{rvrs off}{$b4}{rvrs off}{lt. green}{$b5}{down}{left:2}{rvrs off}{lt. green}{$b4}{rvrs off}{lt. green}{$b5}{rvrs off}"
754 DATA"{lt. green}{rvrs off}{$b6}{rvrs off}{lt. green}{$b7}{down}{left:2}{rvrs off}{lt. green}{$b8}{rvrs off}{lt. green}{$b9}{rvrs off}"
755 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
756 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
757 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
758 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
759 DATA"{brown}{rvrs off}{$ae}{rvrs off}{brown}{$d1}{down}{left:2}{rvrs off}{brown}{$d1}{rvrs off}{brown}{$af}{rvrs off}"
760 DATA"{brown}{rvrs off}{$b0}{rvrs off}{brown}{$b1}{down}{left:2}{rvrs off}{brown}{$b2}{rvrs off}{brown}{$b3}{rvrs off}"
761 DATA"{brown}{rvrs off}{$b4}{rvrs off}{brown}{$b5}{down}{left:2}{rvrs off}{brown}{$b4}{rvrs off}{brown}{$b5}{rvrs off}"
762 DATA"{brown}{rvrs off}{$b6}{rvrs off}{brown}{$b7}{down}{left:2}{rvrs off}{brown}{$b8}{rvrs off}{brown}{$b9}{rvrs off}"
763 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
764 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
765 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
766 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
767 DATA"{gray1}{rvrs off}{$d1}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$d1}{rvrs off}{gray1}{$d1}{rvrs off}"
768 DATA"{gray1}{rvrs off}{$ba}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$ba}{rvrs off}{gray1}{$d1}{rvrs off}"
769 DATA"{gray1}{rvrs off}{$ba}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$bb}{rvrs off}{gray1}{$d1}{rvrs off}"
770 DATA"{gray1}{rvrs off}{$bc}{rvrs off}{gray1}{$bd}{down}{left:2}{rvrs off}{gray1}{$be}{rvrs off}{gray1}{$be}{rvrs off}"
771 DATA"{gray1}{rvrs off}{$bf}{rvrs off}{gray1}{$bf}{down}{left:2}{rvrs on}{gray1}@{rvrs on}{gray1}@{rvrs off}"
772 DATA"{gray2}{rvrs on}A{rvrs on}{gray2}B{down}{left:2}{rvrs on}{gray2}C{rvrs on}{gray2}D{rvrs off}"
773 DATA"{gray1}{rvrs on}E{rvrs on}{gray1}F{down}{left:2}{rvrs on}{gray1}G{rvrs on}{gray1}H{rvrs off}"
774 DATA"{brown}{rvrs on}I{rvrs on}{brown}I{down}{left:2}{rvrs on}{brown}I{rvrs on}{brown}I{rvrs off}"
775 DATA"{lt. red}{rvrs on}J{rvrs on}{lt. red}K{down}{left:2}{rvrs on}{lt. red}L{rvrs on}{lt. red}M{rvrs off}"
776 DATA"{gray1}{rvrs on}N{rvrs on}{gray1}K{down}{left:2}{rvrs on}{gray1}L{rvrs on}{gray1}M{rvrs off}"
777 DATA"{lt. green}{rvrs on}O{rvrs on}{lt. green}P{down}{left:2}{rvrs on}{lt. green}Q{rvrs on}{lt. green}R{rvrs off}"
778 DATA"{gray2}{rvrs on}S{rvrs on}{gray2}T{down}{left:2}{rvrs on}{gray2}U{rvrs on}{gray2}V{rvrs off}"
779 DATA"{gray1}{rvrs on}W{rvrs on}{gray1}X{down}{left:2}{rvrs on}{gray1}Y{rvrs on}{gray1}Z{rvrs off}"
780 DATA"{gray1}{rvrs on}[{rvrs on}{gray1}{pound}{down}{left:2}{rvrs on}{gray1}]{rvrs on}{gray1}{^}{rvrs off}"
781 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
782 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
783 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
784 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
785 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
786 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
787 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
788 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
789 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
790 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
791 DATA"{lt. green}{rvrs on}{back arrow}{rvrs on}{lt. green}!{down}{left:2}{rvrs on}{lt. green}#{rvrs on}{lt. green}${rvrs off}"
792 DATA"{gray3}{rvrs on}%{rvrs on}{gray3}&{down}{left:2}{rvrs on}{gray3}'{rvrs on}{gray3}({rvrs off}"
793 DATA"{gray1}{rvrs on}%{rvrs on}{gray1}&{down}{left:2}{rvrs on}{gray1}'{rvrs on}{gray1}({rvrs off}"
794 DATA"{lt. green}{rvrs on}){rvrs on}{lt. green}*{down}{left:2}{rvrs on}{lt. green}+{rvrs on}{lt. green},{rvrs off}"
795 DATA"{gray1}{rvrs on}-{rvrs on}{gray1}.{down}{left:2}{rvrs on}{gray1}/{rvrs on}{gray1}0{rvrs off}"
796 DATA"{gray1}{rvrs on}1{rvrs on}{gray1}2{down}{left:2}{rvrs on}{gray1}3{rvrs on}{gray1}4{rvrs off}"
797 DATA"{gray1}{rvrs on}5{rvrs on}{gray1}6{down}{left:2}{rvrs on}{gray1}7{rvrs on}{gray1}8{rvrs off}"
798 DATA"{gray1}{rvrs on}9{rvrs on}{gray1}:{down}{left:2}{rvrs on}{gray1};{rvrs on}{gray1}<{rvrs off}"
799 DATA"{gray1}{rvrs on}={rvrs on}{gray1}>{down}{left:2}{rvrs on}{gray1}?{rvrs on}{gray1}{$c0}{rvrs off}"
800 DATA"{lt. green}{rvrs on}9{rvrs on}{lt. green}:{down}{left:2}{rvrs on}{lt. green};{rvrs on}{lt. green}<{rvrs off}"
801 DATA"{lt. red}{rvrs on}5{rvrs on}{lt. red}6{down}{left:2}{rvrs on}{lt. red}7{rvrs on}{lt. red}8{rvrs off}"
802 DATA"{lt. green}{rvrs on}5{rvrs on}{lt. green}6{down}{left:2}{rvrs on}{lt. green}7{rvrs on}{lt. green}8{rvrs off}"
803 DATA"{gray1}{rvrs on}{$c1}{rvrs on}{gray1}{$c2}{down}{left:2}{rvrs on}{gray1}{$c3}{rvrs on}{gray1}{$c4}{rvrs off}"
804 DATA"{gray1}{rvrs on}{$c5}{rvrs on}{gray1}{$c6}{down}{left:2}{rvrs on}{gray1}{$c7}{rvrs on}{gray1}{$c8}{rvrs off}"
805 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
806 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
807 DATA"        ",00,00,00,00,-1 
808 DATA"DOLCH   ",03,00,00,00,00  
809 DATA"HELM    ",00,02,-1,00,01  
810 DATA"KRAEUTER",00,00,-1,10,02  
811 DATA"FEUER   ",00,00,00,05,03  
812 DATA"EIS     ",00,00,00,08,03  
813 DATA"MUT     ",00,00,00,23,03  
814 DATA"BOMBE   ",00,00,00,50,03  
815 DATA"KIRSCHE ",00,00,-1,15,02  
816 DATA"HEILEN  ",00,00,00,04,03  
817 DATA"SKULL   ",00,00,00,00,04  
818 DATA"FLEGEL  ",07,00,-1,00,00  
819 DATA"LAHM    ",00,00,00,04,03  
820 DATA"STAB    ",12,00,00,00,00  
821 DATA"FEDER   ",00,10,00,00,01  
822 DATA"STOCK   ",23,00,00,00,00  
823 DATA"WESTE   ",00,14,00,00,01  
824 DATA"SCHILD  ",00,24,00,00,01  
825 DATA"ZURUECK ",00,00,00,0 ,09  
826 END
