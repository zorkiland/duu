0 POKE55,240:POKE56,120:CLR
1 QO=53282
2 QP=53283
3 QQ=53281
4 QR=53280
5 QS=49152
6 V=53248
7 QT=30960
8 Q1=0
9 Q2=1
10 Q3=2
11 Q4=3
12 Q5=4
13 Q6=5
14 Q7=6
15 Q8=7
16 Q9=8
17 QA=9
18 QB=10
19 CB=11
20 QC=12
21 QD=13
22 QE=14
23 QF=15
24 QG=8
25 QH=9
26 QI=10
27 QJ=11
28 QK=12
29 QL=13
30 QM=14
31 QN=15
32 MQ=14
33 IC=19
34 HC=14
35 EC=12
36 IX=99
37 TM=80
38 RS=165
39 DD$="{home}{down:20}{white}"
40 CD$="{down:25}"
41 CT$="{home}{down}{white}{right}                                      "
42 DIMM$(MQ),MD$(MQ)
43 DIMMH(MQ),MM(MQ)
44 DIMMA(MQ),MD(MQ)
45 DIMMS(MQ)
46 DIMMB(MQ),MX(MQ)
47 DIMFH(11),FT(11),FC(11)
48 DIMFM(11),FS(11),FX(11),FY(11)
49 DIMFA(11),FD(11),F1(11),F2(11)
50 FORI=0TO3:FX(I)=37:FY(I)=5+3*I:NEXT
51 DIMI$(IC),IA(IC)
52 DIMID(IC),IS(IC)
53 DIMIE(IC),IT(IC)
54 DIMH(HC)
55 DIMER(EC),EX(EC)
56 DIMEY(EC),ET(EC)
57 DIMIN(IX)
58 DIMT$(TM)
59 DIMTN$(TM)
60 DIMTT(IX)
61 FORI=0TOMQ-1
62 READM$(I),MH(I),MM(I),MS(I)
63 READMA(I),MD(I),MB(I),MX(I)
64 NEXT
65 FORI=0TO3
66 READP$(I),PH(I),PM(I),PS(I)
67 READPA(I),PD(I),PW(I),PR(I)
68 NEXT
69 FORI=0TOTM-1
70 READT$(I)
71 NEXT
72 PD$(0)=T$(48)
73 PD$(1)=T$(49)
74 PD$(2)=T$(52)
75 PD$(3)=T$(51)
76 ZZ=64
77 FORI=0TOMQ-1
78 MD$(I)=T$(ZZ)
79 ZZ=ZZ+1
80 NEXTI
81 FORI=0TOIC-1
82 READI$(I),IA(I),ID(I),IS(I),IE(I),IT(I)
83 NEXT
84 POKEV+21,0
85 POKE648,192
86 POKE56576,0
87 POKE53272,8
88 POKE53270,PEEK(53270)OR16
89 POKEQO,QA
90 POKEQP,Q1
91 POKEQR,Q1
92 POKEQQ,Q7
93 GOTO636
94 POKE1020,Q7
95 POKE1021,Q7
96 SYS820
97 GOSUB431
98 GOSUB438
99 PRINT"{home}{down}{right}{white}                                      ";
100 POKE1020,Q7
101 GOSUB536
102 IFPEEK(2)=1THENCP=30960+RS*(CR-0)
103 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)PD$(0);
104 OX=ZX:OY=ZY
105 ZX=X:ZY=Y
106 GOSUB706
107 IFA$=CHR$(145)ORA$="W"THENPRINTCT$;:ZY=Y-1:GOTO113
108 IFA$=CHR$(157)ORA$="A"THENPRINTCT$;:ZX=X-1:GOTO113
109 IFA$=CHR$(29)ORA$="D"THENPRINTCT$;:ZX=X+1:GOTO113
110 IFA$=CHR$(17)ORA$="S"THENPRINTCT$;:ZY=Y+1:GOTO113
111 IFA$=CHR$(13)THEN170
112 GOTO106
113 AA=0
114 FORI=0TOEC
115 IFER(I)=CRANDEX(I)=ZXANDEY(I)=ZYTHENAA=1
116 NEXTI
117 IFZX=-1ORZX=20ORZY=-1ORZY=8THEN129
118 C=PEEK(CP+ZX+ZY*20)
119 IFC=08THEN143
120 IFC=09THEN143
121 IFC=10THEN143
122 IFC=11THEN143
123 IFC=12THEN156
124 IFC=14THEN150
125 IFC=25THEN156
126 IFC=47THEN169
127 IFC>=48THEN160
128 IFC>7THEN104
129 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)T$(PEEK(CP+X+Y*20))
130 IFRND(1)*100>97ANDPEEK(CP+164)>0THENFF=0:GOTO246
131 X=ZX:Y=ZY
132 IFX=-1ANDPEEK(CP+160)=255THENX=0:GOTO103
133 IFX=-1THENCR=PEEK(CP+160):X=19:GOTO100
134 IFX=20ANDPEEK(CP+161)=255THENX=19:GOTO103
135 IFX=20THENCR=PEEK(CP+161):X=0:GOTO100
136 IFY=-1ANDPEEK(CP+162)=255THENY=0:GOTO103
137 IFY=-1THENCR=PEEK(CP+162):Y=7:GOTO100
138 IFY=8ANDPEEK(CP+163)=255THENY=7:GOTO103
139 IFY=8THENCR=PEEK(CP+163):Y=0:GOTO100
140 IFC=2THEN150
141 IFC=3THEN150
142 GOTO103
143 IFCR=16THENCR=16:X=0:Y=0:GOTO99
144 IFCR=16THENCR=16:X=0:Y=0:GOTO99
145 IFCR=16THENCR=16:X=0:Y=0:GOTO99
146 IFCR=16THENCR=16:X=0:Y=0:GOTO99
147 IFCR=16THENCR=16:X=0:Y=0:GOTO99
148 IFCR=16THENCR=16:X=0:Y=0:GOTO99
149 IFCR=16THEN665
150 IFC=14ANDCR=0ANDZX=16THENH(0)=1-H(0):GOSUB549:GOSUB565:GOTO100
151 IFC=14ANDCR=16ANDZX=0THENH(1)=1-H(1):GOSUB549:GOSUB565:GOTO104
152 IFC=2ANDCR=16ANDX=0ANDH(6)=0THENH(6)=1:GOSUB549:GOTO99
153 IFC=2ANDCR=16ANDX=0ANDH(7)=0THENH(7)=1:GOSUB549:GOTO99
154 IFC=3ANDCR=16ANDY=0ANDH(12)=0THENH(12)=1:GOSUB549:GOTO99
155 IFC=3ANDCR=16ANDY=0ANDH(13)=0THENH(13)=1:GOSUB549:GOTO99
156 IFC=12ANDAA=0THENGOTO104
157 IFC=12ANDAA=1THENPRINT"{home}{white}{down}{right}IN DER TRUHE IST WAS!":GOSUB727:GOSUB511:GOTO104
158 IFC=25ANDAA=0THENGOTO104
159 IFC=25ANDAA=1THENPRINT"{home}{white}{down}{right}DER BAUM SIEHT INTRESSANT AUS!":GOSUB727:GOSUB511:GOTO104
160 IFCR=16THENPRINT"{white}{home}{down}{right:2}EIN HEILZAUBER IST UNTER DEM BAUM":GOSUB727:GOSUB511:GOTO104
161 IFCR=16THENPRINT"{white}{home}{down}{right:2}EIN FEUERZAUBER IST IN DEN KATAKOMBEN":GOSUB727:GOSUB511:GOTO104
162 IFC=49THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB727:H(2)=1:GOSUB549:P3(1)=1:GOTO94
163 IFC=52THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB727:H(5)=1:GOSUB549:P3(2)=1:GOTO94
164 IFC=51THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB727:H(9)=1:GOSUB549:P3(3)=1:GOTO94
165 IFC=47THENPRINT"{white}{home}{down}{right:2}DU KANNST HIER NICHT DURCH!":GOSUB727:FF=4:H(4)=1:GOSUB549:GOTO246
166 IFC=72THENPRINT"{white}{home}{down}{right:2}ACH WIE SUESS!":GOSUB727:FF=9:H(3)=1:GOSUB549:GOTO246
167 IFC=76THENPRINT"{white}{home}{down}{right:2}DU WILLST MICH BESIEGEN!":GOSUB727:FF=13:H(8)=1:GOSUB549:GOTO246
168 IFC=77THENPRINT"{white}{home}{down}{right:2}DU HAST MEINE ERWARTUNG UEBERTROFFEN!":GOSUB727:FF=14:H(10)=1:GOSUB549:GOTO246
169 IFC=47THENPRINT"{home}{white}{down}{right}DU BIST GEHEILT!":GOSUB727:GOSUB426:GOSUB438:GOSUB511:GOTO104
170 PRINT"{home}{down}{white}{right}  UNTERSUCHE  EQUIPMENTS  INVENTAR"
171 M=0
172 POKEQS+42+M*12,35
173 GOSUB690
174 POKEQS+42+M*12,32
175 IFA$="A"THENM=M-1-3*(M=0):GOTO172
176 IFA$="D"THENM=M+1+3*(M=2):GOTO172
177 IFA$=CHR$(13)THENPOKEQS+42+M*7,32:GOTO179
178 GOTO172
179 PRINT"{home}{down}{white}{right}                                      ";
180 ONM+1GOTO234,194,181
181 POKE1020,Q1
182 GOSUB507
183 PRINT"{home}{down}{right}{white}INVENTAR: WAEHLE DEIN ITEM           ";
184 MT=2:MX=2:MY=4:MC=14:GOSUB449
185 IFIN(M)=9THEN99
186 IFIN(M)=0ORIT(IN(M))<>MTTHEN99
187 IFIN(M)=3THENGOSUB190:IN(M)=0:GOSUB438:GOTO99
188 IFIN(M)=8THENGOSUB192:IN(M)=0:GOSUB438:GOTO99
189 GOTO99
190 FORI=0TO3:PH(I)=PH(I)+IE(IN(M)):IFPH(I)>P1(I)THENPH(I)=P1(I)
191 FH(I)=PH(I):NEXT:RETURN
192 FORI=0TO3:PM(I)=PM(I)+IE(IN(M)):IFPM(I)>P2(I)THENPM(I)=P2(I)
193 FM(I)=PM(I):NEXT:RETURN
194 POKE1020,Q1
195 GOSUB513
196 PRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINEN SPIELER     ";
197 P=0
198 CP=QS+2+(4-8*(P>1))*40:IFP=1ORP=3THENCP=CP+20
199 POKECP,35
200 GOSUB690
201 POKECP,32
202 IFP=0ANDA$="D"ANDP3(1)=1THENP=1:GOTO198
203 IFP=0ANDA$="S"ANDP3(2)=1THENP=2:GOTO198
204 IFP=0ANDA$="D"ANDP3(1)=OANDP3(2)=0ANDP3(3)=1THENP=3:GOTO198
205 IFP=0ANDA$="S"ANDP3(1)=OANDP3(2)=0ANDP3(3)=1THENP=3:GOTO198
206 IFP=1ANDA$="A"ANDP3(0)=1THENP=0:GOTO198
207 IFP=1ANDA$="S"ANDP3(3)=1THENP=3:GOTO198
208 IFP=2ANDA$="D"ANDP3(3)=1THENP=3:GOTO198
209 IFP=2ANDA$="W"ANDP3(0)=1THENP=0:GOTO198
210 IFP=3ANDA$="A"ANDP3(2)=1THENP=2:GOTO198
211 IFP=3ANDA$="W"ANDP3(1)=1THENP=1:GOTO198
212 IFP=3ANDA$="A"ANDP3(2)=OANDP3(1)=0ANDP3(0)=1THENP=0:GOTO198
213 IFP=3ANDA$="W"ANDP3(2)=OANDP3(1)=0ANDP3(0)=1THENP=0:GOTO198
214 IFA$=CHR$(13)THEN216
215 GOTO199
216 SL=0:CP=CP+81
217 PRINT"{home}{down}{right}{white}EQUIPMENT: WAFFE ODER RUESTUNG       ";
218 POKECP+SL*40,35
219 GOSUB690
220 POKECP+SL*40,32:IFA$="W"ORA$="S"THENSL=1-SL:GOTO218
221 IFA$=CHR$(13)THEN223
222 GOTO218
223 MY=4:MX=23:MC=14:IFP=1ORP=3THENMX=3
224 IFSL=0THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE WAFFFE       ";
225 IFSL=1THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE RUESTUNG     ";
226 PRINT"{home}"LEFT$(CD$,MY-1)SPC(MX-3);"{brown}{$c1}{$c2:18}{$c3}";
227 FORI=0TO13:PRINT"{down}{left:20}{$c4}                  {$c5}";:NEXT
228 PRINT"{down}{left:20}{$c6}{$c7:18}{$c8}";
229 MT=SL:GOSUB449
230 IFIT(IN(M))=9THEN99
231 IFIN(M)<>0ANDIT(IN(M))<>SLTHEN99
232 IFSL=0THENQ=PW(P):PW(P)=IN(M):IN(M)=Q:GOTO99
233 IFSL=1THENQ=PR(P):PR(P)=IN(M):IN(M)=Q:GOTO99
234 GOSUB511
235 GOSUB732
236 EI=0
237 IFER(EI)<>CROREX(EI)<>OXOREY(EI)<>OYTHEN242
238 NI=ET(EI):GOSUB544
239 IFIS=IXTHENPRINT"{home}{white}{down}{right}DU KANNST NICHT MEHR TRAGEN!":GOSUB727:GOSUB511:GOTO104
240 ER(EI)=-1
241 PRINT"{home}{white}{down}{right}GEFUNDEN: ";I$(IN(IS));:GOSUB727:GOSUB511:GOTO104
242 EI=EI+1
243 IFEI=ECTHEN245
244 GOTO237
245 PRINT"{home}{down}{right}{white}NICHTS BESONDERES!":GOSUB727:GOSUB511:GOTO104
246 FORI=4TO11:FT(I)=-1:NEXT
247 PP=4:CC=4:WX=0
248 FORI=0TO3:FT(I)=-2:FH(I)=PH(I):FM(I)=PM(I):FS(I)=PS(I)
249 IFP3(I)=0THENFH(I)=-1
250 IFFH(I)<=0THENFT(I)=-1:PP=PP-1
251 FA(I)=PA(I)+IA(PW(I)):FD(I)=PD(I)+ID(PR(I)):F1(I)=P1(I):F2(I)=P2(I)
252 NEXT
253 PRINT"{brown}{home}{$c1}{$c2:38}{$c3}";
254 PRINT"{$c4}                                      {$c5}";
255 PRINT"{$c6}{$c7:38}{$c8}";
256 PRINT"{home}{down:3}{white}";:FORI=0TO15:PRINT"                                        ";:NEXT
257 PRINTDD$;"{brown}{up}{$c1}{$c2:7}{white}HP{brown}{$c2}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
258 GOSUB407
259 GOSUB438
260 GOSUB446
261 FORI=0TO11
262 IFFT(I)<>-1THENFC(I)=INT(RND(0)*FD(I))
263 NEXT
264 CC=CC-4
265 PRINT"{home}{down}{right}                       ";:GOSUB576
266 FORI=4TO11:IFFT(I)=-1THEN268
267 PRINT"{home}"LEFT$(CD$,FY(I));SPC(FX(I));MD$(FT(I))
268 NEXT
269 C=0
270 IFFT(C)<>-1ANDFC(C)>=100THENFC(C)=FC(C)-100:GOTO273
271 C=C+1:IFC<12THEN270
272 FORI=0TO11:FC(I)=FC(I)+FS(I):NEXT:GOTO269
273 IFFT(C)=-2THEN275
274 GOTO356
275 PRINT"{home}{white}{down}{right}"P$(C)": IST AM ZUG                     "
276 PRINTDD$;SPC(16)"{white} ANGRIFF {down}{left:9} INVENTAR";:M=2
277 PRINTDD$;SPC(16)"{down:2}         {down}{left:9}         ";
278 IFPM(C)>0THENPRINTDD$;SPC(16)"{down:2} MAGIE   ";:M=3
279 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(37)"  {down}{left:2}  ";
280 PRINT"{left:3}{up}"PD$(C);
281 MY=0:FZ=0
282 POKEQS+816+MY*40,35
283 GOSUB690
284 IFA$="S"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY+1:GOTO288
285 IFA$="W"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY-1:GOTO288
286 IFA$=CHR$(13)THENFZ=0:GOTO291
287 GOTO283
288 IFMY>=MTHENMY=0
289 IFMY<0THENMY=M-1
290 GOTO282
291 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(36)"{white}  {down}{left:2}  {left}{up}"PD$(C);
292 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
293 ONMY+1GOTO319,311
294 MT=3:MX=16:MY=20:MC=4:GOSUB449
295 IFIT(IN(M))=9THEN275
296 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         {down}{left:9}         ";
297 IFIN(M)=0ORIT(IN(M))<>3THEN275
298 M=IN(M)
299 IFIE(M)>FM(C)THENPRINT"{home}{down}{right}NICHT GENUG MANA                     ";:GOSUB727:GOTO275
300 GOSUB592
301 GOTO618
302 AP=IE(M)+2-INT(RND(0)*5):FM(C)=FM(C)-IE(M):PM(C)=FM(C):GOSUB438
303 AP=AP+PL(C)*3
304 FH(CE)=FH(CE)-AP:IFFH(CE)<0THENFH(CE)=0
305 IFCE<4THENPH(CE)=FH(CE)
306 IFFH(CE)>0THENGOTO341
307 IFFT(CE)<>-2THENCC=CC-1:WX=WX+MX(FT(CE))
308 IFFT(CE)=-2THENPP=PP-1
309 FT(CE)=-1
310 GOTO341
311 MT=2:MX=16:MY=20:MC=4:GOSUB449
312 IFIT(IN(M))=9THEN275
313 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
314 PRINT"{down}{left:9}         ";
315 IFIN(M)=0ORIT(IN(M))<>MTTHEN275
316 IFIN(M)=3THENGOSUB190:IN(M)=0:GOSUB438:GOTO265
317 IFIN(M)=8THENGOSUB192:IN(M)=0:GOSUB438:GOTO265
318 GOTO265
319 GOSUB592
320 AP=FA(C)-FD(CE)
321 IFAP<=0THENPRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{white}MISS";:GOSUB725:GOTO333
322 AP=INT(RND(0)*AP)+1
323 FH(CE)=FH(CE)-AP:IFFH(CE)<0THENFH(CE)=0
324 IFCE<4THENPH(CE)=FH(CE)
325 IFFH(CE)>0THENGOTO330
326 IFFT(CE)<>-2THENCC=CC-1:WX=WX+MX(FT(CE))
327 IFFT(CE)=-2THENPP=PP-1
328 FT(CE)=-1
329 GOTO330
330 POKEV,(FX(CE)*8+21)AND255:POKEV+1,FY(CE)*8+50:POKE50168,16:POKEV+28,0
331 POKEV+16,-(FX(CE)>30):POKEV+21,1
332 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{white}";AP
333 FORI=0TO5:POKEV,PEEK(V)+2:POKEV+1,PEEK(V+1)+2:GOSUB726:NEXTI
334 POKEV+21,0:IFCE<4THENGOSUB438
335 GOSUB725
336 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
337 IFFT(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE));"  {down}{left:2}  ";
338 IFCC=0THEN364
339 IFPP=0THEN392
340 GOTO265
341 POKEV,(FX(CE)*8+24)AND255:POKEV+1,FY(CE)*8+50
342 IFM=4THENPOKEV+37,8:POKEV+38,2:POKEV+39,7:POKEV+28,1
343 B=17:IFM=6THENB=19:POKEV+28,0
344 IFM=5THENB=21:POKEV+28,0
345 IFM=7THENB=23:POKEV+28,0
346 POKEV+16,-(FX(CE)>30):POKE50168,B:POKEV+21,1
347 PRINT"{white}";:IFM=9THENPRINT"{green}";
348 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}";AP
349 FORK=0TO3:FORI=0TO1:POKE50168,B+I:FORJ=1TO40:NEXTJ,I,K
350 POKEV+21,0:IFCE<4THENGOSUB438
351 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
352 IFFT(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE));"  {down}{left:2}  ";
353 IFCC=0THEN364
354 IFPP=0THEN392
355 GOTO265
356 GOSUB725
357 PRINT"{white}{home}{down}{right}"M$(FT(C))": GREIFT AN                    "
358 FC(C)=FC(C)-100
359 IFFT(C)=7THENIFFM(C)>=IE(9)THENM=9:GOSUB725:GOSUB401:GOTO621
360 IFFT(C)=9THENIFFM(C)>=IE(6)THENM=6:GOSUB725:GOSUB395:GOTO302
361 IFFT(C)=10THENIFFM(C)>=IE(4)THENM=4:GOSUB725:GOSUB395:GOTO302
362 IFFT(C)=11THENIFFM(C)>=IE(12)THENM=12:GOSUB725:GOSUB395:GOTO628
363 GOSUB725:GOSUB395:GOTO320
364 POKE1020,Q1
365 PRINTDD$;"{brown}{up}{$c1}{$c2:10}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}{white}";
366 PRINT"{home}{down}{right}DU HAST GEWONNEN                     ";:GOSUB576
367 GOSUB507
368 FORI=0TO3:IFP3(I)=0THEN372
369 PRINT"{home}{white}{right:3}";:IFPH(I)=0THENPRINT"{red}";
370 PRINTLEFT$(CD$,4+I*3)P$(I)
371 PRINT"{home}"LEFT$(CD$,5+I*3)"{right:3}LV";PL(I)+1;" EX";PX(I)
372 NEXT
373 FORI=0TO3:IFPH(I)=0ORP3(I)=0THEN383
374 TX=WX
375 PRINT"{home}{white}"LEFT$(CD$,5+I*3)SPC(20)"EXPERIENCE {left}"TX"{left} ";
376 IFTX=0THEN383
377 XS=1:IFTX>9THENXS=10
378 IFTX>99THENXS=100
379 TX=TX-XS
380 PX(I)=PX(I)+XS
381 LL=10+PL(I)*PL(I)*10:IFPX(I)>=LLTHENGOSUB385:GOTO381
382 PRINT"{home}{white}"LEFT$(CD$,5+I*3)"{right:3}LV";PL(I)+1;" EX";PX(I):GOTO375
383 NEXTI
384 GOTO390
385 PL(I)=PL(I)+1:PA(I)=PA(I)+1:PD(I)=PD(I)+1:PS(I)=PS(I)+1
386 P1(I)=P1(I)+PL(I)*2:IFP2(I)>0THENP2(I)=P2(I)+PL(I)*3
387 IFP1(I)>999THENP1(I)=999
388 IFP2(I)>999THENP2(I)=999
389 RETURN
390 GOSUB718
391 GOTO99
392 PRINT"{home}{down}{right}{white}DU HAST VERLOREN                     "
393 GOSUB718
394 GOTO636
395 RE=INT(RND(0)*4):CE=0
396 IFFT(CE)=-2ANDPH(CE)>0THEN399
397 CE=CE+1:IFCE=4THENCE=0
398 GOTO396
399 IFRE>0THENRE=RE-1:GOTO397
400 RETURN
401 RE=INT(RND(0)*8):CE=4
402 IFFT(CE)>=0THEN405
403 CE=CE+1:IFCE=12THENCE=4
404 GOTO402
405 IFRE>0THENRE=RE-1:GOTO403
406 RETURN
407 IFFF>0THENL=FF:GOTO409
408 L=PEEK(CP+164):IFL=0THENRETURN
409 IFL=3THENA=0:LC=INT(RND(0))*2+2:FORI=0TOLC:GOSUB570:NEXT:RETURN
410 IFL=1THENA=1:FORI=0TO2:GOSUB570:NEXT:RETURN
411 IFL=2THENA=1:FORI=0TO1:GOSUB570:NEXT:A=2:FORI=0TO1:GOSUB570:NEXT:RETURN
412 IFL=4THENA=3:GOSUB570:A=1:GOSUB570:GOSUB570:RETURN
413 IFL=5THENA=4:LC=INT(RND(0)*3)+1:FORI=0TOLC:GOSUB570:NEXT:RETURN
414 IFL=6THENONINT(RND(1)*2)+1GOTO424,425
415 IFL=7THENLC=INT(RND(0)*3)+1:A=6:FORI=0TOLC:GOSUB570:NEXT:RETURN
416 IFL=8THENA=7:GOSUB570:LC=INT(RND(0)*2)+1:A=6:GOSUB570:RETURN
417 IFL=9THENA=9:GOSUB570:A=8:GOSUB570:A=5:GOSUB570:RETURN
418 IFL=10THENA=6:LC=INT(RND(0)*2)+1:FORI=1TOLC:GOSUB570:NEXT:RETURN
419 IFL=11THENA=7:GOSUB570:A=10:GOSUB570:RETURN
420 IFL=12ANDINT(RND(1)*2)=1THENA=11:GOSUB570:RETURN
421 IFL=12THENA=5:GOSUB570:GOSUB570:A=9:GOSUB570:RETURN
422 IFL=13THENA=9:GOSUB570:A=12:GOSUB570:A=9:GOSUB570:RETURN
423 IFL=14THENA=9:GOSUB570:A=13:GOSUB570:A=7:GOSUB570:RETURN
424 A=5:LC=INT(RND(1)*2)+1:FORI=0TOLC:GOSUB570:NEXT:RETURN
425 A=9:LC=INT(RND(1)*2)+1:FORI=1TOLC:GOSUB570:NEXT:RETURN
426 FORI=0TO3
427 IFP3(I)=0THEN430
428 PH(I)=P1(I)
429 PM(I)=P2(I)
430 NEXT:RETURN
431 PRINT"{brown}{home}{$c1}{$c2:38}{$c3}";
432 PRINT"{$c4}                                      {$c5}";
433 PRINT"{$c6}{$c7:38}{$c8}";
434 PRINTDD$;"{brown}{up}{$c1}{$c2:10}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
435 FORI=0TO3:PRINT"{$c4}          {$c5}{$c4}            {$c5}{$c4}            {$c5}";:NEXT
436 PRINT"{$c6}{$c7:10}{$c8}{$c6}{$c7:12}{$c8}{$c6}{$c7:12}";
437 POKE50151,72:POKE56295,9:RETURN
438 PRINTDD$;
439 FORI=0TO3:IFP3(I)=0THENPRINTSPC(40);:GOTO445
440 PRINT"{white}";:IFPH(I)=0THENPRINT"{red}";
441 PRINTSPC(27)P$(I)" ";:A=PH(I):GOSUB688
442 PRINTD$;
443 A=PM(I):GOSUB688
444 PRINT" "D$"{right}";
445 NEXT:RETURN
446 FORI=0TO3:IFPH(I)<=0ORP3(I)=0THENNEXT:RETURN
447 PRINT"{home}"LEFT$(CD$,5+I*3);SPC(37);PD$(I):NEXT
448 RETURN
449 GOSUB484
450 MI=-1:M=0:MO=0
451 MI=MO-1:CY=MY:MM=0
452 IFMC=4THENGOSUB479
453 MI=MI+1
454 PRINT"{home}{white}"LEFT$(CD$,CY)SPC(MX)" ";
455 IFIT(IN(MI))<>MTTHENPRINT"{cyan}";
456 IFIT(IN(MI))=9THENPRINT"{white}";
457 IFIT(IN(MI))=-1THENPRINT"{cyan}";
458 PRINTI$(IN(MI));
459 CY=CY+1
460 MM=MM+1
461 IFMM=MCTHEN463
462 IFMI<13THEN453
463 CP=QS+MX+MY*40
464 POKECP+(M-MO)*40,35
465 GOSUB690
466 POKECP+(M-MO)*40,32
467 IFA$="W"THENM=M-1:GOTO471
468 IFA$="S"THENM=M+1:GOTO471
469 IFA$=CHR$(13)THENRETURN
470 GOTO464
471 IFM=-1THENM=14-1:MO=14-MC:GOTO451
472 IFM=14THENM=0:MO=0:GOTO451
473 M%=0
474 IFM<MOTHENMO=MO-MC:M%=1:GOTO474
475 IFM>=MO+MCTHENMO=MO+MC:M%=1
476 IFMO<0THENMO=0:M%=1
477 IFM%=1THEN451
478 GOTO464
479 FORI=0TO3
480 PRINT"{home}{white}"LEFT$(CD$,CY+I)SPC(MX)" ";
481 PRINT"        ";
482 NEXTI
483 RETURN
484 A=0
485 FORI=0TO99:TT(I)=0:NEXTI
486 FORI=0TO99:TT(I)=IN(I):IN(I)=0:NEXTI
487 IFMT=0THENGOSUB492:GOSUB495:A=50:GOSUB498:GOSUB501:GOSUB504
488 IFMT=1THENGOSUB492:GOSUB498:A=50:GOSUB495:GOSUB501:GOSUB504
489 IFMT=2THENGOSUB492:GOSUB501:A=50:GOSUB495:GOSUB498:GOSUB504
490 IFMT=3THENGOSUB492:GOSUB504:A=50:GOSUB495:GOSUB498:GOSUB501
491 RETURN
492 FORI=0TO99:IFIT(TT(I))=9THENIN(A)=TT(I):A=A+1
493 NEXTI
494 RETURN
495 FORI=0TO99:IFIT(TT(I))=0THENIN(A)=TT(I):A=A+1
496 NEXTI
497 RETURN
498 FORI=0TO99:IFIT(TT(I))=1THENIN(A)=TT(I):A=A+1
499 NEXTI
500 RETURN
501 FORI=0TO99:IFIT(TT(I))=2THENIN(A)=TT(I):A=A+1
502 NEXTI
503 RETURN
504 FORI=0TO99:IFIT(TT(I))=3THENIN(A)=TT(I):A=A+1
505 NEXTI
506 RETURN
507 PRINT"{brown}{home}{down:3}{$c1}{$c2:38}{$c3}";
508 FORI=0TO13
509 PRINT"{$c4}                                      {$c5}";:NEXT
510 PRINT"{$c6}{$c7:38}{$c8}";:RETURN
511 PRINT"{home}{down}{right}{white}                                      ";
512 RETURN
513 PRINT"{brown}{home}{down:3}{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
514 FORI=0TO5
515 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
516 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
517 PRINT"{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
518 FORI=0TO5
519 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
520 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
521 DX=3:DY=4:P=0:GOSUB524:DX=23:P=1:GOSUB524
522 DX=3:DY=12:P=2:GOSUB524:DX=23:P=3:GOSUB524
523 RETURN
524 IFP3(P)=0THENRETURN
525 PRINT"{home}{white}";:IFPH(P)=0THENPRINT"{red}";
526 PRINT"{home}"LEFT$(CD$,DY)SPC(DX-1)" "P$(P)"  LV"PL(P)+1
527 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX)" "I$(PW(P));
528 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX)" "I$(PR(P));
529 T1=PA(P)+IA(PW(P))
530 T2=PD(P)+ID(PR(P))
531 IFT1>999THENT1=999
532 IFT2>999THENT2=999
533 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX+9)"ATK"T1;
534 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX+9)"DEF"T2;
535 RETURN
536 IFPEEK(2)=1THENSP=30960+RS*(CR-0)
537 FORI=0TO7
538 PRINT"{home}{down:3}"LEFT$(CD$,I*2);
539 FORJ=0TO19
540 PRINTT$(PEEK(SP))"{up}";
541 SP=SP+1
542 NEXTJ,I
543 RETURN
544 IS=0
545 IFIN(IS)=0THENIN(IS)=NI:NI=-1:RETURN
546 IS=IS+1
547 IFIS=IXTHENRETURN
548 GOTO545
549 F=15-H(0)*15:C=30960+0*RS:POKEC+8+4*20,F+1
550 F=13-H(1)*13:C=30960+16*RS:POKEC+12+1*20,F:POKEC+12+2*20,F:POKEC+11+1*20,F
551 POKE30960+00*RS+13+5*20,49-H(2)*49
552 POKE30960+00*RS+06+5*20,52-H(5)*52
553 POKE30960+00*RS+06+3*20,51-H(9)*51
554 POKE30960+16*RS+00+0*20,14-H(3)*47
555 POKE30960+16*RS+01+0*20,14-H(4)*72
556 POKE30960+16*RS+02+0*20,14-H(8)*76
557 POKE30960+16*RS+03+0*20,14-H(10)*77
558 POKE30960+16*RS+3+5*20,16-H(6)*16
559 POKE30960+16*RS+8+2*20,11-H(7)*11
560 POKE30960+16*RS+15+2*20,6-H(12)*6
561 POKE30960+16*RS+15+6*20,6-H(13)*6
562 T=13:IFH(12)+H(13)=2THENT=0
563 POKE30960+16*RS+08+0,T
564 RETURN
565 IFCR=00ANDZX=16ANDZY=2ANDH(0)=0THENPRINT"{home}{white}{down}{right}DER SCHALTER IST AUS!";:GOSUB727:GOSUB511
566 IFCR=00ANDZX=16ANDZY=2ANDH(0)=1THENPRINT"{home}{white}{down}{right}DER SCHALTER IST EIN!";:GOSUB727:GOSUB511
567 IFCR=16ANDZX=00ANDZY=0ANDH(1)=0THENPRINT"{home}{white}{down}{right}DER SCHALTER IST AUS!";:GOSUB727:GOSUB511
568 IFCR=16ANDZX=00ANDZY=0ANDH(1)=1THENPRINT"{home}{white}{down}{right}DER SCHALTER IST EIN!";:GOSUB727:GOSUB511
569 RETURN
570 IFCC>=11THENRETURN
571 FH(CC)=MH(A):FM(CC)=MM(A):FT(CC)=A:FC(CC)=INT(RND(0)*MS(A)):FS(CC)=MS(A)
572 FX(CC)=2:FY(CC)=5+CC*3:FA(CC)=MA(A):FD(CC)=MD(A):FB(CC)=MB(A)
573 F1(CC)=MH(A):F2(CC)=MM(A)
574 IFCC>=4THENFX(CC)=5:FY(CC)=5+(CC-4)*3
575 CC=CC+1:RETURN
576 ZM=0
577 FORJ=4TO7
578 IFFT(J)=-1THENZM=ZM-1
579 ZM=ZM+1
580 NEXTJ
581 IFZM<>CMTHENPRINTDD$;"{right}          {down}{left:10}          {down}{left:10}          {down}{left:10}          ";
582 PRINTDD$;
583 CM=0
584 FORI=4TO7
585 IFFT(I)=-1THENNEXT:RETURN
586 A=FH(I):GOSUB688
587 PRINT"{home}{right}"LEFT$(CD$,20+CM)M$(FT(I));
588 PRINT"{home}{right}"LEFT$(CD$,20+CM);"{right:7}";D$
589 CM=CM+1
590 NEXT
591 RETURN
592 CE=4:PRINT"{home}{down}{right}{white}"P$(C)": WAHELE DEIN ZIEL               "
593 IFFT(CE)=-1THEN602
594 POKEQS-1+FX(CE)+FY(CE)*40,35
595 GOSUB690
596 IFA$=CHR$(13)THENPOKEQS-1+FX(CE)+FY(CE)*40,32:RETURN
597 IFA$="S"THENGOTO602
598 IFA$="W"THENGOTO610
599 IFA$="D"ANDCE>3THENPOKEQS-1+FX(CE)+FY(CE)*40,32:CE=0:GOTO593
600 IFA$="A"ANDCE<4THENPOKEQS-1+FX(CE)+FY(CE)*40,32:CE=4:GOTO593
601 GOTO595
602 CO=CE
603 POKEQS-1+FX(CE)+FY(CE)*40,32
604 CE=CE+1
605 IFCE>=12THENCE=0
606 IFCO<4ANDCE>3THENCE=CO
607 IFCO>3ANDCE<4THENCE=CO
608 IFFT(CE)<>-1THEN594
609 GOTO604
610 CO=CE
611 POKEQS-1+FX(CE)+FY(CE)*40,32
612 CE=CE-1
613 IFCE<0THENCE=11
614 IFCO<4ANDCE>3THENCE=CO
615 IFCO>3ANDCE<4THENCE=CO
616 IFFT(CE)<>-1THEN594
617 GOTO612
618 IFFT(C)=-2THEN634
619 IFFT(C)=7ANDFM(C)>=IE(9)THENM=9:GOTO621
620 GOTO302
621 AP=IE(M)*4+2-INT(RND(0)*5):FM(C)=FM(C)-IE(M):IFC<4THENPM(C)=FM(C):GOSUB438
622 FH(CE)=FH(CE)+AP:IFFH(CE)>F1(CE)THENFH(CE)=F1(CE)
623 IFCE<4THENPH(CE)=FH(CE):GOSUB438
624 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{green}";AP
625 GOSUB732
626 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
627 GOTO265
628 FM(C)=FM(C)-IE(M):IFC<4THENPM(C)=FM(C):GOSUB438
629 FS(CE)=FS(CE)/2:IFFS(CE)<5THENFS(CE)=5
630 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{green}SLOW";
631 GOSUB732
632 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
633 GOTO265
634 IFM=9THEN621
635 GOTO302
636 FORI=0TOHC-1:H(I)=0:NEXT
637 FORI=0TO3:PX(I)=0:NEXT
638 PA(0)=5:PD(0)=2:PA(1)=4:PD(1)=3:PA(2)=1:PD(2)=1:PA(3)=1:PD(3)=1:GOSUB549
639 FORI=0TO13:IN(I)=0:NEXT
640 IN(0)=18
641 ER(1)=0:EX(1)=16:EY(1)=5:ET(1)=3
642 ER(2)=0:EX(2)=3:EY(2)=5:ET(2)=15
643 ER(3)=16:EX(3)=0:EY(3)=0:ET(3)=4
644 ER(4)=16:EX(4)=0:EY(4)=0:ET(4)=8
645 ER(5)=16:EX(5)=0:EY(5)=0:ET(5)=5
646 ER(6)=16:EX(6)=0:EY(6)=0:ET(6)=16
647 ER(7)=16:EX(7)=0:EY(7)=0:ET(7)=17
648 ER(8)=16:EX(8)=0:EY(8)=0:ET(8)=13
649 ER(9)=16:EX(9)=0:EY(9)=0:ET(9)=15
650 ER(10)=16:EX(10)=0:EY(10)=0:ET(10)=11
651 ER(11)=16:EX(11)=0:EY(11)=0:ET(11)=7
652 ER(12)=16:EX(12)=0:EY(12)=0:ET(12)=9
653 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
654 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
655 PRINT"{$c6}{$c7:38}{up:3}";
656 POKE50151,72:POKE56295,9
657 X=13:Y=3:CR=0
658 P3(0)=1:P3(1)=0:P3(2)=0:P3(3)=0
659 P1(0)=15:P1(1)=17:P1(2)=9:P1(3)=9:P2(0)=0:P2(1)=0:P2(2)=7:P2(3)=23
660 FORI=0TO3:PH(I)=P1(I):PM(I)=P2(I):PX(I)=0:PW(I)=0:PR(I)=0:NEXT
661 PW(0)=1:PR(0)=2
662 GOSUB549
663 AA$="INTRO":GOSUB676
664 GOTO94
665 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
666 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
667 PRINT"{$c6}{$c7:38}{up:3}";
668 POKE50151,72:POKE56295,9
669 AA$="ENDE":GOSUB676
670 GOTO94
671 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
672 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
673 PRINT"{$c6}{$c7:38}{up:3}";
674 POKE50151,72:POKE56295,9
675 RETURN
676 POKE1020,Q7
677 POKE56322,224
678 IFAA$="INTRO"THENOPEN1,8,4,"TXT.INTRO,S,R"
679 IFAA$="ENDE"THENOPEN1,8,4,"TXT.ENDE,S,R"
680 IFAA$=""THENRETURN
681 GET#1,B$
682 IFB$="$"THENPRINT"{home}{down:2}{right:2}{white}";:GOTO681
683 IFB$="%"THENPRINT"{right:2}{white}";:GOTO681
684 IFB$="#"THENCLOSE1:POKE56322,255:GOSUB718:RETURN
685 PRINTB$;
686 IFB$<>" "THENPOKE162,0:WAIT162,4
687 GOTO681
688 T$=STR$(A)
689 D$=RIGHT$("000"+RIGHT$(T$,LEN(T$)-1),3):RETURN
690 A$=""
691 J=PEEK(56320)
692 IFJ=127THEN694
693 GOTO691
694 J=PEEK(56320)
695 IFJ=127ANDA$=CHR$(13)THENRETURN
696 IFJ=107THENA$="E":GOSUB723:RETURN
697 IFJ=103THENA$="E":GOSUB723:RETURN
698 IFJ=110THENA$="E":GOSUB723:RETURN
699 IFJ=109THENA$="E":GOSUB723:RETURN
700 IFJ=123THENA$="A":GOSUB723:RETURN
701 IFJ=119THENA$="D":GOSUB723:RETURN
702 IFJ=126THENA$="W":GOSUB723:RETURN
703 IFJ=125THENA$="S":GOSUB723:RETURN
704 IFJ=111THENA$=CHR$(13)
705 GOTO694
706 A$=""
707 J=PEEK(56320)
708 IFJ<>111ANDJ<>107ANDJ<>103ANDJ<>110ANDJ<>109THEN710
709 GOTO707
710 J=PEEK(56320)
711 IFJ=127ANDA$=CHR$(13)THENRETURN
712 IFJ=123THENA$="A":GOSUB724:RETURN
713 IFJ=119THENA$="D":GOSUB724:RETURN
714 IFJ=126THENA$="W":GOSUB724:RETURN
715 IFJ=125THENA$="S":GOSUB724:RETURN
716 IFJ=111THENA$=CHR$(13)
717 GOTO710
718 A$=""
719 J=PEEK(56320)
720 IFJ=127ANDA$=CHR$(13)THENRETURN
721 IFJ=111THENA$=CHR$(13)
722 GOTO719
723 POKE162,0:WAIT162,16:RETURN
724 POKE162,0:WAIT162,8:RETURN
725 POKE162,0:WAIT162,32:RETURN
726 POKE162,0:WAIT162,2:RETURN
727 POKE162,0:WAIT162,32
728 POKE162,0:WAIT162,32
729 POKE162,0:WAIT162,32
730 POKE162,0:WAIT162,32
731 RETURN
732 POKE162,0:WAIT162,32
733 RETURN
734 POKE162,0:WAIT162,4
735 RETURN
736 DATA"IMP",5,0,8,4,1,0,3
737 DATA"SNAKE",8,0,12,6,1,0,5
738 DATA"VIPER",12,0,18,6,1,0,9
739 DATA"NACHO",30,0,22,15,5,0,50
740 DATA"GOLEM",20,0,4,20,8,0,17
741 DATA"MOSS",30,0,7,26,9,0,21
742 DATA"BONES",50,0,20,18,8,0,15
743 DATA"WISP",25,50,15,0,8,0,17
744 DATA"TROLL",100,0,9,40,13,0,100
745 DATA"SPARK",30,50,22,0,8,0,17
746 DATA"BURNER",80,90,30,25,13,0,42
747 DATA"TBERRY",120,10,60,60,20,0,150
748 DATA"DRACUL",250,60,50,70,25,0,500
749 DATA"GLADOS",500,70,60,80,30,0,1000
750 DATA"KRON",15 ,0  ,10 ,4 ,3 ,0 ,0
751 DATA"LENA",17 ,0  ,10 ,3 ,3 ,0 ,0
752 DATA"DOLM",9  ,7  ,8  ,1 ,1 ,0 ,0
753 DATA"MIRA",9  ,23 ,7  ,1 ,1 ,1 ,2
754 DATA"{gray1}{rvrs off}{$c9}{rvrs off}{gray1}{$ca}{down}{left:2}{rvrs off}{gray1}{$ca}{rvrs off}{gray1}{$c9}{rvrs off}"
755 DATA"{gray1}{rvrs off}{$cb}{rvrs off}{gray1}{$cc}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1}{$ca}{rvrs off}"
756 DATA"{gray1}{rvrs off}{$c9}{rvrs off}{gray1}{$ca}{down}{left:2}{rvrs off}{gray1}{$ca}{rvrs off}{gray1}{$c9}{rvrs off}"
757 DATA"{gray1}{rvrs off}{$cd}{rvrs off}{gray1}{$ce}{down}{left:2}{rvrs off}{gray1}{$cf}{rvrs off}{gray1}{$d0}{rvrs off}"
758 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
759 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
760 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
761 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
762 DATA"{orange}{rvrs off}{$d1}{rvrs off}{orange}{$d1}{down}{left:2}{rvrs off}{orange}{$d1}{rvrs off}{orange}{$d1}{rvrs off}"
763 DATA"{brown}{rvrs off}{$d2}{rvrs off}{brown}{$d3}{down}{left:2}{rvrs off}{brown}{$d4}{rvrs off}{brown}{$d5}{rvrs off}"
764 DATA"{lt. red}{rvrs off}{$d6}{rvrs off}{lt. red}{$d7}{down}{left:2}{rvrs off}{lt. red}{$d8}{rvrs off}{lt. red}{$d9}{rvrs off}"
765 DATA"{gray1}{rvrs off}{$da}{rvrs off}{gray1}{$db}{down}{left:2}{rvrs off}{gray1}{$dc}{rvrs off}{gray1}{$dd}{rvrs off}"
766 DATA"{gray1}{rvrs off}{$a2}{rvrs off}{gray1}{$a3}{down}{left:2}{rvrs off}{gray1}{$a4}{rvrs off}{gray1}{$a5}{rvrs off}"
767 DATA"{brown}{rvrs off}{$a1}{rvrs off}{brown}{$a1}{down}{left:2}{rvrs off}{brown}{$a1}{rvrs off}{brown}{$a1}{rvrs off}"
768 DATA"{gray1}{rvrs off}{$de}{rvrs off}{gray1}{$df}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1}{$ca}{rvrs off}"
769 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
770 DATA"{lt. red}{rvrs off}{$a6}{rvrs off}{lt. red}{$a7}{down}{left:2}{rvrs off}{lt. red}{cmdr pound}{rvrs off}{lt. red}{shft pound}{rvrs off}"
771 DATA"{lt. red}{rvrs off}{$aa}{rvrs off}{lt. red}{$aa}{down}{left:2}{rvrs off}{lt. red}{$aa}{rvrs off}{lt. red}{$aa}{rvrs off}"
772 DATA"{brown}{rvrs off}{$ab}{rvrs off}{brown}{$ab}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
773 DATA"{brown}{rvrs off}{$ab}{rvrs off}{brown}{$ad}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
774 DATA"{brown}{rvrs off}{$ad}{rvrs off}{brown}{$ab}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
775 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
776 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
777 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
778 DATA"{lt. green}{rvrs off}{$ae}{rvrs off}{lt. green}{$d1}{down}{left:2}{rvrs off}{lt. green}{$d1}{rvrs off}{lt. green}{$af}{rvrs off}"
779 DATA"{lt. green}{rvrs off}{$b0}{rvrs off}{lt. green}{$b1}{down}{left:2}{rvrs off}{lt. green}{$b2}{rvrs off}{lt. green}{$b3}{rvrs off}"
780 DATA"{lt. green}{rvrs off}{$b4}{rvrs off}{lt. green}{$b5}{down}{left:2}{rvrs off}{lt. green}{$b4}{rvrs off}{lt. green}{$b5}{rvrs off}"
781 DATA"{lt. green}{rvrs off}{$b6}{rvrs off}{lt. green}{$b7}{down}{left:2}{rvrs off}{lt. green}{$b8}{rvrs off}{lt. green}{$b9}{rvrs off}"
782 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
783 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
784 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
785 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
786 DATA"{brown}{rvrs off}{$ae}{rvrs off}{brown}{$d1}{down}{left:2}{rvrs off}{brown}{$d1}{rvrs off}{brown}{$af}{rvrs off}"
787 DATA"{brown}{rvrs off}{$b0}{rvrs off}{brown}{$b1}{down}{left:2}{rvrs off}{brown}{$b2}{rvrs off}{brown}{$b3}{rvrs off}"
788 DATA"{brown}{rvrs off}{$b4}{rvrs off}{brown}{$b5}{down}{left:2}{rvrs off}{brown}{$b4}{rvrs off}{brown}{$b5}{rvrs off}"
789 DATA"{brown}{rvrs off}{$b6}{rvrs off}{brown}{$b7}{down}{left:2}{rvrs off}{brown}{$b8}{rvrs off}{brown}{$b9}{rvrs off}"
790 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
791 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
792 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
793 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
794 DATA"{brown}{rvrs off}{$d1}{rvrs off}{brown}{$d1}{down}{left:2}{rvrs off}{brown}{$d1}{rvrs off}{brown}{$d1}{rvrs off}"
795 DATA"{brown}{rvrs off}{$ba}{rvrs off}{brown}{$d1}{down}{left:2}{rvrs off}{brown}{$ba}{rvrs off}{brown}{$d1}{rvrs off}"
796 DATA"{brown}{rvrs off}{$ba}{rvrs off}{brown}{$d1}{down}{left:2}{rvrs off}{brown}{$bb}{rvrs off}{brown}{$d1}{rvrs off}"
797 DATA"{brown}{rvrs off}{$bc}{rvrs off}{brown}{$bd}{down}{left:2}{rvrs off}{brown}{$be}{rvrs off}{brown}{$be}{rvrs off}"
798 DATA"{gray1}{rvrs off}{$bf}{rvrs off}{gray1}{$bf}{down}{left:2}{rvrs on}{gray1}@{rvrs on}{gray1}@{rvrs off}"
799 DATA"{gray1}{rvrs on}A{rvrs on}{gray1}B{down}{left:2}{rvrs on}{gray1}C{rvrs on}{gray1}D{rvrs off}"
800 DATA"{gray1}{rvrs on}E{rvrs on}{gray1}F{down}{left:2}{rvrs on}{gray1}G{rvrs on}{gray1}H{rvrs off}"
801 DATA"{gray1}{rvrs on}I{rvrs on}{gray1}I{down}{left:2}{rvrs on}{gray1}I{rvrs on}{gray1}I{rvrs off}"
802 DATA"{orange}{rvrs on}J{rvrs on}{orange}K{down}{left:2}{rvrs on}{orange}L{rvrs on}{orange}M{rvrs off}"
803 DATA"{gray1}{rvrs on}N{rvrs on}{gray1}K{down}{left:2}{rvrs on}{gray1}L{rvrs on}{gray1}M{rvrs off}"
804 DATA"{lt. green}{rvrs on}O{rvrs on}{lt. green}P{down}{left:2}{rvrs on}{lt. green}Q{rvrs on}{lt. green}R{rvrs off}"
805 DATA"{gray2}{rvrs on}S{rvrs on}{gray2}T{down}{left:2}{rvrs on}{gray2}U{rvrs on}{gray2}V{rvrs off}"
806 DATA"{gray1}{rvrs on}W{rvrs on}{gray1}X{down}{left:2}{rvrs on}{gray1}Y{rvrs on}{gray1}Z{rvrs off}"
807 DATA"{gray1}{rvrs on}[{rvrs on}{gray1}{pound}{down}{left:2}{rvrs on}{gray1}]{rvrs on}{gray1}{^}{rvrs off}"
808 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
809 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
810 DATA"{orange}{rvrs off} {rvrs off}{orange} {down}{left:2}{rvrs off}{orange} {rvrs off}{orange} {rvrs off}"
811 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
812 DATA"{lt. green}{rvrs off} {rvrs off}{lt. green} {down}{left:2}{rvrs off}{lt. green} {rvrs off}{lt. green} {rvrs off}"
813 DATA"{gray2}{rvrs off} {rvrs off}{gray2} {down}{left:2}{rvrs off}{gray2} {rvrs off}{gray2} {rvrs off}"
814 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
815 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
816 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
817 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
818 DATA"{lt. green}{rvrs on}{back arrow}{rvrs on}{lt. green}!{down}{left:2}{rvrs on}{lt. green}#{rvrs on}{lt. green}${rvrs off}"
819 DATA"{gray3}{rvrs on}%{rvrs on}{gray3}&{down}{left:2}{rvrs on}{gray3}'{rvrs on}{gray3}({rvrs off}"
820 DATA"{gray1}{rvrs on}%{rvrs on}{gray1}&{down}{left:2}{rvrs on}{gray1}'{rvrs on}{gray1}({rvrs off}"
821 DATA"{lt. green}{rvrs on}){rvrs on}{lt. green}*{down}{left:2}{rvrs on}{lt. green}+{rvrs on}{lt. green},{rvrs off}"
822 DATA"{gray1}{rvrs on}-{rvrs on}{gray1}.{down}{left:2}{rvrs on}{gray1}/{rvrs on}{gray1}0{rvrs off}"
823 DATA"{gray1}{rvrs on}1{rvrs on}{gray1}2{down}{left:2}{rvrs on}{gray1}3{rvrs on}{gray1}4{rvrs off}"
824 DATA"{gray1}{rvrs on}5{rvrs on}{gray1}6{down}{left:2}{rvrs on}{gray1}7{rvrs on}{gray1}8{rvrs off}"
825 DATA"{gray1}{rvrs on}9{rvrs on}{gray1}:{down}{left:2}{rvrs on}{gray1};{rvrs on}{gray1}<{rvrs off}"
826 DATA"{lt. green}{rvrs on}={rvrs on}{lt. green}>{down}{left:2}{rvrs on}{lt. green}?{rvrs on}{lt. green}{$c0}{rvrs off}"
827 DATA"{gray3}{rvrs on}9{rvrs on}{gray3}:{down}{left:2}{rvrs on}{gray3};{rvrs on}{gray3}<{rvrs off}"
828 DATA"{gray1}{rvrs on}5{rvrs on}{gray1}6{down}{left:2}{rvrs on}{gray1}7{rvrs on}{gray1}8{rvrs off}"
829 DATA"{lt. green}{rvrs on}5{rvrs on}{lt. green}6{down}{left:2}{rvrs on}{lt. green}7{rvrs on}{lt. green}8{rvrs off}"
830 DATA"{gray1}{rvrs on}{$c1}{rvrs on}{gray1}{$c2}{down}{left:2}{rvrs on}{gray1}{$c3}{rvrs on}{gray1}{$c4}{rvrs off}"
831 DATA"{gray1}{rvrs on}{$c5}{rvrs on}{gray1}{$c6}{down}{left:2}{rvrs on}{gray1}{$c7}{rvrs on}{gray1}{$c8}{rvrs off}"
832 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
833 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
834 DATA"        ",0    ,0    ,0    ,0    ,-1 
835 DATA"DOLCH   ",3    ,0    ,0    ,0    ,0  
836 DATA"HELM    ",0    ,2    ,-1   ,0    ,1  
837 DATA"KRAEUTER",0    ,0    ,-1   ,10   ,2  
838 DATA"FEUER   ",0    ,0    ,0    ,5    ,3  
839 DATA"EIS     ",0    ,0    ,0    ,8    ,3  
840 DATA"MUT     ",0    ,0    ,0    ,23   ,3  
841 DATA"BOMBE   ",0    ,0    ,0    ,50   ,3  
842 DATA"KIRSCHE ",0    ,0    ,-1   ,15   ,2  
843 DATA"HEILEN  ",0    ,0    ,0    ,4    ,3  
844 DATA"SKULL   ",0    ,0    ,0    ,0    ,4  
845 DATA"FLEGEL  ",7    ,0    ,-1   ,0    ,0  
846 DATA"LAHM    ",0    ,0    ,0    ,4    ,3  
847 DATA"STAB    ",12   ,0    ,0    ,0    ,0  
848 DATA"FEDER   ",0    ,10   ,0    ,0    ,1  
849 DATA"STOCK   ",23   ,0    ,0    ,0    ,0  
850 DATA"WESTE   ",0    ,14   ,0    ,0    ,1  
851 DATA"SCHILD  ",0    ,24   ,0    ,0    ,1  
852 DATA"ZURUECK ",0    ,0    ,0    ,0    ,9  
853 END
