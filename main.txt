0 POKE55,240:POKE56,120:CLR
1 QO=53282
2 QP=53283
3 QQ=53281
4 QR=53280
5 QS=49152
6 V=53248
7 Q1%=0
8 Q7%=6
9 QA%=9
10 RS=165
11 DD$="{home}{down:20}{white}"
12 CD$="{down:25}"
13 DIMM$(13),MD$(13),MH%(13),MM%(13),MA%(13),MD%(13),MS%(13),MB%(13),MX%(13)
14 DIMFH%(11),FT%(11),FC%(11),FM%(11),FS%(11),FX%(11),FY%(11),FA%(11),FD(11),F1%(11),F2%(11)
15 FORI=0TO3:FX%(I)=37:FY%(I)=5+3*I:NEXT
16 DIMI$(18),IA%(18),ID%(18),IS%(18),IE%(18),IT%(18)
17 DIMER%(12),EX%(12),EY%(12),ET%(12)
18 DIMIN%(99),TT%(99)
19 DIMT$(79)
20 DIMS0%(10),S1%(10),S2%(10),S3%(10),A0%(10),A1%(10),A2%(10)
21 DIMN0%(10),N1%(10),N2%(8),N3%(10)
22 FORI=0TO13
23 READM$(I),MH%(I),MM%(I),MS%(I)
24 READMA%(I),MD%(I),MB%(I),MX%(I)
25 NEXT
26 FORI=0TO3
27 READP$(I),PH%(I),PM%(I),PS%(I)
28 READPA%(I),PD%(I),PW%(I),PR%(I)
29 NEXT
30 FORI=0TO79
31 READT$(I)
32 NEXT
33 PD$(0)=T$(48)
34 PD$(1)=T$(50)
35 PD$(2)=T$(52)
36 PD$(3)=T$(51)
37 ZZ=64
38 FORI=0TO13
39 MD$(I)=T$(ZZ)
40 ZZ=ZZ+1
41 NEXTI
42 FORI=0TO18
43 READI$(I),IA%(I),ID%(I),IS%(I),IE%(I),IT%(I)
44 NEXT
45 POKEV+21,0
46 POKE648,192
47 POKE56576,0
48 POKE53272,8
49 POKE53270,PEEK(53270)OR16
50 POKEQO,QA%
51 POKEQP,Q1%
52 POKEQR,Q1%
53 POKEQQ,Q7%
54 POKE1020,Q7%
55 POKE1021,Q7%
56 SYS820
57 GOSUB147
58 GOTO618
59 GOSUB640
60 GOSUB510
61 GOSUB649
62 GOSUB441
63 PRINT"{home}{down}{right}{white}                                      ";
64 POKE1020,Q7%
65 GOSUB539
66 IFPEEK(2)=1THENCP=30960+RS*(CR-0)
67 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)PD$(0);
68 OX=ZX:OY=ZY
69 ZX=X:ZY=Y
70 GOSUB717
71 IFA$=CHR$(145)ORA$="W"THENZY=Y-1:GOTO77
72 IFA$=CHR$(157)ORA$="A"THENZX=X-1:GOTO77
73 IFA$=CHR$(29)ORA$="D"THENZX=X+1:GOTO77
74 IFA$=CHR$(17)ORA$="S"THENZY=Y+1:GOTO77
75 IFA$=CHR$(13)THEN180
76 GOTO70
77 AA%=0
78 FORI=0TO12
79 IFER%(I)=CRANDEX%(I)=ZXANDEY%(I)=ZYTHENAA%=1
80 NEXTI
81 IFZX=-1ORZX=20ORZY=-1ORZY=8THEN93
82 C=PEEK(CP+ZX+ZY*20)
83 IFC=08THEN107
84 IFC=09THEN107
85 IFC=10THEN107
86 IFC=11THEN107
87 IFC=12THEN114
88 IFC=14THEN109
89 IFC=25THEN114
90 IFC=40THEN127
91 IFC>=48THEN118
92 IFC>7THEN68
93 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)T$(PEEK(CP+X+Y*20))
94 IFRND(1)*100>97ANDPEEK(CP+164)>0THENFF=0:GOTO256
95 X=ZX:Y=ZY
96 IFX=-1ANDPEEK(CP+160)=255THENX=0:GOTO67
97 IFX=-1THENCR=PEEK(CP+160):X=19:GOTO64
98 IFX=20ANDPEEK(CP+161)=255THENX=19:GOTO67
99 IFX=20THENCR=PEEK(CP+161):X=0:GOTO64
100 IFY=-1ANDPEEK(CP+162)=255THENY=0:GOTO67
101 IFY=-1THENCR=PEEK(CP+162):Y=7:GOTO64
102 IFY=8ANDPEEK(CP+163)=255THENY=7:GOTO67
103 IFY=8THENCR=PEEK(CP+163):Y=0:GOTO64
104 IFC=2THEN109
105 IFC=3THEN109
106 GOTO67
107 IFCR=16THENCR=16:X=0:Y=0:GOTO63
108 IFCR=16THEN634
109 IFC=14ANDCR=S0%(0)ANDZX=S1%(0)THENS3%(0)=1-S3%(0):GOSUB131:TX=A1%(0):TY=A2%(0):GOSUB128:GOSUB144:GOTO68
110 IFC=2ANDCR=S0%(1)ANDX=S1%(1)THENS3%(1)=1:GOSUB131:TX=A1%(1):TY=A2%(1):GOSUB128:GOTO67
111 IFC=2ANDCR=S0%(2)ANDX=S1%(2)THENS3%(2)=1:GOSUB131:TX=A1%(2):TY=A2%(2):GOSUB128:GOTO67
112 IFC=3ANDCR=S0%(3)ANDX=S1%(3)THENS3%(3)=1:GOSUB131:TX=A1%(3):TY=A2%(3):GOSUB128:GOTO67
113 IFC=3ANDCR=S0%(4)ANDX=S1%(4)THENS3%(4)=1:GOSUB131:TX=A1%(4):TY=A2%(4):GOSUB128:GOTO67
114 IFC=12ANDAA%=0THENGOTO68
115 IFC=12ANDAA%=1THENVA$="IN DER TRUHE IST WAS":GOSUB682:GOTO68
116 IFC=25ANDAA%=0THENGOTO68
117 IFC=25ANDAA%=1THENVA$="DER BAUM SIEHT INTRESSANT AUS":GOSUB682:GOTO68
118 IFC=49THENPRINT"{white}{home}{down}{right}EIN HEILZAUBER IST UNTER DEM BAUM":GOSUB738:GOSUB514:GOTO68
119 IFC=53THENPRINT"{white}{home}{down}{right}EIN FEUERZAUBER IST IN DEN KATAKOMBEN":GOSUB738:GOSUB514:GOTO68
120 IFC=50THENAA$="TXT.WELCOME.LENA":GOSUB658:N3%(0)=1:GOSUB131:P3%(1)=1:TX=12:TY=4:GOSUB128:GOSUB441:GOTO68
121 IFC=52THENAA$="TXT.WELCOME.DOLM":GOSUB658:N3%(1)=1:GOSUB131:P3%(2)=1:TX=7:TY=2:GOSUB128:GOSUB441:GOTO68
122 IFC=51THENAA$="TXT.WELCOME.MIRA":GOSUB658:N3%(2)=1:GOSUB131:P3%(3)=1:TX=3:TY=2:GOSUB128:GOSUB441:GOTO68
123 IFC=67THENPRINT"{white}{home}{down}{right}DU KANNST HIER NICHT DURCH!":GOSUB738:FF=4:N3%(3)=1:GOSUB131:GOTO256
124 IFC=72THENPRINT"{white}{home}{down}{right}ACH WIE SUESS!":GOSUB738:FF=9:N3%(4)=1:GOSUB131:GOTO256
125 IFC=76THENPRINT"{white}{home}{down}{right}DU WILLST MICH BESIEGEN!":GOSUB738:FF=13:N3%(5)=1:GOSUB131:GOTO256
126 IFC=77THENPRINT"{white}{home}{down}{right}DU HAST MEINE ERWARTUNG UEBERTROFFEN!":GOSUB738:FF=14:N3%(6)=1:GOSUB131:GOTO256
127 IFC=40THENVA$="DU BIST GEHEILT":GOSUB436:GOSUB441:GOSUB682:GOTO68
128 SP=30960+(CR*RS)+(TX+TY*20)
129 PRINT"{home}{down:3}"LEFT$(CD$,TY*2)SPC(TX*2)T$(PEEK(SP));
130 RETURN
131 POKE30960+N0%(0)*RS+N1%(0)+N2%(0)*20,50-N3%(0)*50
132 POKE30960+N0%(1)*RS+N1%(1)+N2%(1)*20,52-N3%(1)*52
133 POKE30960+N0%(2)*RS+N1%(2)+N2%(2)*20,51-N3%(2)*51
134 POKE30960+N0%(3)*RS+N1%(3)+N2%(3)*20,67-N3%(3)*67
135 POKE30960+N0%(4)*RS+N1%(4)+N2%(4)*20,72-N3%(4)*72
136 POKE30960+N0%(5)*RS+N1%(5)+N2%(5)*20,76-N3%(5)*76
137 POKE30960+N0%(6)*RS+N1%(6)+N2%(6)*20,77-N3%(6)*77
138 POKE30960+A0%(0)*RS+A1%(0)+A2%(0)*20,15-S3%(0)*15+1
139 POKE30960+A0%(1)*RS+A1%(1)+A2%(1)*20,13-S3%(1)*13
140 POKE30960+A0%(2)*RS+A1%(2)+A2%(2)*20,13-S3%(2)*12
141 T=13:IFS3%(3)+S3%(4)=2THENT=0
142 POKE30960+A0%(3)*RS+A1%(3)+A2%(3)*20,T
143 RETURN
144 IFCR=S0%(0)ANDZX=S1%(0)ANDS3%(0)=0THENVA$="DER SCHALTER IST AUS":GOSUB682
145 IFCR=S0%(0)ANDZX=S1%(0)ANDS3%(0)=1THENVA$="SCHALTER IST EIN":GOSUB682
146 RETURN
147 S0%(0)=0:S1%(0)=11:S2%(0)=1:S3%(0)=0
148 S0%(1)=0:S1%(1)=16:S2%(1)=6:S3%(1)=0
149 S0%(2)=0:S1%(2)=18:S2%(2)=6:S3%(2)=0
150 S0%(3)=0:S1%(3)=1:S2%(3)=6:S3%(3)=0
151 S0%(4)=0:S1%(4)=5:S2%(4)=6:S3%(4)=0
152 A0%(0)=0:A1%(0)=14:A2%(0)=6
153 A0%(1)=0:A1%(1)=7:A2%(1)=4
154 A0%(2)=0:A1%(2)=9:A2%(2)=5
155 A0%(3)=0:A1%(3)=3:A2%(3)=4
156 A0%(4)=0:A1%(4)=3:A2%(4)=4
157 N0%(0)=0:N1%(0)=12:N2%(0)=4:N3%(0)=0
158 N0%(1)=0:N1%(1)=7:N2%(1)=2:N3%(1)=0
159 N0%(2)=0:N1%(2)=3:N2%(2)=2:N3%(2)=0
160 N0%(3)=16:N1%(3)=0:N2%(3)=0:N3%(3)=0
161 N0%(4)=16:N1%(4)=0:N2%(4)=0:N3%(4)=0
162 N0%(5)=16:N1%(5)=0:N2%(5)=0:N3%(5)=0
163 N0%(6)=16:N1%(6)=0:N2%(6)=0:N3%(6)=0
164 N0%(7)=16:N1%(7)=0:N2%(7)=0:N3%(7)=0
165 N0%(8)=16:N1%(8)=0:N2%(8)=0:N3%(8)=0
166 ER%(0)=16:EX%(0)=0:EY%(0)=0:ET%(1)=0
167 ER%(1)=0:EX%(1)=1:EY%(1)=2:ET%(1)=3
168 ER%(2)=0:EX%(2)=18:EY%(2)=2:ET%(2)=15
169 ER%(3)=16:EX%(3)=0:EY%(3)=0:ET%(3)=4
170 ER%(4)=16:EX%(4)=0:EY%(4)=0:ET%(4)=8
171 ER%(5)=16:EX%(5)=0:EY%(5)=0:ET%(5)=5
172 ER%(6)=16:EX%(6)=0:EY%(6)=0:ET%(6)=16
173 ER%(7)=16:EX%(7)=0:EY%(7)=0:ET%(7)=17
174 ER%(8)=16:EX%(8)=0:EY%(8)=0:ET%(8)=13
175 ER%(9)=16:EX%(9)=0:EY%(9)=0:ET%(9)=15
176 ER%(10)=16:EX%(10)=0:EY%(10)=0:ET%(10)=11
177 ER%(11)=16:EX%(11)=0:EY%(11)=0:ET%(11)=7
178 ER%(12)=16:EX%(12)=0:EY%(12)=0:ET%(12)=9
179 RETURN
180 PRINT"{home}{down}{white}{right}  UNTERSUCHE  EQUIPMENTS  INVENTAR"
181 M=0
182 POKEQS+42+M*12,35
183 GOSUB700
184 POKEQS+42+M*12,32
185 IFA$="A"THENM=M-1-3*(M=0):GOTO182
186 IFA$="D"THENM=M+1+3*(M=2):GOTO182
187 IFA$=CHR$(13)THENPOKEQS+42+M*7,32:GOTO189
188 GOTO182
189 PRINT"{home}{down}{white}{right}                                      ";
190 ONM+1GOTO244,204,191
191 POKE1020,Q1%
192 GOSUB654
193 PRINT"{home}{down}{right}{white}INVENTAR: WAEHLE DEIN ITEM           ";
194 MT=2:MX=2:MY=4:MC=14:GOSUB452
195 IFIN%(M)=9THEN63
196 IFIN%(M)=0ORIT%(IN%(M))<>MTTHEN63
197 IFIN%(M)=3THENGOSUB200:IN%(M)=0:GOSUB441:GOTO63
198 IFIN%(M)=8THENGOSUB202:IN%(M)=0:GOSUB441:GOTO63
199 GOTO63
200 FORI=0TO3:PH%(I)=PH%(I)+IE%(IN%(M)):IFPH%(I)>P1%(I)THENPH%(I)=P1%(I)
201 FH%(I)=PH%(I):NEXT:RETURN
202 FORI=0TO3:PM%(I)=PM%(I)+IE%(IN%(M)):IFPM%(I)>P2%(I)THENPM%(I)=P2%(I)
203 FM%(I)=PM%(I):NEXT:RETURN
204 POKE1020,Q1%
205 GOSUB516
206 PRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINEN SPIELER     ";
207 P=0
208 CP=QS+2+(4-8*(P>1))*40:IFP=1ORP=3THENCP=CP+20
209 POKECP,35
210 GOSUB700
211 POKECP,32
212 IFP=0ANDA$="D"ANDP3%(1)=1THENP=1:GOTO208
213 IFP=0ANDA$="S"ANDP3%(2)=1THENP=2:GOTO208
214 IFP=0ANDA$="D"ANDP3%(1)=OANDP3%(2)=0ANDP3%(3)=1THENP=3:GOTO208
215 IFP=0ANDA$="S"ANDP3%(1)=OANDP3%(2)=0ANDP3%(3)=1THENP=3:GOTO208
216 IFP=1ANDA$="A"ANDP3%(0)=1THENP=0:GOTO208
217 IFP=1ANDA$="S"ANDP3%(3)=1THENP=3:GOTO208
218 IFP=2ANDA$="D"ANDP3%(3)=1THENP=3:GOTO208
219 IFP=2ANDA$="W"ANDP3%(0)=1THENP=0:GOTO208
220 IFP=3ANDA$="A"ANDP3%(2)=1THENP=2:GOTO208
221 IFP=3ANDA$="W"ANDP3%(1)=1THENP=1:GOTO208
222 IFP=3ANDA$="A"ANDP3%(2)=OANDP3%(1)=0ANDP3%(0)=1THENP=0:GOTO208
223 IFP=3ANDA$="W"ANDP3%(2)=OANDP3%(1)=0ANDP3%(0)=1THENP=0:GOTO208
224 IFA$=CHR$(13)THEN226
225 GOTO209
226 SL=0:CP=CP+81
227 PRINT"{home}{down}{right}{white}EQUIPMENT: WAFFE ODER RUESTUNG       ";
228 POKECP+SL*40,35
229 GOSUB700
230 POKECP+SL*40,32:IFA$="W"ORA$="S"THENSL=1-SL:GOTO228
231 IFA$=CHR$(13)THEN233
232 GOTO228
233 MY=4:MX=23:MC=14:IFP=1ORP=3THENMX=3
234 IFSL=0THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE WAFFE        ";
235 IFSL=1THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE RUESTUNG     ";
236 PRINT"{home}"LEFT$(CD$,MY-1)SPC(MX-3);"{brown}{$c1}{$c2:18}{$c3}";
237 FORI=0TO13:PRINT"{down}{left:20}{$c4}                  {$c5}";:NEXT
238 PRINT"{down}{left:20}{$c6}{$c7:18}{$c8}";
239 MT=SL:GOSUB452
240 IFIT%(IN%(M))=9THEN63
241 IFIN%(M)<>0ANDIT%(IN%(M))<>SLTHEN63
242 IFSL=0THENQ=PW%(P):PW%(P)=IN%(M):IN%(M)=Q:GOTO63
243 IFSL=1THENQ=PR%(P):PR%(P)=IN%(M):IN%(M)=Q:GOTO63
244 GOSUB514
245 GOSUB743
246 EI=0
247 IFER%(EI)<>CROREX%(EI)<>OXOREY%(EI)<>OYTHEN252
248 NI%=ET%(EI):GOSUB547
249 IFIS=99THENVA$="DU KANNST NICHT MEHR TRAGEN":GOSUB682:GOTO68
250 ER%(EI)=-1
251 VA$="GEFUNDEN: "+I$(IN%(IS)):GOSUB682:GOTO68
252 EI=EI+1
253 IFEI=12THEN255
254 GOTO247
255 VA$="NICHTS BESONDERES":GOSUB682:GOTO68
256 FORI=4TO11:FT%(I)=-1:NEXT
257 PP=4:CC=4:WX=0
258 FORI=0TO3:FT%(I)=-2:FH%(I)=PH%(I):FM%(I)=PM%(I):FS%(I)=PS%(I)
259 IFP3%(I)=0THENFH%(I)=-1
260 IFFH%(I)<=0THENFT%(I)=-1:PP=PP-1
261 FA%(I)=PA%(I)+IA%(PW%(I)):FD(I)=PD%(I)+ID%(PR%(I)):F1%(I)=P1%(I):F2%(I)=P2%(I)
262 NEXT
263 PRINT"{home}{down:3}{white}";:FORI=0TO15:PRINT"                                        ";:NEXT
264 GOSUB644
265 GOSUB417
266 GOSUB441
267 GOSUB449
268 FORI=0TO11
269 IFFT%(I)<>-1THENFC%(I)=INT(RND(0)*FD%(I))
270 NEXT
271 CC=CC-4
272 PRINT"{home}{down}{right}                       ";:GOSUB558
273 FORI=4TO11:IFFT%(I)=-1THEN275
274 PRINT"{home}"LEFT$(CD$,FY%(I));SPC(FX%(I));MD$(FT%(I))
275 NEXT
276 C=0
277 IFFT%(C)<>-1ANDFC%(C)>=100THENFC%(C)=FC%(C)-100:GOTO280
278 C=C+1:IFC<12THEN277
279 FORI=0TO11:FC%(I)=FC%(I)+FS%(I):NEXT:GOTO276
280 IFFT%(C)=-2THEN282
281 GOTO366
282 PRINT"{home}{white}{down}{right}"P$(C)": IST AM ZUG                     "
283 PRINTDD$;SPC(16)"{white} ANGRIFF {down}{left:9} INVENTAR";:M=2
284 PRINTDD$;SPC(16)"{down:2}         {down}{left:9}         ";
285 IFPM%(C)>0THENPRINTDD$;SPC(16)"{down:2} MAGIE   ";:M=3
286 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(37)"  {down}{left:2}  ";
287 PRINT"{left:3}{up}"PD$(C);
288 MY=0:FZ=0
289 POKEQS+816+MY*40,35
290 GOSUB700
291 IFA$="S"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY+1:GOTO298
292 IFA$="W"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY-1:GOTO298
293 IFA$=CHR$(13)THENFZ=0:GOTO301
294 IFA$="FR"ANDCR=14ORCR=15ORCR=16THENPRINT"{home}{down}{right}{white}FLUCHT NICHT MOEGLICH                 ":GOSUB743:GOSUB514:GOSUB745:GOTO290
295 IFA$="FR"ANDFZ=3THENFZ=0:GOTO59
296 IFA$="FR"ANDFZ<=3THENFZ=FZ+1:PRINT"{home}{down}{right}{white}FLUCHT                                ":GOSUB743:GOSUB514:GOSUB745:GOTO289
297 GOTO290
298 IFMY>=MTHENMY=0
299 IFMY<0THENMY=M-1
300 GOTO289
301 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(36)"{white}  {down}{left:2}  {left}{up}"PD$(C);
302 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
303 ONMY+1GOTO329,321
304 MT=3:MX=16:MY=20:MC=4:GOSUB452
305 IFIT%(IN%(M))=9THEN282
306 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         {down}{left:9}         ";
307 IFIN%(M)=0ORIT%(IN%(M))<>3THEN282
308 M=IN%(M)
309 IFIE%(M)>FM%(C)THENPRINT"{home}{down}{right}NICHT GENUG MANA                     ";:GOSUB738:GOTO282
310 GOSUB574
311 GOTO600
312 AP=IE%(M)+2-INT(RND(0)*5):FM%(C)=FM%(C)-IE%(M):PM%(C)=FM%(C):GOSUB441
313 AP=AP+PL%(C)*3
314 FH%(CE)=FH%(CE)-AP:IFFH%(CE)<0THENFH%(CE)=0
315 IFCE<4THENPH%(CE)=FH%(CE)
316 IFFH%(CE)>0THENGOTO351
317 IFFT%(CE)<>-2THENCC=CC-1:WX=WX+MX%(FT%(CE))
318 IFFT%(CE)=-2THENPP=PP-1
319 FT%(CE)=-1
320 GOTO351
321 MT=2:MX=16:MY=20:MC=4:GOSUB452
322 IFIT%(IN%(M))=9THEN282
323 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
324 PRINT"{down}{left:9}         ";
325 IFIN%(M)=0ORIT%(IN%(M))<>MTTHEN282
326 IFIN%(M)=3THENGOSUB200:IN%(M)=0:GOSUB441:GOTO272
327 IFIN%(M)=8THENGOSUB202:IN%(M)=0:GOSUB441:GOTO272
328 GOTO272
329 GOSUB574
330 AP=FA%(C)-FD(CE)
331 IFAP<=0THENPRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{white}MISS";:GOSUB736:GOTO343
332 AP=INT(RND(0)*AP)+1
333 FH%(CE)=FH%(CE)-AP:IFFH%(CE)<0THENFH%(CE)=0
334 IFCE<4THENPH%(CE)=FH%(CE)
335 IFFH%(CE)>0THENGOTO340
336 IFFT%(CE)<>-2THENCC=CC-1:WX=WX+MX%(FT%(CE))
337 IFFT%(CE)=-2THENPP=PP-1
338 FT%(CE)=-1
339 GOTO340
340 POKEV,(FX%(CE)*8+21)AND255:POKEV+1,FY%(CE)*8+50:POKE50168,16:POKEV+28,0
341 POKEV+16,-(FX%(CE)>30):POKEV+21,1
342 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{white}";AP
343 FORI=0TO5:POKEV,PEEK(V)+2:POKEV+1,PEEK(V+1)+2:GOSUB737:NEXTI
344 POKEV+21,0:IFCE<4THENGOSUB441
345 GOSUB736
346 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
347 IFFT%(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE));"  {down}{left:2}  ";
348 IFCC=0THEN374
349 IFPP=0THEN402
350 GOTO272
351 POKEV,(FX%(CE)*8+24)AND255:POKEV+1,FY%(CE)*8+50
352 IFM=4THENPOKEV+37,8:POKEV+38,2:POKEV+39,7:POKEV+28,1
353 B=17:IFM=6THENB=19:POKEV+28,0
354 IFM=5THENB=21:POKEV+28,0
355 IFM=7THENB=23:POKEV+28,0
356 POKEV+16,-(FX%(CE)>30):POKE50168,B:POKEV+21,1
357 PRINT"{white}";:IFM=9THENPRINT"{green}";
358 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}";AP
359 FORK=0TO3:FORI=0TO1:POKE50168,B+I:FORJ=1TO40:NEXTJ,I,K
360 POKEV+21,0:IFCE<4THENGOSUB441
361 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
362 IFFT%(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE));"  {down}{left:2}  ";
363 IFCC=0THEN374
364 IFPP=0THEN402
365 GOTO272
366 GOSUB736
367 PRINT"{white}{home}{down}{right}"M$(FT%(C))": GREIFT AN                    "
368 FC%(C)=FC%(C)-100
369 IFFT%(C)=7THENIFFM%(C)>=IE%(9)THENM=9:GOSUB736:GOSUB411:GOTO603
370 IFFT%(C)=9THENIFFM%(C)>=IE%(6)THENM=6:GOSUB736:GOSUB405:GOTO312
371 IFFT%(C)=10THENIFFM%(C)>=IE%(4)THENM=4:GOSUB736:GOSUB405:GOTO312
372 IFFT%(C)=11THENIFFM%(C)>=IE%(12)THENM=12:GOSUB736:GOSUB405:GOTO610
373 GOSUB736:GOSUB405:GOTO330
374 POKE1020,Q1%
375 PRINTDD$;"{brown}{up}{$c1}{$c2:10}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}{white}";
376 PRINT"{home}{down}{right}DU HAST GEWONNEN                     ";:GOSUB558
377 GOSUB654
378 FORI=0TO3:IFP3%(I)=0THEN382
379 PRINT"{home}{white}{right:3}";:IFPH%(I)=0THENPRINT"{red}";
380 PRINTLEFT$(CD$,4+I*3)P$(I)
381 PRINT"{home}"LEFT$(CD$,5+I*3)"{right:3}LV";PL%(I)+1;" EX";PX%(I)
382 NEXT
383 FORI=0TO3:IFPH%(I)=0ORP3%(I)=0THEN393
384 TX=WX
385 PRINT"{home}{white}"LEFT$(CD$,5+I*3)SPC(20)"EXPERIENCE {left}"TX"{left} ";
386 IFTX=0THEN393
387 XS=1:IFTX>9THENXS=10
388 IFTX>99THENXS=100
389 TX=TX-XS
390 PX%(I)=PX%(I)+XS
391 LL=10+PL%(I)*PL%(I)*10:IFPX%(I)>=LLTHENGOSUB395:GOTO391
392 PRINT"{home}{white}"LEFT$(CD$,5+I*3)"{right:3}LV";PL%(I)+1;" EX";PX%(I):GOTO385
393 NEXTI
394 GOTO400
395 PL%(I)=PL%(I)+1:PA%(I)=PA%(I)+1:PD%(I)=PD%(I)+1:PS%(I)=PS%(I)+1
396 P1%(I)=P1%(I)+PL%(I)*2:IFP2%(I)>0THENP2%(I)=P2%(I)+PL%(I)*3
397 IFP1%(I)>999THENP1%(I)=999
398 IFP2%(I)>999THENP2%(I)=999
399 RETURN
400 GOSUB729
401 GOTO59
402 PRINT"{home}{down}{right}{white}DU HAST VERLOREN                     "
403 GOSUB729
404 GOTO618
405 RE=INT(RND(0)*4):CE=0
406 IFFT%(CE)=-2ANDPH%(CE)>0THEN409
407 CE=CE+1:IFCE=4THENCE=0
408 GOTO406
409 IFRE>0THENRE=RE-1:GOTO407
410 RETURN
411 RE=INT(RND(0)*8):CE=4
412 IFFT%(CE)>=0THEN415
413 CE=CE+1:IFCE=12THENCE=4
414 GOTO412
415 IFRE>0THENRE=RE-1:GOTO413
416 RETURN
417 IFFF>0THENL=FF:GOTO419
418 L=PEEK(CP+164):IFL=0THENRETURN
419 IFL=3THENA=0:LC=INT(RND(0))*2+2:FORI=0TOLC:GOSUB552:NEXT:RETURN
420 IFL=1THENA=1:FORI=0TO2:GOSUB552:NEXT:RETURN
421 IFL=2THENA=1:FORI=0TO1:GOSUB552:NEXT:A=2:FORI=0TO1:GOSUB552:NEXT:RETURN
422 IFL=4THENA=3:GOSUB552:A=1:GOSUB552:GOSUB552:RETURN
423 IFL=5THENA=4:LC=INT(RND(0)*3)+1:FORI=0TOLC:GOSUB552:NEXT:RETURN
424 IFL=6THENONINT(RND(1)*2)+1GOTO434,435
425 IFL=7THENLC=INT(RND(0)*3)+1:A=6:FORI=0TOLC:GOSUB552:NEXT:RETURN
426 IFL=8THENA=7:GOSUB552:LC=INT(RND(0)*2)+1:A=6:GOSUB552:RETURN
427 IFL=9THENA=9:GOSUB552:A=8:GOSUB552:A=5:GOSUB552:RETURN
428 IFL=10THENA=6:LC=INT(RND(0)*2)+1:FORI=1TOLC:GOSUB552:NEXT:RETURN
429 IFL=11THENA=7:GOSUB552:A=10:GOSUB552:RETURN
430 IFL=12ANDINT(RND(1)*2)=1THENA=11:GOSUB552:RETURN
431 IFL=12THENA=5:GOSUB552:GOSUB552:A=9:GOSUB552:RETURN
432 IFL=13THENA=9:GOSUB552:A=12:GOSUB552:A=9:GOSUB552:RETURN
433 IFL=14THENA=9:GOSUB552:A=13:GOSUB552:A=7:GOSUB552:RETURN
434 A=5:LC=INT(RND(1)*2)+1:FORI=0TOLC:GOSUB552:NEXT:RETURN
435 A=9:LC=INT(RND(1)*2)+1:FORI=1TOLC:GOSUB552:NEXT:RETURN
436 FORI=0TO3
437 IFP3%(I)=0THEN440
438 PH%(I)=P1%(I)
439 PM%(I)=P2%(I)
440 NEXT:RETURN
441 PRINTDD$;
442 FORI=0TO3:IFP3%(I)=0THENPRINTSPC(40);:GOTO448
443 PRINT"{white}";:IFPH%(I)=0THENPRINT"{red}";
444 PRINTSPC(27)P$(I)" ";:A=PH%(I):GOSUB698
445 PRINTD$;
446 A=PM%(I):GOSUB698
447 PRINT" "D$"{right}";
448 NEXT:RETURN
449 FORI=0TO3:IFPH%(I)<=0ORP3%(I)=0THENNEXT:RETURN
450 PRINT"{home}"LEFT$(CD$,5+I*3);SPC(37);PD$(I):NEXT
451 RETURN
452 GOSUB487
453 MI=-1:M=0:MO=0
454 MI=MO-1:CY=MY:MM=0
455 IFMC=4THENGOSUB482
456 MI=MI+1
457 PRINT"{home}{white}"LEFT$(CD$,CY)SPC(MX)" ";
458 IFIT%(IN%(MI))<>MTTHENPRINT"{cyan}";
459 IFIT%(IN%(MI))=9THENPRINT"{white}";
460 IFIT%(IN%(MI))=-1THENPRINT"{cyan}";
461 PRINTI$(IN%(MI));
462 CY=CY+1
463 MM=MM+1
464 IFMM=MCTHEN466
465 IFMI<13THEN456
466 CP=QS+MX+MY*40
467 POKECP+(M-MO)*40,35
468 GOSUB700
469 POKECP+(M-MO)*40,32
470 IFA$="W"THENM=M-1:GOTO474
471 IFA$="S"THENM=M+1:GOTO474
472 IFA$=CHR$(13)THENRETURN
473 GOTO467
474 IFM=-1THENM=14-1:MO=14-MC:GOTO454
475 IFM=14THENM=0:MO=0:GOTO454
476 M%=0
477 IFM<MOTHENMO=MO-MC:M%=1:GOTO477
478 IFM>=MO+MCTHENMO=MO+MC:M%=1
479 IFMO<0THENMO=0:M%=1
480 IFM%=1THEN454
481 GOTO467
482 FORI=0TO3
483 PRINT"{home}{white}"LEFT$(CD$,CY+I)SPC(MX)" ";
484 PRINT"        ";
485 NEXTI
486 RETURN
487 A=0
488 FORI=0TO99:TT%(I)=0:NEXTI
489 FORI=0TO99:TT%(I)=IN%(I):IN%(I)=0:NEXTI
490 IFMT=0THENGOSUB495:GOSUB498:A=50:GOSUB501:GOSUB504:GOSUB507
491 IFMT=1THENGOSUB495:GOSUB501:A=50:GOSUB498:GOSUB504:GOSUB507
492 IFMT=2THENGOSUB495:GOSUB504:A=50:GOSUB498:GOSUB501:GOSUB507
493 IFMT=3THENGOSUB495:GOSUB507:A=50:GOSUB498:GOSUB501:GOSUB504
494 RETURN
495 FORI=0TO99:IFIT%(TT%(I))=9THENIN%(A)=TT%(I):A=A+1
496 NEXTI
497 RETURN
498 FORI=0TO99:IFIT%(TT%(I))=0THENIN%(A)=TT%(I):A=A+1
499 NEXTI
500 RETURN
501 FORI=0TO99:IFIT%(TT%(I))=1THENIN%(A)=TT%(I):A=A+1
502 NEXTI
503 RETURN
504 FORI=0TO99:IFIT%(TT%(I))=2THENIN%(A)=TT%(I):A=A+1
505 NEXTI
506 RETURN
507 FORI=0TO99:IFIT%(TT%(I))=3THENIN%(A)=TT%(I):A=A+1
508 NEXTI
509 RETURN
510 PRINT"{brown}{home}{down:3}";
511 FORI=0TO15
512 PRINT"                                        ";:NEXT
513 RETURN
514 PRINT"{home}{down}{right}{white}                                      ";
515 RETURN
516 PRINT"{brown}{home}{down:3}{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
517 FORI=0TO5
518 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
519 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
520 PRINT"{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
521 FORI=0TO5
522 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
523 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
524 DX=3:DY=4:P=0:GOSUB527:DX=23:P=1:GOSUB527
525 DX=3:DY=12:P=2:GOSUB527:DX=23:P=3:GOSUB527
526 RETURN
527 IFP3%(P)=0THENRETURN
528 PRINT"{home}{white}";:IFPH%(P)=0THENPRINT"{red}";
529 PRINT"{home}"LEFT$(CD$,DY)SPC(DX-1)" "P$(P)"  LV"PL%(P)+1
530 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX)" "I$(PW%(P));
531 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX)" "I$(PR%(P));
532 T1%=PA%(P)+IA%(PW%(P))
533 T2%=PD%(P)+ID%(PR%(P))
534 IFT1%>999THENT1%=999
535 IFT2%>999THENT2%=999
536 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX+9)"ATK"T1%;
537 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX+9)"DEF"T2%;
538 RETURN
539 IFPEEK(2)=1THENSP=30960+RS*(CR-0)
540 FORI=0TO7
541 PRINT"{home}{down:3}"LEFT$(CD$,I*2);
542 FORJ=0TO19
543 PRINTT$(PEEK(SP))"{up}";
544 SP=SP+1
545 NEXTJ,I
546 RETURN
547 IS=0
548 IFIN%(IS)=0THENIN%(IS)=NI%:NI%=-1:RETURN
549 IS=IS+1
550 IFIS=99THENRETURN
551 GOTO548
552 IFCC>=11THENRETURN
553 FH%(CC)=MH%(A):FM%(CC)=MM%(A):FT%(CC)=A:FC%(CC)=INT(RND(0)*MS%(A)):FS%(CC)=MS%(A)
554 FX%(CC)=2:FY%(CC)=5+CC*3:FA%(CC)=MA%(A):FD(CC)=MD%(A):FB(CC)=MB%(A)
555 F1%(CC)=MH%(A):F2%(CC)=MM%(A)
556 IFCC>=4THENFX%(CC)=5:FY%(CC)=5+(CC-4)*3
557 CC=CC+1:RETURN
558 ZM=0
559 FORJ=4TO7
560 IFFT%(J)=-1THENZM=ZM-1
561 ZM=ZM+1
562 NEXTJ
563 IFZM<>CMTHENPRINTDD$;"{right}          {down}{left:10}          {down}{left:10}          {down}{left:10}          ";
564 PRINTDD$;
565 CM=0
566 FORI=4TO7
567 IFFT%(I)=-1THENNEXT:RETURN
568 A=FH%(I):GOSUB698
569 PRINT"{home}{right}"LEFT$(CD$,20+CM)M$(FT%(I));
570 PRINT"{home}{right}"LEFT$(CD$,20+CM);"{right:7}";D$
571 CM=CM+1
572 NEXT
573 RETURN
574 CE=4:PRINT"{home}{down}{right}{white}"P$(C)": WAHELE DEIN ZIEL               "
575 IFFT%(CE)=-1THEN584
576 POKEQS-2+FX%(CE)+FY%(CE)*40,35
577 GOSUB700
578 IFA$=CHR$(13)THENPOKEQS-2+FX%(CE)+FY%(CE)*40,32:RETURN
579 IFA$="S"THENGOTO584
580 IFA$="W"THENGOTO592
581 IFA$="D"ANDCE>3THENPOKEQS-2+FX%(CE)+FY%(CE)*40,32:CE=0:GOTO575
582 IFA$="A"ANDCE<4THENPOKEQS-2+FX%(CE)+FY%(CE)*40,32:CE=4:GOTO575
583 GOTO577
584 CO=CE
585 POKEQS-2+FX%(CE)+FY%(CE)*40,32
586 CE=CE+1
587 IFCE>=12THENCE=0
588 IFCO<4ANDCE>3THENCE=CO
589 IFCO>3ANDCE<4THENCE=CO
590 IFFT%(CE)<>-1THEN576
591 GOTO586
592 CO=CE
593 POKEQS-2+FX%(CE)+FY%(CE)*40,32
594 CE=CE-1
595 IFCE<0THENCE=11
596 IFCO<4ANDCE>3THENCE=CO
597 IFCO>3ANDCE<4THENCE=CO
598 IFFT%(CE)<>-1THEN576
599 GOTO594
600 IFFT%(C)=-2THEN616
601 IFFT%(C)=7ANDFM%(C)>=IE%(9)THENM=9:GOTO603
602 GOTO312
603 AP=IE%(M)*4+2-INT(RND(0)*5):FM%(C)=FM%(C)-IE%(M):IFC<4THENPM%(C)=FM%(C):GOSUB441
604 FH%(CE)=FH%(CE)+AP:IFFH%(CE)>F1%(CE)THENFH%(CE)=F1%(CE)
605 IFCE<4THENPH%(CE)=FH%(CE):GOSUB441
606 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{green}";AP
607 GOSUB743
608 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
609 GOTO272
610 FM%(C)=FM%(C)-IE%(M):IFC<4THENPM%(C)=FM%(C):GOSUB441
611 FS%(CE)=FS%(CE)/2:IFFS%(CE)<5THENFS%(CE)=5
612 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}{green}SLOW";
613 GOSUB743
614 PRINT"{home}"LEFT$(CD$,FY%(CE))SPC(FX%(CE))"{up}{left}    "
615 GOTO272
616 IFM=9THEN603
617 GOTO312
618 FORI=0TO3:PX%(I)=0:NEXT
619 PA%(0)=5:PD%(0)=2:PA%(1)=4:PD%(1)=3:PA%(2)=1:PD%(2)=1:PA%(3)=1:PD%(3)=1
620 FORI=0TO13:IN%(I)=0:NEXT
621 IN%(0)=18
622 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
623 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
624 PRINT"{$c6}{$c7:38}{up:3}";
625 POKE50151,72:POKE56295,9
626 X=15:Y=2:CR=0
627 P3%(0)=1:P3%(1)=0:P3%(2)=0:P3%(3)=0
628 P1%(0)=15:P1%(1)=17:P1%(2)=9:P1%(3)=9:P2%(0)=0:P2%(1)=0:P2%(2)=7:P2%(3)=23
629 FORI=0TO3:PH%(I)=P1%(I):PM%(I)=P2%(I):PX%(I)=0:PW%(I)=0:PR%(I)=0:NEXT
630 PW%(0)=1:PR%(0)=2
631 GOSUB131
632 AA$="INTRO":GOSUB686
633 GOTO59
634 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
635 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
636 PRINT"{$c6}{$c7:38}{up:3}";
637 POKE50151,72:POKE56295,9
638 AA$="ENDE":GOSUB686
639 GOTO59
640 PRINT"{brown}{home}{$c1}{$c2:38}{$c3}";
641 PRINT"{$c4}                                      {$c5}";
642 PRINT"{$c6}{$c7:38}{$c8}"
643 RETURN
644 PRINTDD$;"{brown}{up}{$c1}{$c2:7}{white}HP{brown}{$c2}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
645 FORI=0TO3:PRINT"{$c4}          {$c5}{$c4}            {$c5}{$c4}{right:12}{$c5}";:NEXT
646 PRINT"{$c6}{$c7:10}{$c8}{$c6}{$c7:12}{$c8}{$c6}{$c7:12}";
647 POKE50151,72:POKE56295,9
648 RETURN
649 PRINTDD$;"{brown}{up}{$c1}{$c2:24}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
650 FORI=0TO3:PRINT"{$c4}                        {$c5}{$c4}{right:12}{$c5}";:NEXT
651 PRINT"{$c6}{$c7:24}{$c8}{$c6}{$c7:12}";
652 POKE50151,72:POKE56295,9
653 RETURN
654 PRINT"{brown}{home}{down:3}{$c1}{$c2:38}{$c3}";
655 FORI=0TO13
656 PRINT"{$c4}                                      {$c5}";:NEXT
657 PRINT"{$c6}{$c7:38}{$c8}";:RETURN
658 POKE56322,224
659 IFAA$="TXT.WELCOME.LENA"THENOPEN1,8,4,"TXT.WELCOME.LENA,S,R"
660 IFAA$="TXT.WELCOME.DOLM"THENOPEN1,8,4,"TXT.WELCOME.DOLM,S,R"
661 IFAA$="TXT.WELCOME.MIRA"THENOPEN1,8,4,"TXT.WELCOME.MIRA,S,R"
662 IFAA$=""THENRETURN
663 I=0:SB$(0)="":SB$(1)="":SB$(2)=""
664 INPUT#1,SB$(I)
665 IFST=0THENI=I+1:GOTO664
666 CLOSE1
667 POKE56322,225
668 PRINT"{home}{right}{down:20}"+"                        "
669 PRINT"{home}{right}{down:21}"+"                        "
670 PRINT"{home}{right}{down:22}"+"                        "
671 PRINT"{home}{right}{down:23}"+"                        "
672 PRINT"{home}{right}{down:20}{white}"+SB$(0)
673 PRINT"{home}{right}{down:21}{white}"+SB$(1)
674 PRINT"{home}{right}{down:22}{white}"+SB$(2)
675 PRINT"{home}{right}{down:23}{white}"+SB$(3)
676 GOSUB729
677 PRINT"{home}{right}{down:20}"+"                        "
678 PRINT"{home}{right}{down:21}"+"                        "
679 PRINT"{home}{right}{down:22}"+"                        "
680 PRINT"{home}{right}{down:23}"+"                        "
681 RETURN
682 PRINT"{home}{down}{white}{right}";VA$
683 GOSUB729
684 GOSUB514
685 RETURN
686 POKE1020,Q7%
687 POKE56322,224
688 IFAA$="INTRO"THENOPEN1,8,4,"TXT.INTRO,S,R"
689 IFAA$="ENDE"THENOPEN1,8,4,"TXT.ENDE,S,R"
690 IFAA$=""THENRETURN
691 GET#1,B$
692 IFB$="$"THENPRINT"{home}{down:2}{right:2}{white}";:GOTO691
693 IFB$="%"THENPRINT"{right:2}{white}";:GOTO691
694 IFB$="#"THENCLOSE1:POKE56322,255:GOSUB729:RETURN
695 PRINTB$;
696 IFB$<>" "THENPOKE162,0:WAIT162,4
697 GOTO691
698 T$=STR$(A)
699 D$=RIGHT$("000"+RIGHT$(T$,LEN(T$)-1),3):RETURN
700 A$=""
701 J=PEEK(56320)
702 IFJ=127THEN705
703 IFJ=107ORJ=103ORJ=110ORJ=109THEN705
704 GOTO701
705 J=PEEK(56320)
706 IFJ=127ANDA$=CHR$(13)THENRETURN
707 IFJ=107THENA$="FL":GOSUB734:RETURN
708 IFJ=103THENA$="FR":GOSUB734:RETURN
709 IFJ=110THENA$="FU":GOSUB734:RETURN
710 IFJ=109THENA$="FD":GOSUB734:RETURN
711 IFJ=123THENA$="A":GOSUB734:RETURN
712 IFJ=119THENA$="D":GOSUB734:RETURN
713 IFJ=126THENA$="W":GOSUB734:RETURN
714 IFJ=125THENA$="S":GOSUB734:RETURN
715 IFJ=111THENA$=CHR$(13)
716 GOTO705
717 A$=""
718 J=PEEK(56320)
719 IFJ<>111ANDJ<>107ANDJ<>103ANDJ<>110ANDJ<>109THEN721
720 GOTO718
721 J=PEEK(56320)
722 IFJ=127ANDA$=CHR$(13)THENRETURN
723 IFJ=123THENA$="A":GOSUB735:RETURN
724 IFJ=119THENA$="D":GOSUB735:RETURN
725 IFJ=126THENA$="W":GOSUB735:RETURN
726 IFJ=125THENA$="S":GOSUB735:RETURN
727 IFJ=111THENA$=CHR$(13)
728 GOTO721
729 A$=""
730 J=PEEK(56320)
731 IFJ=127ANDA$=CHR$(13)THENRETURN
732 IFJ=111THENA$=CHR$(13)
733 GOTO730
734 POKE162,0:WAIT162,16:RETURN
735 POKE162,0:WAIT162,8:RETURN
736 POKE162,0:WAIT162,32:RETURN
737 POKE162,0:WAIT162,2:RETURN
738 POKE162,0:WAIT162,32
739 POKE162,0:WAIT162,32
740 POKE162,0:WAIT162,32
741 POKE162,0:WAIT162,32
742 RETURN
743 POKE162,0:WAIT162,32
744 RETURN
745 POKE162,0:WAIT162,4
746 RETURN
747 DATA"IMP",5,0,8,4,1,0,3
748 DATA"SNAKE",8,0,12,6,1,0,5
749 DATA"VIPER",12,0,18,6,1,0,9
750 DATA"NACHO",30,0,22,15,5,0,50
751 DATA"GOLEM",20,0,4,20,8,0,17
752 DATA"MOSS",30,0,7,26,9,0,21
753 DATA"BONES",50,0,20,18,8,0,15
754 DATA"WISP",25,50,15,0,8,0,17
755 DATA"TROLL",100,0,9,40,13,0,100
756 DATA"SPARK",30,50,22,0,8,0,17
757 DATA"BURNER",80,90,30,25,13,0,42
758 DATA"TBERRY",120,10,60,60,20,0,150
759 DATA"DRACUL",250,60,50,70,25,0,500
760 DATA"GLADOS",500,70,60,80,30,0,1000
761 DATA"KRON",15 ,0  ,10 ,4 ,3 ,0 ,0
762 DATA"LENA",17 ,0  ,10 ,3 ,3 ,0 ,0
763 DATA"DOLM",9  ,7  ,8  ,1 ,1 ,0 ,0
764 DATA"MIRA",9  ,23 ,7  ,1 ,1 ,1 ,2
765 DATA"{gray1}{rvrs off}{$c9}{rvrs off}{gray1}{$ca}{down}{left:2}{rvrs off}{gray1}{$ca}{rvrs off}{gray1}{$c9}{rvrs off}"
766 DATA"{gray1}{rvrs off}{$cb}{rvrs off}{gray1}{$cc}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1}{$ca}{rvrs off}"
767 DATA"{gray1}{rvrs off}{$c9}{rvrs off}{gray1}{$ca}{down}{left:2}{rvrs off}{gray1}{$ca}{rvrs off}{gray1}{$c9}{rvrs off}"
768 DATA"{gray1}{rvrs off}{$cd}{rvrs off}{gray1}{$ce}{down}{left:2}{rvrs off}{gray1}{$cf}{rvrs off}{gray1}{$d0}{rvrs off}"
769 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
770 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
771 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
772 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
773 DATA"{orange}{rvrs off}{$d1}{rvrs off}{orange}{$d1}{down}{left:2}{rvrs off}{orange}{$d1}{rvrs off}{orange}{$d1}{rvrs off}"
774 DATA"{brown}{rvrs off}{$d2}{rvrs off}{brown}{$d3}{down}{left:2}{rvrs off}{brown}{$d4}{rvrs off}{brown}{$d5}{rvrs off}"
775 DATA"{lt. red}{rvrs off}{$d6}{rvrs off}{lt. red}{$d7}{down}{left:2}{rvrs off}{lt. red}{$d8}{rvrs off}{lt. red}{$d9}{rvrs off}"
776 DATA"{gray1}{rvrs off}{$da}{rvrs off}{gray1}{$db}{down}{left:2}{rvrs off}{gray1}{$dc}{rvrs off}{gray1}{$dd}{rvrs off}"
777 DATA"{gray1}{rvrs off}{$a2}{rvrs off}{gray1}{$a3}{down}{left:2}{rvrs off}{gray1}{$a4}{rvrs off}{gray1}{$a5}{rvrs off}"
778 DATA"{brown}{rvrs off}{$a1}{rvrs off}{brown}{$a1}{down}{left:2}{rvrs off}{brown}{$a1}{rvrs off}{brown}{$a1}{rvrs off}"
779 DATA"{gray1}{rvrs off}{$de}{rvrs off}{gray1}{$df}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1} {rvrs off}"
780 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
781 DATA"{lt. red}{rvrs off}{$a6}{rvrs off}{lt. red}{$a7}{down}{left:2}{rvrs off}{lt. red}{cmdr pound}{rvrs off}{lt. red}{shft pound}{rvrs off}"
782 DATA"{lt. red}{rvrs off}{$aa}{rvrs off}{lt. red}{$aa}{down}{left:2}{rvrs off}{lt. red}{$aa}{rvrs off}{lt. red}{$aa}{rvrs off}"
783 DATA"{brown}{rvrs off}{$ab}{rvrs off}{brown}{$ab}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
784 DATA"{brown}{rvrs off}{$ab}{rvrs off}{brown}{$ad}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
785 DATA"{brown}{rvrs off}{$ad}{rvrs off}{brown}{$ab}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
786 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
787 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
788 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
789 DATA"{lt. green}{rvrs off}{$ae}{rvrs off}{lt. green}{$d1}{down}{left:2}{rvrs off}{lt. green}{$d1}{rvrs off}{lt. green}{$af}{rvrs off}"
790 DATA"{lt. green}{rvrs off}{$b0}{rvrs off}{lt. green}{$b1}{down}{left:2}{rvrs off}{lt. green}{$b2}{rvrs off}{lt. green}{$b3}{rvrs off}"
791 DATA"{lt. green}{rvrs off}{$b4}{rvrs off}{lt. green}{$b5}{down}{left:2}{rvrs off}{lt. green}{$b4}{rvrs off}{lt. green}{$b5}{rvrs off}"
792 DATA"{lt. green}{rvrs off}{$b6}{rvrs off}{lt. green}{$b7}{down}{left:2}{rvrs off}{lt. green}{$b8}{rvrs off}{lt. green}{$b9}{rvrs off}"
793 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
794 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
795 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
796 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
797 DATA"{brown}{rvrs off}{$ae}{rvrs off}{brown}{$d1}{down}{left:2}{rvrs off}{brown}{$d1}{rvrs off}{brown}{$af}{rvrs off}"
798 DATA"{brown}{rvrs off}{$b0}{rvrs off}{brown}{$b1}{down}{left:2}{rvrs off}{brown}{$b2}{rvrs off}{brown}{$b3}{rvrs off}"
799 DATA"{brown}{rvrs off}{$b4}{rvrs off}{brown}{$b5}{down}{left:2}{rvrs off}{brown}{$b4}{rvrs off}{brown}{$b5}{rvrs off}"
800 DATA"{brown}{rvrs off}{$b6}{rvrs off}{brown}{$b7}{down}{left:2}{rvrs off}{brown}{$b8}{rvrs off}{brown}{$b9}{rvrs off}"
801 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
802 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
803 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
804 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
805 DATA"{gray1}{rvrs off}{$d1}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$d1}{rvrs off}{gray1}{$d1}{rvrs off}"
806 DATA"{gray1}{rvrs off}{$ba}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$ba}{rvrs off}{gray1}{$d1}{rvrs off}"
807 DATA"{gray1}{rvrs off}{$ba}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$bb}{rvrs off}{gray1}{$d1}{rvrs off}"
808 DATA"{gray1}{rvrs off}{$bc}{rvrs off}{gray1}{$bd}{down}{left:2}{rvrs off}{gray1}{$be}{rvrs off}{gray1}{$be}{rvrs off}"
809 DATA"{gray1}{rvrs off}{$bf}{rvrs off}{gray1}{$bf}{down}{left:2}{rvrs on}{gray1}@{rvrs on}{gray1}@{rvrs off}"
810 DATA"{gray2}{rvrs on}A{rvrs on}{gray2}B{down}{left:2}{rvrs on}{gray2}C{rvrs on}{gray2}D{rvrs off}"
811 DATA"{gray1}{rvrs on}E{rvrs on}{gray1}F{down}{left:2}{rvrs on}{gray1}G{rvrs on}{gray1}H{rvrs off}"
812 DATA"{brown}{rvrs on}I{rvrs on}{brown}I{down}{left:2}{rvrs on}{brown}I{rvrs on}{brown}I{rvrs off}"
813 DATA"{lt. red}{rvrs on}J{rvrs on}{lt. red}K{down}{left:2}{rvrs on}{lt. red}L{rvrs on}{lt. red}M{rvrs off}"
814 DATA"{gray1}{rvrs on}N{rvrs on}{gray1}K{down}{left:2}{rvrs on}{gray1}L{rvrs on}{gray1}M{rvrs off}"
815 DATA"{lt. green}{rvrs on}O{rvrs on}{lt. green}P{down}{left:2}{rvrs on}{lt. green}Q{rvrs on}{lt. green}R{rvrs off}"
816 DATA"{gray2}{rvrs on}S{rvrs on}{gray2}T{down}{left:2}{rvrs on}{gray2}U{rvrs on}{gray2}V{rvrs off}"
817 DATA"{gray1}{rvrs on}W{rvrs on}{gray1}X{down}{left:2}{rvrs on}{gray1}Y{rvrs on}{gray1}Z{rvrs off}"
818 DATA"{gray1}{rvrs on}[{rvrs on}{gray1}{pound}{down}{left:2}{rvrs on}{gray1}]{rvrs on}{gray1}{^}{rvrs off}"
819 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
820 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
821 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
822 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
823 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
824 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
825 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
826 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
827 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
828 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
829 DATA"{lt. green}{rvrs on}{back arrow}{rvrs on}{lt. green}!{down}{left:2}{rvrs on}{lt. green}#{rvrs on}{lt. green}${rvrs off}"
830 DATA"{gray3}{rvrs on}%{rvrs on}{gray3}&{down}{left:2}{rvrs on}{gray3}'{rvrs on}{gray3}({rvrs off}"
831 DATA"{gray1}{rvrs on}%{rvrs on}{gray1}&{down}{left:2}{rvrs on}{gray1}'{rvrs on}{gray1}({rvrs off}"
832 DATA"{lt. green}{rvrs on}){rvrs on}{lt. green}*{down}{left:2}{rvrs on}{lt. green}+{rvrs on}{lt. green},{rvrs off}"
833 DATA"{gray1}{rvrs on}-{rvrs on}{gray1}.{down}{left:2}{rvrs on}{gray1}/{rvrs on}{gray1}0{rvrs off}"
834 DATA"{gray1}{rvrs on}1{rvrs on}{gray1}2{down}{left:2}{rvrs on}{gray1}3{rvrs on}{gray1}4{rvrs off}"
835 DATA"{gray1}{rvrs on}5{rvrs on}{gray1}6{down}{left:2}{rvrs on}{gray1}7{rvrs on}{gray1}8{rvrs off}"
836 DATA"{gray1}{rvrs on}9{rvrs on}{gray1}:{down}{left:2}{rvrs on}{gray1};{rvrs on}{gray1}<{rvrs off}"
837 DATA"{gray1}{rvrs on}={rvrs on}{gray1}>{down}{left:2}{rvrs on}{gray1}?{rvrs on}{gray1}{$c0}{rvrs off}"
838 DATA"{lt. green}{rvrs on}9{rvrs on}{lt. green}:{down}{left:2}{rvrs on}{lt. green};{rvrs on}{lt. green}<{rvrs off}"
839 DATA"{lt. red}{rvrs on}5{rvrs on}{lt. red}6{down}{left:2}{rvrs on}{lt. red}7{rvrs on}{lt. red}8{rvrs off}"
840 DATA"{lt. green}{rvrs on}5{rvrs on}{lt. green}6{down}{left:2}{rvrs on}{lt. green}7{rvrs on}{lt. green}8{rvrs off}"
841 DATA"{gray1}{rvrs on}{$c1}{rvrs on}{gray1}{$c2}{down}{left:2}{rvrs on}{gray1}{$c3}{rvrs on}{gray1}{$c4}{rvrs off}"
842 DATA"{gray1}{rvrs on}{$c5}{rvrs on}{gray1}{$c6}{down}{left:2}{rvrs on}{gray1}{$c7}{rvrs on}{gray1}{$c8}{rvrs off}"
843 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
844 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
845 DATA"        ",00,00,00,00,-1 
846 DATA"DOLCH   ",03,00,00,00,00  
847 DATA"HELM    ",00,02,-1,00,01  
848 DATA"KRAEUTER",00,00,-1,10,02  
849 DATA"FEUER   ",00,00,00,05,03  
850 DATA"EIS     ",00,00,00,08,03  
851 DATA"MUT     ",00,00,00,23,03  
852 DATA"BOMBE   ",00,00,00,50,03  
853 DATA"KIRSCHE ",00,00,-1,15,02  
854 DATA"HEILEN  ",00,00,00,04,03  
855 DATA"SKULL   ",00,00,00,00,04  
856 DATA"FLEGEL  ",07,00,-1,00,00  
857 DATA"LAHM    ",00,00,00,04,03  
858 DATA"STAB    ",12,00,00,00,00  
859 DATA"FEDER   ",00,10,00,00,01  
860 DATA"STOCK   ",23,00,00,00,00  
861 DATA"WESTE   ",00,14,00,00,01  
862 DATA"SCHILD  ",00,24,00,00,01  
863 DATA"ZURUECK ",00,00,00,0 ,09  
864 END
