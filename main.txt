0 POKE55,240:POKE56,120:CLR
1 QO=53282
2 QP=53283
3 QQ=53281
4 QR=53280
5 QS=49152
6 V=53248
7 QT=30960
8 Q1=0
9 Q2=1
10 Q3=2
11 Q4=3
12 Q5=4
13 Q6=5
14 Q7=6
15 Q8=7
16 Q9=8
17 QA=9
18 QB=10
19 CB=11
20 QC=12
21 QD=13
22 QE=14
23 QF=15
24 QG=8
25 QH=9
26 QI=10
27 QJ=11
28 QK=12
29 QL=13
30 QM=14
31 QN=15
32 MQ=14
33 IC=19
34 HC=14
35 EC=12
36 IX=99
37 TM=80
38 RS=165
39 DD$="{home}{down:20}{white}"
40 CD$="{down:25}"
41 CT$="{home}{down}{white}{right}                                      "
42 DIMM$(MQ),MD$(MQ)
43 DIMMH(MQ),MM(MQ)
44 DIMMA(MQ),MD(MQ)
45 DIMMS(MQ)
46 DIMMB(MQ),MX(MQ)
47 DIMFH(11),FT(11),FC(11)
48 DIMFM(11),FS(11),FX(11),FY(11)
49 DIMFA(11),FD(11),F1(11),F2(11)
50 FORI=0TO3:FX(I)=37:FY(I)=5+3*I:NEXT
51 DIMI$(IC),IA(IC)
52 DIMID(IC),IS(IC)
53 DIMIE(IC),IT(IC)
54 DIMH(HC)
55 DIMER(EC),EX(EC)
56 DIMEY(EC),ET(EC)
57 DIMIN(IX)
58 DIMT$(TM)
59 DIMTN$(TM)
60 DIMTT(IX)
61 FORI=0TOMQ-1
62 READM$(I),MH(I),MM(I),MS(I)
63 READMA(I),MD(I),MB(I),MX(I)
64 NEXT
65 FORI=0TO3
66 READP$(I),PH(I),PM(I),PS(I)
67 READPA(I),PD(I),PW(I),PR(I)
68 NEXT
69 FORI=0TOTM-1
70 READT$(I)
71 NEXT
72 PD$(0)=T$(48)
73 PD$(1)=T$(49)
74 PD$(2)=T$(52)
75 PD$(3)=T$(51)
76 ZZ=64
77 FORI=0TOMQ-1
78 MD$(I)=T$(ZZ)
79 ZZ=ZZ+1
80 NEXTI
81 FORI=0TOIC-1
82 READI$(I),IA(I),ID(I),IS(I),IE(I),IT(I)
83 NEXT
84 POKEV+21,0
85 POKE648,192
86 POKE56576,0
87 POKE53272,8
88 POKE53270,PEEK(53270)OR16
89 POKEQO,QA
90 POKEQP,Q1
91 POKEQR,Q1
92 POKEQQ,Q7
93 GOTO632
94 POKE1020,Q7
95 POKE1021,Q7
96 SYS820
97 GOSUB448
98 GOSUB455
99 PRINT"{home}{down}{right}{white}                                      ";
100 POKE1020,Q7
101 GOSUB553
102 IFPEEK(2)=1THENCP=30960+RS*(CR-0)
103 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)PD$(0);
104 OX=ZX:OY=ZY
105 ZX=X:ZY=Y
106 GOSUB698
107 IFA$=CHR$(145)ORA$="W"THENPRINTCT$;:ZY=Y-1:GOTO113
108 IFA$=CHR$(157)ORA$="A"THENPRINTCT$;:ZX=X-1:GOTO113
109 IFA$=CHR$(29)ORA$="D"THENPRINTCT$;:ZX=X+1:GOTO113
110 IFA$=CHR$(17)ORA$="S"THENPRINTCT$;:ZY=Y+1:GOTO113
111 IFA$=CHR$(13)THEN187
112 GOTO106
113 AA=0
114 FORI=0TOEC
115 IFER(I)=CRANDEX(I)=ZXANDEY(I)=ZYTHENAA=1
116 NEXTI
117 IFZX=-1ORZX=20ORZY=-1ORZY=8THEN129
118 C=PEEK(CP+ZX+ZY*20)
119 IFC=08THEN143
120 IFC=09THEN143
121 IFC=10THEN143
122 IFC=11THEN143
123 IFC=12THEN151
124 IFC=14THEN145
125 IFC=25THEN151
126 IFC=47THEN164
127 IFC>=48THEN155
128 IFC>7THEN104
129 PRINT"{home}"LEFT$(CD$,3+Y*2)SPC(X*2)T$(PEEK(CP+X+Y*20))
130 IFRND(1)*100>97ANDPEEK(CP+164)>0THENFF=0:GOTO263
131 X=ZX:Y=ZY
132 IFX=-1ANDPEEK(CP+160)=255THENX=0:GOTO103
133 IFX=-1THENCR=PEEK(CP+160):X=19:GOTO100
134 IFX=20ANDPEEK(CP+161)=255THENX=19:GOTO103
135 IFX=20THENCR=PEEK(CP+161):X=0:GOTO100
136 IFY=-1ANDPEEK(CP+162)=255THENY=0:GOTO103
137 IFY=-1THENCR=PEEK(CP+162):Y=7:GOTO100
138 IFY=8ANDPEEK(CP+163)=255THENY=7:GOTO103
139 IFY=8THENCR=PEEK(CP+163):Y=0:GOTO100
140 IFC=2THEN145
141 IFC=3THEN145
142 GOTO103
143 IFCR=16THENCR=16:X=0:Y=0:GOTO99
144 IFCR=16THEN662
145 IFC=14ANDCR=0ANDZX=11THENH(0)=1-H(0):GOSUB165:TX=14:TY=6:GOSUB184:GOSUB179:GOTO104
146 IFC=14ANDCR=16ANDZX=0THENH(1)=1-H(1):GOSUB165:GOSUB179:GOTO104
147 IFC=2ANDCR=0ANDX=16THENH(6)=1:GOSUB165:TX=7:TY=4:GOSUB184:GOTO103
148 IFC=2ANDCR=0ANDX=18THENH(7)=1:GOSUB165:TX=9:TY=5:GOSUB184:GOTO103
149 IFC=3ANDCR=0ANDX=1THENH(12)=1:GOSUB165:TX=3:TY=4:GOSUB184:GOTO103
150 IFC=3ANDCR=0ANDX=5THENH(13)=1:GOSUB165:TX=3:TY=4:GOSUB184:GOTO103
151 IFC=12ANDAA=0THENGOTO104
152 IFC=12ANDAA=1THENPRINT"{home}{white}{down}{right}IN DER TRUHE IST WAS!":GOSUB719:GOSUB528:GOTO104
153 IFC=25ANDAA=0THENGOTO104
154 IFC=25ANDAA=1THENPRINT"{home}{white}{down}{right}DER BAUM SIEHT INTRESSANT AUS!":GOSUB719:GOSUB528:GOTO104
155 IFCR=16THENPRINT"{white}{home}{down}{right:2}EIN HEILZAUBER IST UNTER DEM BAUM":GOSUB719:GOSUB528:GOTO104
156 IFCR=16THENPRINT"{white}{home}{down}{right:2}EIN FEUERZAUBER IST IN DEN KATAKOMBEN":GOSUB719:GOSUB528:GOTO104
157 IFC=49THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB719:H(2)=1:GOSUB165:P3(1)=1:GOTO94
158 IFC=52THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB719:H(5)=1:GOSUB165:P3(2)=1:GOTO94
159 IFC=51THENPRINT"{white}{home}{down}{right:2}ICH KOMME MIT DIR!":GOSUB719:H(9)=1:GOSUB165:P3(3)=1:GOTO94
160 IFC=47THENPRINT"{white}{home}{down}{right:2}DU KANNST HIER NICHT DURCH!":GOSUB719:FF=4:H(4)=1:GOSUB165:GOTO263
161 IFC=72THENPRINT"{white}{home}{down}{right:2}ACH WIE SUESS!":GOSUB719:FF=9:H(3)=1:GOSUB165:GOTO263
162 IFC=76THENPRINT"{white}{home}{down}{right:2}DU WILLST MICH BESIEGEN!":GOSUB719:FF=13:H(8)=1:GOSUB165:GOTO263
163 IFC=77THENPRINT"{white}{home}{down}{right:2}DU HAST MEINE ERWARTUNG UEBERTROFFEN!":GOSUB719:FF=14:H(10)=1:GOSUB165:GOTO263
164 IFC=47THENPRINT"{home}{white}{down}{right}DU BIST GEHEILT!":GOSUB719:GOSUB443:GOSUB455:GOSUB528:GOTO104
165 F=15-H(0)*15:C=30960+00*RS:POKEC+14+6*20,F+1
166 F=47-H(1)*47:C=30960+16*RS:POKEC+12+1*20,F
167 POKE30960+00*RS+12+4*20,49-H(2)*49
168 POKE30960+00*RS+07+2*20,52-H(5)*52
169 POKE30960+00*RS+03+2*20,51-H(9)*51
170 POKE30960+16*RS+00+0*20,47-H(3)*47
171 POKE30960+16*RS+01+0*20,72-H(4)*72
172 POKE30960+16*RS+02+0*20,76-H(8)*76
173 POKE30960+16*RS+03+0*20,77-H(10)*77
174 POKE30960+00*RS+07+4*20,13-H(6)*13
175 POKE30960+00*RS+09+5*20,13-H(7)*13
176 T=13:IFH(12)+H(13)=2THENT=0
177 POKE30960+00*RS+03+4*20,T
178 RETURN
179 IFCR=00ANDZX=11ANDZY=1ANDH(0)=0THENPRINT"{home}{white}{down}{right}DER SCHALTER IST AUS!";:GOSUB719:GOSUB528
180 IFCR=00ANDZX=11ANDZY=1ANDH(0)=1THENPRINT"{home}{white}{down}{right}DER SCHALTER IST EIN!";:GOSUB719:GOSUB528
181 IFCR=16ANDZX=00ANDZY=0ANDH(1)=0THENPRINT"{home}{white}{down}{right}DER SCHALTER IST AUS!";:GOSUB719:GOSUB528
182 IFCR=16ANDZX=00ANDZY=0ANDH(1)=1THENPRINT"{home}{white}{down}{right}DER SCHALTER IST EIN!";:GOSUB719:GOSUB528
183 RETURN
184 SP=30960+(CR*RS)+(TX+TY*20)
185 PRINT"{home}{down:3}"LEFT$(CD$,TY*2)SPC(TX*2)T$(PEEK(SP));
186 RETURN
187 PRINT"{home}{down}{white}{right}  UNTERSUCHE  EQUIPMENTS  INVENTAR"
188 M=0
189 POKEQS+42+M*12,35
190 GOSUB682
191 POKEQS+42+M*12,32
192 IFA$="A"THENM=M-1-3*(M=0):GOTO189
193 IFA$="D"THENM=M+1+3*(M=2):GOTO189
194 IFA$=CHR$(13)THENPOKEQS+42+M*7,32:GOTO196
195 GOTO189
196 PRINT"{home}{down}{white}{right}                                      ";
197 ONM+1GOTO251,211,198
198 POKE1020,Q1
199 GOSUB524
200 PRINT"{home}{down}{right}{white}INVENTAR: WAEHLE DEIN ITEM           ";
201 MT=2:MX=2:MY=4:MC=14:GOSUB466
202 IFIN(M)=9THEN99
203 IFIN(M)=0ORIT(IN(M))<>MTTHEN99
204 IFIN(M)=3THENGOSUB207:IN(M)=0:GOSUB455:GOTO99
205 IFIN(M)=8THENGOSUB209:IN(M)=0:GOSUB455:GOTO99
206 GOTO99
207 FORI=0TO3:PH(I)=PH(I)+IE(IN(M)):IFPH(I)>P1(I)THENPH(I)=P1(I)
208 FH(I)=PH(I):NEXT:RETURN
209 FORI=0TO3:PM(I)=PM(I)+IE(IN(M)):IFPM(I)>P2(I)THENPM(I)=P2(I)
210 FM(I)=PM(I):NEXT:RETURN
211 POKE1020,Q1
212 GOSUB530
213 PRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINEN SPIELER     ";
214 P=0
215 CP=QS+2+(4-8*(P>1))*40:IFP=1ORP=3THENCP=CP+20
216 POKECP,35
217 GOSUB682
218 POKECP,32
219 IFP=0ANDA$="D"ANDP3(1)=1THENP=1:GOTO215
220 IFP=0ANDA$="S"ANDP3(2)=1THENP=2:GOTO215
221 IFP=0ANDA$="D"ANDP3(1)=OANDP3(2)=0ANDP3(3)=1THENP=3:GOTO215
222 IFP=0ANDA$="S"ANDP3(1)=OANDP3(2)=0ANDP3(3)=1THENP=3:GOTO215
223 IFP=1ANDA$="A"ANDP3(0)=1THENP=0:GOTO215
224 IFP=1ANDA$="S"ANDP3(3)=1THENP=3:GOTO215
225 IFP=2ANDA$="D"ANDP3(3)=1THENP=3:GOTO215
226 IFP=2ANDA$="W"ANDP3(0)=1THENP=0:GOTO215
227 IFP=3ANDA$="A"ANDP3(2)=1THENP=2:GOTO215
228 IFP=3ANDA$="W"ANDP3(1)=1THENP=1:GOTO215
229 IFP=3ANDA$="A"ANDP3(2)=OANDP3(1)=0ANDP3(0)=1THENP=0:GOTO215
230 IFP=3ANDA$="W"ANDP3(2)=OANDP3(1)=0ANDP3(0)=1THENP=0:GOTO215
231 IFA$=CHR$(13)THEN233
232 GOTO216
233 SL=0:CP=CP+81
234 PRINT"{home}{down}{right}{white}EQUIPMENT: WAFFE ODER RUESTUNG       ";
235 POKECP+SL*40,35
236 GOSUB682
237 POKECP+SL*40,32:IFA$="W"ORA$="S"THENSL=1-SL:GOTO235
238 IFA$=CHR$(13)THEN240
239 GOTO235
240 MY=4:MX=23:MC=14:IFP=1ORP=3THENMX=3
241 IFSL=0THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE WAFFFE       ";
242 IFSL=1THENPRINT"{home}{down}{right}{white}EQUIPMENT: WAEHLE DEINE RUESTUNG     ";
243 PRINT"{home}"LEFT$(CD$,MY-1)SPC(MX-3);"{brown}{$c1}{$c2:18}{$c3}";
244 FORI=0TO13:PRINT"{down}{left:20}{$c4}                  {$c5}";:NEXT
245 PRINT"{down}{left:20}{$c6}{$c7:18}{$c8}";
246 MT=SL:GOSUB466
247 IFIT(IN(M))=9THEN99
248 IFIN(M)<>0ANDIT(IN(M))<>SLTHEN99
249 IFSL=0THENQ=PW(P):PW(P)=IN(M):IN(M)=Q:GOTO99
250 IFSL=1THENQ=PR(P):PR(P)=IN(M):IN(M)=Q:GOTO99
251 GOSUB528
252 GOSUB724
253 EI=0
254 IFER(EI)<>CROREX(EI)<>OXOREY(EI)<>OYTHEN259
255 NI=ET(EI):GOSUB561
256 IFIS=IXTHENPRINT"{home}{white}{down}{right}DU KANNST NICHT MEHR TRAGEN!":GOSUB719:GOSUB528:GOTO104
257 ER(EI)=-1
258 PRINT"{home}{white}{down}{right}GEFUNDEN: ";I$(IN(IS));:GOSUB719:GOSUB528:GOTO104
259 EI=EI+1
260 IFEI=ECTHEN262
261 GOTO254
262 PRINT"{home}{down}{right}{white}NICHTS BESONDERES!":GOSUB719:GOSUB528:GOTO104
263 FORI=4TO11:FT(I)=-1:NEXT
264 PP=4:CC=4:WX=0
265 FORI=0TO3:FT(I)=-2:FH(I)=PH(I):FM(I)=PM(I):FS(I)=PS(I)
266 IFP3(I)=0THENFH(I)=-1
267 IFFH(I)<=0THENFT(I)=-1:PP=PP-1
268 FA(I)=PA(I)+IA(PW(I)):FD(I)=PD(I)+ID(PR(I)):F1(I)=P1(I):F2(I)=P2(I)
269 NEXT
270 PRINT"{brown}{home}{$c1}{$c2:38}{$c3}";
271 PRINT"{$c4}                                      {$c5}";
272 PRINT"{$c6}{$c7:38}{$c8}";
273 PRINT"{home}{down:3}{white}";:FORI=0TO15:PRINT"                                        ";:NEXT
274 PRINTDD$;"{brown}{up}{$c1}{$c2:7}{white}HP{brown}{$c2}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
275 GOSUB424
276 GOSUB455
277 GOSUB463
278 FORI=0TO11
279 IFFT(I)<>-1THENFC(I)=INT(RND(0)*FD(I))
280 NEXT
281 CC=CC-4
282 PRINT"{home}{down}{right}                       ";:GOSUB572
283 FORI=4TO11:IFFT(I)=-1THEN285
284 PRINT"{home}"LEFT$(CD$,FY(I));SPC(FX(I));MD$(FT(I))
285 NEXT
286 C=0
287 IFFT(C)<>-1ANDFC(C)>=100THENFC(C)=FC(C)-100:GOTO290
288 C=C+1:IFC<12THEN287
289 FORI=0TO11:FC(I)=FC(I)+FS(I):NEXT:GOTO286
290 IFFT(C)=-2THEN292
291 GOTO373
292 PRINT"{home}{white}{down}{right}"P$(C)": IST AM ZUG                     "
293 PRINTDD$;SPC(16)"{white} ANGRIFF {down}{left:9} INVENTAR";:M=2
294 PRINTDD$;SPC(16)"{down:2}         {down}{left:9}         ";
295 IFPM(C)>0THENPRINTDD$;SPC(16)"{down:2} MAGIE   ";:M=3
296 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(37)"  {down}{left:2}  ";
297 PRINT"{left:3}{up}"PD$(C);
298 MY=0:FZ=0
299 POKEQS+816+MY*40,35
300 GOSUB682
301 IFA$="S"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY+1:GOTO305
302 IFA$="W"THENPOKEQS+816+MY*40,32:FZ=0:MY=MY-1:GOTO305
303 IFA$=CHR$(13)THENFZ=0:GOTO308
304 GOTO300
305 IFMY>=MTHENMY=0
306 IFMY<0THENMY=M-1
307 GOTO299
308 PRINT"{home}"LEFT$(CD$,5+C*3)SPC(36)"{white}  {down}{left:2}  {left}{up}"PD$(C);
309 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
310 ONMY+1GOTO336,328
311 MT=3:MX=16:MY=20:MC=4:GOSUB466
312 IFIT(IN(M))=9THEN292
313 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         {down}{left:9}         ";
314 IFIN(M)=0ORIT(IN(M))<>3THEN292
315 M=IN(M)
316 IFIE(M)>FM(C)THENPRINT"{home}{down}{right}NICHT GENUG MANA                     ";:GOSUB719:GOTO292
317 GOSUB588
318 GOTO614
319 AP=IE(M)+2-INT(RND(0)*5):FM(C)=FM(C)-IE(M):PM(C)=FM(C):GOSUB455
320 AP=AP+PL(C)*3
321 FH(CE)=FH(CE)-AP:IFFH(CE)<0THENFH(CE)=0
322 IFCE<4THENPH(CE)=FH(CE)
323 IFFH(CE)>0THENGOTO358
324 IFFT(CE)<>-2THENCC=CC-1:WX=WX+MX(FT(CE))
325 IFFT(CE)=-2THENPP=PP-1
326 FT(CE)=-1
327 GOTO358
328 MT=2:MX=16:MY=20:MC=4:GOSUB466
329 IFIT(IN(M))=9THEN292
330 PRINTDD$;SPC(16)"{white}         {down}{left:9}         {down}{left:9}         ";
331 PRINT"{down}{left:9}         ";
332 IFIN(M)=0ORIT(IN(M))<>MTTHEN292
333 IFIN(M)=3THENGOSUB207:IN(M)=0:GOSUB455:GOTO282
334 IFIN(M)=8THENGOSUB209:IN(M)=0:GOSUB455:GOTO282
335 GOTO282
336 GOSUB588
337 AP=FA(C)-FD(CE)
338 IFAP<=0THENPRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{white}MISS";:GOSUB717:GOTO350
339 AP=INT(RND(0)*AP)+1
340 FH(CE)=FH(CE)-AP:IFFH(CE)<0THENFH(CE)=0
341 IFCE<4THENPH(CE)=FH(CE)
342 IFFH(CE)>0THENGOTO347
343 IFFT(CE)<>-2THENCC=CC-1:WX=WX+MX(FT(CE))
344 IFFT(CE)=-2THENPP=PP-1
345 FT(CE)=-1
346 GOTO347
347 POKEV,(FX(CE)*8+21)AND255:POKEV+1,FY(CE)*8+50:POKE50168,16:POKEV+28,0
348 POKEV+16,-(FX(CE)>30):POKEV+21,1
349 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{white}";AP
350 FORI=0TO5:POKEV,PEEK(V)+2:POKEV+1,PEEK(V+1)+2:GOSUB718:NEXTI
351 POKEV+21,0:IFCE<4THENGOSUB455
352 GOSUB717
353 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
354 IFFT(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE));"  {down}{left:2}  ";
355 IFCC=0THEN381
356 IFPP=0THEN409
357 GOTO282
358 POKEV,(FX(CE)*8+24)AND255:POKEV+1,FY(CE)*8+50
359 IFM=4THENPOKEV+37,8:POKEV+38,2:POKEV+39,7:POKEV+28,1
360 B=17:IFM=6THENB=19:POKEV+28,0
361 IFM=5THENB=21:POKEV+28,0
362 IFM=7THENB=23:POKEV+28,0
363 POKEV+16,-(FX(CE)>30):POKE50168,B:POKEV+21,1
364 PRINT"{white}";:IFM=9THENPRINT"{green}";
365 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}";AP
366 FORK=0TO3:FORI=0TO1:POKE50168,B+I:FORJ=1TO40:NEXTJ,I,K
367 POKEV+21,0:IFCE<4THENGOSUB455
368 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
369 IFFT(CE)=-1THENPRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE));"  {down}{left:2}  ";
370 IFCC=0THEN381
371 IFPP=0THEN409
372 GOTO282
373 GOSUB717
374 PRINT"{white}{home}{down}{right}"M$(FT(C))": GREIFT AN                    "
375 FC(C)=FC(C)-100
376 IFFT(C)=7THENIFFM(C)>=IE(9)THENM=9:GOSUB717:GOSUB418:GOTO617
377 IFFT(C)=9THENIFFM(C)>=IE(6)THENM=6:GOSUB717:GOSUB412:GOTO319
378 IFFT(C)=10THENIFFM(C)>=IE(4)THENM=4:GOSUB717:GOSUB412:GOTO319
379 IFFT(C)=11THENIFFM(C)>=IE(12)THENM=12:GOSUB717:GOSUB412:GOTO624
380 GOSUB717:GOSUB412:GOTO337
381 POKE1020,Q1
382 PRINTDD$;"{brown}{up}{$c1}{$c2:10}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}{white}";
383 PRINT"{home}{down}{right}DU HAST GEWONNEN                     ";:GOSUB572
384 GOSUB524
385 FORI=0TO3:IFP3(I)=0THEN389
386 PRINT"{home}{white}{right:3}";:IFPH(I)=0THENPRINT"{red}";
387 PRINTLEFT$(CD$,4+I*3)P$(I)
388 PRINT"{home}"LEFT$(CD$,5+I*3)"{right:3}LV";PL(I)+1;" EX";PX(I)
389 NEXT
390 FORI=0TO3:IFPH(I)=0ORP3(I)=0THEN400
391 TX=WX
392 PRINT"{home}{white}"LEFT$(CD$,5+I*3)SPC(20)"EXPERIENCE {left}"TX"{left} ";
393 IFTX=0THEN400
394 XS=1:IFTX>9THENXS=10
395 IFTX>99THENXS=100
396 TX=TX-XS
397 PX(I)=PX(I)+XS
398 LL=10+PL(I)*PL(I)*10:IFPX(I)>=LLTHENGOSUB402:GOTO398
399 PRINT"{home}{white}"LEFT$(CD$,5+I*3)"{right:3}LV";PL(I)+1;" EX";PX(I):GOTO392
400 NEXTI
401 GOTO407
402 PL(I)=PL(I)+1:PA(I)=PA(I)+1:PD(I)=PD(I)+1:PS(I)=PS(I)+1
403 P1(I)=P1(I)+PL(I)*2:IFP2(I)>0THENP2(I)=P2(I)+PL(I)*3
404 IFP1(I)>999THENP1(I)=999
405 IFP2(I)>999THENP2(I)=999
406 RETURN
407 GOSUB710
408 GOTO99
409 PRINT"{home}{down}{right}{white}DU HAST VERLOREN                     "
410 GOSUB710
411 GOTO632
412 RE=INT(RND(0)*4):CE=0
413 IFFT(CE)=-2ANDPH(CE)>0THEN416
414 CE=CE+1:IFCE=4THENCE=0
415 GOTO413
416 IFRE>0THENRE=RE-1:GOTO414
417 RETURN
418 RE=INT(RND(0)*8):CE=4
419 IFFT(CE)>=0THEN422
420 CE=CE+1:IFCE=12THENCE=4
421 GOTO419
422 IFRE>0THENRE=RE-1:GOTO420
423 RETURN
424 IFFF>0THENL=FF:GOTO426
425 L=PEEK(CP+164):IFL=0THENRETURN
426 IFL=3THENA=0:LC=INT(RND(0))*2+2:FORI=0TOLC:GOSUB566:NEXT:RETURN
427 IFL=1THENA=1:FORI=0TO2:GOSUB566:NEXT:RETURN
428 IFL=2THENA=1:FORI=0TO1:GOSUB566:NEXT:A=2:FORI=0TO1:GOSUB566:NEXT:RETURN
429 IFL=4THENA=3:GOSUB566:A=1:GOSUB566:GOSUB566:RETURN
430 IFL=5THENA=4:LC=INT(RND(0)*3)+1:FORI=0TOLC:GOSUB566:NEXT:RETURN
431 IFL=6THENONINT(RND(1)*2)+1GOTO441,442
432 IFL=7THENLC=INT(RND(0)*3)+1:A=6:FORI=0TOLC:GOSUB566:NEXT:RETURN
433 IFL=8THENA=7:GOSUB566:LC=INT(RND(0)*2)+1:A=6:GOSUB566:RETURN
434 IFL=9THENA=9:GOSUB566:A=8:GOSUB566:A=5:GOSUB566:RETURN
435 IFL=10THENA=6:LC=INT(RND(0)*2)+1:FORI=1TOLC:GOSUB566:NEXT:RETURN
436 IFL=11THENA=7:GOSUB566:A=10:GOSUB566:RETURN
437 IFL=12ANDINT(RND(1)*2)=1THENA=11:GOSUB566:RETURN
438 IFL=12THENA=5:GOSUB566:GOSUB566:A=9:GOSUB566:RETURN
439 IFL=13THENA=9:GOSUB566:A=12:GOSUB566:A=9:GOSUB566:RETURN
440 IFL=14THENA=9:GOSUB566:A=13:GOSUB566:A=7:GOSUB566:RETURN
441 A=5:LC=INT(RND(1)*2)+1:FORI=0TOLC:GOSUB566:NEXT:RETURN
442 A=9:LC=INT(RND(1)*2)+1:FORI=1TOLC:GOSUB566:NEXT:RETURN
443 FORI=0TO3
444 IFP3(I)=0THEN447
445 PH(I)=P1(I)
446 PM(I)=P2(I)
447 NEXT:RETURN
448 PRINT"{brown}{home}{$c1}{$c2:38}{$c3}";
449 PRINT"{$c4}                                      {$c5}";
450 PRINT"{$c6}{$c7:38}{$c8}";
451 PRINTDD$;"{brown}{up}{$c1}{$c2:10}{$c3}{$c1}{$c2:12}{$c3}{$c1}{$c2:5}{white}HP{brown}{$c2:2}{white}MP{brown}{$c2}{$c3}";
452 FORI=0TO3:PRINT"{$c4}          {$c5}{$c4}            {$c5}{$c4}            {$c5}";:NEXT
453 PRINT"{$c6}{$c7:10}{$c8}{$c6}{$c7:12}{$c8}{$c6}{$c7:12}";
454 POKE50151,72:POKE56295,9:RETURN
455 PRINTDD$;
456 FORI=0TO3:IFP3(I)=0THENPRINTSPC(40);:GOTO462
457 PRINT"{white}";:IFPH(I)=0THENPRINT"{red}";
458 PRINTSPC(27)P$(I)" ";:A=PH(I):GOSUB680
459 PRINTD$;
460 A=PM(I):GOSUB680
461 PRINT" "D$"{right}";
462 NEXT:RETURN
463 FORI=0TO3:IFPH(I)<=0ORP3(I)=0THENNEXT:RETURN
464 PRINT"{home}"LEFT$(CD$,5+I*3);SPC(37);PD$(I):NEXT
465 RETURN
466 GOSUB501
467 MI=-1:M=0:MO=0
468 MI=MO-1:CY=MY:MM=0
469 IFMC=4THENGOSUB496
470 MI=MI+1
471 PRINT"{home}{white}"LEFT$(CD$,CY)SPC(MX)" ";
472 IFIT(IN(MI))<>MTTHENPRINT"{cyan}";
473 IFIT(IN(MI))=9THENPRINT"{white}";
474 IFIT(IN(MI))=-1THENPRINT"{cyan}";
475 PRINTI$(IN(MI));
476 CY=CY+1
477 MM=MM+1
478 IFMM=MCTHEN480
479 IFMI<13THEN470
480 CP=QS+MX+MY*40
481 POKECP+(M-MO)*40,35
482 GOSUB682
483 POKECP+(M-MO)*40,32
484 IFA$="W"THENM=M-1:GOTO488
485 IFA$="S"THENM=M+1:GOTO488
486 IFA$=CHR$(13)THENRETURN
487 GOTO481
488 IFM=-1THENM=14-1:MO=14-MC:GOTO468
489 IFM=14THENM=0:MO=0:GOTO468
490 M%=0
491 IFM<MOTHENMO=MO-MC:M%=1:GOTO491
492 IFM>=MO+MCTHENMO=MO+MC:M%=1
493 IFMO<0THENMO=0:M%=1
494 IFM%=1THEN468
495 GOTO481
496 FORI=0TO3
497 PRINT"{home}{white}"LEFT$(CD$,CY+I)SPC(MX)" ";
498 PRINT"        ";
499 NEXTI
500 RETURN
501 A=0
502 FORI=0TO99:TT(I)=0:NEXTI
503 FORI=0TO99:TT(I)=IN(I):IN(I)=0:NEXTI
504 IFMT=0THENGOSUB509:GOSUB512:A=50:GOSUB515:GOSUB518:GOSUB521
505 IFMT=1THENGOSUB509:GOSUB515:A=50:GOSUB512:GOSUB518:GOSUB521
506 IFMT=2THENGOSUB509:GOSUB518:A=50:GOSUB512:GOSUB515:GOSUB521
507 IFMT=3THENGOSUB509:GOSUB521:A=50:GOSUB512:GOSUB515:GOSUB518
508 RETURN
509 FORI=0TO99:IFIT(TT(I))=9THENIN(A)=TT(I):A=A+1
510 NEXTI
511 RETURN
512 FORI=0TO99:IFIT(TT(I))=0THENIN(A)=TT(I):A=A+1
513 NEXTI
514 RETURN
515 FORI=0TO99:IFIT(TT(I))=1THENIN(A)=TT(I):A=A+1
516 NEXTI
517 RETURN
518 FORI=0TO99:IFIT(TT(I))=2THENIN(A)=TT(I):A=A+1
519 NEXTI
520 RETURN
521 FORI=0TO99:IFIT(TT(I))=3THENIN(A)=TT(I):A=A+1
522 NEXTI
523 RETURN
524 PRINT"{brown}{home}{down:3}{$c1}{$c2:38}{$c3}";
525 FORI=0TO13
526 PRINT"{$c4}                                      {$c5}";:NEXT
527 PRINT"{$c6}{$c7:38}{$c8}";:RETURN
528 PRINT"{home}{down}{right}{white}                                      ";
529 RETURN
530 PRINT"{brown}{home}{down:3}{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
531 FORI=0TO5
532 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
533 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
534 PRINT"{$c1}{$c2:18}{$c3}{$c1}{$c2:18}{$c3}";
535 FORI=0TO5
536 PRINT"{$c4}                  {$c5}{$c4}                  {$c5}";:NEXT
537 PRINT"{$c6}{$c7:18}{$c8}{$c6}{$c7:18}{$c8}";
538 DX=3:DY=4:P=0:GOSUB541:DX=23:P=1:GOSUB541
539 DX=3:DY=12:P=2:GOSUB541:DX=23:P=3:GOSUB541
540 RETURN
541 IFP3(P)=0THENRETURN
542 PRINT"{home}{white}";:IFPH(P)=0THENPRINT"{red}";
543 PRINT"{home}"LEFT$(CD$,DY)SPC(DX-1)" "P$(P)"  LV"PL(P)+1
544 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX)" "I$(PW(P));
545 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX)" "I$(PR(P));
546 T1=PA(P)+IA(PW(P))
547 T2=PD(P)+ID(PR(P))
548 IFT1>999THENT1=999
549 IFT2>999THENT2=999
550 PRINT"{home}{white}{down:2}"LEFT$(CD$,DY)SPC(DX+9)"ATK"T1;
551 PRINT"{home}{white}{down:3}"LEFT$(CD$,DY)SPC(DX+9)"DEF"T2;
552 RETURN
553 IFPEEK(2)=1THENSP=30960+RS*(CR-0)
554 FORI=0TO7
555 PRINT"{home}{down:3}"LEFT$(CD$,I*2);
556 FORJ=0TO19
557 PRINTT$(PEEK(SP))"{up}";
558 SP=SP+1
559 NEXTJ,I
560 RETURN
561 IS=0
562 IFIN(IS)=0THENIN(IS)=NI:NI=-1:RETURN
563 IS=IS+1
564 IFIS=IXTHENRETURN
565 GOTO562
566 IFCC>=11THENRETURN
567 FH(CC)=MH(A):FM(CC)=MM(A):FT(CC)=A:FC(CC)=INT(RND(0)*MS(A)):FS(CC)=MS(A)
568 FX(CC)=2:FY(CC)=5+CC*3:FA(CC)=MA(A):FD(CC)=MD(A):FB(CC)=MB(A)
569 F1(CC)=MH(A):F2(CC)=MM(A)
570 IFCC>=4THENFX(CC)=5:FY(CC)=5+(CC-4)*3
571 CC=CC+1:RETURN
572 ZM=0
573 FORJ=4TO7
574 IFFT(J)=-1THENZM=ZM-1
575 ZM=ZM+1
576 NEXTJ
577 IFZM<>CMTHENPRINTDD$;"{right}          {down}{left:10}          {down}{left:10}          {down}{left:10}          ";
578 PRINTDD$;
579 CM=0
580 FORI=4TO7
581 IFFT(I)=-1THENNEXT:RETURN
582 A=FH(I):GOSUB680
583 PRINT"{home}{right}"LEFT$(CD$,20+CM)M$(FT(I));
584 PRINT"{home}{right}"LEFT$(CD$,20+CM);"{right:7}";D$
585 CM=CM+1
586 NEXT
587 RETURN
588 CE=4:PRINT"{home}{down}{right}{white}"P$(C)": WAHELE DEIN ZIEL               "
589 IFFT(CE)=-1THEN598
590 POKEQS-1+FX(CE)+FY(CE)*40,35
591 GOSUB682
592 IFA$=CHR$(13)THENPOKEQS-1+FX(CE)+FY(CE)*40,32:RETURN
593 IFA$="S"THENGOTO598
594 IFA$="W"THENGOTO606
595 IFA$="D"ANDCE>3THENPOKEQS-1+FX(CE)+FY(CE)*40,32:CE=0:GOTO589
596 IFA$="A"ANDCE<4THENPOKEQS-1+FX(CE)+FY(CE)*40,32:CE=4:GOTO589
597 GOTO591
598 CO=CE
599 POKEQS-1+FX(CE)+FY(CE)*40,32
600 CE=CE+1
601 IFCE>=12THENCE=0
602 IFCO<4ANDCE>3THENCE=CO
603 IFCO>3ANDCE<4THENCE=CO
604 IFFT(CE)<>-1THEN590
605 GOTO600
606 CO=CE
607 POKEQS-1+FX(CE)+FY(CE)*40,32
608 CE=CE-1
609 IFCE<0THENCE=11
610 IFCO<4ANDCE>3THENCE=CO
611 IFCO>3ANDCE<4THENCE=CO
612 IFFT(CE)<>-1THEN590
613 GOTO608
614 IFFT(C)=-2THEN630
615 IFFT(C)=7ANDFM(C)>=IE(9)THENM=9:GOTO617
616 GOTO319
617 AP=IE(M)*4+2-INT(RND(0)*5):FM(C)=FM(C)-IE(M):IFC<4THENPM(C)=FM(C):GOSUB455
618 FH(CE)=FH(CE)+AP:IFFH(CE)>F1(CE)THENFH(CE)=F1(CE)
619 IFCE<4THENPH(CE)=FH(CE):GOSUB455
620 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{green}";AP
621 GOSUB724
622 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
623 GOTO282
624 FM(C)=FM(C)-IE(M):IFC<4THENPM(C)=FM(C):GOSUB455
625 FS(CE)=FS(CE)/2:IFFS(CE)<5THENFS(CE)=5
626 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}{green}SLOW";
627 GOSUB724
628 PRINT"{home}"LEFT$(CD$,FY(CE))SPC(FX(CE))"{up}{left}    "
629 GOTO282
630 IFM=9THEN617
631 GOTO319
632 FORI=0TOHC-1:H(I)=0:NEXT
633 FORI=0TO3:PX(I)=0:NEXT
634 PA(0)=5:PD(0)=2:PA(1)=4:PD(1)=3:PA(2)=1:PD(2)=1:PA(3)=1:PD(3)=1:GOSUB165
635 FORI=0TO13:IN(I)=0:NEXT
636 IN(0)=18
637 ER(0)=16:EX(0)=0:EY(0)=0:ET(1)=0
638 ER(1)=0:EX(1)=1:EY(1)=2:ET(1)=3
639 ER(2)=0:EX(2)=18:EY(2)=2:ET(2)=15
640 ER(3)=16:EX(3)=0:EY(3)=0:ET(3)=4
641 ER(4)=16:EX(4)=0:EY(4)=0:ET(4)=8
642 ER(5)=16:EX(5)=0:EY(5)=0:ET(5)=5
643 ER(6)=16:EX(6)=0:EY(6)=0:ET(6)=16
644 ER(7)=16:EX(7)=0:EY(7)=0:ET(7)=17
645 ER(8)=16:EX(8)=0:EY(8)=0:ET(8)=13
646 ER(9)=16:EX(9)=0:EY(9)=0:ET(9)=15
647 ER(10)=16:EX(10)=0:EY(10)=0:ET(10)=11
648 ER(11)=16:EX(11)=0:EY(11)=0:ET(11)=7
649 ER(12)=16:EX(12)=0:EY(12)=0:ET(12)=9
650 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
651 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
652 PRINT"{$c6}{$c7:38}{up:3}";
653 POKE50151,72:POKE56295,9
654 X=15:Y=2:CR=0
655 P3(0)=1:P3(1)=0:P3(2)=0:P3(3)=0
656 P1(0)=15:P1(1)=17:P1(2)=9:P1(3)=9:P2(0)=0:P2(1)=0:P2(2)=7:P2(3)=23
657 FORI=0TO3:PH(I)=P1(I):PM(I)=P2(I):PX(I)=0:PW(I)=0:PR(I)=0:NEXT
658 PW(0)=1:PR(0)=2
659 GOSUB165
660 AA$="INTRO":GOSUB668
661 GOTO94
662 PRINT"{white}{clear}{brown}{$c1}{$c2:38}{$c3}";
663 FORI=1TO23:PRINT"{$c4}"SPC(38)"{$c5}";:NEXT
664 PRINT"{$c6}{$c7:38}{up:3}";
665 POKE50151,72:POKE56295,9
666 AA$="ENDE":GOSUB668
667 GOTO94
668 POKE1020,Q7
669 POKE56322,224
670 IFAA$="INTRO"THENOPEN1,8,4,"TXT.INTRO,S,R"
671 IFAA$="ENDE"THENOPEN1,8,4,"TXT.ENDE,S,R"
672 IFAA$=""THENRETURN
673 GET#1,B$
674 IFB$="$"THENPRINT"{home}{down:2}{right:2}{white}";:GOTO673
675 IFB$="%"THENPRINT"{right:2}{white}";:GOTO673
676 IFB$="#"THENCLOSE1:POKE56322,255:GOSUB710:RETURN
677 PRINTB$;
678 IFB$<>" "THENPOKE162,0:WAIT162,4
679 GOTO673
680 T$=STR$(A)
681 D$=RIGHT$("000"+RIGHT$(T$,LEN(T$)-1),3):RETURN
682 A$=""
683 J=PEEK(56320)
684 IFJ=127THEN686
685 GOTO683
686 J=PEEK(56320)
687 IFJ=127ANDA$=CHR$(13)THENRETURN
688 IFJ=107THENA$="E":GOSUB715:RETURN
689 IFJ=103THENA$="E":GOSUB715:RETURN
690 IFJ=110THENA$="E":GOSUB715:RETURN
691 IFJ=109THENA$="E":GOSUB715:RETURN
692 IFJ=123THENA$="A":GOSUB715:RETURN
693 IFJ=119THENA$="D":GOSUB715:RETURN
694 IFJ=126THENA$="W":GOSUB715:RETURN
695 IFJ=125THENA$="S":GOSUB715:RETURN
696 IFJ=111THENA$=CHR$(13)
697 GOTO686
698 A$=""
699 J=PEEK(56320)
700 IFJ<>111ANDJ<>107ANDJ<>103ANDJ<>110ANDJ<>109THEN702
701 GOTO699
702 J=PEEK(56320)
703 IFJ=127ANDA$=CHR$(13)THENRETURN
704 IFJ=123THENA$="A":GOSUB716:RETURN
705 IFJ=119THENA$="D":GOSUB716:RETURN
706 IFJ=126THENA$="W":GOSUB716:RETURN
707 IFJ=125THENA$="S":GOSUB716:RETURN
708 IFJ=111THENA$=CHR$(13)
709 GOTO702
710 A$=""
711 J=PEEK(56320)
712 IFJ=127ANDA$=CHR$(13)THENRETURN
713 IFJ=111THENA$=CHR$(13)
714 GOTO711
715 POKE162,0:WAIT162,16:RETURN
716 POKE162,0:WAIT162,8:RETURN
717 POKE162,0:WAIT162,32:RETURN
718 POKE162,0:WAIT162,2:RETURN
719 POKE162,0:WAIT162,32
720 POKE162,0:WAIT162,32
721 POKE162,0:WAIT162,32
722 POKE162,0:WAIT162,32
723 RETURN
724 POKE162,0:WAIT162,32
725 RETURN
726 POKE162,0:WAIT162,4
727 RETURN
728 DATA"IMP",5,0,8,4,1,0,3
729 DATA"SNAKE",8,0,12,6,1,0,5
730 DATA"VIPER",12,0,18,6,1,0,9
731 DATA"NACHO",30,0,22,15,5,0,50
732 DATA"GOLEM",20,0,4,20,8,0,17
733 DATA"MOSS",30,0,7,26,9,0,21
734 DATA"BONES",50,0,20,18,8,0,15
735 DATA"WISP",25,50,15,0,8,0,17
736 DATA"TROLL",100,0,9,40,13,0,100
737 DATA"SPARK",30,50,22,0,8,0,17
738 DATA"BURNER",80,90,30,25,13,0,42
739 DATA"TBERRY",120,10,60,60,20,0,150
740 DATA"DRACUL",250,60,50,70,25,0,500
741 DATA"GLADOS",500,70,60,80,30,0,1000
742 DATA"KRON",15 ,0  ,10 ,4 ,3 ,0 ,0
743 DATA"LENA",17 ,0  ,10 ,3 ,3 ,0 ,0
744 DATA"DOLM",9  ,7  ,8  ,1 ,1 ,0 ,0
745 DATA"MIRA",9  ,23 ,7  ,1 ,1 ,1 ,2
746 DATA"{gray1}{rvrs off}{$c9}{rvrs off}{gray1}{$ca}{down}{left:2}{rvrs off}{gray1}{$ca}{rvrs off}{gray1}{$c9}{rvrs off}"
747 DATA"{gray1}{rvrs off}{$cb}{rvrs off}{gray1}{$cc}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1}{$ca}{rvrs off}"
748 DATA"{gray1}{rvrs off}{$c9}{rvrs off}{gray1}{$ca}{down}{left:2}{rvrs off}{gray1}{$ca}{rvrs off}{gray1}{$c9}{rvrs off}"
749 DATA"{gray1}{rvrs off}{$cd}{rvrs off}{gray1}{$ce}{down}{left:2}{rvrs off}{gray1}{$cf}{rvrs off}{gray1}{$d0}{rvrs off}"
750 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
751 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
752 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
753 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
754 DATA"{orange}{rvrs off}{$d1}{rvrs off}{orange}{$d1}{down}{left:2}{rvrs off}{orange}{$d1}{rvrs off}{orange}{$d1}{rvrs off}"
755 DATA"{brown}{rvrs off}{$d2}{rvrs off}{brown}{$d3}{down}{left:2}{rvrs off}{brown}{$d4}{rvrs off}{brown}{$d5}{rvrs off}"
756 DATA"{lt. red}{rvrs off}{$d6}{rvrs off}{lt. red}{$d7}{down}{left:2}{rvrs off}{lt. red}{$d8}{rvrs off}{lt. red}{$d9}{rvrs off}"
757 DATA"{gray1}{rvrs off}{$da}{rvrs off}{gray1}{$db}{down}{left:2}{rvrs off}{gray1}{$dc}{rvrs off}{gray1}{$dd}{rvrs off}"
758 DATA"{gray1}{rvrs off}{$a2}{rvrs off}{gray1}{$a3}{down}{left:2}{rvrs off}{gray1}{$a4}{rvrs off}{gray1}{$a5}{rvrs off}"
759 DATA"{brown}{rvrs off}{$a1}{rvrs off}{brown}{$a1}{down}{left:2}{rvrs off}{brown}{$a1}{rvrs off}{brown}{$a1}{rvrs off}"
760 DATA"{gray1}{rvrs off}{$de}{rvrs off}{gray1}{$df}{down}{left:2}{rvrs off}{gray1}{$c9}{rvrs off}{gray1}{$ca}{rvrs off}"
761 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
762 DATA"{lt. red}{rvrs off}{$a6}{rvrs off}{lt. red}{$a7}{down}{left:2}{rvrs off}{lt. red}{cmdr pound}{rvrs off}{lt. red}{shft pound}{rvrs off}"
763 DATA"{lt. red}{rvrs off}{$aa}{rvrs off}{lt. red}{$aa}{down}{left:2}{rvrs off}{lt. red}{$aa}{rvrs off}{lt. red}{$aa}{rvrs off}"
764 DATA"{brown}{rvrs off}{$ab}{rvrs off}{brown}{$ab}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
765 DATA"{brown}{rvrs off}{$ab}{rvrs off}{brown}{$ad}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
766 DATA"{brown}{rvrs off}{$ad}{rvrs off}{brown}{$ab}{down}{left:2}{rvrs off}{brown}{$ac}{rvrs off}{brown}{$ac}{rvrs off}"
767 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
768 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
769 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
770 DATA"{lt. green}{rvrs off}{$ae}{rvrs off}{lt. green}{$d1}{down}{left:2}{rvrs off}{lt. green}{$d1}{rvrs off}{lt. green}{$af}{rvrs off}"
771 DATA"{lt. green}{rvrs off}{$b0}{rvrs off}{lt. green}{$b1}{down}{left:2}{rvrs off}{lt. green}{$b2}{rvrs off}{lt. green}{$b3}{rvrs off}"
772 DATA"{lt. green}{rvrs off}{$b4}{rvrs off}{lt. green}{$b5}{down}{left:2}{rvrs off}{lt. green}{$b4}{rvrs off}{lt. green}{$b5}{rvrs off}"
773 DATA"{lt. green}{rvrs off}{$b6}{rvrs off}{lt. green}{$b7}{down}{left:2}{rvrs off}{lt. green}{$b8}{rvrs off}{lt. green}{$b9}{rvrs off}"
774 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
775 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
776 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
777 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
778 DATA"{brown}{rvrs off}{$ae}{rvrs off}{brown}{$d1}{down}{left:2}{rvrs off}{brown}{$d1}{rvrs off}{brown}{$af}{rvrs off}"
779 DATA"{brown}{rvrs off}{$b0}{rvrs off}{brown}{$b1}{down}{left:2}{rvrs off}{brown}{$b2}{rvrs off}{brown}{$b3}{rvrs off}"
780 DATA"{brown}{rvrs off}{$b4}{rvrs off}{brown}{$b5}{down}{left:2}{rvrs off}{brown}{$b4}{rvrs off}{brown}{$b5}{rvrs off}"
781 DATA"{brown}{rvrs off}{$b6}{rvrs off}{brown}{$b7}{down}{left:2}{rvrs off}{brown}{$b8}{rvrs off}{brown}{$b9}{rvrs off}"
782 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
783 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
784 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
785 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
786 DATA"{gray1}{rvrs off}{$d1}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$d1}{rvrs off}{gray1}{$d1}{rvrs off}"
787 DATA"{gray1}{rvrs off}{$ba}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$ba}{rvrs off}{gray1}{$d1}{rvrs off}"
788 DATA"{gray1}{rvrs off}{$ba}{rvrs off}{gray1}{$d1}{down}{left:2}{rvrs off}{gray1}{$bb}{rvrs off}{gray1}{$d1}{rvrs off}"
789 DATA"{gray1}{rvrs off}{$bc}{rvrs off}{gray1}{$bd}{down}{left:2}{rvrs off}{gray1}{$be}{rvrs off}{gray1}{$be}{rvrs off}"
790 DATA"{gray1}{rvrs off}{$bf}{rvrs off}{gray1}{$bf}{down}{left:2}{rvrs on}{gray1}@{rvrs on}{gray1}@{rvrs off}"
791 DATA"{gray2}{rvrs on}A{rvrs on}{gray2}B{down}{left:2}{rvrs on}{gray2}C{rvrs on}{gray2}D{rvrs off}"
792 DATA"{gray1}{rvrs on}E{rvrs on}{gray1}F{down}{left:2}{rvrs on}{gray1}G{rvrs on}{gray1}H{rvrs off}"
793 DATA"{brown}{rvrs on}I{rvrs on}{brown}I{down}{left:2}{rvrs on}{brown}I{rvrs on}{brown}I{rvrs off}"
794 DATA"{orange}{rvrs on}J{rvrs on}{orange}K{down}{left:2}{rvrs on}{orange}L{rvrs on}{orange}M{rvrs off}"
795 DATA"{gray1}{rvrs on}N{rvrs on}{gray1}K{down}{left:2}{rvrs on}{gray1}L{rvrs on}{gray1}M{rvrs off}"
796 DATA"{lt. green}{rvrs on}O{rvrs on}{lt. green}P{down}{left:2}{rvrs on}{lt. green}Q{rvrs on}{lt. green}R{rvrs off}"
797 DATA"{gray2}{rvrs on}S{rvrs on}{gray2}T{down}{left:2}{rvrs on}{gray2}U{rvrs on}{gray2}V{rvrs off}"
798 DATA"{gray1}{rvrs on}W{rvrs on}{gray1}X{down}{left:2}{rvrs on}{gray1}Y{rvrs on}{gray1}Z{rvrs off}"
799 DATA"{gray1}{rvrs on}[{rvrs on}{gray1}{pound}{down}{left:2}{rvrs on}{gray1}]{rvrs on}{gray1}{^}{rvrs off}"
800 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
801 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
802 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
803 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
804 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
805 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
806 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
807 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
808 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
809 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
810 DATA"{lt. green}{rvrs on}{back arrow}{rvrs on}{lt. green}!{down}{left:2}{rvrs on}{lt. green}#{rvrs on}{lt. green}${rvrs off}"
811 DATA"{gray3}{rvrs on}%{rvrs on}{gray3}&{down}{left:2}{rvrs on}{gray3}'{rvrs on}{gray3}({rvrs off}"
812 DATA"{gray1}{rvrs on}%{rvrs on}{gray1}&{down}{left:2}{rvrs on}{gray1}'{rvrs on}{gray1}({rvrs off}"
813 DATA"{lt. green}{rvrs on}){rvrs on}{lt. green}*{down}{left:2}{rvrs on}{lt. green}+{rvrs on}{lt. green},{rvrs off}"
814 DATA"{gray1}{rvrs on}-{rvrs on}{gray1}.{down}{left:2}{rvrs on}{gray1}/{rvrs on}{gray1}0{rvrs off}"
815 DATA"{gray1}{rvrs on}1{rvrs on}{gray1}2{down}{left:2}{rvrs on}{gray1}3{rvrs on}{gray1}4{rvrs off}"
816 DATA"{gray1}{rvrs on}5{rvrs on}{gray1}6{down}{left:2}{rvrs on}{gray1}7{rvrs on}{gray1}8{rvrs off}"
817 DATA"{gray1}{rvrs on}9{rvrs on}{gray1}:{down}{left:2}{rvrs on}{gray1};{rvrs on}{gray1}<{rvrs off}"
818 DATA"{gray1}{rvrs on}={rvrs on}{gray1}>{down}{left:2}{rvrs on}{gray1}?{rvrs on}{gray1}{$c0}{rvrs off}"
819 DATA"{lt. green}{rvrs on}9{rvrs on}{lt. green}:{down}{left:2}{rvrs on}{lt. green};{rvrs on}{lt. green}<{rvrs off}"
820 DATA"{lt. red}{rvrs on}5{rvrs on}{lt. red}6{down}{left:2}{rvrs on}{lt. red}7{rvrs on}{lt. red}8{rvrs off}"
821 DATA"{lt. green}{rvrs on}5{rvrs on}{lt. green}6{down}{left:2}{rvrs on}{lt. green}7{rvrs on}{lt. green}8{rvrs off}"
822 DATA"{gray1}{rvrs on}{$c1}{rvrs on}{gray1}{$c2}{down}{left:2}{rvrs on}{gray1}{$c3}{rvrs on}{gray1}{$c4}{rvrs off}"
823 DATA"{gray1}{rvrs on}{$c5}{rvrs on}{gray1}{$c6}{down}{left:2}{rvrs on}{gray1}{$c7}{rvrs on}{gray1}{$c8}{rvrs off}"
824 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
825 DATA"{gray1}{rvrs off} {rvrs off}{gray1} {down}{left:2}{rvrs off}{gray1} {rvrs off}{gray1} {rvrs off}"
826 DATA"        ",0    ,0    ,0    ,0    ,-1 
827 DATA"DOLCH   ",3    ,0    ,0    ,0    ,0  
828 DATA"HELM    ",0    ,2    ,-1   ,0    ,1  
829 DATA"KRAEUTER",0    ,0    ,-1   ,10   ,2  
830 DATA"FEUER   ",0    ,0    ,0    ,5    ,3  
831 DATA"EIS     ",0    ,0    ,0    ,8    ,3  
832 DATA"MUT     ",0    ,0    ,0    ,23   ,3  
833 DATA"BOMBE   ",0    ,0    ,0    ,50   ,3  
834 DATA"KIRSCHE ",0    ,0    ,-1   ,15   ,2  
835 DATA"HEILEN  ",0    ,0    ,0    ,4    ,3  
836 DATA"SKULL   ",0    ,0    ,0    ,0    ,4  
837 DATA"FLEGEL  ",7    ,0    ,-1   ,0    ,0  
838 DATA"LAHM    ",0    ,0    ,0    ,4    ,3  
839 DATA"STAB    ",12   ,0    ,0    ,0    ,0  
840 DATA"FEDER   ",0    ,10   ,0    ,0    ,1  
841 DATA"STOCK   ",23   ,0    ,0    ,0    ,0  
842 DATA"WESTE   ",0    ,14   ,0    ,0    ,1  
843 DATA"SCHILD  ",0    ,24   ,0    ,0    ,1  
844 DATA"ZURUECK ",0    ,0    ,0    ,0    ,9  
845 END
